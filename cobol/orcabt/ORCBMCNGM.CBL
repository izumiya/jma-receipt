      *******************************************************************
      * Project code name "ORCA"
      * 日医標準レセプトソフト（JMA standard receipt software）
      * Copyright(C) 2002 JMA (Japan Medical Association)
      *
      * This program is part of "JMA standard receipt software".
      *
      *     This program is distributed in the hope that it will be useful
      * for further advancement in medical care, according to JMA Open
      * Source License, but WITHOUT ANY WARRANTY.
      *     Everyone is granted permission to use, copy, modify and
      * redistribute this program, but only under the conditions described
      * in the JMA Open Source License. You should have received a copy of
      * this licence along with this program. If not, stop using this
      * program and contact JMA, 2-28-16 Honkomagome, Bunkyo-ku, Tokyo,
      * 113-8621, Japan.
      ********************************************************************
       IDENTIFICATION          DIVISION.
       PROGRAM-ID.             ORCBMCNGM.
      *****************************************************************
      *  システム名        : ＯＲＣＡ
      *  サブシステム名    : 
      *  コンポーネント名  : 入院・外来置換え処理（無床診療所）
      *  管理者            : 
      *  作成日付    作業者        記述
      *  03/03/12    NACL-多々納   新規作成
      *****************************************************************
      *  プログラム修正履歴
      * Maj/Min/Rev  修正者       日付      内容
      *****************************************************************
      *
       ENVIRONMENT             DIVISION.
       CONFIGURATION               SECTION.
       INPUT-OUTPUT                SECTION.
       FILE-CONTROL.
      *
      *    出力更新ファイル
      *    SELECT  PTHKN-FILE      ASSIGN
      *                            ASS-PTHKN
      *                            ORGANIZATION    IS  LINE SEQUENTIAL
      *                            FILE    STATUS  IS  STS-PTHKN.
      *    出力更新ファイル
      *    SELECT  OUT-FILE        ASSIGN
      *                            ASS-OUT
      *                            ORGANIZATION    IS  LINE SEQUENTIAL
      *                            FILE    STATUS  IS  STS-OUT.
      *    エラーファイル
           SELECT  RECEERR-FILE        ASSIGN  RECEERR
                                       FILE    STATUS  IS  STS-RECEERR.
      *
      *
       DATA                        DIVISION.
       FILE                        SECTION.
      *
      *    出力更新ファイル
      *FD  PTHKN-FILE.
      *01  PTHKN-REC                   PIC X(500).
      *    出力更新ファイル
      *FD  OUT-FILE.
      *01  OUT-REC                     PIC X(250).
      *
      *    エラーファイル
       FD  RECEERR-FILE.
       01  RECEERR-REC                 PIC X(200).
      *
       WORKING-STORAGE             SECTION.
      *    ファイル指定領域
      *01  ASS-PTHKN.
      *    03  FILLER              PIC X(09)   VALUE   "/var/tmp/".
      *    03  FILLER              PIC X(30)   VALUE   "ORCPTHKN.TXT".
      *01  ASS-OUT.
      *    03  FILLER              PIC X(09)   VALUE   "/var/tmp/".
      *    03  FILLER              PIC X(30)   VALUE   "ORCBMCNGM.TXT".
      *
      *    エラーファイル 名称領域 
           COPY    "CPCOMMONDAT2.INC"  REPLACING  //RECE01PARA//
                                       BY         //RECEERR//.
           03  FILLER                  PIC X(04)   VALUE   ".dat".
      *
      *    一覧
           COPY    "HCMSL80.INC".
      *
           COPY    "HCMSL55.INC".
      *
      *    フラグ領域
       01  STS-AREA.
           03  STS-OUT                 PIC X(02).
           03  STS-PTHKN               PIC X(02).
           03  STS-ERR                 PIC X(02).
           03  STS-RECLST              PIC X(02).
           03  STS-RECEERR             PIC X(02).
      *
      *    フラグ領域
       01  FLG-AREA.
           03  FLG-END                 PIC 9(01).
           03  FLG-PTNUM               PIC 9(01).
           03  FLG-JYURRK              PIC 9(01).
           03  FLG-PTINF               PIC 9(01).
           03  FLG-HKNNUM              PIC 9(01).
           03  FLG-SYSKANRI            PIC 9(01).
           03  FLG-SRYKARRK            PIC 9(01).
      *
           03  FLG-SYUNOU          PIC 9(01).
           03  FLG-WKSRYACT        PIC 9(01).
           03  FLG-SRYACT          PIC 9(01).
           03  FLG-SRYACCT         PIC 9(01).
           03  FLG-SYUMEI          PIC 9(01).
           03  FLG-TENSU           PIC 9(01).
           03  FLG-SANTEI          PIC 9(01).
      *
           03  FLG-DELETE          PIC 9(01).
           03  FLG-ERR             PIC 9(01).
           03  FLG-ERR2            PIC 9(01).
           03  FLG-PTNYUINRRK          PIC 9(01).
      *
           03  FLG-NYUIN               PIC 9(01).
      *
      *    カウント領域
       01  CNT-AREA.
           03  CNT-PTHKNINF            PIC 9(06).
           03  CNT-UPD                 PIC 9(06).
           03  CNT-IN                  PIC 9(06).
           03  CNT-IN-SEL              PIC 9(06).
           03  CNT-COMMIT              PIC 9(06).
           03  CNT-HEN                 PIC 9(06).
      *
           03  CNT-OUT                 PIC 9(06).
           03  CNT-ERR                 PIC 9(06).
           03  CNT-ERR2                PIC 9(06).
           03  CNT-OK                  PIC 9(06).
           03  CNT-PTINF               PIC 9(06).
           03  CNT-CNT                 PIC 9(06).
      *
           03  CNT-LCNT1               PIC 9(02).
           03  CNT-LCNT2               PIC 9(02).
      *
      *    システム領域
       01  SYS-AREA.
           03  SYS-YMD.
               05  SYS-YY              PIC 9(02).
               05  SYS-MM              PIC 9(02).
               05  SYS-DD              PIC 9(02).
      *
      *    添字領域
       01  IDX-AREA.
           03  IDX                     PIC 9(04).
           03  IDX-1                   PIC 9(04).
           03  IDX-2                   PIC 9(02).
           03  IDY                     PIC 9(04).
      *
      *    パラメタ領域
       01  WRK-PARA.
           COPY    "CPORCSPRTLNK.INC".
           03  WRK-PARA-JOBID          PIC 9(07).
           03  WRK-PARA-SHELLID        PIC X(08).
           03  WRK-PARA-HOSPID         PIC X(24).
           03  WRK-PARA-KBN            PIC X(01).
           03  WRK-PARA-SRYYMD         PIC X(08).
      *
      *    一時領域
       01  WRK-AREA.
      *
           03  WRK-RECEERR             PIC X(200).
      *
           03  WRK-SYMD.
               05  WRK-SYY             PIC 9(04).
               05  WRK-SMM             PIC 9(02).
               05  WRK-SDD             PIC 9(02).
           03  WRK-SYMD2.
               05  WRK-SYY2            PIC 9(02).
               05  WRK-SMM2            PIC 9(02).
               05  WRK-SDD2            PIC 9(02).
      *
           03  WRK-PTID                PIC 9(10).
           03  WRK-SRYCD           PIC 9(09).
      *
           03  WRK-Z3                  PIC ZZ9.
      *
           03  WRK-LSTYMD              PIC X(08).
      *    回数
           03  WRK-KAISU           PIC 9(08).
      *
           03  WRK-NYUINYMD            PIC X(08).
           03  WRK-TAIINYMD            PIC X(08).
      *
      *    帳票出力領域
       01  ERRMES-AREA.
           03  ERRMES-TITLE1.
               05  FILLER              PIC X(02)   VALUE  SPACE.
               05  FILLER              PIC X(60)   VALUE
               "＜＜　入院→外来置換一覧　＞＞".
               05  ERRMES-TITLE1-YY    PIC X(04).
               05  FILLER              PIC X(01) VALUE ".".
               05  ERRMES-TITLE1-MM    PIC X(02).
               05  FILLER              PIC X(01) VALUE ".".
               05  ERRMES-TITLE1-DD    PIC X(02).
           03  ERRMES-TITLE3.
               05  FILLER              PIC X(04) VALUE
                                                 "NO".
               05  FILLER              PIC X(20) VALUE
                   "患者番号".
               05  FILLER              PIC X(05) VALUE SPACE.
               05  FILLER              PIC X(10) VALUE
                   "受診日".
               05  FILLER              PIC X(03) VALUE SPACE.
               05  FILLER              PIC X(02) VALUE
                   "科".
               05  FILLER              PIC X(03) VALUE SPACE.
               05  FILLER              PIC X(10) VALUE
                   "患者ＩＤ".
      *
      *    出力エラーファイル
       01  ERR-REC.
               05  ERR-NO              PIC X(05).
               05  FILLER              PIC X(01).
               05  ERR-PTNUM           PIC X(20).
               05  FILLER              PIC X(01).
               05  ERR-SRYYMD          PIC X(10).
               05  FILLER              PIC X(03).
               05  ERR-SRYKA           PIC X(02).
               05  FILLER              PIC X(03).
               05  ERR-PTID            PIC X(10).
               05  FILLER              PIC X(03).
               05  ERR-ERRMSG          PIC X(50).
      *
      *****************************************************************
      *    ファイルレイアウト
      *****************************************************************
      *
      *    システム管理マスタ
           COPY    "CPSYSKANRI.INC".
      *
      *    システム管理（医療機関情報基本情報）
           COPY    "CPSK1001.INC".
      *
      *    患者入院履歴
       01  PTNYUINRRK-REC.
           COPY    "CPPTNYUINRRK.INC".
      *    患者番号変換マスタ
       01  PTNUM-REC.
           COPY    "CPPTNUM.INC".
      *
      *    患者マスタ−
       01  PTINF-REC.
           COPY    "CPPTINF.INC".
      *
      *    受診履歴
       01  JYURRK-REC.
           COPY    "CPJYURRK.INC".
      *
      *    収納マスタワーク
       01  SYUNOU-REC.
           COPY    "CPSYUNOU.INC".
      *
      *    診療行為マスター
       01  SRYACT-REC.
           COPY    "CPSRYACT.INC".
      *
      *    収納明細マスタ−
       01  SYUMEI-REC.
           COPY    "CPSYUMEI.INC".
      *
      *    診療会計マスタ−
       01  SRYACCT-REC.
           COPY    "CPSRYACCT.INC".
      *
      *    算定履歴
       01  SANTEI-REC.
           COPY    "CPSANTEI.INC".
      *
      *    点数マスタ−
      *01  TENSU-REC.
           COPY    "CPTENSU.INC".
      *
      *    ジョブ管理マスタ
       01  JOBKANRI-REC.
           COPY    "CPJOBKANRI.INC".
      *    診療科履歴
       01  SRYKARRK-REC.
           COPY    "CPSRYKARRK.INC".
      *
      *    保存用収納ワーク
       01  HZN-SYUNOU-REC.
           COPY    "CPSYUNOU.INC"  REPLACING
                                     //SYU-// BY //HZN-SYU-//.
      *
       01  HZN-JYURRK-REC.
           COPY    "CPJYURRK.INC"  REPLACING
                                     //JYURRK-// BY //HZN-JYURRK-//.
      *
       01  HZN-SRYACT-REC.
           COPY    "CPSRYACT.INC"  REPLACING
                                     //SRY-// BY //HZN-SRY-//.
      *
       01  HZN-SRYACCT-REC.
           COPY    "CPSRYACCT.INC"  REPLACING
                                     //ACCT-// BY //HZN-ACCT-//.
      *
      *    印刷管理
       01  PRTKANRI-REC.
           COPY    "CPPRTKANRI.INC".
      *
      *    印刷用データ
       01  PRTDATA-REC.
           COPY    "CPPRTDATA.INC".
      *
           COPY    "CPSHELLTBL.INC".
      *
           COPY    "ORCA-DBPATH".
      *
      *
      *****************************************************************
      *    サブプロ用　領域
      *****************************************************************
      *
      *    ジョブ管理ＤＢ制御サブ
           COPY    "CPORCSJOBKANRI.INC".
      *
      *    印刷ＤＢ更新サブ
           COPY    "CPORCSPRT.INC".
      *
      *    日付変換サブ
           COPY    "CPORCSDAY.INC".
           COPY    "CPORCSLNK.INC".
      *
      *   算定履歴更新サブ
           COPY    "CPORCSSANTEI.INC".
      *
      *    ＤＢ検索
           COPY    "MCPAREA".
      *
           COPY    "MCPDATA.INC".
           COPY    "CPORCMCP.INC".
      *
           COPY    "COMMON-SPA".
      *
      *****************************************************************
      *    連絡　領域
      *****************************************************************
       LINKAGE                     SECTION.
      *
       01  COMMAND-PARAM.
           02  FILLER                  PIC X(256).
      *
      ******************************************************************
      *
       PROCEDURE               DIVISION
                                       USING
                                       COMMAND-PARAM.
      *
      *****************************************************************
      *    主　　処理
      *****************************************************************
       000-PROC-SEC                SECTION.
      *
           PERFORM 100-INIT-SEC
      *
           PERFORM 200-MAIN-SEC
      *
           PERFORM 300-END-SEC
      *
           STOP    RUN
           .
      *****************************************************************
      *    初期　処理
      *****************************************************************
       100-INIT-SEC                SECTION.
      *
           INITIALIZE                  FLG-AREA
           INITIALIZE                  WRK-AREA
           INITIALIZE                  CNT-AREA
      *
           PERFORM 100-DBOPEN-SEC
      *
           UNSTRING    COMMAND-PARAM   DELIMITED  BY  ","
                       INTO            LNK-PRTKANRI-RENNUM
                                       LNK-PRTKANRI-TBL-KEY
                                       LNK-PRTKANRI-TBL-GROUP
                                       LNK-PRTKANRI-SHORI-RENNUM
                                       LNK-PRTKANRI-SRYYM
                                       LNK-PRTKANRI-SKYYMD
                                       LNK-PRTKANRI-SHELLID
                                       LNK-PRTKANRI-PRIORITY
                                       LNK-PRTKANRI-TERMID
                                       LNK-PRTKANRI-OPID
                                       LNK-PRTKANRI-PRTNM
                                       WRK-PARA-JOBID
                                       WRK-PARA-SHELLID
                                       WRK-PARA-HOSPID
                                       WRK-PARA-KBN
                                       WRK-PARA-SRYYMD
      *
      *    ステップ管理開始処理
           MOVE    "STS"           TO  SJOBKANRI-MODE
           INITIALIZE                  JOBKANRI-REC
           MOVE    WRK-PARA-JOBID  TO  JOB-JOBID
           MOVE    WRK-PARA-SHELLID
                                   TO  JOB-SHELLID
           MOVE    "ORCBMCNGM"    TO  JOB-PGID
           MOVE    "入院・外来エラー一覧表"
                                   TO  JOB-SHELLMSG
           CALL    "ORCSJOB"           USING
                                       ORCSJOBKANRIAREA
                                       JOBKANRI-REC
      *    パラメタ編集処理
           PERFORM 110-PARA-HENSYU-SEC
      *
           INITIALIZE                  SPA-AREA
      *
      *    システム日付を２００３０４０１とする
           MOVE    "20030401"      TO  SPA-SYSYMD
      *    作成日
           ACCEPT  WRK-SYMD2       FROM    DATE
           COMPUTE WRK-SYY         =   WRK-SYY2    +   2000
           MOVE    WRK-SMM2        TO  WRK-SMM
           MOVE    WRK-SDD2        TO  WRK-SDD
      *
           MOVE    WRK-SYY         TO  ERRMES-TITLE1-YY
           MOVE    WRK-SMM         TO  ERRMES-TITLE1-MM
           MOVE    WRK-SDD         TO  ERRMES-TITLE1-DD
      *
      *
      *    システム管理情報　検索処理
           PERFORM 2200-CP1001-KENSAKU-SEC
      *
      *    端末ＩＤ固定
           MOVE    "ORCATERM"          TO  SPA-TERMID
      *
           MOVE    WRK-PARA-HOSPID     TO  SPA-HOSPID
           MOVE    WRK-PARA-SRYYMD     TO  SPA-SRYYMD
      *
      *
           .
       100-INIT-EXT.
           EXIT.
      *
      *****************************************************************
      *    システム管理情報　検索処理
      *****************************************************************
       2200-CP1001-KENSAKU-SEC          SECTION.
      *
           MOVE    ZERO                TO  FLG-SYSKANRI
           MOVE    SPACE               TO  SYS-1001-REC
           MOVE    "1001"              TO  SYS-1001-KANRICD
           MOVE    SYS-1001-REC        TO  MCPDATA-REC
           MOVE    "DBSELECT"          TO  MCP-FUNC
           MOVE    "SYSKANRI-KEY2"     TO  ORC-DBPATH
           MOVE    WRK-SYMD            TO  ORC-DBYMD
           CALL    "ORCMCPSUB"      USING   MCPAREA
                                            ORCMCPAREA
                                            MCPDATA-REC
      *
           IF      MCP-RC              =   ZERO
               MOVE    "SYSKANRI-KEY2"     TO  ORC-DBPATH
               PERFORM 900-SYSTEM-NEXT-SEC
           ELSE
               MOVE    1                   TO  FLG-SYSKANRI
           END-IF
           IF  FLG-SYSKANRI            =   1
               MOVE    1                   TO  FLG-END
           ELSE
               MOVE    MCPDATA-REC         TO  SYS-1001-REC
               MOVE    SYS-1001-HOSPID     TO  SPA-HOSPID
               MOVE    SYS-1001-HOSPSBT    TO  SPA-HOSPSBT
               MOVE    SYS-1001-ROUPAYKBN  TO  SPA-ROUPAYKBN
           END-IF
      *
           MOVE    "DBCLOSE"               TO  MCP-FUNC
           MOVE    "SYSKANRI-KEY2"         TO  ORC-DBPATH
           CALL    "ORCMCPSUB"      USING   MCPAREA
                                            ORCMCPAREA
                                            MCPDATA-REC
      *
           .
       2200-CP1001-KENSAKU-EXT.
           EXIT.
      *
      *
      *****************************************************************
      *    パラメタ編集処理
      *****************************************************************
       110-PARA-HENSYU-SEC         SECTION.
      *
      *
           .
       110-PARA-HENSYU-EXT.
           EXIT.
      *
      *
      *****************************************************************
      *    主　　処理
      *****************************************************************
       200-MAIN-SEC         SECTION.
      *
           PERFORM 2001-NYUIN-CHK-SEC
      *
           .
       200-MAIN-EXT.
           EXIT.
      *
      *****************************************************************
      *    入院チェック処理
      *****************************************************************
       2001-NYUIN-CHK-SEC         SECTION.
      *
           MOVE    SPACE               TO  JYURRK-REC
           INITIALIZE                  JYURRK-KEY
           MOVE    SPA-HOSPID          TO  JYURRK-HOSPID
           MOVE    "1"                 TO  JYURRK-NYUGAIKBN
           IF      SPA-SRYYMD          =   SPACE
               MOVE    "00000000"          TO  JYURRK-SRYYMD
           ELSE
               MOVE    SPA-SRYYMD          TO  JYURRK-SRYYMD
           END-IF
           MOVE    "99999999"          TO  JYURRK-UPSRYYMD
      *
      *
           MOVE    JYURRK-REC          TO  MCPDATA-REC
           MOVE    "DBSELECT"          TO  MCP-FUNC
           MOVE    "JYURRK-KEY21"      TO  ORC-DBPATH
           CALL    "ORCMCPSUB"          USING
                                        MCPAREA
                                        ORCMCPAREA
                                        MCPDATA-REC
           IF      MCP-RC              =   ZERO
               MOVE    "JYURRK-KEY21"      TO  ORC-DBPATH
               PERFORM 970-JYURRK-READ-SEC
           ELSE
               MOVE    SPACE               TO  JYURRK-REC
               INITIALIZE                  JYURRK-REC
               MOVE    1               TO  FLG-JYURRK
           END-IF
           INITIALIZE                  HZN-JYURRK-REC
           PERFORM
                   UNTIL    FLG-JYURRK     =   1
      *        入院チェック
               IF      WRK-PARA-KBN        =   SPACE
      *
                   PERFORM 2001-SYORI-CHK-SEC
               ELSE
                   PERFORM 1001-NYUIN-CHK-SEC
                   IF      FLG-NYUIN       =   ZERO
                       PERFORM 2001-SYORI-CHK-SEC
                   END-IF
               END-IF
      *
               MOVE    "JYURRK-KEY21"      TO  ORC-DBPATH
               PERFORM 970-JYURRK-READ-SEC
           END-PERFORM
      *
           .
       2001-NYUIN-CHK-EXT.
           EXIT.
      *
      *****************************************************************
      *    主　　処理
      *****************************************************************
       1001-NYUIN-CHK-SEC                SECTION.
      *    入院履歴判定
           MOVE    SPACE               TO  PTNYUINRRK-REC
           INITIALIZE                  PTNYUINRRK-REC
           MOVE    JYURRK-HOSPID       TO  PTNYUINRRK-HOSPID
           MOVE    JYURRK-PTID         TO  PTNYUINRRK-PTID
           MOVE    JYURRK-SRYYMD       TO  PTNYUINRRK-TENNYUYMD
           MOVE    JYURRK-SRYYMD       TO  PTNYUINRRK-TENSTUYMD
      *
           MOVE    PTNYUINRRK-REC      TO  MCPDATA-REC
           MOVE    "DBSELECT"          TO  MCP-FUNC
           MOVE    "PTNYUINRRK-KEY19"   TO  ORC-DBPATH
           CALL    "ORCMCPSUB"         USING
                                       MCPAREA
                                       ORCMCPAREA
                                       MCPDATA-REC
           IF      MCP-RC              =   ZERO
               MOVE    "PTNYUINRRK-KEY19"   TO  ORC-DBPATH
               PERFORM 910-PTNYUINRRK-READ-SEC
           ELSE
               MOVE    1               TO  FLG-PTNYUINRRK
               INITIALIZE                  PTNYUINRRK-REC
           END-IF
           MOVE    SPACE               TO  WRK-NYUINYMD
           MOVE    SPACE               TO  WRK-TAIINYMD
      *    入院が１件もないとき、外来とする
           IF      FLG-PTNYUINRRK      =   1
               MOVE    ZERO                TO  FLG-NYUIN
           ELSE
               MOVE    1                   TO  FLG-NYUIN
      *    入院日
               MOVE    PTNYUINRRK-NYUINYMD TO  WRK-NYUINYMD
      *    退院日
               MOVE    PTNYUINRRK-TAIINYMD TO  WRK-TAIINYMD
           END-IF
      *
           .
       1001-NYUIN-CHK-EXT.
           EXIT.
      *****************************************************************
      *    印刷処理
      *****************************************************************
       2001-SYORI-CHK-SEC             SECTION.
      *
           IF     (JYURRK-PTID         =   HZN-JYURRK-PTID )  AND
                  (JYURRK-NYUGAIKBN    =   HZN-JYURRK-NYUGAIKBN) AND
                  (JYURRK-SRYKA        =   HZN-JYURRK-SRYKA  ) AND
                  (JYURRK-SRYYMD       =   HZN-JYURRK-SRYYMD) AND
                  (JYURRK-RENNUM       =   HZN-JYURRK-RENNUM )
               CONTINUE
           ELSE
               MOVE    ZERO               TO  FLG-DELETE
               MOVE    ZERO               TO  FLG-ERR2
           END-IF
      *
      *
           MOVE    JYURRK-REC          TO  HZN-JYURRK-REC
           MOVE    "2"                 TO  JYURRK-NYUGAIKBN
      *
           MOVE    JYURRK-REC          TO  MCPDATA-REC
           MOVE    "DBSELECT"          TO  MCP-FUNC
           MOVE    "JYURRK-KEY22"      TO  ORC-DBPATH
           CALL    "ORCMCPSUB"          USING
                                        MCPAREA
                                        ORCMCPAREA
                                        MCPDATA-REC
           IF      MCP-RC              =   ZERO
               MOVE    "JYURRK-KEY22"      TO  ORC-DBPATH
               PERFORM 970-JYURRK-READ-SEC
           ELSE
               MOVE    1               TO  FLG-JYURRK
           END-IF
           MOVE    HZN-JYURRK-REC      TO  JYURRK-REC
      *    受診履歴連番が０の時、訂正分として削除する
           IF      JYURRK-RENNUM       =   ZERO
      *?       MOVE    1               TO  FLG-DELETE
               MOVE    4               TO  FLG-ERR2
           END-IF
           IF     (FLG-JYURRK          =   ZERO )  OR
                  (FLG-DELETE          =   1    )
               MOVE    1               TO  FLG-DELETE
               PERFORM 2003-DEL-SYORI-SEC
           ELSE
               PERFORM 2002-UPDATE-SYORI-SEC
           END-IF
      *
           .
       2001-SYORI-CHK-EXT.
           EXIT.
      *****************************************************************
      *    更新処理
      *****************************************************************
       2002-UPDATE-SYORI-SEC             SECTION.
      *
      *    診療行為・診療会計
           PERFORM VARYING     IDX     FROM    1   BY  1
                   UNTIL      (IDX         >   15  )  OR
                              (JYURRK-ZAINUM (IDX) =   ZERO )
               PERFORM 20021-SRYACT-UPDATE-SEC
           END-PERFORM
      *    収納
           INITIALIZE                  SYUNOU-REC
           MOVE    JYURRK-HOSPID       TO  SYU-HOSPID
           MOVE    JYURRK-PTID         TO  SYU-PTID
           MOVE    JYURRK-NYUGAIKBN    TO  SYU-NYUGAIKBN
           MOVE    JYURRK-DENPNUM      TO  SYU-DENPNUM
      *
           MOVE    SYUNOU-REC          TO  MCPDATA-REC
           MOVE    "DBSELECT"          TO  MCP-FUNC
           MOVE    "SYUNOU-KEY"        TO  ORC-DBPATH
           CALL    "ORCMCPSUB"         USING
                                       MCPAREA
                                       ORCMCPAREA
                                       MCPDATA-REC
           IF      MCP-RC              =   ZERO
               MOVE    "SYUNOU-KEY"        TO  ORC-DBPATH
               PERFORM 980-SYUNOU-READ-SEC
           ELSE
               MOVE    1                   TO  FLG-SYUNOU
           END-IF
           IF      FLG-SYUNOU          =   ZERO
               MOVE    SYUNOU-REC          TO  HZN-SYUNOU-REC
      *
               PERFORM 2009-SYUNOUMEI-SEC
      *
               MOVE    SYUNOU-REC          TO  MCPDATA-REC
               MOVE    "DBDELETE"          TO  MCP-FUNC
               MOVE    "SYUNOU-KEY"        TO  ORC-DBPATH
               CALL    "ORCMCPSUB"         USING
                                           MCPAREA
                                           ORCMCPAREA
                                           MCPDATA-REC
               IF      MCP-RC          NOT =   ZERO
                    DISPLAY "ORCBMCNGM SYUNOU DEL ERR:" MCP-RC
                            ",KEY:" SYU-KEY
               END-IF
      *
               MOVE    HZN-SYUNOU-REC      TO  SYUNOU-REC
               MOVE    "2"                 TO  SYU-NYUGAIKBN
               MOVE    SYUNOU-REC          TO  MCPDATA-REC
               MOVE    "DBINSERT"          TO  MCP-FUNC
               MOVE    "SYUNOU-KEY"        TO  ORC-DBPATH
               CALL    "ORCMCPSUB"         USING
                                           MCPAREA
                                           ORCMCPAREA
                                           MCPDATA-REC
               IF      MCP-RC          NOT =   ZERO
                    DISPLAY "ORCBMCNGM SYUNOU INS ERR:" MCP-RC
                            ",KEY:" SYU-KEY
               END-IF
           END-IF
      *    受診履歴
           MOVE    JYURRK-REC          TO  MCPDATA-REC
           MOVE    "DBDELETE"          TO  MCP-FUNC
           MOVE    "JYURRK-KEY"        TO  ORC-DBPATH
           CALL    "ORCMCPSUB"         USING
                                       MCPAREA
                                       ORCMCPAREA
                                       MCPDATA-REC
           IF      MCP-RC          NOT =   ZERO
                DISPLAY "ORCBMCNGM JYURRK DEL ERR:" MCP-RC
                            ",KEY:" JYURRK-KEY
           END-IF
      *
           MOVE    "2"                 TO  JYURRK-NYUGAIKBN
      *
           MOVE    JYURRK-REC          TO  MCPDATA-REC
           MOVE    "DBINSERT"          TO  MCP-FUNC
           MOVE    "JYURRK-KEY"        TO  ORC-DBPATH
           CALL    "ORCMCPSUB"         USING
                                       MCPAREA
                                       ORCMCPAREA
                                       MCPDATA-REC
           IF      MCP-RC          NOT =   ZERO
                DISPLAY "ORCBMCNGM JYURRK INS ERR:" MCP-RC
                            ",KEY:" JYURRK-KEY
           END-IF
      *
           ADD     1                   TO  CNT-UPD
           MOVE    ZERO                TO  FLG-ERR
           PERFORM 290-ERR-PRINT-SEC
      *
           .
       2002-UPDATE-SYORI-EXT.
           EXIT.
      *****************************************************************
      *    収納明細処理
      *****************************************************************
       2009-SYUNOUMEI-SEC             SECTION.
      *
           INITIALIZE                      SYUMEI-REC
           MOVE    SYU-HOSPID          TO  SMEI-HOSPID
           MOVE    SYU-NYUGAIKBN       TO  SMEI-NYUGAIKBN
           MOVE    SYU-PTID            TO  SMEI-PTID
           MOVE    SYU-DENPNUM         TO  SMEI-DENPNUM
      *
           MOVE    SYUMEI-REC          TO  MCPDATA-REC
           MOVE    "DBSELECT"          TO  MCP-FUNC
           MOVE    "SYUMEI-KEY2"       TO  ORC-DBPATH
           CALL    "ORCMCPSUB"          USING
                                        MCPAREA
                                        ORCMCPAREA
                                        MCPDATA-REC
           IF      MCP-RC              =   ZERO
               MOVE    "SYUMEI-KEY2"       TO  ORC-DBPATH
               PERFORM 900-SYUMEI-READ-SEC
           ELSE
               MOVE    1                   TO  FLG-SYUMEI
           END-IF
           PERFORM
               UNTIL    FLG-SYUMEI     =   1
      *
               MOVE    SYUMEI-REC          TO  MCPDATA-REC
               MOVE    "DBDELETE"          TO  MCP-FUNC
               MOVE    "SYUMEI-KEY"        TO  ORC-DBPATH
               CALL    "ORCMCPSUB"         USING
                                           MCPAREA
                                           ORCMCPAREA
                                           MCPDATA-REC
               IF      MCP-RC          NOT =   ZERO
                   DISPLAY "ORCBMCNGM SYUMEI DEL ERR:"  MCP-RC
                           ",KEY:" SMEI-KEY
               END-IF
      *
               MOVE    "2"                 TO  SMEI-NYUGAIKBN
      *
               MOVE    SYUMEI-REC          TO  MCPDATA-REC
               MOVE    "DBINSERT"          TO  MCP-FUNC
               MOVE    "SYUMEI-KEY"        TO  ORC-DBPATH
               CALL    "ORCMCPSUB"         USING
                                           MCPAREA
                                           ORCMCPAREA
                                           MCPDATA-REC
               IF      MCP-RC          NOT =   ZERO
                   DISPLAY "ORCBMCNGM SYUMEI INS ERR:"  MCP-RC
                           ",KEY:" SMEI-KEY
               END-IF
      *
               MOVE    "SYUMEI-KEY2"       TO  ORC-DBPATH
               PERFORM 900-SYUMEI-READ-SEC
           END-PERFORM
      *
      *
           .
       2009-SYUNOUMEI-EXT.
           EXIT.
      *****************************************************************
      *    診療行為更新処理
      *****************************************************************
       20021-SRYACT-UPDATE-SEC             SECTION.
      *
           INITIALIZE                      SRYACCT-REC
           MOVE    JYURRK-HOSPID       TO  ACCT-HOSPID
           MOVE    JYURRK-NYUGAIKBN    TO  ACCT-NYUGAIKBN
           MOVE    JYURRK-PTID         TO  ACCT-PTID
           MOVE    JYURRK-SRYKA        TO  ACCT-SRYKA
           MOVE    JYURRK-SRYYMD(1:6)  TO  ACCT-SRYYM
           MOVE    JYURRK-ZAINUM (IDX) TO  ACCT-ZAINUM
      *
           MOVE    SRYACCT-REC         TO  MCPDATA-REC
           MOVE    "DBSELECT"          TO  MCP-FUNC
           MOVE    "SRYACCT-KEY5"      TO  ORC-DBPATH
           CALL    "ORCMCPSUB"          USING
                                        MCPAREA
                                        ORCMCPAREA
                                        MCPDATA-REC
           IF      MCP-RC              =   ZERO
               MOVE    "SRYACCT-KEY5"      TO  ORC-DBPATH
               PERFORM 930-SRYACCT-NEXT-SEC
           ELSE
               MOVE    1                   TO  FLG-SRYACCT
           END-IF
           PERFORM
               UNTIL    FLG-SRYACCT        =   1
      *
      *        診療行為
               INITIALIZE                  SRYACT-REC
               MOVE    ACCT-HOSPID         TO  SRY-HOSPID
               MOVE    ACCT-NYUGAIKBN      TO  SRY-NYUGAIKBN
               MOVE    ACCT-PTID           TO  SRY-PTID
               MOVE    ACCT-SRYKA          TO  SRY-SRYKA
               MOVE    ACCT-SRYYM          TO  SRY-SRYYM
               MOVE    ACCT-ZAINUM         TO  SRY-ZAINUM
      *
               MOVE    SRYACT-REC          TO  MCPDATA-REC
               MOVE    "DBSELECT"          TO  MCP-FUNC
               MOVE    "SRYACT-KEY2"       TO  ORC-DBPATH
               CALL    "ORCMCPSUB"         USING
                                           MCPAREA
                                           ORCMCPAREA
                                           MCPDATA-REC
               IF      MCP-RC              =   ZERO
                   MOVE    "SRYACT-KEY2"       TO  ORC-DBPATH
                   PERFORM 940-SRYACT-READ-SEC
               ELSE
                   MOVE    1                   TO  FLG-SRYACT
               END-IF
               PERFORM UNTIL   FLG-SRYACT      =   1
      *
                   MOVE    SRYACT-REC         TO  HZN-SRYACT-REC
      *
                   MOVE    SRYACT-REC          TO  MCPDATA-REC
                   MOVE    "DBDELETE"          TO  MCP-FUNC
                   MOVE    "SRYACT-KEY"        TO  ORC-DBPATH
                   CALL    "ORCMCPSUB"          USING
                                                MCPAREA
                                                ORCMCPAREA
                                                MCPDATA-REC
                   IF      MCP-RC          NOT =   ZERO
                       DISPLAY "ORCBMCNGM SRYACT DEL ERR:" MCP-RC
                            ",KEY:" SRY-KEY
                   END-IF
      *
                   MOVE    HZN-SRYACT-REC      TO  SRYACT-REC
                   MOVE    "2"                 TO  SRY-NYUGAIKBN
                   MOVE    SRYACT-REC          TO  MCPDATA-REC
                   MOVE    "DBINSERT"          TO  MCP-FUNC
                   MOVE    "SRYACT-KEY"        TO  ORC-DBPATH
                   CALL    "ORCMCPSUB"          USING
                                                MCPAREA
                                                ORCMCPAREA
                                                MCPDATA-REC
                   IF      MCP-RC          NOT =   ZERO
                       DISPLAY "ORCBMCNGM SRYACT INS ERR:" MCP-RC
                            ",KEY:" SRY-KEY
                   END-IF
      *
                   MOVE    "SRYACT-KEY2"       TO  ORC-DBPATH
                   PERFORM 940-SRYACT-READ-SEC
               END-PERFORM
      *
               MOVE    SRYACCT-REC         TO  HZN-SRYACCT-REC
      *
               MOVE    SRYACCT-REC         TO  MCPDATA-REC
               MOVE    "DBDELETE"          TO  MCP-FUNC
               MOVE    "SRYACCT-KEY"       TO  ORC-DBPATH
               CALL    "ORCMCPSUB"          USING
                                            MCPAREA
                                            ORCMCPAREA
                                            MCPDATA-REC
               IF      MCP-RC          NOT =   ZERO
                   DISPLAY "ORCBMCNGM SRYACCT DEL ERR:" MCP-RC
                            ",KEY:" ACCT-KEY
               END-IF
      *
               MOVE    HZN-SRYACCT-REC     TO  SRYACCT-REC
               MOVE    "2"                 TO  ACCT-NYUGAIKBN
               MOVE    SRYACCT-REC         TO  MCPDATA-REC
               MOVE    "DBINSERT"          TO  MCP-FUNC
               MOVE    "SRYACCT-KEY"       TO  ORC-DBPATH
               CALL    "ORCMCPSUB"         USING
                                           MCPAREA
                                           ORCMCPAREA
                                           MCPDATA-REC
               IF      MCP-RC          NOT =   ZERO
                   DISPLAY "ORCBMCNGM SRYACCT DEL ERR:" MCP-RC
                            ",KEY:" ACCT-KEY
               END-IF
      *
      *
               MOVE    "SRYACCT-KEY5"      TO  ORC-DBPATH
               PERFORM 930-SRYACCT-NEXT-SEC
      *
           END-PERFORM
      *
           .
       20021-SRYACT-UPDATE-EXT.
           EXIT.
      *****************************************************************
      *    削除処理
      *****************************************************************
       2003-DEL-SYORI-SEC             SECTION.
      *
      *    診療会計マスタの診療回数をクリアする
           PERFORM 4500112-TEISEI-SRYACCT-SEC
      *    収納削除
           INITIALIZE                  SYUNOU-REC
           MOVE    JYURRK-HOSPID       TO  SYU-HOSPID
           MOVE    JYURRK-PTID         TO  SYU-PTID
           MOVE    JYURRK-NYUGAIKBN    TO  SYU-NYUGAIKBN
           MOVE    JYURRK-DENPNUM      TO  SYU-DENPNUM
      *
           MOVE    SYUNOU-REC          TO  MCPDATA-REC
           MOVE    "DBDELETE"          TO  MCP-FUNC
           MOVE    "SYUNOU-KEY"        TO  ORC-DBPATH
           CALL    "ORCMCPSUB"         USING
                                       MCPAREA
                                       ORCMCPAREA
                                       MCPDATA-REC
      *    収納明細削除
           MOVE    SPACE               TO  SYUMEI-REC
           INITIALIZE                      SYUMEI-REC
           MOVE    SYU-HOSPID          TO  SMEI-HOSPID
           MOVE    SYU-NYUGAIKBN       TO  SMEI-NYUGAIKBN
           MOVE    SYU-PTID            TO  SMEI-PTID
           MOVE    SYU-DENPNUM         TO  SMEI-DENPNUM
           MOVE    SYUMEI-REC          TO  MCPDATA-REC
           MOVE    "DBDELETE"          TO  MCP-FUNC
           MOVE    "SYUMEI-DEL2"       TO  ORC-DBPATH
           CALL    "ORCMCPSUB"         USING
                                       MCPAREA
                                       ORCMCPAREA
                                       MCPDATA-REC
      *
      *    受診履歴削除
           MOVE    JYURRK-REC          TO  MCPDATA-REC
           MOVE    "DBDELETE"          TO  MCP-FUNC
           MOVE    "JYURRK-KEY"        TO  ORC-DBPATH
           CALL    "ORCMCPSUB"         USING
                                       MCPAREA
                                       ORCMCPAREA
                                       MCPDATA-REC
      *
           ADD     1                   TO  CNT-ERR
           MOVE    1                   TO  FLG-ERR
           PERFORM 290-ERR-PRINT-SEC
           .
       2003-DEL-SYORI-EXT.
           EXIT.
      *****************************************************************
      *    診療会計マスタの診療回数クリア処理
      *****************************************************************
       4500112-TEISEI-SRYACCT-SEC          SECTION.
      *
           PERFORM VARYING     IDX     FROM    1   BY  1
                   UNTIL      (IDX         >   15  )  OR
                              (JYURRK-ZAINUM (IDX) =   ZERO )
      *
               INITIALIZE                      SRYACCT-REC
               MOVE    JYURRK-HOSPID       TO  ACCT-HOSPID
               MOVE    JYURRK-NYUGAIKBN    TO  ACCT-NYUGAIKBN
               MOVE    JYURRK-PTID         TO  ACCT-PTID
               MOVE    JYURRK-SRYKA        TO  ACCT-SRYKA
               MOVE    JYURRK-SRYYMD(1:6)  TO  ACCT-SRYYM
               MOVE    JYURRK-ZAINUM (IDX) TO  ACCT-ZAINUM
      *
               MOVE    SRYACCT-REC         TO  MCPDATA-REC
               MOVE    "DBSELECT"          TO  MCP-FUNC
               MOVE    "SRYACCT-KEY5"      TO  ORC-DBPATH
               CALL    "ORCMCPSUB"          USING
                                            MCPAREA
                                            ORCMCPAREA
                                            MCPDATA-REC
               IF      MCP-RC              =   ZERO
                   MOVE    "SRYACCT-KEY5"      TO  ORC-DBPATH
                   PERFORM 930-SRYACCT-NEXT-SEC
               ELSE
                   MOVE    1                   TO  FLG-SRYACCT
               END-IF
               PERFORM
                   UNTIL   (FLG-SRYACCT        =   1    )
      *
                   MOVE    JYURRK-SRYYMD(7:2)      TO  IDX-2
                   EVALUATE    JYURRK-RENNUM
                       WHEN    1
                           MOVE    2               TO  IDX-1
                       WHEN    2
                           MOVE    3               TO  IDX-1
                       WHEN    OTHER
                           MOVE    4               TO  IDX-1
                   END-EVALUATE
      *
      *            算定履歴の削除処理
                   MOVE    ACCT-DAY (IDX-1 IDX-2)  TO  WRK-KAISU
                   PERFORM 4500113-TEISEI-SANTEI-SEC
      *
      *            剤回数　変更
                   COMPUTE ACCT-ZAIKAISU       =   ACCT-ZAIKAISU
                                               -   ACCT-DAY
                                                       (IDX-1 IDX-2)
                   COMPUTE ACCT-DAY (1 IDX-2)  =   ACCT-DAY (1 IDX-2)
                                               -   ACCT-DAY
                                                       (IDX-1 IDX-2)
                   MOVE    ZERO                TO  ACCT-DAY
                                                       (IDX-1 IDX-2)
      *            回数がゼロの時、削除
                   IF      ACCT-ZAIKAISU       =   ZERO
      *
                       INITIALIZE                  SRYACT-REC
                       MOVE    ACCT-HOSPID         TO  SRY-HOSPID
                       MOVE    ACCT-NYUGAIKBN      TO  SRY-NYUGAIKBN
                       MOVE    ACCT-PTID           TO  SRY-PTID
                       MOVE    ACCT-SRYKA          TO  SRY-SRYKA
                       MOVE    ACCT-SRYYM          TO  SRY-SRYYM
                       MOVE    ACCT-ZAINUM         TO  SRY-ZAINUM
                       MOVE    SRYACT-REC          TO  MCPDATA-REC
                       MOVE    "DBDELETE"          TO  MCP-FUNC
                       MOVE    "SRYACT-KEY2"       TO  ORC-DBPATH
                       CALL    "ORCMCPSUB"          USING
                                                    MCPAREA
                                                    ORCMCPAREA
                                                    MCPDATA-REC
                       IF      MCP-RC          NOT =   ZERO
                       DISPLAY "ORCBMCNGM SRYACT DEL ERR:" MCP-RC
                            ",KEY:" SRY-KEY
                       END-IF
      *
                       MOVE    SRYACCT-REC         TO  MCPDATA-REC
                       MOVE    "DBDELETE"          TO  MCP-FUNC
                       MOVE    "SRYACCT-KEY"       TO  ORC-DBPATH
                       CALL    "ORCMCPSUB"          USING
                                                    MCPAREA
                                                    ORCMCPAREA
                                                    MCPDATA-REC
                       IF      MCP-RC          NOT =   ZERO
                            DISPLAY "K03 SRYACCT DEL ERR:" MCP-RC
                       END-IF
                   ELSE
      *
                       MOVE    SRYACCT-REC         TO  MCPDATA-REC
                       MOVE    "DBUPDATE"          TO  MCP-FUNC
                       MOVE    "SRYACCT-KEY"       TO  ORC-DBPATH
                       CALL    "ORCMCPSUB"          USING
                                                    MCPAREA
                                                    ORCMCPAREA
                                                    MCPDATA-REC
                       IF      MCP-RC          NOT =   ZERO
                            DISPLAY "K03 SRYACCT UPD ERR:" MCP-RC
                       END-IF
                   END-IF
      *
                   MOVE    "SRYACCT-KEY5"      TO  ORC-DBPATH
                   PERFORM 930-SRYACCT-NEXT-SEC
               END-PERFORM
               MOVE    "DBCLOSE"           TO  MCP-FUNC
               MOVE    "SRYACCT-KEY5"      TO  ORC-DBPATH
               CALL    "ORCMCPSUB"          USING
                                            MCPAREA
                                            ORCMCPAREA
                                            MCPDATA-REC
           END-PERFORM
      *
           .
       4500112-TEISEI-SRYACCT-EXT.
           EXIT.
      *
      *****************************************************************
      *    算定履歴の回数クリア処理
      *****************************************************************
       4500113-TEISEI-SANTEI-SEC          SECTION.
      *
      *    PERFORM VARYING     IDX     FROM    1   BY  1
      *            UNTIL      (IDX         >   15  )  OR
      *                       (JYURRK-ZAINUM (IDX) =   ZERO )
      *
               INITIALIZE                      SRYACT-REC
               MOVE    JYURRK-HOSPID       TO  SRY-HOSPID
               MOVE    JYURRK-NYUGAIKBN    TO  SRY-NYUGAIKBN
               MOVE    JYURRK-PTID         TO  SRY-PTID
               MOVE    JYURRK-SRYKA        TO  SRY-SRYKA
               MOVE    JYURRK-SRYYMD(1:6)  TO  SRY-SRYYM
               MOVE    JYURRK-ZAINUM (IDX) TO  SRY-ZAINUM
      *
               MOVE    SRYACT-REC          TO  MCPDATA-REC
               MOVE    "DBSELECT"          TO  MCP-FUNC
               MOVE    "SRYACT-KEY2"       TO  ORC-DBPATH
               CALL    "ORCMCPSUB"          USING
                                            MCPAREA
                                            ORCMCPAREA
                                            MCPDATA-REC
               IF      MCP-RC              =   ZERO
                   MOVE    "SRYACT-KEY2"       TO  ORC-DBPATH
                   PERFORM 940-SRYACT-READ-SEC
               ELSE
                   MOVE    1                   TO  FLG-SRYACT
               END-IF
               PERFORM
                   UNTIL   FLG-SRYACT          =   1
      *
                   PERFORM VARYING     IDY     FROM    1   BY  1
                           UNTIL      (IDY     >   5   )  OR
                                      (SRY-SRYCD (IDY) =   SPACE)
      *
      *            手技料の時、算定歴を判定する
                   IF     (SRY-SRYCD (IDY)(1:1)    =   "1"    )
      *                ダミー追加
                       OR (SRY-SRYCD (IDY)         =   "099110001" )
                       MOVE    SRY-SRYCD (IDY)     TO  WRK-SRYCD
      *
                       MOVE    WRK-SRYCD           TO  TNS-SRYCD
                       PERFORM 910-TENSU-READ-SEC
      *                算定用の診療コードへ変更
                       PERFORM 45020-SANTEI-SRYCD-SEC
                       IF      SRY-SRYCD (IDY) NOT =   WRK-SRYCD
                           MOVE    WRK-SRYCD           TO  TNS-SRYCD
                           PERFORM 910-TENSU-READ-SEC
                       END-IF
      *
                       IF      TNS-SANTEIRRKKBN    NOT =   ZERO
                           PERFORM 45001131-SANTEI-DEL-SEC
                       END-IF
                   END-IF
      *
                   END-PERFORM
      *
                   MOVE    "SRYACT-KEY2"       TO  ORC-DBPATH
                   PERFORM 940-SRYACT-READ-SEC
      *
               END-PERFORM
      *
               MOVE    "DBCLOSE"           TO  MCP-FUNC
               MOVE    "SRYACT-KEY2"       TO  ORC-DBPATH
               CALL    "ORCMCPSUB"          USING
                                            MCPAREA
                                            ORCMCPAREA
                                            MCPDATA-REC
      *
      *****END-PERFORM
      *
           .
       4500113-TEISEI-SANTEI-EXT.
           EXIT.
      *
      *****************************************************************
      *    算定用の診療コードへ変更処理
      *****************************************************************
       45020-SANTEI-SRYCD-SEC              SECTION.
      *
      *    包括逓減区分判定
           EVALUATE    TNS-HOUKATUTEIGENKBN
      *        頭部
               WHEN    201
                   MOVE    "099700101"         TO  WRK-SRYCD
      *        躯幹
               WHEN    202
                   MOVE    "099700102"         TO  WRK-SRYCD
      *        四肢
               WHEN    203
                   MOVE    "099700103"         TO  WRK-SRYCD
           END-EVALUATE
           .
       45020-SANTEI-SRYCD-EXT.
           EXIT.
      *
      *****************************************************************
      *    算定履歴回数減処理
      *****************************************************************
       45001131-SANTEI-DEL-SEC          SECTION.
      *
           INITIALIZE                  ORCSSANTEIAREA
           MOVE    "3"                 TO  SSANTEI-SYORI
           MOVE    "K03"               TO  SSANTEI-PG
           MOVE    WRK-SRYCD           TO  SSANTEI-SRYCD
           MOVE    JYURRK-PTID         TO  SSANTEI-PTID
           MOVE    JYURRK-SRYYMD       TO  SSANTEI-SRYYMD
           MOVE    WRK-KAISU           TO  SSANTEI-KAISU
      *    診療科
           MOVE    SRY-SRYKA           TO  SSANTEI-SRYKA
           CALL    "ORCSSANTEI"        USING
                                       ORCSSANTEIAREA
                                       SPA-AREA
      *
           .
       45001131-SANTEI-DEL-EXT.
           EXIT.
      *
      *****************************************************************
      *    印刷処理
      *****************************************************************
       290-ERR-PRINT-SEC             SECTION.
      *
           ADD     1                   TO  CNT-LCNT1
           IF      CNT-LCNT1           >   80
      *        帳票印刷処理
               PERFORM 400-PRINT-OUT-SEC
               MOVE    1                   TO  CNT-LCNT1
           END-IF
      *
           IF      CNT-LCNT1           =   1
               INITIALIZE                  HCMSL80
               MOVE    ERRMES-TITLE1       TO  HCMSL80-MOJI (CNT-LCNT1)
               ADD     1                   TO  CNT-LCNT1
               MOVE    ERRMES-TITLE3       TO  HCMSL80-MOJI (CNT-LCNT1)
               ADD     1                   TO  CNT-LCNT1
               ADD     1                   TO  CNT-LCNT1
           END-IF
      *    患者番号設定
           PERFORM 220-PTNUM-KEN-SEC
      *
           ADD     1                   TO  CNT-CNT
      *
           MOVE    SPACE               TO  ERR-REC
           MOVE    CNT-CNT             TO  WRK-Z3
           MOVE    WRK-Z3              TO  ERR-NO
           MOVE    PTNUM-PTNUM         TO  ERR-PTNUM
           MOVE    JYURRK-PTID         TO  ERR-PTID
           MOVE    JYURRK-SRYYMD(1:4)  TO  ERR-SRYYMD(1:4)
           MOVE    "."                 TO  ERR-SRYYMD(5:1)
           MOVE    JYURRK-SRYYMD(5:2)  TO  ERR-SRYYMD(6:2)
           MOVE    "."                 TO  ERR-SRYYMD(8:1)
           MOVE    JYURRK-SRYYMD(7:2)  TO  ERR-SRYYMD(9:2)
           MOVE    JYURRK-SRYKA        TO  ERR-SRYKA
           IF      FLG-ERR             =   1
               IF      FLG-ERR2            =   ZERO
                   MOVE    "削除"              TO  ERR-ERRMSG
                ELSE
                   MOVE    "訂正分エラー"      TO  ERR-ERRMSG
                END-IF
           ELSE
               MOVE    "更新"              TO  ERR-ERRMSG
           END-IF
      *
           MOVE    ERR-REC             TO  HCMSL80-MOJI (CNT-LCNT1)
      *
           .
       290-ERR-PRINT-EXT.
           EXIT.
      *
      *****************************************************************
      *     保険ＩＤ設定変更処理
      *****************************************************************
       220-PTNUM-KEN-SEC            SECTION.
      *
      *    患者番号変換
           MOVE    SPACE               TO  PTNUM-REC
           INITIALIZE                      PTNUM-REC
           MOVE    JYURRK-HOSPID       TO  PTNUM-HOSPID
           MOVE    JYURRK-PTID         TO  PTNUM-PTID
      *
           MOVE    PTNUM-REC           TO  MCPDATA-REC
           MOVE    "DBSELECT"          TO  MCP-FUNC
           MOVE    "PTNUM-KEY"         TO  ORC-DBPATH
           CALL    "ORCMCPSUB"          USING
                                        MCPAREA
                                        ORCMCPAREA
                                        MCPDATA-REC
           IF      MCP-RC              =   ZERO
               MOVE    "DBFETCH"           TO  MCP-FUNC
               MOVE    "PTNUM-KEY"         TO  ORC-DBPATH
               CALL    "ORCMCPSUB"          USING
                                            MCPAREA
                                            ORCMCPAREA
                                            MCPDATA-REC
               IF      MCP-RC              =   ZERO
                   MOVE    MCPDATA-REC         TO  PTNUM-REC
                   MOVE    ZERO                TO  FLG-PTNUM
               ELSE
                   MOVE    1                   TO  FLG-PTNUM
                   INITIALIZE                  PTNUM-REC
               END-IF
           ELSE
               MOVE    1                   TO  FLG-PTNUM
               INITIALIZE                  PTNUM-REC
           END-IF
      *
           MOVE    "DBCLOSE"           TO  MCP-FUNC
           MOVE    "PTNUM-KEY"         TO  ORC-DBPATH
           CALL    "ORCMCPSUB"          USING
                                        MCPAREA
                                        ORCMCPAREA
                                        MCPDATA-REC
      *
           .
       220-PTNUM-KEN-EXT.
           EXIT.
      *
      *
      *****************************************************************
      *    帳票印刷処理
      *****************************************************************
       400-PRINT-OUT-SEC                SECTION.
      *
           INITIALIZE                  ORCSPRTAREA
           MOVE    "INS"               TO  SPRT-MODE
           MOVE    LNK-PRTKANRI-RENNUM TO  SPRT-RENNUM
           MOVE    LNK-PRTKANRI-TBL-KEY
                                       TO  SPRT-TBL-KEY
           MOVE    LNK-PRTKANRI-TBL-GROUP
                                       TO  SPRT-TBL-GROUP
           MOVE    LNK-PRTKANRI-SRYYM  TO  SPRT-SRYYM
           MOVE    LNK-PRTKANRI-SKYYMD TO  SPRT-SKYYMD
           MOVE    LNK-PRTKANRI-SHELLID 
                                       TO  SPRT-SHELLID
           MOVE    LNK-PRTKANRI-SHORI-RENNUM
                                       TO  SPRT-SHORI-RENNUM
           MOVE    LNK-PRTKANRI-PRIORITY
                                       TO  SPRT-PRIORITY
           MOVE   "HCMSL80.red"        TO  SPRT-PRTID
           MOVE    "外来変換エラー"
                                       TO  SPRT-TITLE
           MOVE    HCMSL80             TO  SPRT-PRTDATA
           MOVE    LNK-PRTKANRI-TERMID TO  SPRT-TERMID
           MOVE    LNK-PRTKANRI-OPID   TO  SPRT-OPID
           MOVE    LNK-PRTKANRI-PRTNM  TO  SPRT-PRTNM
           MOVE    "1"                 TO  SPRT-SITEKBN
           CALL    "ORCSPRT"           USING
                                       ORCSPRTAREA
           IF      SPRT-RETURN         =   ZERO
               CONTINUE
           ELSE
               PERFORM 500-ERR-HENSYU-SEC
           END-IF
           .
       400-PRINT-OUT-EXT.
           EXIT.
      *
      *****************************************************************
      *    終了　　処理
      *****************************************************************
       300-END-SEC                 SECTION.
      *
           IF      CNT-LCNT1           >   ZERO
      *        帳票印刷処理
               PERFORM 400-PRINT-OUT-SEC
           END-IF
      *
           DISPLAY "*** ORCBMCNGM UPD:"   CNT-UPD
           DISPLAY "*** ORCBMCNGM DEL:"   CNT-ERR
           DISPLAY "*** ORCBMCNGM END ***"
      *
      *    ステップ管理終了処理
           MOVE    "STE"           TO  SJOBKANRI-MODE
           INITIALIZE                  JOBKANRI-REC
           MOVE    WRK-PARA-JOBID  TO  JOB-JOBID
           MOVE    WRK-PARA-SHELLID
                                   TO  JOB-SHELLID
           IF      CNT-COMMIT      =   ZERO
               MOVE    CNT-UPD         TO  CNT-HEN
           ELSE
               COMPUTE CNT-HEN         =   CNT-UPD
                                       -  (CNT-COMMIT   *   50 )
           END-IF
      *****MOVE    CNT-UPD         TO  JOB-UPDCNT
           MOVE    CNT-HEN         TO  JOB-UPDCNT
           CALL    "ORCSJOB"           USING
                                       ORCSJOBKANRIAREA
                                       JOBKANRI-REC
      *
           PERFORM 900-DBDISCONNECT-SEC
           .
       300-END-EXT.
           EXIT.
      *****************************************************************
      *    エラー出力処理
      *****************************************************************
       500-ERR-HENSYU-SEC          SECTION.
      *
           OPEN    INPUT               RECEERR-FILE
           IF        ( STS-RECEERR     =   ZERO )
               CONTINUE
           ELSE
               OPEN    OUTPUT              RECEERR-FILE
      *
               MOVE    WRK-RECEERR     TO  RECEERR-REC
               WRITE   RECEERR-REC
               CLOSE   RECEERR-FILE
      *
      *        ジョブ管理開始処理
               MOVE    "JBE"           TO  SJOBKANRI-MODE
               INITIALIZE                  JOBKANRI-REC
               MOVE    WRK-PARA-JOBID  TO  JOB-JOBID
               MOVE    WRK-PARA-SHELLID
                                       TO  JOB-SHELLID
               MOVE    WRK-RECEERR     TO  JOB-YOBI
               MOVE    "9999"          TO  JOB-ERRCD
               CALL    "ORCSJOB"           USING
                                           ORCSJOBKANRIAREA
                                           JOBKANRI-REC
           END-IF
      *
           MOVE    1                   TO  FLG-END
      *
           .
       500-ERR-HENSYU-EXT.
           EXIT.
      *
      *****************************************************************
      *    システム管理マスタＦＥＴＣＨ処理
      *****************************************************************
       900-SYSTEM-NEXT-SEC         SECTION.
      *
           MOVE    "DBFETCH"           TO  MCP-FUNC
           CALL    "ORCMCPSUB"          USING
                                        MCPAREA
                                        ORCMCPAREA
                                        MCPDATA-REC
           IF      MCP-RC              =   ZERO
               MOVE    ZERO            TO  FLG-SYSKANRI
           ELSE
               MOVE    1               TO  FLG-SYSKANRI
           END-IF
      *
           .
       900-SYSTEM-NEXT-EXT.
           EXIT.
      *
      *****************************************************************
      *    患者マスター読込（主キー）
      *****************************************************************
       900-PTINF-READ-SEC         SECTION.
      *
           MOVE    PTINF-REC           TO  MCPDATA-REC
           MOVE    "DBSELECT"          TO  MCP-FUNC
           MOVE    "PTINF-KEY"         TO  ORC-DBPATH
           CALL    "ORCMCPSUB"          USING
                                        MCPAREA
                                        ORCMCPAREA
                                        MCPDATA-REC
           IF      MCP-RC              =   ZERO
               MOVE    "DBFETCH"           TO  MCP-FUNC
               MOVE    "PTINF-KEY"         TO  ORC-DBPATH
               CALL    "ORCMCPSUB"          USING
                                            MCPAREA
                                            ORCMCPAREA
                                            MCPDATA-REC
               IF      MCP-RC              =   ZERO
                   MOVE    MCPDATA-REC         TO  PTINF-REC
                   MOVE    ZERO                TO  FLG-PTINF
               ELSE
                   MOVE    1                   TO  FLG-PTINF
                   INITIALIZE                  PTINF-REC
               END-IF
           ELSE
               MOVE    1                   TO  FLG-PTINF
               INITIALIZE                  PTINF-REC
           END-IF
      *
           MOVE    "DBCLOSE"           TO  MCP-FUNC
           MOVE    "PTINF-KEY"         TO  ORC-DBPATH
           CALL    "ORCMCPSUB"          USING
                                        MCPAREA
                                        ORCMCPAREA
                                        MCPDATA-REC
      *
           .
       900-PTINF-READ-EXT.
           EXIT.
      *
      *****************************************************************
      *    入院履歴マスター読込
      *****************************************************************
       910-PTNYUINRRK-READ-SEC         SECTION.
      *
           MOVE    "DBFETCH"           TO  MCP-FUNC
           CALL    "ORCMCPSUB"          USING
                                        MCPAREA
                                        ORCMCPAREA
                                        MCPDATA-REC
           IF      MCP-RC              =   ZERO
               MOVE    MCPDATA-REC     TO  PTNYUINRRK-REC
               MOVE    ZERO            TO  FLG-PTNYUINRRK
           ELSE
               INITIALIZE                  PTNYUINRRK-REC
               MOVE    1               TO  FLG-PTNYUINRRK
           END-IF
      *
           .
       910-PTNYUINRRK-READ-EXT.
           EXIT.
      ******************************************************************
      *    受診履歴マスター読込
      *****************************************************************
       970-JYURRK-READ-SEC         SECTION.
      *
           MOVE    "DBFETCH"           TO  MCP-FUNC
           CALL    "ORCMCPSUB"          USING
                                        MCPAREA
                                        ORCMCPAREA
                                        MCPDATA-REC
           IF      MCP-RC              =   ZERO
               MOVE    MCPDATA-REC     TO  JYURRK-REC
               MOVE    ZERO            TO  FLG-JYURRK
           ELSE
               INITIALIZE                  JYURRK-REC
               MOVE    1               TO  FLG-JYURRK
           END-IF
      *
           .
       970-JYURRK-READ-EXT.
           EXIT.
      *
      *****************************************************************
      *    点数マスター読込
      *****************************************************************
       910-TENSU-READ-SEC         SECTION.
      *
           MOVE    ZERO                TO  TNS-YUKOSTYMD
           MOVE    ALL "9"             TO  TNS-YUKOEDYMD
      *
           MOVE    TNS-TENSU-REC       TO  MCPDATA-REC
           MOVE    "DBSELECT"          TO  MCP-FUNC
           MOVE    "TENSU-KEY"         TO  ORC-DBPATH
           MOVE    SPA-SRYYMD          TO  ORC-DBYMD
           CALL    "ORCMCPSUB"          USING
                                        MCPAREA
                                        ORCMCPAREA
                                        MCPDATA-REC
           IF      MCP-RC              =   ZERO
               MOVE    "DBFETCH"           TO  MCP-FUNC
               MOVE    "TENSU-KEY"         TO  ORC-DBPATH
               CALL    "ORCMCPSUB"          USING
                                            MCPAREA
                                            ORCMCPAREA
                                            MCPDATA-REC
               IF      MCP-RC              =   ZERO
                   MOVE    MCPDATA-REC         TO  TNS-TENSU-REC
                   MOVE    ZERO                TO  FLG-TENSU
               ELSE
                   INITIALIZE                  TNS-TENSU-REC
                   MOVE    1                   TO  FLG-TENSU
               END-IF
           ELSE
               INITIALIZE                  TNS-TENSU-REC
               MOVE    1                   TO  FLG-TENSU
           END-IF
      *
           MOVE    "DBCLOSE"           TO  MCP-FUNC
           MOVE    "TENSU-KEY"         TO  ORC-DBPATH
           CALL    "ORCMCPSUB"          USING
                                        MCPAREA
                                        ORCMCPAREA
                                        MCPDATA-REC
      *
           .
       910-TENSU-READ-EXT.
           EXIT.
      *
      *****************************************************************
      *    収納明細マスター読込
      *****************************************************************
       900-SYUMEI-READ-SEC         SECTION.
      *
           MOVE    "DBFETCH"           TO  MCP-FUNC
           CALL    "ORCMCPSUB"          USING
                                        MCPAREA
                                        ORCMCPAREA
                                        MCPDATA-REC
           IF      MCP-RC              =   ZERO
               MOVE    MCPDATA-REC         TO  SYUMEI-REC
               MOVE    ZERO                TO  FLG-SYUMEI
           ELSE
               INITIALIZE                      SYUMEI-REC
               MOVE    1                   TO  FLG-SYUMEI
           END-IF
      *
           .
       900-SYUMEI-READ-EXT.
           EXIT.
      *
      *****************************************************************
      *    診療行為マスター読込
      *****************************************************************
       940-SRYACT-READ-SEC         SECTION.
      *
           MOVE    "DBFETCH"           TO  MCP-FUNC
           CALL    "ORCMCPSUB"          USING
                                        MCPAREA
                                        ORCMCPAREA
                                        MCPDATA-REC
           IF      MCP-RC              =   ZERO
               MOVE    MCPDATA-REC         TO  SRYACT-REC
               MOVE    ZERO                TO  FLG-SRYACT
           ELSE
               INITIALIZE                      SRYACT-REC
               MOVE    1                   TO  FLG-SRYACT
           END-IF
      *
           .
       940-SRYACT-READ-EXT.
           EXIT.
      *****************************************************************
      *    診療会計マスター読込
      *****************************************************************
       930-SRYACCT-NEXT-SEC         SECTION.
      *
           MOVE    "DBFETCH"           TO  MCP-FUNC
           CALL    "ORCMCPSUB"          USING
                                        MCPAREA
                                        ORCMCPAREA
                                        MCPDATA-REC
           IF      MCP-RC              =   ZERO
               MOVE    MCPDATA-REC         TO  SRYACCT-REC
               MOVE    ZERO                TO  FLG-SRYACCT
           ELSE
               INITIALIZE                      SRYACCT-REC
               MOVE    1                   TO  FLG-SRYACCT
           END-IF
      *
           .
       930-SRYACCT-NEXT-EXT.
           EXIT.
      *
      *****************************************************************
      *    収納マスター読込
      *****************************************************************
       980-SYUNOU-READ-SEC         SECTION.
      *
           MOVE    "DBFETCH"           TO  MCP-FUNC
           CALL    "ORCMCPSUB"          USING
                                        MCPAREA
                                        ORCMCPAREA
                                        MCPDATA-REC
           IF      MCP-RC              =   ZERO
               MOVE    MCPDATA-REC     TO  SYUNOU-REC
               MOVE    ZERO            TO  FLG-SYUNOU
           ELSE
               INITIALIZE                  SYUNOU-REC
               MOVE    1               TO  FLG-SYUNOU
           END-IF
      *
           .
       980-SYUNOU-READ-EXT.
           EXIT.
      *
      *****************************************************************
      *    ＤＢ　オープン処理
      *****************************************************************
       100-DBOPEN-SEC              SECTION.
      *
           MOVE    "DBOPEN"        TO  MCP-FUNC.
           CALL    "ORCMCPSUB"         USING
                                       MCPAREA
                                       ORCMCPAREA
                                       MCPDATA-REC.
      *
           PERFORM 1001-DBSTART-SEC
           .
       100-DBOPEN-EXT.
           EXIT.
      *
      *****************************************************************
      *    ＤＢＳＴＡＲＴ処理
      *****************************************************************
       1001-DBSTART-SEC        SECTION.
      *
           MOVE    "DBSTART"       TO  MCP-FUNC.
           CALL    "ORCMCPSUB"         USING
                                       MCPAREA
                                       ORCMCPAREA
                                       MCPDATA-REC.
           .
       1001-DBSTART-EXT.
           EXIT.
      *
      *****************************************************************
      *    ＤＢＳＴＡＲＴ処理
      *****************************************************************
       9001-DBDBCOMMIT-SEC        SECTION.
      *
           MOVE    "DBCOMMIT"      TO  MCP-FUNC
           CALL    "ORCMCPSUB"         USING
                                       MCPAREA
                                       ORCMCPAREA
                                       MCPDATA-REC.
           .
       9001-DBDBCOMMIT-EXT.
           EXIT.
      *
      *****************************************************************
      *    ＤＢ　クローズ処理
      *****************************************************************
       900-DBDISCONNECT-SEC        SECTION.
      *
           MOVE    "DBCOMMIT"      TO  MCP-FUNC
           CALL    "ORCMCPSUB"         USING
                                           MCPAREA
                                           ORCMCPAREA
                                           MCPDATA-REC
      *
           MOVE    "DBDISCONNECT"  TO  MCP-FUNC.
           CALL    "ORCMCPSUB"         USING
                                       MCPAREA
                                       ORCMCPAREA
                                       MCPDATA-REC.
           .
       900-DBCLOSE-EXT.
           EXIT.

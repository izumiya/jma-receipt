      *******************************************************************
      * Project code name "ORCA"
      * 日医標準レセプトソフト（JMA standard receipt software）
      * Copyright(C) 2002 JMA (Japan Medical Association)
      *
      * This program is part of "JMA standard receipt software".
      *
      *     This program is distributed in the hope that it will be useful
      * for further advancement in medical care, according to JMA Open
      * Source License, but WITHOUT ANY WARRANTY.
      *     Everyone is granted permission to use, copy, modify and
      * redistribute this program, but only under the conditions described
      * in the JMA Open Source License. You should have received a copy of
      * this license along with this program. If not, stop using this
      * program and contact JMA, 2-28-16 Honkomagome, Bunkyo-ku, Tokyo,
      * 113-8621, Japan.
      ********************************************************************
       IDENTIFICATION          DIVISION.
       PROGRAM-ID.             ORCBM022.
      *****************************************************************
      *  システム名        : ＯＲＣＡ
      *  サブシステム名    : 月次帳票
      *  コンポーネント名  : 社保・総括表用集計処理
      *                      ２００２．１０改正対応
      *  管理者            : 
      *  作成日付    作業者        記述
      *  02/09/19    NACL-藤原     新規作成
      *****************************************************************
      *  プログラム修正履歴
      * Maj/Min/Rev  修正者       日付      内容
      *****************************************************************
      *
       ENVIRONMENT                 DIVISION.
       CONFIGURATION               SECTION.
       INPUT-OUTPUT                SECTION.
       FILE-CONTROL.
      *
006900*    請求ＤＢ更新用ファイル
           SELECT  UPD-FILE        ASSIGN  RECEUPD
                                   ORGANIZATION    IS  LINE
                                                       SEQUENTIAL 
                                   FILE    STATUS  IS  STS-RECEUPD.
      *
006900*    エラーファイル
           SELECT  RECEERR-FILE    ASSIGN  RECEERR
                                   FILE    STATUS  IS  STS-RECEERR.
      *
       DATA                        DIVISION.
       FILE                        SECTION.
      *
006900*    請求ＤＢ更新用ファイル
       FD  UPD-FILE.
           COPY    "CPRCF010.INC"  REPLACING  //RECE10//
                                   BY         //RECE10X//.
      *
006900*    エラーファイル
       FD  RECEERR-FILE.
       01  RECEERR-REC             PIC X(200). 
      *
       WORKING-STORAGE             SECTION.
      *
006900*    請求ＤＢ更新用ファイル 名称領域 
           COPY    "CPCOMMONDAT2.INC"
                                   REPLACING  //RECE01PARA//
                                   BY         //RECEUPD//.
           03  FILLER              PIC X(04)   VALUE   ".dat".
      *
      *    エラーファイル 名称領域 
           COPY    "CPCOMMONDAT2.INC"
                                   REPLACING  //RECE01PARA//
                                   BY         //RECEERR//.
           03  FILLER              PIC X(04)   VALUE   ".dat".
      *
      *    スパ領域
       01  STS-AREA.
           03  STS-RECEERR         PIC X(02).
           03  STS-RECEUPD         PIC X(02).
      *
      *    フラグ領域
       01  FLG-AREA.
           03  FLG-END             PIC 9(01).
           03  FLG-SEIKYU          PIC 9(01).
           03  FLG-UPD             PIC 9(01).
           03  FLG-UPDFILE         PIC 9(01). 
           03  FLG-ERR             PIC 9(01).
           03  FLG-PTINF           PIC 9(01).
           03  FLG-RECE10          PIC 9(01).
      *
      *    添字領域
       01  IDX-AREA.
           03  IDX                 PIC 9(04).
           03  IDY                 PIC 9(04).
           03  IDZ                 PIC 9(04).
      *     
           03  IDX1                PIC 9(04).
           03  IDX2                PIC 9(04).
           03  IDX3                PIC 9(04).
      *     
           03  IDY1                PIC 9(04).
           03  IDY2                PIC 9(04).
           03  IDY3                PIC 9(04).
      *
      *    パラメタ領域
       01  WRK-PARA.
           COPY    "CPORCSPRTLNK.INC".
           03  WRK-PARA-JOBID      PIC 9(07).
           03  WRK-PARA-SHELLID    PIC X(08).
           03  WRK-PARA-HOSPID     PIC X(24).
           03  WRK-PARA-PAGE       PIC 9(10).
      *          
      *    一時領域
       01  WRK-AREA.
      *****03  WRK-IN              PIC X(01).
      *
           03  WRK-RECEERR         PIC X(200). 
           03  WRK-PATH            PIC X(64).
           03  WRK-INDEX           PIC 9(02).
      *
      *    カウント領域
       01  CNT-AREA.
           03  CNT-RECE10          PIC 9(06).
           03  CNT-UPDFILE         PIC 9(06).
      *
      *    集計領域
       01  SUM-AREA.
      *    1:その他のレセ、2:在総診を算定したレセ 
           03  SUM-TBL-OCC         OCCURS  2.
      *        医療保険　1,2:７０以上 3:本人 4:家族 5:三歳 
               05  SUM-TABLE           OCCURS  5. 
                   07  SUM-TBL             OCCURS  12.
                       09  SUM-KENSU          PIC  9(06).
                       09  SUM-NISSU          PIC  9(06).
                       09  SUM-TOTALTEN       PIC  9(10).
                       09  SUM-FTNMONEY       PIC  9(10).
                       09  SUM-YKZFTN         PIC  9(10).
                       09  SUM-SHOKUJIKENSU   PIC  9(06).
                       09  SUM-SHOKUJINISSU   PIC  9(06).
                       09  SUM-SHOKUJIRYOYOHI PIC  9(10).
                       09  SUM-SHOKUJIFTN     PIC  9(10).
      *        老人保健
               05  SUM-ROU-TABLE           OCCURS  2. 
                   07  SUM-ROU-TBL             OCCURS  3.
                       09  SUM-ROU-KENSU          PIC  9(06).
                       09  SUM-ROU-NISSU          PIC  9(06).
                       09  SUM-ROU-TOTALTEN       PIC  9(10).
                       09  SUM-ROU-FTNMONEY       PIC  9(10).
                       09  SUM-ROU-SHOKUJIKENSU   PIC  9(06).
                       09  SUM-ROU-SHOKUJINISSU   PIC  9(06).
                       09  SUM-ROU-SHOKUJIRYOYOHI PIC  9(10).
                       09  SUM-ROU-SHOKUJIFTN     PIC  9(10).
      *        公費負担 
               05  SUM-KOH-TABLE           OCCURS  3. 
                   07  SUM-KOH-TBL         OCCURS  100.
                       09  SUM-KOH-KOHNUM         PIC  X(03).
                       09  SUM-KOH-KENSU          PIC  9(06).
                       09  SUM-KOH-TOTALTEN       PIC  9(10).
                       09  SUM-KOH-FTNMONEY       PIC  9(10).
                       09  SUM-KOH-YKZFTN         PIC  9(10).
                       09  SUM-KOH-SHOKUJIKENSU   PIC  9(06).
                       09  SUM-KOH-SHOKUJIRYOYOHI PIC  9(10).
                       09  SUM-KOH-SHOKUJIFTN     PIC  9(10).
      *        合計         
               05  SUM-GOK-TBL         OCCURS  4.
                   07  SUM-GOK-KENSU      PIC 9(06).    
                   07  SUM-GOK-SHOKENSU   PIC 9(06).
      *
      *    固定項目
       01  WRK-CONS-AREA.
      *
      *    ２００２年１０月改正
           03  WRK-CONS-SRYYM        PIC X(06)   VALUE   "200210".              
      *
      *****************************************************************
      *    ファイルレイアウト
      *****************************************************************
      *
      *    ジョブ管理マスタ
       01  JOBKANRI-REC.
           COPY    "CPJOBKANRI.INC".
      *
      *    患者
       01  PTINF-REC.
           COPY    "CPPTINF.INC".
      *
           COPY    "CPRCF010.INC".
      *
      *****************************************************************
      *    サブプロ用　領域
      *****************************************************************
      *
      *   ジョブ管理ＤＢ制御サブ
           COPY    "CPORCSJOBKANRI.INC".
      *
      *    共通パラメタ
           COPY    "MCPAREA".
      *
      *    ＤＢ検索
           COPY    "MCPDATA.INC".
           COPY    "CPORCMCP.INC".
      *
      ****************************************************************
       LINKAGE                 SECTION.
       01  COMMAND-PARAM.
           02  FILLER      PIC X(256).
      ****************************************************************
       PROCEDURE           DIVISION
               USING
           COMMAND-PARAM.
      *
      *****************************************************************
      *    主　　処理
      *****************************************************************
       000-PROC-SEC                SECTION.
      *
           PERFORM 100-INIT-SEC
      *
           PERFORM 200-MAIN-SEC
      *
           PERFORM 300-END-SEC
      *
           STOP    RUN
           .
      *****************************************************************
      *    初期　処理
      *****************************************************************
       100-INIT-SEC                SECTION.
      *
           INITIALIZE              FLG-AREA
           INITIALIZE              STS-AREA
           INITIALIZE              WRK-AREA
           INITIALIZE              CNT-AREA
           INITIALIZE              SUM-AREA
      *
           PERFORM 100-DBOPEN-SEC
      *
           UNSTRING   COMMAND-PARAM    DELIMITED  BY  ","
                                       INTO    LNK-PRTKANRI-RENNUM
                                               LNK-PRTKANRI-TBL-KEY
                                               LNK-PRTKANRI-TBL-GROUP
                                               LNK-PRTKANRI-SHORI-RENNUM
                                               LNK-PRTKANRI-SRYYM
                                               LNK-PRTKANRI-SKYYMD
                                               LNK-PRTKANRI-SHELLID
                                               LNK-PRTKANRI-PRIORITY
                                               LNK-PRTKANRI-TERMID
                                               LNK-PRTKANRI-OPID    
                                               LNK-PRTKANRI-PRTNM    
                                               WRK-PARA-HOSPID
                                               WRK-PARA-JOBID
                                               WRK-PARA-SHELLID
      *
      *    ステップ管理開始処理
           MOVE    "STS"           TO  SJOBKANRI-MODE
           INITIALIZE                  JOBKANRI-REC
           MOVE    WRK-PARA-JOBID  TO  JOB-JOBID
           MOVE    WRK-PARA-SHELLID
                                   TO  JOB-SHELLID
           MOVE    "ORCBM022"      TO  JOB-PGID
           MOVE    "社保総括表（改正版）"
                                   TO  JOB-SHELLMSG
           CALL    "ORCSJOB"       USING
                                   ORCSJOBKANRIAREA  
                                   JOBKANRI-REC
      *
           MOVE    "RECEUPD"           TO  RECEUPD-FILE-ID
           MOVE    LNK-PRTKANRI-TERMID TO  RECEUPD-TERMID
      *
           MOVE    "RECEERR"           TO  RECEERR-FILE-ID
           MOVE    LNK-PRTKANRI-TERMID TO  RECEERR-TERMID
      *
           OPEN    OUTPUT              UPD-FILE
           .
       100-INIT-EXT.
           EXIT.
      *
      *****************************************************************
      *    主　　処理
      *****************************************************************
       200-MAIN-SEC                SECTION.
      *
      *    その他のレセ
           MOVE    "SEIKYU-KEY9"           TO  WRK-PATH  
           PERFORM 900-SEIKYU-SELECT-SEC
      *
           MOVE    1                       TO  WRK-INDEX
           PERFORM 2001-SYUKEI-SEC     UNTIL   FLG-RECE10   =   1 
      *
           MOVE    "DBCLOSE"           TO  MCP-FUNC
           MOVE    "SEIKYU-KEY9"       TO  ORC-DBPATH
           CALL    "ORCMCPSUB"         USING
                                       MCPAREA
                                       ORCMCPAREA
                                       MCPDATA-REC
      *
           IF      FLG-ERR             =   ZERO
      *        在総診または在医総を算定したレセ
               MOVE    "SEIKYU-KEY10"      TO  WRK-PATH  
               PERFORM 900-SEIKYU-SELECT-SEC
      *
               MOVE    2                   TO  WRK-INDEX
               PERFORM 2001-SYUKEI-SEC     UNTIL   FLG-RECE10   =   1 
      *
               MOVE    "DBCLOSE"           TO  MCP-FUNC
               MOVE    "SEIKYU-KEY10"      TO  ORC-DBPATH
               CALL    "ORCMCPSUB"         USING
                                           MCPAREA
                                           ORCMCPAREA
                                           MCPDATA-REC
           END-IF
           .
       200-MAIN-EXT.
           EXIT.
      *
      *****************************************************************
      *    集計処理
      *****************************************************************
       2001-SYUKEI-SEC                SECTION.
      *
      *
           MOVE    ZERO                TO  IDX-AREA
                                           FLG-UPD
      *
           MOVE    WRK-INDEX           TO  IDY3 
      *
      *    老人保健欄
           IF      RECE10-RJNHKNNUM    NOT =   SPACE
               EVALUATE    RECE10-KYURATE
                   WHEN    9
                       MOVE    1                   TO  IDY1
                   WHEN    8
                       MOVE    2                   TO  IDY1
               END-EVALUATE        
               IF      RECE10-KOHNUM (1)   NOT =   SPACE
                   MOVE    1                   TO  IDY2 
               ELSE
                   MOVE    2                   TO  IDY2
               END-IF
               PERFORM 210-ROU-SYUKEI-SEC
           END-IF
      *     
      *    医療保険欄
           IF      RECE10-RJNHKNNUM        =   SPACE
           AND     RECE10-HKNNUM       NOT =   SPACE
               IF      RECE10-AGE          >=  70
                   EVALUATE    RECE10-KYURATE
                       WHEN    9
                           MOVE    1                   TO  IDY1
                       WHEN    8
                           MOVE    2                   TO  IDY1
                   END-EVALUATE        
               ELSE
                   IF      RECE10-AGE              <   3
                       MOVE    5                       TO  IDY1
                   ELSE                    
                       IF      RECE10-HONKZKKBN        =   "1"
                           MOVE    3                       TO  IDY1
                       ELSE     
                           MOVE    4                       TO  IDY1
                       END-IF
                   END-IF
               END-IF
      *                      
               IF      RECE10-KOHNUM (1)   NOT =   SPACE
                   MOVE    1                   TO  IDY2 
               ELSE
                   EVALUATE    IDY1
                       WHEN    1
                           EVALUATE    RECE10-HKNNUM
                               WHEN    "001"
                                   MOVE    2               TO  IDY2
                               WHEN    "002"
                                   MOVE    3               TO  IDY2
                               WHEN    "003"
                                   MOVE    5               TO  IDY2
                               WHEN    "004"
                                   MOVE    6               TO  IDY2
                               WHEN    "031"  THRU  "034"
                                   MOVE    8               TO  IDY2
                               WHEN    "006"
                                   MOVE    9               TO  IDY2
                               WHEN    "063"
                               WHEN    "072"  THRU  "075"
                                   MOVE    10              TO  IDY2
                           END-EVALUATE
                       WHEN    2
                           EVALUATE    RECE10-HKNNUM
                               WHEN    "001"
                                   MOVE    2               TO  IDY2
                               WHEN    "002"    
                                   MOVE    3               TO  IDY2
                               WHEN    "031"  THRU  "034"
                                   MOVE    6               TO  IDY2
                               WHEN    "006"
                                   MOVE    7               TO  IDY2
                               WHEN    "063"
                               WHEN    "072"  THRU  "075"
                                   MOVE    9               TO  IDY2
                           END-EVALUATE
                       WHEN    3
                           EVALUATE    RECE10-HKNNUM
                               WHEN    "001"
                                   MOVE    2               TO  IDY2
                               WHEN    "002"    
                                   IF      RECE10-HOJOKBN  =   "1" OR
                                                               "2" OR
                                                               "3"
                                       MOVE    3               TO  IDY2
                                   ELSE
                                       IF      RECE10-HOJOKBN  =   SPACE
                                           MOVE    4           TO  IDY2
                                       END-IF    
                                   END-IF        
                               WHEN    "003"
                                   MOVE    5               TO  IDY2
                               WHEN    "004"
                                   MOVE    6               TO  IDY2
                               WHEN    "031"  THRU  "034"
                                   MOVE    8               TO  IDY2
                               WHEN    "006"
                                   MOVE    9               TO  IDY2
                               WHEN    "007"
                                   MOVE    10              TO  IDY2
                               WHEN    "063"
                               WHEN    "072"  THRU  "075"
                                   MOVE    11              TO  IDY2
                           END-EVALUATE
                       WHEN    4
                       WHEN    5
                           EVALUATE    RECE10-HKNNUM
                               WHEN    "001"
                                   MOVE    2               TO  IDY2
                               WHEN    "002"
                                   MOVE    3               TO  IDY2
                               WHEN    "003"
                                   MOVE    4               TO  IDY2
                               WHEN    "004"
                                   MOVE    5               TO  IDY2
                               WHEN    "031"  THRU  "034"
                                   MOVE    6               TO  IDY2
                               WHEN    "006"
                                   MOVE    7               TO  IDY2
                               WHEN    "063"
                               WHEN    "072"  THRU  "075"
                                   MOVE    8               TO  IDY2
                           END-EVALUATE
                   END-EVALUATE        
               END-IF
               PERFORM 210-HKN-SYUKEI-SEC
           END-IF    
      *
      *    公費負担分     
           IF      RECE10-KOHNUM (1)   NOT =   SPACE
               IF      RECE10-RJNHKNNUM    NOT =   SPACE
               OR      RECE10-HKNNUM       NOT =   SPACE
      *            公費と医保・老人の併用欄
                   MOVE    1               TO  IDX1
                   PERFORM 220-KOH-SYUKEI-SEC
               ELSE
                   IF      RECE10-KOHNUM (2)   NOT =   SPACE  
      *            公費と公費の併用欄
                       MOVE    2               TO  IDX1
                       PERFORM 220-KOH-SYUKEI-SEC
                   ELSE
      *            公費単独欄
                       MOVE    3               TO  IDX1
                       PERFORM 220-KOH-SYUKEI-SEC
                   END-IF
               END-IF     
           END-IF                
      *
      *    集計の対象にならなかったとき                
           IF      FLG-UPD             =   1
               PERFORM 230-UPD-FILE-HENSYU-SEC
      *
               MOVE    WRK-PATH            TO  ORC-DBPATH
               PERFORM 900-SEIKYU-READ-SEC
           ELSE
               DISPLAY "*** ORCBM022 ERR KEY=" RECE10-KEY
               MOVE    SPACE               TO  WRK-RECEERR
               STRING "集計対象外　患者番号＝"     DELIMITED  BY  SIZE
                       RECE10-PTNUM                DELIMITED  BY  SPACE
                       " 診療年月＝"               DELIMITED  BY  SIZE
                       RECE10-SRYYM                DELIMITED  BY  SIZE
                       " レセ種別＝"               DELIMITED  BY  SIZE
                       RECE10-RECESYUBETU          DELIMITED  BY  SIZE
                                           INTO    WRK-RECEERR
               END-STRING                                
               PERFORM 500-ERR-HENSYU-SEC
           END-IF 
           .
       2001-SYUKEI-EXT.
           EXIT.
      *
      *****************************************************************
      *    老人保健分集計処理
      *****************************************************************
       210-ROU-SYUKEI-SEC          SECTION.
      *
           IF      IDY1            =   ZERO
           OR      IDY2            =   ZERO
               GO  TO  210-ROU-SYUKEI-EXT
           END-IF
      *     
           MOVE    1                         TO  IDX1
      *
      *    件数
           ADD     1           TO  SUM-ROU-KENSU (IDY3 IDY1 IDY2)
           IF      RECE10-SHOKUJINISSU(IDX1)   >   ZERO
               ADD     1       TO  SUM-ROU-SHOKUJIKENSU (IDY3 IDY1 IDY2)
           END-IF
      *    （２）合計、総件数
           ADD     1                   TO  SUM-GOK-KENSU (IDY3 2)
                                           SUM-GOK-KENSU (IDY3 4)
           IF      RECE10-SHOKUJINISSU(IDX1)   >   ZERO
               ADD     1               TO  SUM-GOK-SHOKENSU (IDY3 2)
                                           SUM-GOK-SHOKENSU (IDY3 4) 
           END-IF
      *
      *    診療実日数〜一部負担金 
           ADD     RECE10-JNISSU         (IDX1)
                             TO  SUM-ROU-NISSU          (IDY3 IDY1 IDY2)
           ADD     RECE10-TOTALTEN       (IDX1)
                             TO  SUM-ROU-TOTALTEN       (IDY3 IDY1 IDY2)
           ADD     RECE10-FTNMONEY       (IDX1)
                             TO  SUM-ROU-FTNMONEY       (IDY3 IDY1 IDY2)
      *                           
           ADD     RECE10-SHOKUJINISSU   (IDX1)
                             TO  SUM-ROU-SHOKUJINISSU   (IDY3 IDY1 IDY2)
           ADD     RECE10-SHOKUJIRYOYOHI (IDX1)
                             TO  SUM-ROU-SHOKUJIRYOYOHI (IDY3 IDY1 IDY2)
           ADD     RECE10-SHOKUJIFTN     (IDX1)
                             TO  SUM-ROU-SHOKUJIFTN     (IDY3 IDY1 IDY2)
      *
      *    小計   
           ADD     1         TO  SUM-ROU-KENSU    (IDY3 IDY1 3)
           ADD     RECE10-FTNMONEY       (IDX1)
                             TO  SUM-ROU-FTNMONEY (IDY3 IDY1 3)
      *
           MOVE    1               TO  FLG-UPD 
           .
       210-ROU-SYUKEI-EXT.
           EXIT.
      *
      *****************************************************************
      *    医療保険分集計処理
      *****************************************************************
       210-HKN-SYUKEI-SEC          SECTION.
      *
           IF      IDY1            =   ZERO
           OR      IDY2            =   ZERO
               GO  TO  210-HKN-SYUKEI-EXT
           END-IF
      *     
           MOVE    1                   TO  IDX1
      *    
      *    件数
           ADD     1               TO  SUM-KENSU (IDY3 IDY1 IDY2)
           IF      RECE10-SHOKUJINISSU(IDX1)   >   ZERO
               ADD     1           TO  SUM-SHOKUJIKENSU (IDY3 IDY1 IDY2)
           END-IF
      *    （１）合計、総件数
           ADD     1                   TO  SUM-GOK-KENSU (IDY3 1)
                                           SUM-GOK-KENSU (IDY3 4)
           IF      RECE10-SHOKUJINISSU(IDX1)   >   ZERO
               ADD     1               TO  SUM-GOK-SHOKENSU (IDY3 1)
                                           SUM-GOK-SHOKENSU (IDY3 4) 
           END-IF
      *
      *    診療実日数〜一部負担金 
           ADD     RECE10-JNISSU         (IDX1)
                                 TO  SUM-NISSU          (IDY3 IDY1 IDY2)
           ADD     RECE10-TOTALTEN       (IDX1)
                                 TO  SUM-TOTALTEN       (IDY3 IDY1 IDY2)
           IF      IDY1              =   3   OR  4                          
               ADD     RECE10-YKZFTN         (IDX1)
                                 TO  SUM-YKZFTN         (IDY3 IDY1 IDY2)
           END-IF
           IF      IDY1              <   3                          
               ADD     RECE10-FTNMONEY       (IDX1)
                                 TO  SUM-FTNMONEY       (IDY3 IDY1 IDY2)
           END-IF                          
           ADD     RECE10-SHOKUJINISSU   (IDX1)
                                 TO  SUM-SHOKUJINISSU   (IDY3 IDY1 IDY2)
           ADD     RECE10-SHOKUJIRYOYOHI (IDX1)
                                 TO  SUM-SHOKUJIRYOYOHI (IDY3 IDY1 IDY2)
           ADD     RECE10-SHOKUJIFTN     (IDX1)
                                 TO  SUM-SHOKUJIFTN     (IDY3 IDY1 IDY2)
      *
      *    小計
           IF      IDY2            >   1
      *        件数
               ADD     1           TO  SUM-KENSU (IDY3 IDY1 12)
               IF      RECE10-SHOKUJINISSU(IDX1)   >   ZERO
                   ADD     1       TO  SUM-SHOKUJIKENSU (IDY3 IDY1 12)
               END-IF
      *        診療実日数〜一部負担金 
               ADD     RECE10-JNISSU         (IDX1)
                                   TO  SUM-NISSU          (IDY3 IDY1 12)    
               ADD     RECE10-TOTALTEN       (IDX1)
                                   TO  SUM-TOTALTEN       (IDY3 IDY1 12)
               IF      IDY1              =   3   OR  4                          
                   ADD     RECE10-YKZFTN         (IDX1)
                                   TO  SUM-YKZFTN         (IDY3 IDY1 12)
               END-IF                      
               IF      IDY1              <   3                          
                   ADD     RECE10-FTNMONEY       (IDX1)
                                   TO  SUM-FTNMONEY       (IDY3 IDY1 12)
               END-IF                      
               ADD     RECE10-SHOKUJINISSU   (IDX1)
                                   TO  SUM-SHOKUJINISSU   (IDY3 IDY1 12)
               ADD     RECE10-SHOKUJIRYOYOHI (IDX1)
                                   TO  SUM-SHOKUJIRYOYOHI (IDY3 IDY1 12)
               ADD     RECE10-SHOKUJIFTN     (IDX1)
                                   TO  SUM-SHOKUJIFTN     (IDY3 IDY1 12)
           END-IF                              
      *
           MOVE    1               TO  FLG-UPD 
           .
       210-HKN-SYUKEI-EXT.
           EXIT.
      *
      *****************************************************************
      *    公費負担分集計処理
      *****************************************************************
       220-KOH-SYUKEI-SEC        SECTION.
      *
           PERFORM VARYING IDZ     FROM    1   BY  1
                   UNTIL   IDZ     >       4
                   OR  RECE10-KOHNUM (IDZ) =   SPACE
               PERFORM VARYING IDX2    FROM    1   BY  1
                       UNTIL   IDX2 >          100
                   IF      SUM-KOH-KOHNUM (IDY3 IDX1 IDX2) =   SPACE
                       MOVE    RECE10-KOHNUM (IDZ)     TO 
                                       SUM-KOH-KOHNUM (IDY3 IDX1 IDX2)
                   END-IF                                
                   IF      RECE10-KOHNUM (IDZ) 
                                   =   SUM-KOH-KOHNUM (IDY3 IDX1 IDX2)
                       PERFORM 2201-KOH-SYUKEI-01-SEC
                       MOVE    100             TO  IDX2
                   END-IF
               END-PERFORM
           END-PERFORM                 
           .
       220-KOH-SYUKEI-EXT.
           EXIT.
      *
      *****************************************************************
      *    公費集計処理
      *****************************************************************
       2201-KOH-SYUKEI-01-SEC        SECTION.
      *
           COMPUTE IDY     =   IDZ     +   1
      *    
      *    件数
           ADD     1           TO  SUM-KOH-KENSU (IDY3 IDX1 IDX2)
           IF      RECE10-SHOKUJINISSU(IDY)    >   ZERO
               ADD     1       TO  SUM-KOH-SHOKUJIKENSU (IDY3 IDX1 IDX2)
           END-IF
      *   （３）合計、総件数
           ADD     1                   TO  SUM-GOK-KENSU (IDY3 3)
                                           SUM-GOK-KENSU (IDY3 4)
           IF      RECE10-SHOKUJINISSU(IDY)   >   ZERO
               ADD     1               TO  SUM-GOK-SHOKENSU (IDY3 3)
                                           SUM-GOK-SHOKENSU (IDY3 4) 
           END-IF
      *
      *    診療実日数〜一部負担金 
           ADD     RECE10-TOTALTEN       (IDY)
                             TO  SUM-KOH-TOTALTEN       (IDY3 IDX1 IDX2)
           ADD     RECE10-FTNMONEY       (IDY)
                             TO  SUM-KOH-FTNMONEY       (IDY3 IDX1 IDX2)
      *                           
           ADD     RECE10-SHOKUJIRYOYOHI (IDY)
                             TO  SUM-KOH-SHOKUJIRYOYOHI (IDY3 IDX1 IDX2)
           ADD     RECE10-SHOKUJIFTN     (IDY)
                             TO  SUM-KOH-SHOKUJIFTN     (IDY3 IDX1 IDX2)
           IF      IDX1          =   1                      
               ADD     RECE10-YKZFTN     (IDY)
                                 TO  SUM-KOH-YKZFTN     (IDY3 IDX1 IDX2)
           END-IF                      
      *
           MOVE    1               TO  FLG-UPD 
      *
           .
       2201-KOH-SYUKEI-01-EXT.
           EXIT.
057400*
      *****************************************************************
006900*    請求ＤＢ更新用ファイル出力処理
      *****************************************************************
       230-UPD-FILE-HENSYU-SEC         SECTION.
      *
           MOVE    RECE10-REC          TO  RECE10X-REC
           WRITE   RECE10X-REC
      *
           ADD     1                   TO  CNT-UPDFILE
           .
       230-UPD-FILE-HENSYU-EXT.
           EXIT.
057400*
      *****************************************************************
      *    エラー出力処理
      *****************************************************************
       500-ERR-HENSYU-SEC                SECTION.
      *
           OPEN    INPUT   RECEERR-FILE
           IF      STS-RECEERR         =   ZERO
               CLOSE   RECEERR-FILE
           ELSE
               OPEN    OUTPUT              RECEERR-FILE
      *
               MOVE    WRK-RECEERR         TO  RECEERR-REC
               WRITE   RECEERR-REC
               CLOSE   RECEERR-FILE
      *         
      *        ジョブ管理開始処理
               MOVE    "JBE"           TO  SJOBKANRI-MODE
               INITIALIZE                  JOBKANRI-REC
               MOVE    WRK-PARA-JOBID  TO  JOB-JOBID
               MOVE    WRK-PARA-SHELLID
                                       TO  JOB-SHELLID
               MOVE    WRK-RECEERR     TO  JOB-YOBI
               MOVE    "9999"          TO  JOB-ERRCD
               CALL    "ORCSJOB"       USING
                                       ORCSJOBKANRIAREA  
                                       JOBKANRI-REC
           END-IF
      *
           MOVE    1                   TO  FLG-END
                                           FLG-ERR     
      *
           .
       500-ERR-HENSYU-EXT.
           EXIT.
      *
      *****************************************************************
      *    終了　　処理
      *****************************************************************
       300-END-SEC                 SECTION.
      *
           CLOSE   UPD-FILE
      *
           DISPLAY "*** ORCBM022 IN  "  CNT-RECE10
           DISPLAY "***          UPD "  CNT-UPDFILE 
           DISPLAY "*** ORCBM022 END ***"
      ***     ACCEPT  WRK-IN
      *
           IF      CNT-RECE10            =   ZERO 
               CONTINUE
           ELSE
              IF      FLG-UPD        =   1
              AND     FLG-ERR        =   ZERO 
                  IF      CNT-UPDFILE    >   ZERO
                      PERFORM 310-SEIKYU-UPD-SEC
                  END-IF    
      *
                  CALL    "ORCBM023"          USING 
                                                   SUM-AREA
                                                   WRK-PARA
               END-IF
           END-IF    
      *     
      *    ステップ管理終了処理
           MOVE    "STE"           TO  SJOBKANRI-MODE
           INITIALIZE                  JOBKANRI-REC
           MOVE    WRK-PARA-JOBID  TO  JOB-JOBID
           MOVE    WRK-PARA-SHELLID
                                   TO  JOB-SHELLID
           MOVE    WRK-PARA-PAGE   TO  JOB-UPDCNT                         
           CALL    "ORCSJOB"       USING
                                   ORCSJOBKANRIAREA  
                                   JOBKANRI-REC
      *
           PERFORM 900-DBDISCONNECT-SEC
           .
       300-END-EXT.
           EXIT.
      *
      *****************************************************************
      *   請求管理ＤＢ更新処理
      *****************************************************************
       310-SEIKYU-UPD-SEC              SECTION.
      *
           OPEN    INPUT    UPD-FILE
      *
           PERFORM 900-UPD-FILE-READ-SEC
           PERFORM             UNTIL   FLG-UPDFILE    =   1
               MOVE    SPACE           TO  RECE10-REC   
               INITIALIZE                  RECE10-REC
               MOVE    RECE10X-KEY         TO  RECE10-KEY
               MOVE    RECE10-REC          TO  MCPDATA-REC
               PERFORM 910-SEIKYU-INV-SEC
               IF      FLG-SEIKYU          =   ZERO
                   MOVE    "2"             TO  RECE10-SKYKBN
                   MOVE    LNK-PRTKANRI-SRYYM  
                                           TO  RECE10-SKYYM
                   MOVE    LNK-PRTKANRI-SKYYMD
                                           TO  RECE10-UPDYMD  
                   MOVE    RECE10-REC      TO  MCPDATA-REC
                   MOVE    "DBUPDATE"      TO  MCP-FUNC
                   MOVE    "SEIKYU-KEY"    TO  ORC-DBPATH
                   CALL    "ORCMCPSUB"      USING
                                                MCPAREA
                                                ORCMCPAREA
                                                MCPDATA-REC
                   IF      MCP-RC          =   ZERO
                       PERFORM 900-UPD-FILE-READ-SEC
                   ELSE
                       MOVE  "*** ORCBM022 SEIKYU UPD ERR ***"
                                           TO  WRK-RECEERR
                       MOVE    RECE10-KEY  TO  WRK-RECEERR(33:)
                       PERFORM 500-ERR-HENSYU-SEC
                       MOVE    1           TO  FLG-UPDFILE
                   END-IF
               ELSE
                   MOVE  "*** ORCBM022 SEIKYU INVALID ***"
                                           TO  WRK-RECEERR
                   MOVE    RECE10X-KEY     TO  WRK-RECEERR(33:)
                   PERFORM 500-ERR-HENSYU-SEC
                   MOVE    1               TO  FLG-UPDFILE
               END-IF
           END-PERFORM  
      *
           CLOSE   UPD-FILE
           .
       310-SEIKYU-UPD-EXT.
           EXIT.
      *
      *****************************************************************
      *    請求管理読込
      *****************************************************************
       900-SEIKYU-SELECT-SEC             SECTION.
      *
           MOVE    ZERO                TO  FLG-RECE10
      *     
      *    請求管理ＤＢ検索
           MOVE    SPACE               TO  RECE10-REC
           INITIALIZE                  RECE10-REC
           MOVE    WRK-PARA-HOSPID     TO  RECE10-HOSPID
           MOVE    LNK-PRTKANRI-SRYYM  TO  RECE10-SKYYM
           MOVE    WRK-CONS-SRYYM      TO  RECE10-SRYYM 
           MOVE    RECE10-REC          TO  MCPDATA-REC
           MOVE    "DBSELECT"          TO  MCP-FUNC
           MOVE    WRK-PATH            TO  ORC-DBPATH
           CALL    "ORCMCPSUB"          USING
                                        MCPAREA
                                        ORCMCPAREA
                                        MCPDATA-REC
           IF      MCP-RC              =   ZERO
               MOVE    WRK-PATH            TO  ORC-DBPATH
               PERFORM 900-SEIKYU-READ-SEC
           ELSE
               MOVE    1                   TO  FLG-RECE10
           END-IF
           .
       900-SEIKYU-SELECT-EXT.
           EXIT.
      *
      *****************************************************************
      *    請求マスター読込
      *****************************************************************
       900-SEIKYU-READ-SEC           SECTION.
      *
           MOVE    WRK-PATH            TO  ORC-DBPATH
           MOVE    "DBFETCH"           TO  MCP-FUNC
           CALL    "ORCMCPSUB"          USING
                                        MCPAREA
                                        ORCMCPAREA
                                        MCPDATA-REC
           IF      MCP-RC              =   ZERO
               MOVE    ZERO                TO  FLG-RECE10
               MOVE    MCPDATA-REC         TO  RECE10-REC
      *
      *        テスト患者のときは読み飛ばす
               MOVE    SPACE               TO  PTINF-REC
               INITIALIZE                      PTINF-REC
               MOVE    RECE10-HOSPID       TO  PTINF-HOSPID
               MOVE    RECE10-PTID         TO  PTINF-PTID
               MOVE    PTINF-REC           TO  MCPDATA-REC
               PERFORM 800-PTINF-READ-SEC
               IF      FLG-PTINF           =   ZERO
               AND     PTINF-TSTPTNUMKBN   =   "1"
                   GO  TO  900-SEIKYU-READ-SEC
               END-IF
      *         
               ADD     1                   TO  CNT-RECE10
           ELSE
               MOVE    1                   TO  FLG-RECE10
           END-IF
      *
           .
       900-SEIKYU-READ-EXT.
           EXIT.
      *
      *****************************************************************
      *    患者マスタ読込
      *****************************************************************
       800-PTINF-READ-SEC         SECTION.
      *
           MOVE    "DBSELECT"          TO  MCP-FUNC
           MOVE    "PTINF-KEY"         TO  ORC-DBPATH
           CALL    "ORCMCPSUB"          USING
                                        MCPAREA
                                        ORCMCPAREA
                                        MCPDATA-REC
           IF      MCP-RC              =   ZERO
               MOVE    "DBFETCH"           TO  MCP-FUNC
               MOVE    "PTINF-KEY"         TO  ORC-DBPATH
               CALL    "ORCMCPSUB"          USING
                                            MCPAREA
                                            ORCMCPAREA
                                            MCPDATA-REC
               IF      MCP-RC              =   ZERO
                   MOVE    ZERO                TO  FLG-PTINF
                   MOVE    MCPDATA-REC         TO  PTINF-REC
               ELSE
                   MOVE    1                   TO  FLG-PTINF
                   INITIALIZE                  PTINF-REC
               END-IF
           ELSE
               MOVE    1                   TO  FLG-PTINF
               INITIALIZE                  PTINF-REC
           END-IF
      *
           MOVE    "DBCLOSE"           TO  MCP-FUNC
           MOVE    "PTINF-KEY"         TO  ORC-DBPATH
           CALL    "ORCMCPSUB"          USING
                                        MCPAREA
                                        ORCMCPAREA
                                        MCPDATA-REC
      *
           .
       800-PTINF-READ-EXT.
           EXIT.
      *
      *****************************************************************
      *    請求管理ＤＢ更新用ファイル読込
      *****************************************************************
       900-UPD-FILE-READ-SEC             SECTION.
      *
           READ    UPD-FILE
               AT  END
                   MOVE    1           TO  FLG-UPDFILE
               NOT AT  END
                   MOVE    ZERO        TO  FLG-UPDFILE
           END-READ
           .
       900-UPD-FILE-READ-EXT.
           EXIT.
      *
      *****************************************************************
      *    請求管理マスタ読込
      *****************************************************************
       910-SEIKYU-INV-SEC         SECTION.
      *
           MOVE    "DBSELECT"          TO  MCP-FUNC
           MOVE    "SEIKYU-KEY"        TO  ORC-DBPATH
           CALL    "ORCMCPSUB"          USING
                                        MCPAREA
                                        ORCMCPAREA
                                        MCPDATA-REC
           IF      MCP-RC              =   ZERO
               MOVE    "DBFETCH"           TO  MCP-FUNC
               MOVE    "SEIKYU-KEY"        TO  ORC-DBPATH
               CALL    "ORCMCPSUB"          USING
                                            MCPAREA
                                            ORCMCPAREA
                                            MCPDATA-REC
               IF      MCP-RC              =   ZERO
                   MOVE    ZERO                TO  FLG-SEIKYU
                   MOVE    MCPDATA-REC         TO  RECE10-REC
               ELSE
                   MOVE    1                   TO  FLG-SEIKYU
               END-IF
           ELSE
               MOVE    1                   TO  FLG-SEIKYU
           END-IF
      *
           MOVE    "DBCLOSE"           TO  MCP-FUNC
           MOVE    "SEIKYU-KEY"        TO  ORC-DBPATH
           CALL    "ORCMCPSUB"          USING
                                        MCPAREA
                                        ORCMCPAREA
                                        MCPDATA-REC
      *
           .
       910-SEIKYU-INV-EXT.
           EXIT.
      *      
      *****************************************************************
      *    ＤＢ　オープン処理
      *****************************************************************
       100-DBOPEN-SEC                SECTION.
      *
           MOVE    "DBOPEN"            TO  MCP-FUNC.
           CALL    "ORCMCPSUB"          USING
                                        MCPAREA
                                        ORCMCPAREA
                                        MCPDATA-REC.
      *
           MOVE    "DBSTART"           TO  MCP-FUNC.
           CALL    "ORCMCPSUB"          USING
                                        MCPAREA
                                        ORCMCPAREA
                                        MCPDATA-REC.
           .
       100-DBOPEN-EXT.
           EXIT.
      *
      *****************************************************************
      *    ＤＢ　クローズ処理
      *****************************************************************
       900-DBDISCONNECT-SEC            SECTION.
      *
           MOVE    "DBCOMMIT"          TO  MCP-FUNC.
           CALL    "ORCMCPSUB"          USING
                                        MCPAREA
                                        ORCMCPAREA
                                        MCPDATA-REC.
      *
           MOVE    "DBDISCONNECT"      TO  MCP-FUNC.
           CALL    "ORCMCPSUB"          USING
                                        MCPAREA
                                        ORCMCPAREA
                                        MCPDATA-REC.
           .
       900-DBCLOSE-EXT.
           EXIT.

      *******************************************************************
      * Project code name "ORCA"
      * 日医標準レセプトソフト（JMA standard receipt software）
      * Copyright(C) 2002 JMA (Japan Medical Association)
      *
      * This program is part of "JMA standard receipt software".
      *
      *     This program is distributed in the hope that it will be useful
      * for further advancement in medical care, according to JMA Open
      * Source License, but WITHOUT ANY WARRANTY.
      *     Everyone is granted permission to use, copy, modify and
      * redistribute this program, but only under the conditions described
      * in the JMA Open Source License. You should have received a copy of
      * this license along with this program. If not, stop using this
      * program and contact JMA, 2-28-16 Honkomagome, Bunkyo-ku, Tokyo,
      * 113-8621, Japan.
      ********************************************************************
       IDENTIFICATION          DIVISION.
       PROGRAM-ID.             ORHCML04.
      *****************************************************************
      *  システム名        : ＯＲＣＡ
      *  サブシステム名    : 点数マスタ
      *  コンポーネント名  : 点数マスタ一覧表・診療行為コメント
      *  管理者            : 
      *  作成日付    作業者        記述
      *  02/12/06    NACL-森脇     新規作成
      *****************************************************************
      *  プログラム修正履歴
      * Maj/Min/Rev  修正者       日付      内容
      *  01.01.01    NACL-森脇    03/03/10  TERMIDの領域指定なし
      *  01.01.02    NACL-森脇    03/07/14  帳票をHCML03→HCML04に変更
      *  01.01.03    NACL-森脇    04/03/16  点数の桁修正
      *****************************************************************
      *
       ENVIRONMENT             DIVISION.
       CONFIGURATION               SECTION.
       INPUT-OUTPUT                SECTION.
       FILE-CONTROL.
      *    エラーファイル
           SELECT  TENSUERR-FILE    ASSIGN  TENSUERR
                                   FILE    STATUS  IS  STS-TENSUERR.
      *
       DATA                        DIVISION.
       FILE                        SECTION.
      *
      *    エラーファイル
       FD  TENSUERR-FILE.
       01  TENSUERR-REC             PIC X(200). 
      * 
       WORKING-STORAGE             SECTION.
      *    シェル用領域
           COPY    "CPCOMMONSHELL.INC".
           COPY    "CPSHELLTBL.INC".
           COPY    "ORCA-DBPATH".
      *    エラーファイル 名称領域 
           COPY    "CPCOMMONDAT2.INC"
                                   REPLACING  //RECE01PARA//
                                   BY         //TENSUERR//.
           03  FILLER              PIC X(04)   VALUE   ".dat".
      *     
      *----(01.01.02)--UPD-START---
      *     COPY    "HCML03.INC".
           COPY    "HCML04.INC".
      *----(01.01.02)--UPD-END-----
      *
      *    スパ領域
       01  STS-AREA.
           03  STS-TENSUERR         PIC X(02).
      *
      *    フラグ領域
       01  FLG-AREA.
           03  FLG-END             PIC 9(01).
           03  FLG-SYSKANRI        PIC 9(01).
           03  FLG-TENSU           PIC 9(01).
      *
      *    カウント領域
       01  CNT-AREA.
           03  CNT-LINE            PIC 9(02).
           03  CNT-PAGE            PIC 9(03).
      *
      *    集計領域
       01  SUM-AREA.
           03  SUM-PAGE            PIC 9(10).
      *
      *    パラメタ領域
       01  WRK-PARA.
           COPY    "CPORCSPRTLNK.INC".
           03  WRK-PARA-JOBID      PIC 9(07).
           03  WRK-PARA-SHELLID    PIC X(08).
           COPY    "CPHCMLSHELL.INC"
                                   REPLACING  //SHELL-//
                                   BY         //WRK-PARA-//.
      *
       01  INEX-AREA.
           03  IDX                   PIC 9(02).      
      *
      *
      *    一時領域
       01  WRK-AREA.
           03  WRK-PATH              PIC X(64).
           03  WRK-TENSUERR          PIC X(200).        
      *
           03  WRK-LNKYMD            PIC X(10).
      *----(01.01.02)--UPD-START---
           03  WRK-MSTYMDH           PIC X(22).
      *----(01.01.02)--UPD-END-----
      *     
           03  WRK-SYSYMD.
               05  WRK-SYSYY         PIC 9(04).
               05  WRK-SYSMM         PIC 9(02).
               05  WRK-SYSDD         PIC 9(02).
           03  WRK-SYMD.
               05  WRK-SYY           PIC 9(04).
               05  WRK-SMM           PIC 9(02).
               05  WRK-SDD           PIC 9(02).
           03  WRK-HENYMDG           PIC X(22).
      *
           03  WRK-CDKBN.
               05  WRK-CDKBN-KBN         PIC X(1).
               05  WRK-CDKBN-KBNNUM      PIC X(3).
               05  WRK-CDKBN-HI1         PIC X(1).
               05  WRK-CDKBN-KBNNUM-EDA  PIC X(2).
               05  WRK-CDKBN-HI2         PIC X(1).
               05  WRK-CDKBN-KOUBAN      PIC X(2).
      *
           03  WRK-PAGE              PIC ZZ,ZZ9.
      *----(01.01.03)--UPD-START---
           03  WRK-TEN               PIC ZZZ,ZZZ,ZZ9.99.
      *----(01.01.03)--UPD-END-----
      *----(01.01.02)--ADD-START---
           03  WRK-ZZ9               PIC ZZ9.
      *----(01.01.02)--ADD-END-----
      *
           03  WRK-SRYCDSTR          PIC X(09).
           03  WRK-SRYCDEND          PIC X(09).
      *
      *----(01.01.02)--ADD-START---
           03  WRK-MAE-INPUT           PIC X(200).
           03  WRK-OUT-INPUT           PIC X(200).
      *----(01.01.02)--ADD-END-----
      *
      *****************************************************************
      *    ファイルレイアウト
      *****************************************************************
      *
      *    医療機関名      
           COPY  "CPSK1001.INC".
      *
      *    出力先プリンタ名割り当て情報
           COPY  "CPSK1031.INC".
      *     
      *    点数マスタ
           COPY    "CPTENSU.INC".
      *
      *    ジョブ管理マスタ
       01  JOBKANRI-REC.
           COPY    "CPJOBKANRI.INC".
      *
       01  PRTKANRI-REC.
           COPY    "CPPRTKANRI.INC".
      *
       01  PRTDATA-REC.
           COPY    "CPPRTDATA.INC".          
      *
      *****************************************************************
      *    サブプロ用　領域
      *****************************************************************
      *
      *   ジョブ管理ＤＢ制御サブ
           COPY    "CPORCSJOBKANRI.INC".
      *
      *    印刷ＤＢ更新サブ
           COPY    "CPORCSPRT.INC".          
      *
      *   日付変換サブ
           COPY    "CPORCSDAY.INC".
           COPY    "CPORCSLNK.INC".
      *
      *----(01.01.02)--ADD-START---
      *    全角チェックパラメタ
           COPY    "CPORCSKANACHK.INC".
      *----(01.01.02)--ADD-END-----
      *
      *    ＤＢ検索
           COPY    "MCPDATA.INC".
           COPY    "CPORCMCP.INC".
      *
           COPY    "MCPAREA".
           COPY    "COMMON-SPA".
      *
      ****************************************************************
       LINKAGE                 SECTION.
       01  COMMAND-PARAM.
           02  FILLER      PIC X(256).
      ****************************************************************
       PROCEDURE           DIVISION
               USING
           COMMAND-PARAM.
      *****************************************************************
      *    主　　処理
      *****************************************************************
       000-PROC-SEC                SECTION.
      *
           PERFORM 100-INIT-SEC
      *
           PERFORM 200-MAIN-SEC
                   UNTIL       FLG-END     =   1
      *
           PERFORM 300-END-SEC
      *
           STOP    RUN
           .
      *****************************************************************
      *    初期　処理
      *****************************************************************
       100-INIT-SEC                SECTION.
      *
           INITIALIZE                  FLG-AREA
           INITIALIZE                  STS-AREA
           INITIALIZE                  WRK-AREA
           INITIALIZE                  CNT-AREA
           INITIALIZE                  SUM-AREA
      *
           PERFORM 100-DBOPEN-SEC
      *
           UNSTRING   COMMAND-PARAM    DELIMITED  BY  ","
                                       INTO    LNK-PRTKANRI-RENNUM
                                               LNK-PRTKANRI-TBL-KEY
                                               LNK-PRTKANRI-TBL-GROUP
                                               LNK-PRTKANRI-SHORI-RENNUM
                                               LNK-PRTKANRI-SRYYM
                                               LNK-PRTKANRI-SKYYMD
                                               LNK-PRTKANRI-SHELLID
                                               LNK-PRTKANRI-PRIORITY
                                               LNK-PRTKANRI-TERMID
                                               LNK-PRTKANRI-OPID    
                                               LNK-PRTKANRI-PRTNM
                                               WRK-PARA-JOBID
                                               WRK-PARA-SHELLID
                                               WRK-PARA-AREA
      *
      *    ステップ管理開始処理
           MOVE    "STS"           TO  SJOBKANRI-MODE
           INITIALIZE                  JOBKANRI-REC
           MOVE    WRK-PARA-JOBID  TO  JOB-JOBID
           MOVE    WRK-PARA-SHELLID
                                   TO  JOB-SHELLID
           MOVE    "ORHCML01"      TO  JOB-PGID
           MOVE    "点数マスタ一覧表・コメント" 
                                   TO  JOB-SHELLMSG
           CALL    "ORCSJOB"       USING
                                   ORCSJOBKANRIAREA  
                                   JOBKANRI-REC
      *                             
           MOVE    LNK-PRTKANRI-SKYYMD
                                    TO  WRK-LNKYMD
           PERFORM 831-YMDCONV12-SEC
      *----(01.01.02)--UPD-START---
      *     MOVE    WRK-LNKYMD       TO  WRK-MSTYMDH
           MOVE    WRK-LNKYMD       TO  WRK-MAE-INPUT
           PERFORM 800-HENKAN-SEC
           MOVE    WRK-OUT-INPUT    TO  WRK-MSTYMDH
      *----(01.01.02)--UPD-END-----
      *     
           MOVE    "TENSERR"        TO  TENSUERR-FILE-ID
           MOVE    LNK-PRTKANRI-TERMID
                                    TO  TENSUERR-TERMID
      *
      *    パラメタ編集処理
           PERFORM 110-PARA-HENSYU-SEC
      *
           .
       100-INIT-EXT.
           EXIT.
      *
      *****************************************************************
      *    パラメタ編集処理
      *****************************************************************
       110-PARA-HENSYU-SEC                   SECTION.
      *               
      *    医療機関名称編集
           MOVE    SPACE               TO  SYS-1001-REC
           INITIALIZE                  SYS-1001-REC
           MOVE    "1001"              TO  SYS-1001-KANRICD
           MOVE    "*"                 TO  SYS-1001-KBNCD
           MOVE    SYS-1001-REC        TO  MCPDATA-REC
           PERFORM 800-SYSKANRI-READ-SEC
           IF      FLG-SYSKANRI        =   ZERO
               MOVE    MCPDATA-REC         TO  SYS-1001-REC
               IF      SYS-1001-HOSPNAME   =   LOW-VALUE
                   MOVE    SPACE               TO  SYS-1001-HOSPNAME
               END-IF  
           ELSE    
               INITIALIZE                  SYS-1001-REC
               MOVE    "医療機関名称が取得できませんでした。"
                                           TO  WRK-TENSUERR
               PERFORM 500-ERR-HENSYU-SEC
           END-IF                                     
           .                                   
      *
       110-PARA-HENSYU-EXT.
           EXIT.
      *
      *****************************************************************
      *    主　　処理
      *****************************************************************
       200-MAIN-SEC                SECTION.
      *
           PERFORM VARYING IDX FROM    1   BY  1
                   UNTIL   IDX >       3
               IF      WRK-PARA-COMCHK (IDX)      >   ZERO
                   EVALUATE    IDX
                       WHEN    1
                           MOVE    "001000000"    TO  WRK-SRYCDSTR
                           MOVE    "001999999"    TO  WRK-SRYCDEND
                       WHEN    2
                           MOVE    "002000000"    TO  WRK-SRYCDSTR
                           MOVE    "002999999"    TO  WRK-SRYCDEND
                       WHEN    3
                           MOVE    "800000000"    TO  WRK-SRYCDSTR
                           MOVE    "899999999"    TO  WRK-SRYCDEND
                   END-EVALUATE
      *            帳票編集処理
                   PERFORM 2001-PRINT-SEC
               END-IF
           END-PERFORM
      *
           MOVE    1               TO  FLG-END             
           .
       200-MAIN-EXT.
           EXIT.
      *
      *****************************************************************
      *    主　　処理
      *****************************************************************
       2001-PRINT-SEC                SECTION.
      *
           MOVE    ZERO                TO  FLG-TENSU
           INITIALIZE                  CNT-AREA
      *     
      *    帳票印刷開始処理
           PERFORM 900-DBSELECT-SEC
      *
           PERFORM              UNTIL   FLG-TENSU   =   1
      *        帳票編集<見出し>処理
               PERFORM 310-HEAD-HEN-SEC
      *        帳票編集<明細>処理
               PERFORM 320-BODY-HEN-SEC
      *        帳票印刷処理
               PERFORM 390-PRINT-OUT-SEC
           END-PERFORM
      *
           PERFORM 900-DBCLOSE-SEC
      *
           ADD     CNT-PAGE             TO  SUM-PAGE     
           .
       2001-PRINT-EXT.
           EXIT.
      *
      *****************************************************************
      *    帳票編集<ヘッダー部>処理
      *****************************************************************
       310-HEAD-HEN-SEC          SECTION.
      *
           MOVE    ZERO                TO  CNT-LINE
           ADD     1                   TO  CNT-PAGE
      *
           INITIALIZE                      HCML04
           MOVE    SPACE               TO  HCML04
      *
           MOVE    SYS-1001-HOSPNAME   TO  HCML04-HOSPNAME
           MOVE    WRK-MSTYMDH         TO  HCML04-MSTYMD
           MOVE    CNT-PAGE            TO  WRK-PAGE
           MOVE    WRK-PAGE            TO  HCML04-PAGE
      *
           .
       310-HEAD-HEN-EXT.
           EXIT.
      *
      *****************************************************************
      *    帳票編集<明細行>処理
      *****************************************************************
       320-BODY-HEN-SEC      SECTION.
      *
           MOVE    ZERO                TO  CNT-LINE
      *
           PERFORM             UNTIL   FLG-TENSU   =   1
                               OR      CNT-LINE    =  15 
      *
               ADD     1                  TO  CNT-LINE
      *
      *----(01.01.02)--UPD-START---
      *        診療行為コード
               MOVE    TNS-SRYCD          TO  HCML04-SRYCD(CNT-LINE)
      *        品名
               MOVE    TNS-NAME           TO  HCML04-NAME(CNT-LINE)
      *        金額
               MOVE    TNS-TEN            TO  WRK-TEN
               MOVE    WRK-TEN            TO  HCML04-TEN(CNT-LINE)
      *        保険適用
               IF      TNS-HKNTEKKBN  NOT  =  "1"
                   MOVE TNS-HKNTEKKBN     TO  HCML04-HKNTEK(CNT-LINE)
               END-IF
      *
      *        点数欄集計先識別（外来）
               MOVE    TNS-GAITENTTLKBN   TO  WRK-ZZ9
               MOVE    WRK-ZZ9            TO  HCML04-GAITEN (CNT-LINE)
      *        点数欄集計先識別（入院）
               MOVE    TNS-NYUTENTTLKBN   TO  WRK-ZZ9
               MOVE    WRK-ZZ9            TO  HCML04-NYUTEN (CNT-LINE)
      *
               IF      IDX     =   1
      *            朝
                   IF      TNS-SSTKIJUNCD(1)  =   1
                       MOVE  "○"    TO  HCML04-FUKUYOU1 (CNT-LINE)
                   END-IF
      *            昼
                   IF      TNS-SSTKIJUNCD(2)  =   1
                       MOVE  "○"    TO  HCML04-FUKUYOU2 (CNT-LINE)
                   END-IF
      *            夕
                   IF      TNS-SSTKIJUNCD(3)  =   1
                       MOVE  "○"    TO  HCML04-FUKUYOU3 (CNT-LINE)
                   END-IF
      *            寝前
                   IF      TNS-SSTKIJUNCD(4)  =   1
                       MOVE  "○"    TO  HCML04-FUKUYOU4 (CNT-LINE)
                   END-IF
               ELSE
                   IF      IDX     =   2
      *                部位区分
                       EVALUATE    TNS-BUIKBN
                       WHEN    "1"
                           MOVE    "頭部"     TO  HCML04-BUI (CNT-LINE)
                       WHEN    "2"
                           MOVE    "躯幹"     TO  HCML04-BUI (CNT-LINE)
                       WHEN    "3"
                           MOVE    "四肢"     TO  HCML04-BUI (CNT-LINE)
                       WHEN    "5"
                           MOVE    "胸部"     TO  HCML04-BUI (CNT-LINE)
                       WHEN    "6"
                           MOVE    "腹部"     TO  HCML04-BUI (CNT-LINE)
                       WHEN    "7"
                           MOVE    "脊椎"     TO  HCML04-BUI (CNT-LINE)
                       WHEN    "8"
                           MOVE    "消化管"   TO  HCML04-BUI (CNT-LINE)
                       WHEN    "0"
                           MOVE    "その他"   TO  HCML04-BUI (CNT-LINE)
                       END-EVALUATE
                   END-IF
      *
      *            位置１
                   MOVE  TNS-SSTKIJUNCD(1)    TO  WRK-ZZ9
                   MOVE  WRK-ZZ9              TO  HCML04-KRM1 (CNT-LINE)
      *            桁数１
                   MOVE  TNS-SSTKIJUNCD(2)    TO  WRK-ZZ9
                   MOVE  WRK-ZZ9              TO  HCML04-KET1 (CNT-LINE)
      *            位置２
                   MOVE  TNS-SSTKIJUNCD(3)    TO  WRK-ZZ9
                   MOVE  WRK-ZZ9              TO  HCML04-KRM2 (CNT-LINE)
      *            桁数２
                   MOVE  TNS-SSTKIJUNCD(4)    TO  WRK-ZZ9
                   MOVE  WRK-ZZ9              TO  HCML04-KET2 (CNT-LINE)
      *            位置３
                   MOVE  TNS-SSTKIJUNCD(5)    TO  WRK-ZZ9
                   MOVE  WRK-ZZ9              TO  HCML04-KRM3 (CNT-LINE)
      *            桁数３
                   MOVE  TNS-SSTKIJUNCD(6)    TO  WRK-ZZ9
                   MOVE  WRK-ZZ9              TO  HCML04-KET3 (CNT-LINE)
      *            位置４
                   MOVE  TNS-SSTKIJUNCD(7)    TO  WRK-ZZ9
                   MOVE  WRK-ZZ9              TO  HCML04-KRM4 (CNT-LINE)
      *            桁数４
                   MOVE  TNS-SSTKIJUNCD(8)    TO  WRK-ZZ9
                   MOVE  WRK-ZZ9              TO  HCML04-KET4 (CNT-LINE)
               END-IF
      *
      *        有効期間開始日
               MOVE    TNS-YUKOSTYMD      TO  HCML04-YUKOSTYMD(CNT-LINE)
      *        "-"
               MOVE    "-"                TO  HCML04-NYORO(CNT-LINE)
      *        有効期間終了日
               MOVE    TNS-YUKOEDYMD      TO  HCML04-YUKOEDYMD(CNT-LINE)
      *-----(01.01.02)--UPD-END-----
               PERFORM 900-DBFETCH-SEC
      *
           END-PERFORM
      *
           .
       320-BODY-HEN-EXT.
           EXIT.
      *
      *****************************************************************
      *    帳票印刷処理
      *****************************************************************
       390-PRINT-OUT-SEC                SECTION.
      *
           INITIALIZE                  ORCSPRTAREA
           MOVE    "INS"               TO  SPRT-MODE
           MOVE    LNK-PRTKANRI-RENNUM TO  SPRT-RENNUM
           MOVE    LNK-PRTKANRI-TBL-KEY
                                       TO  SPRT-TBL-KEY
           MOVE    LNK-PRTKANRI-TBL-GROUP
                                       TO  SPRT-TBL-GROUP
           MOVE    LNK-PRTKANRI-SRYYM  TO  SPRT-SRYYM
           MOVE    LNK-PRTKANRI-SKYYMD TO  SPRT-SKYYMD
           MOVE    LNK-PRTKANRI-SHELLID 
                                       TO  SPRT-SHELLID
           MOVE    LNK-PRTKANRI-SHORI-RENNUM
                                       TO  SPRT-SHORI-RENNUM
           MOVE    LNK-PRTKANRI-PRIORITY
                                       TO  SPRT-PRIORITY
      *----(01.01.02)--UPD-END-----
           MOVE    "HCML04.red"        TO  SPRT-PRTID
      *----(01.01.02)--UPD-END-----
           MOVE    "点数マスタ一覧表・コメント"
                                       TO  SPRT-TITLE
           MOVE    HCML04              TO  SPRT-PRTDATA
           MOVE    LNK-PRTKANRI-TERMID TO  SPRT-TERMID
           MOVE    LNK-PRTKANRI-OPID   TO  SPRT-OPID
           MOVE    LNK-PRTKANRI-PRTNM  TO  SPRT-PRTNM
           MOVE    "1"                 TO  SPRT-SITEKBN
           CALL    "ORCSPRT"           USING
                                       ORCSPRTAREA
           IF      SPRT-RETURN         =   ZERO
               CONTINUE
           ELSE
               MOVE    "印刷ＤＢに更新できませんでした"
                                          TO  WRK-TENSUERR
               PERFORM 500-ERR-HENSYU-SEC                           
           END-IF                                                              
           .
       390-PRINT-OUT-EXT.
           EXIT.
      *
      *****************************************************************
      *    エラー出力処理
      *****************************************************************
       500-ERR-HENSYU-SEC                SECTION.
      *
      *
           OPEN    INPUT   TENSUERR-FILE
      *     
           IF      STS-TENSUERR         =   ZERO
               CONTINUE
           ELSE
               OPEN    OUTPUT              TENSUERR-FILE
      *
               MOVE    WRK-TENSUERR         TO  TENSUERR-REC
               WRITE   TENSUERR-REC
      *         
      *        ジョブ管理開始処理
               MOVE    "JBE"           TO  SJOBKANRI-MODE
               INITIALIZE                  JOBKANRI-REC
               MOVE    WRK-PARA-JOBID  TO  JOB-JOBID
               MOVE    WRK-PARA-SHELLID
                                       TO  JOB-SHELLID
               MOVE    WRK-TENSUERR     TO  JOB-YOBI
               MOVE    "9999"          TO  JOB-ERRCD
               CALL    "ORCSJOB"       USING
                                       ORCSJOBKANRIAREA  
                                       JOBKANRI-REC
           END-IF
      *
           CLOSE   TENSUERR-FILE
           MOVE    1               TO  FLG-END                             
      * 
           .
       500-ERR-HENSYU-EXT.
           EXIT.
      *
      *****************************************************************
      *    終了　　処理
      *****************************************************************
       300-END-SEC                 SECTION.
      *
           DISPLAY "*** ORHCML04 END ***"
      *     
      *    ステップ管理終了処理
           MOVE    "STE"           TO  SJOBKANRI-MODE
           INITIALIZE                  JOBKANRI-REC
           MOVE    SUM-PAGE        TO  JOB-UPDCNT 
           MOVE    WRK-PARA-JOBID  TO  JOB-JOBID
           MOVE    WRK-PARA-SHELLID
                                   TO  JOB-SHELLID
           CALL    "ORCSJOB"       USING
                                   ORCSJOBKANRIAREA  
                                   JOBKANRI-REC
      *                              
           PERFORM 900-DBDISCONNECT-SEC
           .
       300-END-EXT.
           EXIT.
      *
      *----(01.01.02)--ADD-START---
      *****************************************************************
      *    全角変換処理
      *****************************************************************
       800-HENKAN-SEC                 SECTION.
      *
           INITIALIZE                  WRK-OUT-INPUT
      *
           INITIALIZE                  ORCSKANACHKAREA
           MOVE    "2"                 TO  KANACHK-SYORI
           MOVE    WRK-MAE-INPUT       TO  KANACHK-MAE-INPUT
           CALL    "ORCSKANACHK"       USING
                                       ORCSKANACHKAREA
           IF      KANACHK-RC          =   ZERO
               MOVE    KANACHK-OUT-INPUT
                                           TO  WRK-OUT-INPUT
           ELSE
               MOVE    WRK-MAE-INPUT       TO  WRK-OUT-INPUT
           END-IF
      *
           .
       800-HENKAN-EXT.
           EXIT.
      *----(01.01.02)--ADD-END-----
      *****************************************************************
      *   和暦年月日→西暦年月日（機能１２）
      *****************************************************************
       831-YMDCONV12-SEC                         SECTION.
      *
           INITIALIZE                      STS-AREA-DAY
           INITIALIZE                      LNK-DAY2-AREA
           MOVE    "21"                TO  LNK-DAY2-IRAI
           MOVE    WRK-LNKYMD          TO  LNK-DAY2-YMD
           CALL    "ORCSDAY"           USING   STS-AREA-DAY
                                               LNK-DAY2-AREA
           MOVE    LNK-DAY2-EDTYMD1    TO  WRK-LNKYMD
           .
      *
       831-YMDCONV12-EXT.
           EXIT.
      *
      *****************************************************************
      *    点数マスタ　検索 
      *****************************************************************
       900-DBSELECT-SEC                 SECTION.
      *
           INITIALIZE                  TNS-TENSU-REC
           MOVE    WRK-SRYCDSTR        TO  TNS-SRYCD
           MOVE    WRK-SRYCDEND        TO  TNSUP-SRYCD 
           MOVE    "  "                TO  TNSXX-STSRYKBN
           MOVE    "99"                TO  TNSXX-EDSRYKBN
      *
           EVALUATE    WRK-PARA-CYUKBN ALSO    WRK-PARA-SYUKBN        
               WHEN    1               ALSO    1
                   MOVE    "TENSU-KEY10S"      TO  WRK-PATH 
               WHEN    1               ALSO    2
                   MOVE    "TENSU-KEY10M"      TO  WRK-PATH 
               WHEN    1               ALSO    3
                   MOVE    "TENSU-KEY10K"      TO  WRK-PATH
               WHEN    2               ALSO    1
                   MOVE    "TENSU-KEY10S1"     TO  WRK-PATH 
               WHEN    2               ALSO    2
                   MOVE    "TENSU-KEY10M1"     TO  WRK-PATH 
               WHEN    2               ALSO    3
                   MOVE    "TENSU-KEY10K1"     TO  WRK-PATH
           END-EVALUATE        
      *
           MOVE    TNS-TENSU-REC       TO  MCPDATA-REC
           MOVE    "DBSELECT"          TO  MCP-FUNC
           MOVE    WRK-PATH            TO  ORC-DBPATH
           MOVE    WRK-PARA-MSTYMD     TO  ORC-DBYMD
           CALL    "ORCMCPSUB"          USING
                                        MCPAREA
                                        ORCMCPAREA
                                        MCPDATA-REC
           IF      MCP-RC              =   ZERO
               PERFORM 900-DBFETCH-SEC
           ELSE
               MOVE    1                   TO  FLG-TENSU
           END-IF
           .
       900-DBSELECT-EXT.
           EXIT.
      *
      *****************************************************************
      *     点数マスタを検索処理
      *****************************************************************
       900-DBFETCH-SEC                SECTION.
      *
           MOVE    "DBFETCH"           TO  MCP-FUNC
           MOVE    WRK-PATH            TO  ORC-DBPATH
           CALL    "ORCMCPSUB"          USING
                                        MCPAREA
                                        ORCMCPAREA
                                        MCPDATA-REC
           IF      MCP-RC              =   ZERO
               MOVE    ZERO            TO  FLG-END
               MOVE    MCPDATA-REC         TO  TNS-TENSU-REC
           ELSE
               MOVE    1                       TO  FLG-TENSU
           END-IF  
           .
       900-DBFETCH-EXT.
           EXIT.
      *
      *****************************************************************
      *    点数マスタ　クローズ処理
      *****************************************************************
       900-DBCLOSE-SEC                 SECTION.
      *
           MOVE    "DBCLOSE"           TO  MCP-FUNC
           MOVE    WRK-PATH      TO  ORC-DBPATH
           CALL    "ORCMCPSUB"          USING
                                        MCPAREA
                                        ORCMCPAREA
                                        MCPDATA-REC
           .
       900-DBCLOSE-EXT.
           EXIT.
      *
      *****************************************************************
      *    システム管理マスタ読み込み
      *****************************************************************
       800-SYSKANRI-READ-SEC           SECTION.
      *
           MOVE    WRK-PARA-MSTYMD     TO  ORC-DBYMD
           MOVE    "DBSELECT"          TO  MCP-FUNC
           MOVE    "SYSKANRI-KEY"      TO  ORC-DBPATH
           CALL    "ORCMCPSUB"          USING
                                        MCPAREA
                                        ORCMCPAREA
                                        MCPDATA-REC
           IF      MCP-RC              =   ZERO
               MOVE    "DBFETCH"           TO  MCP-FUNC
               MOVE    "SYSKANRI-KEY"      TO  ORC-DBPATH
               CALL    "ORCMCPSUB"          USING
                                            MCPAREA
                                            ORCMCPAREA
                                            MCPDATA-REC
               IF      MCP-RC              =   ZERO
                   MOVE    ZERO            TO  FLG-SYSKANRI
               ELSE
                   MOVE    1               TO  FLG-SYSKANRI
               END-IF
           ELSE
               MOVE    1               TO  FLG-SYSKANRI
           END-IF
      *
           MOVE    "DBCLOSE"           TO  MCP-FUNC
           MOVE    "SYSKANRI-KEY"      TO  ORC-DBPATH
           CALL    "ORCMCPSUB"          USING
                                        MCPAREA
                                        ORCMCPAREA
                                        MCPDATA-REC
      *
           .
       800-SYSKANRI-READ-EXT.
           EXIT.
      *      
      *****************************************************************
      *    ＤＢ　オープン処理
      *****************************************************************
       100-DBOPEN-SEC                SECTION.
      *
           MOVE    "DBOPEN"            TO  MCP-FUNC.
           CALL    "ORCMCPSUB"          USING
                                        MCPAREA
                                        ORCMCPAREA
                                        MCPDATA-REC.
      *
           MOVE    "DBSTART"           TO  MCP-FUNC.
           CALL    "ORCMCPSUB"          USING
                                        MCPAREA
                                        ORCMCPAREA
                                        MCPDATA-REC.
           .
       100-DBOPEN-EXT.
           EXIT.
      *
      *****************************************************************
      *    ＤＢ　クローズ処理
      *****************************************************************
       900-DBDISCONNECT-SEC            SECTION.
      *
           MOVE    "DBCOMMIT"          TO  MCP-FUNC.
           CALL    "ORCMCPSUB"          USING
                                        MCPAREA
                                        ORCMCPAREA
                                        MCPDATA-REC.
      *
           MOVE    "DBDISCONNECT"      TO  MCP-FUNC.
           CALL    "ORCMCPSUB"          USING
                                        MCPAREA
                                        ORCMCPAREA
                                        MCPDATA-REC.
           .
       900-DBCLOSE-EXT.
           EXIT.

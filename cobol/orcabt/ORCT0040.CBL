      *******************************************************************
      * Project code name "ORCA"
      * 日医標準レセプトソフト（JMA standard receipt software）
      * Copyright(C) 2002 JMA (Japan Medical Association)
      *
      * This program is part of "JMA standard receipt software".
      *
      *     This program is distributed in the hope that it will be useful
      * for further advancement in medical care, according to JMA Open
      * Source License, but WITHOUT ANY WARRANTY.
      *     Everyone is granted permission to use, copy, modify and
      * redistribute this program, but only under the conditions described
      * in the JMA Open Source License. You should have received a copy of
      * this license along with this program. If not, stop using this
      * program and contact JMA, 2-28-16 Honkomagome, Bunkyo-ku, Tokyo,
      * 113-8621, Japan.
      ********************************************************************
       IDENTIFICATION          DIVISION.
       PROGRAM-ID.             ORCT0040.
      *****************************************************************
      *  システム名        : ＯＲＣＡ
      *  サブシステム名    : 定期請求
      *  コンポーネント名  : 定期請求データ更新
      *  管理者            :
      *  作成日付    作業者        記述
      *  02/01/07    NACL−太田    新規作成
      *****************************************************************
      *  プログラム修正履歴
      * Maj/Min/Rev  修正者       日付      内容
      * 01.00.01     NACL−太田   03.09.02  定期請求印刷情報に設定する
      *                                     氏名をカナ氏名に修正
      * 01.00.02     NACL-太田    03.11.12  更新日付編集対応
      * 01.00.03     NACL-太田    04.03.30  訂正更新処理追加
      *****************************************************************
      *
       ENVIRONMENT             DIVISION.
       CONFIGURATION           SECTION.
       INPUT-OUTPUT            SECTION.
       FILE-CONTROL.
      *
      *
      *    定期請求収納ファイル(IN) <-- 新規作成
           SELECT  TEIKI06-FILE        ASSIGN  TEIKI06PARA
                   ORGANIZATION        LINE SEQUENTIAL
                                       FILE    STATUS  IS  STS-TEIKI06.
      *
      *    請求取消対象収納ファイル(IN)
           SELECT  TEIKI07-FILE        ASSIGN   TEIKI07PARA
                   ORGANIZATION        LINE SEQUENTIAL
                                       FILE    STATUS  IS  STS-TEIKI07.
      *
      *    単独更新用日別収納ファイル(IN)
           SELECT  TEIKI08-FILE        ASSIGN   TEIKI08PARA
                   ORGANIZATION        LINE SEQUENTIAL
                                       FILE    STATUS  IS  STS-TEIKI08.
      *
      *    定期請求収納ファイル(IN) <-- 訂正更新
           SELECT  TEIKI09-FILE        ASSIGN  TEIKI09PARA
                   ORGANIZATION        LINE SEQUENTIAL
                                       FILE    STATUS  IS  STS-TEIKI09.
      *
      *    エラーファイル
           SELECT  TEIKIERR-FILE       ASSIGN  TEIKIERR
                                       FILE    STATUS  IS  STS-TEIKIERR.
      *
       DATA                    DIVISION.
       FILE                    SECTION.
      *
      *    定期請求収納ファイル(OT) <-- 新規追加用
       FD  TEIKI06-FILE.
       01  TEIKI06-REC.
           COPY  "CPTEIKI01.INC"       REPLACING   //TEIKI01-//
                                       BY          //TEIKI06-//.
      *
      *    請求取消対象収納ファイル
       FD  TEIKI07-FILE.
       01  TEIKI07-REC.
           COPY    "CPSYUNOU.INC"      REPLACING   //SYU-//
                                       BY          //TEIKI07-//.
      *
      *    単独更新用日別収納ファイル
       FD  TEIKI08-FILE.
       01  TEIKI08-REC.
      *    更新区分（１：更新（追加）／２：削除）
         02    TEIKI08-UPD-FLG         PIC 9(01).
         02    TEIKI08-SYUDAY-DAT.
           COPY    "CPSYUDAY.INC"      REPLACING   //SDAY-//
                                       BY          //TEIKI08-SDAY-//.
      *
      *    定期請求収納ファイル(OT) <-- 訂正更新用
       FD  TEIKI09-FILE.
       01  TEIKI09-REC.
           COPY  "CPTEIKI01.INC"       REPLACING   //TEIKI01-//
                                       BY          //TEIKI09-//.
      *
      *    エラーファイル
       FD  TEIKIERR-FILE.
       01  TEIKIERR-REC                PIC X(200). 
      *
       WORKING-STORAGE             SECTION.
      *
      *
      *    定期請求収納ファイル名称領域 
           COPY    "CPCOMMONDAT5.INC"  REPLACING  //TEIKI01//
                                       BY         //TEIKI06//.
      *
      *    請求取消対象収納ファイル名称領域 
           COPY    "CPCOMMONDAT5.INC"  REPLACING  //TEIKI01//
                                       BY         //TEIKI07//.
      *
      *    単独更新用日別収納ファイル(IN)
           COPY    "CPCOMMONDAT5.INC"  REPLACING  //TEIKI01//
                                       BY         //TEIKI08//.
      *
      *    定期請求収納ファイル(OT) <-- 訂正更新用
           COPY    "CPCOMMONDAT5.INC"  REPLACING  //TEIKI01//
                                       BY         //TEIKI09//.
      *
      *    エラーファイル 名称領域 
           COPY    "CPCOMMONDAT5.INC"  REPLACING  //TEIKI01PARA//
                                       BY         //TEIKIERR//.
           03  FILLER                  PIC X(04)  VALUE   ".dat".
      *
      *    フラグ領域
       01  STS-AREA.
           03  STS-TEIKIERR            PIC X(02).
           03  STS-TEIKI06             PIC X(02).
           03  STS-TEIKI07             PIC X(02).
           03  STS-TEIKI08             PIC X(02).
           03  STS-TEIKI09             PIC X(02).
      *
       01  FLG-AREA.
           03  FLG-END                 PIC 9(01).
           03  FLG-ERR                 PIC 9(01).
           03  FLG-TEIKI06             PIC 9(01).
           03  FLG-TEIKI07             PIC 9(01).
           03  FLG-TEIKI08             PIC 9(01).
           03  FLG-TEIKI09             PIC 9(01).
           03  FLG-SYUNOU              PIC 9(01).
           03  FLG-SYUMEI              PIC 9(01).
           03  FLG-SYUDAY              PIC 9(01).
           03  FLG-SYSKANRI            PIC 9(01).
           03  FLG-SYS5012             PIC 9(01).
           03  FLG-PTINF               PIC 9(01).
           03  FLG-PTNYUINRRK          PIC 9(01).
           03  FLG-TEIKIPRT            PIC 9(01).
           03  FLG-PTTEIKIRRK          PIC 9(01).
           03  FLG-TEIKIKANRI          PIC 9(01).
           03  FLG-TEIKIRRK            PIC 9(01).
           03  FLG-HIT                 PIC 9(01).
           03  FLG-DENPNUM-ERR         PIC 9(01).
           03  FLG-PTNUM               PIC 9(01).
      *
      *    カウント領域
       01  CNT-AREA.
           03  CNT-TEIKI06             PIC 9(06).
           03  CNT-TEIKI08             PIC 9(06).
           03  CNT-TEIKI09             PIC 9(06).
      *
       01  INDEX-AREA.
           03  IDX1                    PIC 9(04). 
           03  IDX2                    PIC 9(04). 
           03  IDX3                    PIC 9(04). 
      *
      *    一時領域
       01  WRK-AREA.
           03  WRK-DBPATH              PIC X(100).
           03  WRK-TEIKIERR            PIC X(200).
           03  WRK-SKYMONEY            PIC S9(07).
           03  WRK-NYUKIN-TOTAL        PIC S9(07).
           03  WRK-DENPNUM             PIC 9(07).
           03  WRK-DENPNUM-X           PIC X(08).
           03  WRK-STR                 PIC X(200).
           03  WRK-LEN                 PIC 9(05).
           03  WRK-SHO                 PIC 9(05).
           03  WRK-AMARI               PIC 9(05).
           03  WRK-CREYMD              PIC X(08).
           03  WRK-TEIKIRRK-SKYTEN     PIC S9(09).
           03  WRK-TEIKIRRK-SKYMONEY   PIC S9(09).
           03  WRK-DENPEDANUM          PIC 9(02).
           03  WRK-NYUKINRENNUM        PIC 9(02).
           03  WRK-KUGIRI              PIC X(02).
           03  WRK-PTID                PIC 9(10).
      *
       01  ERR-EDIT-AREA.
           03  ERR-PTID                PIC 9(10).
           03  ERR-PTNUM               PIC X(20).
           03  ERR-ERRMSG              PIC X(80).
           03  ERR-HEN-ERRMSG          PIC X(80).
      *
       01  ERR-MES-AREA.
           03  ERR-MES01                   PIC X(100)    VALUE
               "定期請求管理情報が取得できませんでした".
           03  ERR-MES02                   PIC X(100)    VALUE
               "定期請求管理情報の削除に失敗しました".
           03  ERR-MES03                   PIC X(100)    VALUE
               "定期請求印刷情報情報の削除に失敗しました".
           03  ERR-MES04                   PIC X(100)    VALUE
               "仮収納データ削除処理に失敗しました".
           03  ERR-MES05                   PIC X(100)    VALUE
               "患者定期請求履歴情報の削除に失敗しました".
           03  ERR-MES06                   PIC X(100)    VALUE
               "患者定期請求履歴情報の削除に失敗しました".
           03  ERR-MES07                   PIC X(100)    VALUE
               "収納明細情報の更新に失敗しました".
           03  ERR-MES08                   PIC X(100)    VALUE
               "収納情報の更新に失敗しました".
           03  ERR-MES09                   PIC X(100)    VALUE
               "伝票番号の更新に失敗しました".
           03  ERR-MES10                   PIC X(100)    VALUE
               "日別収納マスタの更新に失敗しました".
           03  ERR-MES11                   PIC X(100)    VALUE
               "日別収納マスタの削除に失敗しました".
           03  ERR-MES12                   PIC X(100)    VALUE
               "患者定期請求履歴情報の更新に失敗しました".
           03  ERR-MES13                   PIC X(100)    VALUE
               "患者情報の取得に失敗しました".
           03  ERR-MES14                   PIC X(100)    VALUE
               "最新入院履歴の取得に失敗しました".
           03  ERR-MES15                   PIC X(100)    VALUE
               "患者番号の取得に失敗しました".
           03  ERR-MES16                   PIC X(100)    VALUE
               "定期請求印刷情報の作成に失敗しました".
           03  ERR-MES17                   PIC X(100)    VALUE
               "定期請求管理情報の作成に失敗しました".
           03  ERR-MES18                   PIC X(100)    VALUE
               "定期請求履歴情報の更新に失敗しました".
           03  ERR-MES19                   PIC X(100)    VALUE
               "定期請求履歴情報の作成に失敗しました".
           03  ERR-MES20                   PIC X(100)    VALUE
               "処理対象のデータがありませんでした".
      *
      *    定期請求管理集計用ワーク
       01  SUMTEIKIKANRI-REC.
         02    SUMTEIKIKANRI-MAX       PIC 9(05).
         02    SUMTEIKIKANRI-OCC       OCCURS 50.
           COPY  "CPTEIKIKANRI.INC"    REPLACING  //TEIKIKANRI-//
                                       BY         //SUMTEIKIKANRI-//.
      *
      *    定期請求履歴集計用ワーク
       01  SUMTEIKIRRK-REC.
         02    SUMTEIKIRRK-MAX         PIC 9(05).
         02    SUMTEIKIRRK-OCC         OCCURS 500.
           COPY  "CPTEIKIRRK.INC"      REPLACING  //TEIKIRRK-//
                                       BY         //SUMTEIKIRRK-//.
      *
      *    定期請求履歴情報ワーク
       01  WKTEIKIRRK-REC.
           COPY  "CPTEIKIRRK.INC"      REPLACING  //TEIKIRRK-//
                                       BY         //WKTEIKIRRK-//.
      *
      *    日別収納退避ワーク
       01  WKSYUDAY-REC.
           COPY  "CPSYUDAY.INC"        REPLACING  //SDAY-//
                                       BY         //WKSDAY-//.
      *
           COPY    "MCPAREA".
      *
           COPY    "COMMON-SPA".
      *
      *    定期請求一括処理パラメータ
           COPY    "CPTEIKI1PARA.INC".
      *
      *****************************************************************
      *    ファイルレイアウト
      *****************************************************************
      *
      *    ジョブ管理マスタ
       01  JOBKANRI-REC.
           COPY    "CPJOBKANRI.INC".
      *
      *    患者マスタ
       01  PTINF-REC.
           COPY    "CPPTINF.INC".
      *
      *    患者マスタ
       01  PTNUM-REC.
           COPY    "CPPTNUM.INC".
      *
      *    患者入院履歴
       01  PTNYUINRRK-REC.
           COPY    "CPPTNYUINRRK.INC".
      *
      *    収納マスター
       01  SYUNOU-REC.
           COPY    "CPSYUNOU.INC".
      *
      *    日別収納
       01  SYUDAY-REC.
           COPY    "CPSYUDAY.INC".
      *
      *    収納明細
       01  SYUMEI-REC.
           COPY    "CPSYUMEI.INC".
      *
      *    定期請求管理
       01  TEIKIKANRI-REC.
           COPY    "CPTEIKIKANRI.INC".
      *
      *    定期請求印刷
       01  TEIKIPRT-REC.
           COPY    "CPTEIKIPRT.INC".
      *
      *    定期請求履歴
       01  TEIKIRRK-REC.
           COPY    "CPTEIKIRRK.INC".
      *
      *    患者定期請求履歴
       01  PTTEIKIRRK-REC.
           COPY    "CPPTTEIKIRRK.INC".
      *
      *
      *    システム管理マスタ
           COPY    "CPSYSKANRI.INC".
      *    伝票番号
           COPY    "CPSK0042.INC".
      *    定期請求管理情報
           COPY  "CPSK5010.INC".
      *    定期請求作成指示（個別指示）コード
           COPY  "CPSK5012.INC".
      *
      *****************************************************************
      *    サブプロ用領域
      *****************************************************************
      *
      *    ジョブ管理ＤＢ制御サブ
           COPY    "CPORCSJOBKANRI.INC".
      *
      *    ＤＢ検索
           COPY    "MCPDATA.INC".
           COPY    "CPORCMCP.INC".
      *
      *    マシン日付取得サブ
           COPY    "CPORCSMCNDATE.INC".
      *
      ****************************************************************
       LINKAGE                 SECTION.
      *
           COPY    "CPCOMMAND-PARAM.INC".
      *
      ****************************************************************
       PROCEDURE           DIVISION
               USING
           COMMAND-PARAM.
      *
      *****************************************************************
      *    主処理
      *****************************************************************
       000-PROC-SEC                SECTION.
      *
           PERFORM 100-INIT-SEC
      *
           PERFORM 200-MAIN-SEC
      *
           PERFORM 300-TERM-SEC
      *
           PERFORM 990-PROGRAM-FINISH-SEC
      *
           .
       000-PROC-EXT.
           EXIT.
           .
      *****************************************************************
      *    初期　処理
      *****************************************************************
       100-INIT-SEC                SECTION.
      *
           INITIALIZE                  INDEX-AREA
                                       CNT-AREA
                                       WRK-AREA
                                       SPA-AREA
                                       SUMTEIKIKANRI-REC
                                       SUMTEIKIRRK-REC
      *
           UNSTRING   COMMAND-PARAM    DELIMITED  BY  ","
                                       INTO    TEIKI1P-ARG1
                                               TEIKI1P-SYOKBN
                                               TEIKI1P-KIKAN-OCC (1)
                                               TEIKI1P-KIKAN-OCC (2)
                                               TEIKI1P-KIKAN-OCC (3)
                                               TEIKI1P-JOBID
                                               TEIKI1P-SHELLID
                                               TEIKI1P-HOSPID
                                               TEIKI1P-ERRFILE
      *
           PERFORM 100-DBOPEN-SEC
      *
      *    ステップ管理開始処理
           MOVE    "STS"               TO  SJOBKANRI-MODE
           INITIALIZE                      JOBKANRI-REC
           MOVE    TEIKI1P-JOBID       TO  JOB-JOBID
           MOVE    TEIKI1P-SHELLID     TO  JOB-SHELLID
           MOVE    "ORCT0040"          TO  JOB-PGID
           MOVE    "定期請求データ更新"
                                       TO  JOB-SHELLMSG
           CALL    "ORCSJOB"           USING
                                       ORCSJOBKANRIAREA
                                       JOBKANRI-REC
      *
           MOVE    ZERO                TO  FLG-END
                                           FLG-ERR
      *
           MOVE    "TEIKI006"          TO  TEIKI06PARA-FILE-ID
           MOVE    TEIKI1P-TERMID      TO  TEIKI06PARA-TERMID
      *
           MOVE    "TEIKI007"          TO  TEIKI07PARA-FILE-ID
           MOVE    TEIKI1P-TERMID      TO  TEIKI07PARA-TERMID
      *
           MOVE    "TEIKI008"          TO  TEIKI08PARA-FILE-ID
           MOVE    TEIKI1P-TERMID      TO  TEIKI08PARA-TERMID
      *
           MOVE    "TEIKI009"          TO  TEIKI09PARA-FILE-ID
           MOVE    TEIKI1P-TERMID      TO  TEIKI09PARA-TERMID
      *
           MOVE    "TEIKIERR"          TO  TEIKIERR-FILE-ID
           MOVE    TEIKI1P-TERMID      TO  TEIKIERR-TERMID
      *
           OPEN    INPUT               TEIKI06-FILE
           OPEN    INPUT               TEIKI07-FILE
           OPEN    INPUT               TEIKI08-FILE
           OPEN    INPUT               TEIKI09-FILE
      *
      *    定期請求管理情報
           INITIALIZE                  SYS-5010-REC
           MOVE    "5010"              TO  SYS-5010-KANRICD
           MOVE    "*"                 TO  SYS-5010-KBNCD
           MOVE    TEIKI1P-SYSYMD      TO  ORC-DBYMD
           MOVE    SYS-5010-REC        TO  MCPDATA-REC
           PERFORM 900-SYSKANRI-KEY-SEL-SEC
           IF    ( FLG-SYSKANRI        =   ZERO )
               MOVE    MCPDATA-REC     TO  SYS-5010-REC
           ELSE
               MOVE    ERR-MES01       TO  WRK-TEIKIERR
               PERFORM 500-ERR-HENSYU-SEC
           END-IF
      *
           .
       100-INIT-EXT.
           EXIT.
      * 
      *****************************************************************
      *    主処理
      *****************************************************************
       200-MAIN-SEC                SECTION.
      *
      *    定期請求管理情報削除処理
           PERFORM 900-TEIKIKANRI-DEL-SEC
      *
      *    定期請求印刷情報削除処理
           PERFORM 900-TEIKIPRT-DEL-SEC
      *
      *    ダミー収納削除処理
           PERFORM 2001-DUMMY-SYUNOU-DEL-SEC
      *
      *    前回定期請求情報更新処理
           PERFORM 2002-MAE-SEIKYU-KOUSIN-SEC
      *
      *    定期請求情報更新処理（新規追加）
           PERFORM 2003-SEIKYU-KOUSIN-SEC
      *
      *    定期請求情報更新処理（訂正更新）
           PERFORM 2007-SEIKYU-KOUSIN-SEC
      *
      *    単独更新用日別収納更新処理
           PERFORM 2004-TEIKI08-KOUSIN-SEC
      *
      *    定期請求管理情報更新処理
           PERFORM 2005-TEIKIKANRI-KOUSIN-SEC
      *
      *    定期請求履歴情報更新処理
           PERFORM 2006-TEIKIRRK-KOUSIN-SEC
      *
           MOVE    1                   TO  FLG-END
      *
           .
       200-MAIN-EXT.
           EXIT.
      *
      *****************************************************************
      *    ダミー収納削除処理
      *****************************************************************
       2001-DUMMY-SYUNOU-DEL-SEC       SECTION.
      *
           INITIALIZE                  SYUNOU-REC
           MOVE    TEIKI1P-HOSPID      TO  SYU-HOSPID
           MOVE    "1"                 TO  SYU-NYUGAIKBN
           MOVE    "3"                 TO  SYU-CREATEKBN
           MOVE    SYUNOU-REC          TO  MCPDATA-REC
           MOVE    "SYUNOU-DEL2"       TO  ORC-DBPATH
           PERFORM 910-DBDELETE-SEC
           IF  (   MCP-RC          NOT =   ZERO )
               MOVE    ERR-MES04       TO  WRK-TEIKIERR
               PERFORM 500-ERR-HENSYU-SEC
           END-IF
      *
           .
       2001-DUMMY-SYUNOU-DEL-EXT.
           EXIT.
      *****************************************************************
      *    前回定期請求情報更新処理
      *****************************************************************
       2002-MAE-SEIKYU-KOUSIN-SEC      SECTION.
      *
           MOVE    ZERO                TO  FLG-TEIKI07
      *
      *    請求取消対象収納ファイル読込
           PERFORM 910-TEIKI07-READ-SEC
      *
           PERFORM UNTIL ( FLG-TEIKI07 NOT =   ZERO )
      *
      *        患者定期請求履歴データ更新処理
               PERFORM 20021-PTTEIKIRRK-KOUSIN-SEC
      *
      *        定期請求管理情報集計（マイナス分）
               PERFORM 20021-TEIKIRRK-SUM-SEC
      *
      *        収納データ更新処理
               PERFORM 20021-SYUNOU-KOUSIN-SEC
      *
      *        請求取消対象収納ファイル読込
               PERFORM 910-TEIKI07-READ-SEC
      *
           END-PERFORM
      *
           .
       2002-MAE-SEIKYU-KOUSIN-EXT.
           EXIT.
      *****************************************************************
      *    収納データ更新処理
      *****************************************************************
       20021-SYUNOU-KOUSIN-SEC         SECTION.
      *
           MOVE    TEIKI07-REC         TO  SYUNOU-REC
      *
      *    収納明細作成処理
           PERFORM 20021-SYUMEI-SAKUSEI-SEC
      *
      *    請求取消として収納マスターを更新
           MOVE    ZERO                TO  SYU-SKYMONEY
      *                                    SYU-NYUKIN-TOTAL
           MOVE    "3"                 TO  SYU-DENPJTIKBN
      *
           PERFORM 800-MCNDATE-SEC
           MOVE    SMCNDATE-YMD        TO  SYU-UPYMD
           MOVE    SMCNDATE-HMS        TO  SYU-UPHMS
      *
           MOVE    SYUNOU-REC          TO  MCPDATA-REC
           MOVE    "SYUNOU-KEY"        TO  ORC-DBPATH
           PERFORM 910-DBUPDATE-SEC
           IF    ( MCP-RC          NOT =   ZERO )
               MOVE    SYU-PTID        TO  ERR-PTID
               MOVE    ERR-MES08       TO  ERR-ERRMSG
               PERFORM 800-ERRMSG-EDIT-SEC
           END-IF
      *
           .
       20021-SYUNOU-KOUSIN-EXT.
           EXIT. 
      *****************************************************************
      *    収納明細作成処理
      *****************************************************************
       20021-SYUMEI-SAKUSEI-SEC        SECTION.
      *
           COMPUTE WRK-SKYMONEY    =   SYU-SKYMONEY    *   -1
      *
      *    伝票番号枝番を求める
           INITIALIZE                      SYUMEI-REC
           MOVE    SYU-HOSPID          TO  SMEI-HOSPID
           MOVE    SYU-NYUGAIKBN       TO  SMEI-NYUGAIKBN
           MOVE    SYU-PTID            TO  SMEI-PTID
           MOVE    SYU-DENPNUM         TO  SMEI-DENPNUM
           MOVE    SYUMEI-REC          TO  MCPDATA-REC
           MOVE    "SYUMEI-KEY7"       TO  ORC-DBPATH
           PERFORM 910-DBSELECT-SEC
           IF    ( MCP-RC              =   ZERO )
                MOVE    MCPDATA-REC    TO  SYUMEI-REC
           ELSE
               INITIALIZE                  SYUMEI-REC
           END-IF
      *
           MOVE    "SYUMEI-KEY7"       TO  ORC-DBPATH
           PERFORM 910-DBCLOSE-SEC
      *
           COMPUTE WRK-DENPEDANUM      =   SMEI-DENPEDANUM +   1
      *
           MOVE    SPACE               TO  SYUMEI-REC
           INITIALIZE                      SYUMEI-REC
           MOVE    SYU-HOSPID          TO  SMEI-HOSPID
           MOVE    SYU-NYUGAIKBN       TO  SMEI-NYUGAIKBN
           MOVE    SYU-PTID            TO  SMEI-PTID
      *    伝票番号
           MOVE    SYU-DENPNUM         TO  SMEI-DENPNUM
      *    伝票番号枝番
           MOVE    WRK-DENPEDANUM      TO  SMEI-DENPEDANUM
      *    入金連番
           MOVE    ZERO                TO  SMEI-NYUKINRENNUM
      *    診療科
           MOVE    SYU-SRYKA           TO  SMEI-SRYKA
      *    伝票状態区分
           MOVE    SPACE               TO  SMEI-MEISAIJOUTAIKBN
      *    請求発行日（発行日の西暦）
           MOVE    SPACE               TO  SMEI-SKYPRINTYMD
      *    請求再発行日
           MOVE    SPACE               TO  SMEI-SKYREPRINTYMD
      *    領収書発行日
           MOVE    SPACE               TO  SMEI-RYOSYUPRINTYMD
      *    領収書再発行日
           MOVE    SPACE               TO  SMEI-RYOSYUREPRINTYMD
      *    請求金額
           MOVE    WRK-SKYMONEY        TO  SMEI-SKYMONEY
      *
      *    入返金区分
           MOVE    SPACE               TO  SMEI-NYUHEN-KBN
      *    入返金額
           MOVE    ZERO                TO  SMEI-NYUHEN-MONEY
      *    入返金日
           MOVE    TEIKI1P-SYSYMD      TO  SMEI-NYUHEN-YMD
      *    入金方法
           MOVE    "00"                TO  SMEI-NYUKIN-HOHO
      *    状態区分
           MOVE    "5"                 TO  SMEI-JOUTAIKBN
      *
      *    収納明細存在チェック（すでに存在する場合はエラー）
           PERFORM 400-SYUMEI-CHK-SEC
           IF    ( FLG-SYUMEI          =   ZERO )
               MOVE    SMEI-PTID       TO  ERR-PTID
               MOVE    ERR-MES07       TO  ERR-ERRMSG
               PERFORM 800-ERRMSG-EDIT-SEC
           END-IF
      *
      *    収納明細追加
           PERFORM 800-MCNDATE-SEC
           MOVE    SMCNDATE-YMD        TO  SMEI-CREYMD
                                           SMEI-UPYMD
           MOVE    SMCNDATE-HMS        TO  SMEI-UPHMS
      *
           MOVE    SYUMEI-REC          TO  MCPDATA-REC
           MOVE    "SYUMEI-KEY"        TO  ORC-DBPATH
           PERFORM 910-DBINSERT-SEC
           IF    ( MCP-RC          NOT =   ZERO )
               MOVE    SMEI-PTID       TO  ERR-PTID
               MOVE    ERR-MES07       TO  ERR-ERRMSG
               PERFORM 800-ERRMSG-EDIT-SEC
           END-IF
      *
           .
       20021-SYUMEI-SAKUSEI-EXT.
           EXIT. 
      *****************************************************************
      *    患者定期請求履歴データ更新処理
      *****************************************************************
       20021-PTTEIKIRRK-KOUSIN-SEC     SECTION.
      *
      *    伝票番号より削除対象のデータを取得
      *    （存在しない場合はエラー）
           INITIALIZE                      PTTEIKIRRK-REC
           MOVE    TEIKI07-HOSPID      TO  PTTEIKIRRK-HOSPID
           MOVE    TEIKI07-PTID        TO  PTTEIKIRRK-PTID
           MOVE    TEIKI07-DENPNUM     TO  PTTEIKIRRK-DENPNUM
      *
           PERFORM 900-PTTEIKIRRK-KEY6-SEL-SEC
           IF    ( FLG-PTTEIKIRRK  NOT =   ZERO )
               MOVE    PTTEIKIRRK-PTID TO  ERR-PTID
               MOVE    ERR-MES05       TO  ERR-ERRMSG
               PERFORM 800-ERRMSG-EDIT-SEC
           END-IF
      *
      *    患者定期請求履歴を削除
           MOVE    PTTEIKIRRK-REC      TO  MCPDATA-REC
           MOVE    "PTTEIKIRRK-KEY"    TO  ORC-DBPATH
           PERFORM 910-DBDELETE-SEC
           IF  (   MCP-RC          NOT =   ZERO )
               MOVE    PTTEIKIRRK-PTID TO  ERR-PTID
               MOVE    ERR-MES06       TO  ERR-ERRMSG
               PERFORM 800-ERRMSG-EDIT-SEC
           END-IF
      *
           .
       20021-PTTEIKIRRK-KOUSIN-EXT.
           EXIT. 
      *
      *****************************************************************
      *    定期請求管理情報集計（マイナス分）
      *****************************************************************
       20021-TEIKIRRK-SUM-SEC          SECTION.
      *
      *    定期請求管理情報集計（マイナス分）
           PERFORM VARYING IDX1    FROM    1   BY  1
                   UNTIL ( IDX1    >   500 )
               EVALUATE SUMTEIKIRRK-SRYYM (IDX1)
                                   ALSO    SUMTEIKIRRK-SAKKBN (IDX1)
               WHEN    SPACE       ALSO    ANY
                   MOVE    PTTEIKIRRK-SRYYM
                                   TO  SUMTEIKIRRK-SRYYM   (IDX1)
                   MOVE    PTTEIKIRRK-SAKKBN
                                   TO  SUMTEIKIRRK-SAKKBN  (IDX1)
                   MOVE    PTTEIKIRRK-SKYSTYMD
                                   TO  SUMTEIKIRRK-SKYSTYMD(IDX1)
                   MOVE    PTTEIKIRRK-SKYEDYMD
                                   TO  SUMTEIKIRRK-SKYEDYMD(IDX1)
                   COMPUTE SUMTEIKIRRK-SKYTEN (IDX1)
                       =   SYU-TOTAL-HKNTEN      *   -1
                   COMPUTE SUMTEIKIRRK-SKYMONEY (IDX1)
                       =   SYU-SKYMONEY          *   -1
                   COMPUTE SUMTEIKIRRK-MAX
                       =   SUMTEIKIRRK-MAX     +   1
                   MOVE    500         TO  IDX1
               WHEN    PTTEIKIRRK-SRYYM
                                   ALSO    PTTEIKIRRK-SAKKBN
                   COMPUTE SUMTEIKIRRK-SKYTEN (IDX1)
                       =   SUMTEIKIRRK-SKYTEN (IDX1)
                       + ( SYU-TOTAL-HKNTEN      *   -1 )
                   COMPUTE SUMTEIKIRRK-SKYMONEY (IDX1)
                       =   SUMTEIKIRRK-SKYMONEY (IDX1)
                       + ( SYU-SKYMONEY          *   -1 )
                   MOVE    500         TO  IDX1
               END-EVALUATE
           END-PERFORM
      *
           .
       20021-TEIKIRRK-SUM-EXT.
           EXIT. 
      *****************************************************************
      *    定期請求情報更新処理（新規追加）
      *****************************************************************
       2003-SEIKYU-KOUSIN-SEC          SECTION.
      *
           MOVE    ZERO                TO  FLG-TEIKI06
      *
      *    定期請求収納ファイル読込
           PERFORM 910-TEIKI06-READ-SEC
      *
           PERFORM UNTIL ( FLG-TEIKI06 NOT =   ZERO )
      *
      *        伝票番号取得処理
               PERFORM 20031-DENPNUM-GET-SEC
      *
               MOVE    TEIKI06-SYU-DAT TO  SYUNOU-REC
      *
               MOVE    WRK-DENPNUM     TO  SYU-DENPNUM
      *
      *        更新前チェック（既に重複するデータがある場合はエラー）
               PERFORM 400-SYUNOU-CHK-SEC
               IF    ( FLG-SYUNOU      =   ZERO )
                   MOVE    SYU-PTID    TO  ERR-PTID
                   MOVE    ERR-MES08   TO  ERR-ERRMSG
                   PERFORM 800-ERRMSG-EDIT-SEC
               END-IF
      *
               PERFORM 800-MCNDATE-SEC
               MOVE    SMCNDATE-YMD    TO  SYU-CREYMD
                                           SYU-UPYMD
               MOVE    SMCNDATE-HMS    TO  SYU-UPHMS
      *
               MOVE    SYUNOU-REC      TO  MCPDATA-REC
               MOVE    "SYUNOU-KEY"    TO  ORC-DBPATH
               PERFORM 910-DBINSERT-SEC
               IF    ( MCP-RC      NOT =   ZERO )
                   MOVE    SYU-PTID    TO  ERR-PTID
                   MOVE    ERR-MES08   TO  ERR-ERRMSG
                   PERFORM 800-ERRMSG-EDIT-SEC
               END-IF
      *
      *        収納明細作成処理
               PERFORM 20031-SYUMEI-SAKUSEI-SEC
      *
      *        日別収納作成処理
               IF    ( TEIKI06-SYUDAY-EXIST-FLG    =   1 )
                   PERFORM 20031-SYUDAY-SAKUSEI-SEC
               END-IF
      *
      *        患者定期請求履歴更新処理
               PERFORM 20031-PTTEIKIRRK-KOUSIN-SEC
      *
      *        定期請求管理情報集計（プラス分）
               MOVE    SYU-TOTAL-HKNTEN
                                       TO  WRK-TEIKIRRK-SKYTEN
               MOVE    SYU-SKYMONEY
                                       TO  WRK-TEIKIRRK-SKYMONEY
               PERFORM 20031-TEIKIRRK-SUM-SEC
      *
      *        最新病棟番号別集計処理
               PERFORM 20031-NOW-BTUNUM-SUM-SEC
      *
      *        定期請求印刷情報作成処理
               PERFORM 20031-TEIKIPRT-SAKUSEI-SEC
      *
      *        定期請求収納ファイル読込
               PERFORM 910-TEIKI06-READ-SEC
      *
           END-PERFORM
      *
           .
       2003-SEIKYU-KOUSIN-EXT.
           EXIT. 
      *****************************************************************
      *    伝票番号取得処理
      *****************************************************************
       20031-DENPNUM-GET-SEC        SECTION.
      *
           MOVE    ZERO                TO  FLG-SYSKANRI
                                           WRK-DENPNUM
                                           FLG-DENPNUM-ERR
      *
           INITIALIZE                  SYS-0042-REC
           MOVE    "0042"              TO  SYS-0042-KANRICD
           MOVE    "*"                 TO  SYS-0042-KBNCD
           MOVE    ZERO                TO  SYS-0042-STYUKYMD
           MOVE    ALL "9"             TO  SYS-0042-EDYUKYMD
      *
           MOVE    TEIKI1P-SYSYMD      TO  ORC-DBYMD
           MOVE    SYS-0042-REC        TO  MCPDATA-REC
           PERFORM 900-SYSKANRI-KEY-SEL-SEC
           IF    ( FLG-SYSKANRI        =   ZERO )
               MOVE    MCPDATA-REC     TO  SYS-0042-REC
               MOVE    SYS-0042-DENPNUMMAX-NYU
                                       TO  WRK-DENPNUM-X
               IF    ( WRK-DENPNUM-X   =   SPACE )
                   MOVE    ZERO        TO  SYS-0042-DENPNUMMAX-NYU
               ELSE
                   MOVE    WRK-DENPNUM-X
                                       TO  SYS-0042-DENPNUMMAX-NYU
               END-IF
      *
               COMPUTE SYS-0042-DENPNUMMAX-NYU
                   =   SYS-0042-DENPNUMMAX-NYU +   1
      *
               MOVE    SYS-0042-DENPNUMMAX-NYU
                                       TO  WRK-DENPNUM
      *
               PERFORM 800-MCNDATE-SEC
               MOVE    SMCNDATE-YMD    TO  SYS-0042-UPYMD
               MOVE    SMCNDATE-HMS    TO  SYS-0042-UPHMS
      *
               MOVE    SYS-0042-REC    TO  MCPDATA-REC
               MOVE    "SYSKANRI-KEY"  TO  ORC-DBPATH
               PERFORM 910-DBUPDATE-SEC
               IF    ( MCP-RC          NOT =   ZERO )
                   MOVE    1           TO  FLG-DENPNUM-ERR
               END-IF
           ELSE
                   MOVE    1           TO  FLG-DENPNUM-ERR
           END-IF
      *
           IF    ( FLG-DENPNUM-ERR NOT =   ZERO )
               MOVE    SPACE           TO  WRK-TEIKIERR
               STRING  ERR-MES09      DELIMITED    BY  SIZE
                       "[伝票番号:"   DELIMITED    BY  SIZE
                       SYS-0042-DENPNUMMAX-NYU
                                      DELIMITED    BY  SIZE
                       "]"            DELIMITED    BY  SIZE
               INTO    WRK-TEIKIERR
               END-STRING
               PERFORM 500-ERR-HENSYU-SEC
           END-IF
      *
           .
       20031-DENPNUM-GET-EXT.
           EXIT.
      *****************************************************************
      *    患者定期請求履歴データ更新処理
      *****************************************************************
       20031-PTTEIKIRRK-KOUSIN-SEC     SECTION.
      *
           INITIALIZE                      PTTEIKIRRK-REC
           MOVE    SYU-HOSPID          TO  PTTEIKIRRK-HOSPID
           MOVE    SYU-PTID            TO  PTTEIKIRRK-PTID
           MOVE    SYU-SRYYMD (1:6)    TO  PTTEIKIRRK-SRYYM
           MOVE    TEIKI06-SAKKBN      TO  PTTEIKIRRK-SAKKBN
           MOVE    SYU-HKNCOMBINUM     TO  PTTEIKIRRK-HKNCOMBINUM
           MOVE    TEIKI06-SKYSTYMD    TO  PTTEIKIRRK-SKYSTYMD
           MOVE    TEIKI06-SKYEDYMD    TO  PTTEIKIRRK-SKYEDYMD
           MOVE    SYU-TOTAL-HKNTEN    TO  PTTEIKIRRK-SKYTEN
           MOVE    SYU-SKYMONEY-TAX-SAI
                                       TO  PTTEIKIRRK-SKYMONEY-TAX-SAI
           MOVE    SYU-SKYMONEY        TO  PTTEIKIRRK-SKYMONEY
           MOVE    SYU-NYUIN-RRKNUM    TO  PTTEIKIRRK-RRKNUM
           MOVE    SYU-DENPNUM         TO  PTTEIKIRRK-DENPNUM
           MOVE    1                   TO  PTTEIKIRRK-DENPEDANUM
           MOVE    ZERO                TO  PTTEIKIRRK-SKYUPDFLG
      *
           PERFORM 800-MCNDATE-SEC
           MOVE    SMCNDATE-YMD        TO  PTTEIKIRRK-CREYMD
                                           PTTEIKIRRK-UPYMD
           MOVE    SMCNDATE-HMS        TO  PTTEIKIRRK-UPHMS
      *
      *    更新前チェック（既に重複するデータがある場合はエラー）
           PERFORM 400-PTTEIKIRRK-CHK-SEC
           IF    ( FLG-PTTEIKIRRK      =   ZERO )
      *
               MOVE    PTTEIKIRRK-PTID TO  ERR-PTID
               MOVE    ERR-MES12       TO  ERR-ERRMSG
               PERFORM 800-ERRMSG-EDIT-SEC
           END-IF
      *
           MOVE    PTTEIKIRRK-REC      TO  MCPDATA-REC
           MOVE    "PTTEIKIRRK-KEY"    TO  ORC-DBPATH
           PERFORM 910-DBINSERT-SEC
           IF  (   MCP-RC          NOT =   ZERO )
               MOVE    PTTEIKIRRK-PTID TO  ERR-PTID
               MOVE    ERR-MES12       TO  ERR-ERRMSG
               PERFORM 800-ERRMSG-EDIT-SEC
           END-IF
      *
           .
       20031-PTTEIKIRRK-KOUSIN-EXT.
           EXIT. 
      *****************************************************************
      *    日別収納作成処理
      *****************************************************************
       20031-SYUDAY-SAKUSEI-SEC        SECTION.
      *
           INITIALIZE                      WKSYUDAY-REC
      *
           MOVE    TEIKI06-SYUDAY-DAT  TO  WKSYUDAY-REC
           MOVE    SYU-DENPNUM         TO  WKSDAY-DENPNUM
      *
      *    日別収納更新処理
           PERFORM 900-SYUDAY-UPD-SEC
      *
           .
       20031-SYUDAY-SAKUSEI-EXT.
           EXIT.
      *****************************************************************
      *    収納明細作成処理
      *****************************************************************
       20031-SYUMEI-SAKUSEI-SEC        SECTION.
      *
           MOVE    SPACE               TO  SYUMEI-REC
           INITIALIZE                      SYUMEI-REC
           MOVE    SYU-HOSPID          TO  SMEI-HOSPID
           MOVE    SYU-NYUGAIKBN       TO  SMEI-NYUGAIKBN
           MOVE    SYU-PTID            TO  SMEI-PTID
      *
      *    伝票番号
           MOVE    SYU-DENPNUM         TO  SMEI-DENPNUM
      *    伝票番号枝番
           MOVE    01                  TO  SMEI-DENPEDANUM
      *    診療科
           MOVE    SYU-SRYKA           TO  SMEI-SRYKA
      *    伝票状態区分
           MOVE    SPACE               TO  SMEI-MEISAIJOUTAIKBN
      *    請求発行日（発行日の西暦）
           MOVE    TEIKI1P-SYSYMD      TO  SMEI-SKYPRINTYMD
      *    請求再発行日
           MOVE    SPACE               TO  SMEI-SKYREPRINTYMD
      *    領収書発行日
           MOVE    SMEI-SKYPRINTYMD    TO  SMEI-RYOSYUPRINTYMD
      *    領収書再発行日
           MOVE    SPACE               TO  SMEI-RYOSYUREPRINTYMD
      *    請求金額
           MOVE    SYU-SKYMONEY        TO  SMEI-SKYMONEY
      *    入返金日
           MOVE    SMEI-SKYPRINTYMD    TO  SMEI-NYUHEN-YMD
      *    入金連番
           MOVE    01                  TO  SMEI-NYUKINRENNUM
      *
           MOVE    "00"                TO  SMEI-NYUKIN-HOHO
           IF    ( SYS-5010-NYUKINKBN  =   "2" )
               IF    ( SYU-NYUKIN-TOTAL    =   ZERO )
                   CONTINUE
               ELSE
      *            入返金区分
                   MOVE    "1"         TO  SMEI-NYUHEN-KBN
      *            入返金額
                   MOVE    SYU-NYUKIN-TOTAL
                                       TO  SMEI-NYUHEN-MONEY
      *            入金方法
                   MOVE    "01"        TO  SMEI-NYUKIN-HOHO
               END-IF
           END-IF
      *
      *    状態区分
           IF    ( SYU-SKYMONEY        =   ZERO )
               MOVE    SPACE               TO  SMEI-JOUTAIKBN
           ELSE
               IF   (  SYU-SKYMONEY    >   SYU-NYUKIN-TOTAL )
                   MOVE    "9"         TO  SMEI-JOUTAIKBN
               ELSE
                   MOVE    "1"         TO  SMEI-JOUTAIKBN
               END-IF
           END-IF

      *
      *    更新前チェック（既に重複するデータがある場合はエラー）
           PERFORM 400-SYUMEI-CHK-SEC
           IF    ( FLG-SYUMEI  =   ZERO )
               MOVE    SMEI-PTID       TO  ERR-PTID
               MOVE    ERR-MES07       TO  ERR-ERRMSG
               PERFORM 800-ERRMSG-EDIT-SEC
           END-IF
      *
           PERFORM 800-MCNDATE-SEC
           MOVE    SMCNDATE-YMD        TO  SMEI-CREYMD
                                           SMEI-UPYMD
           MOVE    SMCNDATE-HMS        TO  SMEI-UPHMS
      *
           MOVE    SYUMEI-REC          TO  MCPDATA-REC
           MOVE    "SYUMEI-KEY"        TO  ORC-DBPATH
           PERFORM 910-DBINSERT-SEC
           IF    ( MCP-RC          NOT =   ZERO )
               MOVE    SMEI-PTID       TO  ERR-PTID
               MOVE    ERR-MES07       TO  ERR-ERRMSG
               PERFORM 800-ERRMSG-EDIT-SEC
           END-IF
      *
           .
       20031-SYUMEI-SAKUSEI-EXT.
           EXIT. 
      *****************************************************************
      *    最新病棟番号別集計処理
      *****************************************************************
       20031-NOW-BTUNUM-SUM-SEC        SECTION.
      *
      *    患者情報読み込み
           INITIALIZE                  PTINF-REC
           MOVE    SYU-HOSPID          TO  PTINF-HOSPID
           MOVE    SYU-PTID            TO  PTINF-PTID
           PERFORM 900-PTINF-KEY-SEL-SEC
           IF    ( FLG-PTINF       NOT =   ZERO )
               MOVE    PTINF-PTID      TO  ERR-PTID
               MOVE    ERR-MES13       TO  ERR-ERRMSG
               PERFORM 800-ERRMSG-EDIT-SEC
           END-IF
      *
      *    最新の入院履歴情報より病棟番号を取得
           INITIALIZE  PTNYUINRRK-REC
           MOVE    PTINF-HOSPID        TO  PTNYUINRRK-HOSPID
           MOVE    PTINF-PTID          TO  PTNYUINRRK-PTID
           MOVE    PTINF-RRKNUM        TO  PTNYUINRRK-RRKNUM
           MOVE    PTINF-RRKEDANUM     TO  PTNYUINRRK-RRKEDANUM
           PERFORM 900-PTNYUINRRK-KEY-SEL-SEC
           IF    ( FLG-PTNYUINRRK  NOT =   ZERO )
               MOVE    PTNYUINRRK-PTID TO  ERR-PTID
               MOVE    ERR-MES14       TO  ERR-ERRMSG
               PERFORM 800-ERRMSG-EDIT-SEC
           END-IF
      *
           PERFORM VARYING IDX1    FROM    1   BY  1
                   UNTIL ( IDX1    >   50 )
               EVALUATE SUMTEIKIKANRI-BTUNUM (IDX1)
               WHEN    SPACE
                   MOVE    PTNYUINRRK-BTUNUM
                                   TO  SUMTEIKIKANRI-BTUNUM (IDX1)
                   MOVE    1       TO  SUMTEIKIKANRI-KENSU (IDX1)
                   COMPUTE SUMTEIKIKANRI-MAX
                       =   SUMTEIKIKANRI-MAX   +   1
                   MOVE    50      TO  IDX1
               WHEN    PTNYUINRRK-BTUNUM
                   COMPUTE SUMTEIKIKANRI-KENSU (IDX1)
                       =   SUMTEIKIKANRI-KENSU (IDX1)  +   1
                   MOVE    50      TO  IDX1
               END-EVALUATE
           END-PERFORM
           .
      *
       20031-NOW-BTUNUM-SUM-EXT.
           EXIT. 
      *****************************************************************
      *    定期請求印刷情報作成
      *****************************************************************
       20031-TEIKIPRT-SAKUSEI-SEC          SECTION.
      *
           INITIALIZE  TEIKIPRT-REC
           MOVE    PTNYUINRRK-BTUNUM   TO  TEIKIPRT-BTUNUM
           MOVE    PTINF-HOSPID        TO  TEIKIPRT-HOSPID
           MOVE    PTINF-PTID          TO  TEIKIPRT-PTID
      *
      *    患者番号を取得
           MOVE    PTINF-PTID          TO  WRK-PTID
           PERFORM 900-PTNUM-KEY-SEL-SEC
           IF    ( FLG-PTNUM           NOT =   ZERO )
               MOVE    PTINF-PTID      TO  ERR-PTID
               MOVE    ERR-MES15       TO  ERR-ERRMSG
               PERFORM 800-ERRMSG-EDIT-SEC
           END-IF
           MOVE    PTNUM-PTNUM         TO  TEIKIPRT-PTNUM
      *
           MOVE    PTINF-KANANAME      TO  TEIKIPRT-NAME
           MOVE    SYU-DENPNUM         TO  TEIKIPRT-DENPNUM
      *
      *    更新対象のデータがすでに存在する場合はエラー
           PERFORM 400-TEIKIPRT-CHK-SEC
           IF    ( FLG-TEIKIPRT    =   ZERO )
               MOVE    TEIKIPRT-PTID   TO  ERR-PTID
               MOVE    ERR-MES16       TO  ERR-ERRMSG
               PERFORM 800-ERRMSG-EDIT-SEC
           END-IF
      *
      *    定期請求印刷データ作成
           MOVE    TEIKIPRT-REC        TO  MCPDATA-REC
           MOVE    "TEIKIPRT-KEY"      TO  ORC-DBPATH
           PERFORM 910-DBINSERT-SEC
           IF  (   MCP-RC          NOT =   ZERO )
               MOVE    TEIKIPRT-PTID   TO  ERR-PTID
               MOVE    ERR-MES16       TO  ERR-ERRMSG
               PERFORM 800-ERRMSG-EDIT-SEC
           END-IF
      *
           .
       20031-TEIKIPRT-SAKUSEI-EXT.
            EXIT.
      *****************************************************************
      *    定期請求管理情報集計（プラス分）
      *****************************************************************
       20031-TEIKIRRK-SUM-SEC          SECTION.
      *
      *    定期請求管理情報集計（プラス分）
           PERFORM VARYING IDX1    FROM    1   BY  1
                   UNTIL ( IDX1    >   500 )
               EVALUATE SUMTEIKIRRK-SRYYM (IDX1)
                                   ALSO    SUMTEIKIRRK-SAKKBN (IDX1)
               WHEN    SPACE       ALSO    ANY
                   MOVE    PTTEIKIRRK-SRYYM
                                   TO  SUMTEIKIRRK-SRYYM   (IDX1)
                   MOVE    PTTEIKIRRK-SAKKBN
                                   TO  SUMTEIKIRRK-SAKKBN  (IDX1)
                   MOVE    PTTEIKIRRK-SKYSTYMD
                                   TO  SUMTEIKIRRK-SKYSTYMD(IDX1)
                   MOVE    PTTEIKIRRK-SKYEDYMD
                                   TO  SUMTEIKIRRK-SKYEDYMD(IDX1)
                   COMPUTE SUMTEIKIRRK-SKYTEN (IDX1)
                       =   WRK-TEIKIRRK-SKYTEN
                   COMPUTE SUMTEIKIRRK-SKYMONEY (IDX1)
                       =   WRK-TEIKIRRK-SKYMONEY
                   COMPUTE SUMTEIKIRRK-MAX
                       =   SUMTEIKIRRK-MAX     +   1
                   MOVE    500         TO  IDX1
               WHEN    PTTEIKIRRK-SRYYM
                                   ALSO    PTTEIKIRRK-SAKKBN
                   COMPUTE SUMTEIKIRRK-SKYTEN (IDX1)
                       =   SUMTEIKIRRK-SKYTEN (IDX1)
                       +   WRK-TEIKIRRK-SKYTEN
                   COMPUTE SUMTEIKIRRK-SKYMONEY (IDX1)
                       =   SUMTEIKIRRK-SKYMONEY (IDX1)
                       +   WRK-TEIKIRRK-SKYMONEY
                   MOVE    500         TO  IDX1
               END-EVALUATE
           END-PERFORM
      *
           .
       20031-TEIKIRRK-SUM-EXT.
           EXIT. 
      *****************************************************************
      *    定期請求情報更新処理（訂正更新）
      *****************************************************************
       2007-SEIKYU-KOUSIN-SEC          SECTION.
      *
           MOVE    ZERO                TO  FLG-TEIKI09
      *
      *    定期請求収納ファイル読込
           PERFORM 910-TEIKI09-READ-SEC
      *
           PERFORM UNTIL ( FLG-TEIKI09 NOT =   ZERO )
      *
               MOVE    TEIKI09-SYU-DAT TO  SYUNOU-REC
      *
      *        更新前チェック（更新対象の収納がなければエラー）
               PERFORM 400-SYUNOU-CHK-SEC
               IF    ( FLG-SYUNOU  NOT =   ZERO )
                   MOVE    SYU-PTID    TO  ERR-PTID
                   MOVE    ERR-MES08   TO  ERR-ERRMSG
                   PERFORM 800-ERRMSG-EDIT-SEC
               END-IF
      *
               PERFORM 20071-SYUNOU-HEN-SEC
      *
               PERFORM 800-MCNDATE-SEC
               MOVE    SMCNDATE-YMD        TO  SYU-UPYMD
               MOVE    SMCNDATE-HMS        TO  SYU-UPHMS
      *
               MOVE    SYUNOU-REC          TO  MCPDATA-REC
               MOVE    "SYUNOU-KEY"        TO  ORC-DBPATH
               MOVE    "DBUPDATE"          TO  MCP-FUNC
               PERFORM 910-DBUPDATE-SEC
               IF    ( MCP-RC          NOT =   ZERO )
                   MOVE    SYU-PTID        TO  ERR-PTID
                   MOVE    ERR-MES08   TO  ERR-ERRMSG
                   PERFORM 800-ERRMSG-EDIT-SEC
               END-IF
      *
      *        日別収納作成処理
               IF    ( TEIKI09-SYUDAY-EXIST-FLG    =   1 )
                   PERFORM 20071-SYUDAY-SAKUSEI-SEC
               END-IF
      *
      *        収納明細作成処理
               PERFORM 20071-SYUMEI-SAKUSEI-SEC
      *
      *        患者定期請求履歴更新処理
               PERFORM 20071-PTTEIKIRRK-KOUSIN-SEC
      *
      *        定期請求管理情報集計（プラス分）
               MOVE    TEIKI09-TSI-SKYTEN
                                       TO  WRK-TEIKIRRK-SKYTEN
               MOVE    TEIKI09-TSI-SKYMONEY
                                       TO  WRK-TEIKIRRK-SKYMONEY
               PERFORM 20031-TEIKIRRK-SUM-SEC
      *
      *        最新病棟番号別集計処理
               PERFORM 20031-NOW-BTUNUM-SUM-SEC
      *
      *        定期請求印刷情報作成処理
               PERFORM 20031-TEIKIPRT-SAKUSEI-SEC
      *
      *        定期請求収納ファイル読込
               PERFORM 910-TEIKI09-READ-SEC
      *
           END-PERFORM
      *
           .
       2007-SEIKYU-KOUSIN-EXT.
           EXIT. 
      *****************************************************************
      *    収納マスタ編集処理
      *****************************************************************
       20071-SYUNOU-HEN-SEC            SECTION.
      *
           MOVE    TEIKI09-SYU-DAT     TO  SYUNOU-REC
      *
      *    入金済みとして処理する場合、請求額の差分がプラスで計上される
      *    場合は同額を入金額に加算する
           IF    ( SYS-5010-NYUKINKBN  =   "2" )
               IF    ( TEIKI09-TSI-SKYMONEY    >   ZERO )
      *
                   COMPUTE SYU-NYUKIN-TOTAL
                       =   SYU-NYUKIN-TOTAL
                       +   TEIKI09-TSI-SKYMONEY
      *
                   COMPUTE SYU-NYUKIN-KAISU
                       =   SYU-NYUKIN-KAISU
                       +   1
               END-IF
           END-IF
      *
           IF    ( SYU-SKYMONEY        >   SYU-NYUKIN-TOTAL )
               MOVE    "2"             TO  SYU-DENPJTIKBN
           ELSE
               MOVE    "1"             TO  SYU-DENPJTIKBN
           END-IF
      *
           .
       20071-SYUNOU-HEN-EXT.
           EXIT. 
      *****************************************************************
      *    日別収納作成処理
      *****************************************************************
       20071-SYUDAY-SAKUSEI-SEC        SECTION.
      *
           INITIALIZE                      WKSYUDAY-REC
      *
           MOVE    TEIKI09-SYUDAY-DAT  TO  WKSYUDAY-REC
           MOVE    SYU-DENPNUM         TO  WKSDAY-DENPNUM
      *
      *    日別収納更新処理
           PERFORM 900-SYUDAY-UPD-SEC
      *
           .
       20071-SYUDAY-SAKUSEI-EXT.
           EXIT.
      *****************************************************************
      *    患者定期請求履歴データ更新処理
      *****************************************************************
       20071-PTTEIKIRRK-KOUSIN-SEC     SECTION.
      *
           INITIALIZE                      PTTEIKIRRK-REC
           MOVE    SYU-HOSPID          TO  PTTEIKIRRK-HOSPID
           MOVE    SYU-PTID            TO  PTTEIKIRRK-PTID
           MOVE    SYU-DENPNUM         TO  PTTEIKIRRK-DENPNUM
      *
      *    更新対象のデータを取得（更新対象のデータがなければエラー）
           MOVE    ZERO                TO  FLG-PTTEIKIRRK
           PERFORM 900-PTTEIKIRRK-KEY6-SEL-SEC
           IF    ( FLG-PTTEIKIRRK  NOT =   ZERO )
               MOVE    PTTEIKIRRK-PTID TO  ERR-PTID
               MOVE    ERR-MES12       TO  ERR-ERRMSG
               PERFORM 800-ERRMSG-EDIT-SEC
           END-IF
      *
      *    患者定期請求履歴削除処理
           MOVE    PTTEIKIRRK-REC      TO  MCPDATA-REC
           MOVE    "PTTEIKIRRK-KEY"    TO  ORC-DBPATH
           PERFORM 910-DBDELETE-SEC
           IF    ( FLG-PTTEIKIRRK  NOT =   ZERO )
               MOVE    PTTEIKIRRK-PTID TO  ERR-PTID
               MOVE    ERR-MES12       TO  ERR-ERRMSG
               PERFORM 800-ERRMSG-EDIT-SEC
           END-IF
      *
           INITIALIZE                      PTTEIKIRRK-REC
           MOVE    SYU-HOSPID          TO  PTTEIKIRRK-HOSPID
           MOVE    SYU-PTID            TO  PTTEIKIRRK-PTID
           MOVE    SYU-SRYYMD (1:6)    TO  PTTEIKIRRK-SRYYM
           MOVE    TEIKI09-SAKKBN      TO  PTTEIKIRRK-SAKKBN
           MOVE    SYU-HKNCOMBINUM     TO  PTTEIKIRRK-HKNCOMBINUM
           MOVE    TEIKI09-SKYSTYMD    TO  PTTEIKIRRK-SKYSTYMD
           MOVE    TEIKI09-SKYEDYMD    TO  PTTEIKIRRK-SKYEDYMD
      *
           MOVE    TEIKI09-TSI-SKYTEN
                                       TO  PTTEIKIRRK-SKYTEN
      *
           MOVE    TEIKI09-TSI-SKYMONEY-TAX-SAI
                                       TO  PTTEIKIRRK-SKYMONEY-TAX-SAI
      *
           MOVE    TEIKI09-TSI-SKYMONEY
                                       TO  PTTEIKIRRK-SKYMONEY
      *
           MOVE    SYU-NYUIN-RRKNUM    TO  PTTEIKIRRK-RRKNUM
           MOVE    SYU-DENPNUM         TO  PTTEIKIRRK-DENPNUM
           MOVE    SMEI-DENPEDANUM     TO  PTTEIKIRRK-DENPEDANUM
      *
           MOVE    1                   TO  PTTEIKIRRK-SKYUPDFLG
      *
           PERFORM 800-MCNDATE-SEC
           MOVE    SMCNDATE-YMD        TO  PTTEIKIRRK-CREYMD
                                           PTTEIKIRRK-UPYMD
           MOVE    SMCNDATE-HMS        TO  PTTEIKIRRK-UPHMS
      *
      *    患者定期請求データ作成
           MOVE    PTTEIKIRRK-REC      TO  MCPDATA-REC
           MOVE    "PTTEIKIRRK-KEY"    TO  ORC-DBPATH
           PERFORM 910-DBINSERT-SEC
           IF  (   MCP-RC          NOT =   ZERO )
               MOVE    PTTEIKIRRK-PTID TO  ERR-PTID
               MOVE    ERR-MES12       TO  ERR-ERRMSG
               PERFORM 800-ERRMSG-EDIT-SEC
           END-IF
      *
           .
       20071-PTTEIKIRRK-KOUSIN-EXT.
           EXIT. 
      *****************************************************************
      *    収納明細作成処理
      *****************************************************************
       20071-SYUMEI-SAKUSEI-SEC        SECTION.
      *
           MOVE    ZERO                TO  FLG-SYUMEI
      *
           MOVE    ZERO                TO  WRK-DENPEDANUM
           MOVE    ZERO                TO  WRK-NYUKINRENNUM
      *
           INITIALIZE                      SYUMEI-REC
           MOVE    SYU-HOSPID          TO  SMEI-HOSPID
           MOVE    SYU-NYUGAIKBN       TO  SMEI-NYUGAIKBN
           MOVE    SYU-PTID            TO  SMEI-PTID
           MOVE    SYU-DENPNUM         TO  SMEI-DENPNUM
      *
           MOVE    SYUMEI-REC          TO  MCPDATA-REC
           MOVE    "SYUMEI-KEY2"       TO  ORC-DBPATH
           PERFORM 910-DBSELECT-SEC
           IF    ( MCP-RC              =   ZERO )
               MOVE    MCPDATA-REC     TO  SYUMEI-REC
           ELSE
               MOVE    1               TO  FLG-SYUMEI
           END-IF
      *
           PERFORM UNTIL ( FLG-SYUMEI  NOT =   ZERO )
      *
               IF    ( SMEI-DENPEDANUM     >   WRK-DENPEDANUM )
                   MOVE    SMEI-DENPEDANUM     TO  WRK-DENPEDANUM
               END-IF
      *
               IF    ( SMEI-NYUKINRENNUM   >   WRK-NYUKINRENNUM )
                   MOVE    SMEI-NYUKINRENNUM   TO  WRK-NYUKINRENNUM
               END-IF
      *
      *        診療科変更
               IF    ( SYU-SRYKA       NOT =   SMEI-SRYKA )
      *
                   MOVE    SYU-SRYKA           TO  SMEI-SRYKA
      *
                   MOVE    SMCNDATE-YMD        TO  SMEI-UPYMD
                   MOVE    SMCNDATE-HMS        TO  SMEI-UPHMS
      *
                   MOVE    SYUMEI-REC          TO  MCPDATA-REC
                   MOVE    "SYUMEI-KEY"        TO  ORC-DBPATH
                   PERFORM 910-DBUPDATE-SEC
                   IF    ( MCP-RC          NOT =   ZERO )
                       MOVE    SMEI-PTID       TO  ERR-PTID
                       MOVE    ERR-MES07       TO  ERR-ERRMSG
                       PERFORM 800-ERRMSG-EDIT-SEC
                   END-IF
               END-IF
      *
               MOVE    "SYUMEI-KEY2"   TO  ORC-DBPATH
               PERFORM 910-DBFETCH-SEC
               IF    ( MCP-RC              =   ZERO )
                   MOVE    MCPDATA-REC TO  SYUMEI-REC
               ELSE
                   MOVE    1           TO  FLG-SYUMEI
               END-IF
           END-PERFORM
      *
           INITIALIZE                      SYUMEI-REC
      *
           MOVE    SYU-HOSPID          TO  SMEI-HOSPID
           MOVE    SYU-NYUGAIKBN       TO  SMEI-NYUGAIKBN
           MOVE    SYU-PTID            TO  SMEI-PTID
           MOVE    SYU-DENPNUM         TO  SMEI-DENPNUM
      *
           COMPUTE WRK-DENPEDANUM
               =   WRK-DENPEDANUM      +   1
      *
           MOVE    WRK-DENPEDANUM      TO  SMEI-DENPEDANUM
      *
           COMPUTE WRK-NYUKINRENNUM
               =   WRK-NYUKINRENNUM    +   1
      *
           MOVE    WRK-NYUKINRENNUM    TO  SMEI-NYUKINRENNUM
      *
           MOVE    SPACE               TO  SMEI-MEISAIJOUTAIKBN
      *
           MOVE    TEIKI1P-SYSYMD      TO  SMEI-SKYPRINTYMD
      *
           MOVE    SPACE               TO  SMEI-SKYREPRINTYMD
      *
           MOVE    SPACE               TO  SMEI-RYOSYUPRINTYMD
      *
           MOVE    SPACE               TO  SMEI-RYOSYUREPRINTYMD
      *
           MOVE    SYU-SRYKA           TO  SMEI-SRYKA
      *
           MOVE    TEIKI09-TSI-SKYMONEY
                                       TO  SMEI-SKYMONEY
      *
           MOVE    SMEI-SKYPRINTYMD    TO  SMEI-NYUHEN-YMD 
      *
           MOVE    "00"                TO  SMEI-NYUKIN-HOHO
      *
      *    入金額がゼロの時、入金力なし
           IF    ( SYS-5010-NYUKINKBN  =   "2" )
               IF    ( TEIKI09-TSI-SKYMONEY >   ZERO )
                   MOVE    "1"         TO  SMEI-NYUHEN-KBN
                   MOVE    TEIKI09-TSI-SKYMONEY
                                       TO  SMEI-NYUHEN-MONEY
                   MOVE    "01"        TO  SMEI-NYUKIN-HOHO
               END-IF
           END-IF
      *
           MOVE    "8"                 TO  SMEI-JOUTAIKBN
      *
      *    更新前チェック（既に重複するデータがある場合はエラー）
           PERFORM 400-SYUMEI-CHK-SEC
           IF    ( FLG-SYUMEI  =   ZERO )
               MOVE    SMEI-PTID       TO  ERR-PTID
               MOVE    ERR-MES07       TO  ERR-ERRMSG
               PERFORM 800-ERRMSG-EDIT-SEC
           END-IF
      *
           PERFORM 800-MCNDATE-SEC
           MOVE    SMCNDATE-YMD    TO  SMEI-CREYMD
                                       SMEI-UPYMD
           MOVE    SMCNDATE-HMS    TO  SMEI-UPHMS
      *
           MOVE    SYUMEI-REC      TO  MCPDATA-REC
           MOVE    "SYUMEI-KEY"    TO  ORC-DBPATH
           PERFORM 910-DBINSERT-SEC
           IF    ( MCP-RC          NOT =   ZERO )
               MOVE    SMEI-PTID       TO  ERR-PTID
               MOVE    ERR-MES07       TO  ERR-ERRMSG
               PERFORM 800-ERRMSG-EDIT-SEC
           END-IF
      *
           .
       20071-SYUMEI-SAKUSEI-EXT.
           EXIT. 
      *****************************************************************
      *    定期請求管理情報更新処理
      *****************************************************************
       2005-TEIKIKANRI-KOUSIN-SEC      SECTION.
      *
           PERFORM VARYING IDX1    FROM    1   BY  1
                   UNTIL ( IDX1    >   SUMTEIKIKANRI-MAX )
      *
               INITIALIZE  TEIKIKANRI-REC
      *
               MOVE    SUMTEIKIKANRI-BTUNUM (IDX1)
                                       TO  TEIKIKANRI-BTUNUM
               MOVE    TEIKI1P-SYOKBN
                                       TO  TEIKIKANRI-SYOKBN
      *
               PERFORM 800-MCNDATE-SEC
      *
               MOVE    SMCNDATE-YMD    TO  TEIKIKANRI-CREYMD
               MOVE    SMCNDATE-HMS    TO  TEIKIKANRI-CREHMS
      *
               IF    ( TEIKI1P-SYOKBN =   "1" )
                   MOVE    TEIKI1P-SRYYM
                                       TO  TEIKIKANRI-SRYYM
               END-IF
      *
               MOVE    SUMTEIKIKANRI-KENSU (IDX1)
                                       TO  TEIKIKANRI-PAGE
                                           TEIKIKANRI-KENSU
               MOVE    TEIKI1P-TERMID  TO  TEIKIKANRI-TERMID
      *
      *        更新対象のデータがすでに存在する場合はエラー
               PERFORM 400-TEIKIKANRI-CHK-SEC
               IF    ( FLG-TEIKIKANRI  =   ZERO )
                   MOVE  ERR-MES17         TO  WRK-TEIKIERR
                   PERFORM 500-ERR-HENSYU-SEC
               END-IF
      *
      *        定期請求管理データ作成
               MOVE    TEIKIKANRI-REC      TO  MCPDATA-REC
               MOVE    "TEIKIKANRI-KEY"    TO  ORC-DBPATH
               PERFORM 910-DBINSERT-SEC
               IF  (   MCP-RC          NOT =   ZERO )
                   MOVE  ERR-MES17         TO  WRK-TEIKIERR
                   PERFORM 500-ERR-HENSYU-SEC
               END-IF
      *
           END-PERFORM
      *
           .
       2005-TEIKIKANRI-KOUSIN-EXT.
           EXIT. 
      *****************************************************************
      *    定期請求履歴情報更新処理
      *****************************************************************
       2006-TEIKIRRK-KOUSIN-SEC        SECTION.
      *
           PERFORM VARYING IDX1    FROM    1   BY  1
                   UNTIL ( IDX1    >   SUMTEIKIRRK-MAX )
      *
               INITIALIZE  TEIKIRRK-REC
      *
               MOVE    SUMTEIKIRRK-SRYYM    (IDX1)
                                       TO  TEIKIRRK-SRYYM
               MOVE    SUMTEIKIRRK-SAKKBN   (IDX1)
                                       TO  TEIKIRRK-SAKKBN
               MOVE    SUMTEIKIRRK-SKYSTYMD (IDX1)
                                       TO  TEIKIRRK-SKYSTYMD
               MOVE    SUMTEIKIRRK-SKYEDYMD (IDX1)
                                       TO  TEIKIRRK-SKYEDYMD
               MOVE    SUMTEIKIRRK-SKYTEN   (IDX1)
                                       TO  TEIKIRRK-SKYTEN
               MOVE    SUMTEIKIRRK-SKYMONEY (IDX1)
                                       TO  TEIKIRRK-SKYMONEY
               MOVE    TEIKI1P-TERMID  TO  TEIKIRRK-TERMID
      *
               PERFORM 800-MCNDATE-SEC
      *
               MOVE    SMCNDATE-YMD    TO  TEIKIRRK-CREYMD
                                           TEIKIRRK-UPYMD
               MOVE    SMCNDATE-HMS    TO  TEIKIRRK-UPHMS
      *
               MOVE    TEIKIRRK-REC    TO  WKTEIKIRRK-REC
      *
               PERFORM 900-TEIKIRRK-KEY-SEL-SEC
      *        更新対象のデータがすでに存在する場合は更新
      *        存在しない場合は追加
               IF    ( FLG-TEIKIRRK  =   ZERO )
      *
                   COMPUTE TEIKIRRK-SKYTEN
                       =   TEIKIRRK-SKYTEN
                       +   SUMTEIKIRRK-SKYTEN   (IDX1)
      *
                   COMPUTE TEIKIRRK-SKYMONEY
                       =   TEIKIRRK-SKYMONEY
                       +   SUMTEIKIRRK-SKYMONEY (IDX1)
      *
                   PERFORM 800-MCNDATE-SEC
                   MOVE    SMCNDATE-YMD    TO  TEIKIRRK-UPYMD
                   MOVE    SMCNDATE-HMS    TO  TEIKIRRK-UPHMS
      *
      *            定期請求管理データ更新
                   MOVE    TEIKIRRK-REC    TO  MCPDATA-REC
                   MOVE    "TEIKIRRK-KEY"  TO  ORC-DBPATH
                   PERFORM 910-DBUPDATE-SEC
                   IF  (   MCP-RC          NOT =   ZERO )
                       MOVE  ERR-MES18     TO  WRK-TEIKIERR
                       PERFORM 500-ERR-HENSYU-SEC
                   END-IF
               ELSE
      *
                   MOVE    WKTEIKIRRK-REC  TO  TEIKIRRK-REC
      *
                   PERFORM 800-MCNDATE-SEC
                   MOVE    SMCNDATE-YMD    TO  TEIKIRRK-CREYMD
                                               TEIKIRRK-UPYMD
                   MOVE    SMCNDATE-HMS    TO  TEIKIRRK-UPHMS
      *
      *            定期請求管理データ作成
                   MOVE    TEIKIRRK-REC    TO  MCPDATA-REC
                   MOVE    "TEIKIRRK-KEY"  TO  ORC-DBPATH
                   PERFORM 910-DBINSERT-SEC
                   IF  (   MCP-RC          NOT =   ZERO )
                       MOVE  ERR-MES19     TO  WRK-TEIKIERR
                       PERFORM 500-ERR-HENSYU-SEC
                   END-IF
               END-IF
      *
      *
           END-PERFORM
      *
           .
       2006-TEIKIRRK-KOUSIN-EXT.
           EXIT. 
      *****************************************************************
      *    単独更新用日別収納更新処理
      *****************************************************************
       2004-TEIKI08-KOUSIN-SEC         SECTION.
      *
           MOVE    ZERO                TO  FLG-TEIKI08
           PERFORM 910-TEIKI08-READ-SEC
      *
           PERFORM UNTIL ( FLG-TEIKI08 NOT =   ZERO )
      *
               MOVE    TEIKI08-SYUDAY-DAT
                                       TO  WKSYUDAY-REC
      *
               EVALUATE    TEIKI08-UPD-FLG
      *
      *        更新
               WHEN    1
                   PERFORM 900-SYUDAY-UPD-SEC
      *        削除
               WHEN    2
                   PERFORM 900-SYUDAY-DEL-SEC
               END-EVALUATE
      *
               PERFORM 910-TEIKI08-READ-SEC
      *
           END-PERFORM
      *
           .
       2004-TEIKI08-KOUSIN-EXT.
           EXIT. 
      *****************************************************************
      *    エラー出力処理
      *****************************************************************
       500-ERR-HENSYU-SEC              SECTION.
      *
           PERFORM 800-TEIKIERR-WRITE-SEC
      *
           MOVE    1                   TO  FLG-ERR
      *
           PERFORM 900-DBDISCONNECT-SEC
      *
      *    プログラム終了処理
           PERFORM 990-PROGRAM-FINISH-SEC
      *
           .
       500-ERR-HENSYU-EXT.
           EXIT.
      *****************************************************************
      *    終了処理
      *****************************************************************
       300-TERM-SEC                SECTION.
      *
           CLOSE   TEIKI06-FILE
                   TEIKI07-FILE
                   TEIKI08-FILE
                   TEIKI09-FILE
      *
      *    ステップ管理終了処理
           MOVE    "STE"           TO  SJOBKANRI-MODE
           INITIALIZE                  JOBKANRI-REC
           MOVE    TEIKI1P-JOBID   TO  JOB-JOBID
           MOVE    TEIKI1P-SHELLID
                                   TO  JOB-SHELLID
           CALL    "ORCSJOB"       USING
                                   ORCSJOBKANRIAREA  
                                   JOBKANRI-REC
      *
           PERFORM 900-DBDISCONNECT-SEC
      *
      *    日別収納の単独更新のみの場合は、対象データなしとして
      *    エラーファイルを出力する
           IF    ( CNT-TEIKI06      =   ZERO )
            AND  ( CNT-TEIKI09      =   ZERO )
            AND  ( CNT-TEIKI08      >   ZERO )
               MOVE    ERR-MES20    TO  WRK-TEIKIERR
               PERFORM 800-TEIKIERR-WRITE-SEC
           END-IF
      *
           DISPLAY "TEIKI06 (ORCT0040) CNT " CNT-TEIKI06
           DISPLAY "TEIKI09 (ORCT0040) CNT " CNT-TEIKI09
           DISPLAY "TEIKI08 (ORCT0040) CNT " CNT-TEIKI08
           DISPLAY "*** ORCT0040 END ***"
      *
      ***  ACCEPT  WRK-IN FROM CONSOLE 
           .
       300-TERM-EXT.
           EXIT.
      *
      *****************************************************************
      *    収納マスター存在チェック
      *****************************************************************
       400-SYUNOU-CHK-SEC              SECTION.
      *
           MOVE    ZERO            TO  FLG-SYUNOU
      *
           MOVE    SYUNOU-REC      TO  MCPDATA-REC
           MOVE    "SYUNOU-KEY"    TO  ORC-DBPATH
           PERFORM 910-DBSELECT-SEC
           IF    ( MCP-RC      NOT =   ZERO )
               MOVE    1           TO  FLG-SYUNOU
           END-IF
      *
           MOVE    "SYUNOU-KEY"    TO  ORC-DBPATH
           PERFORM 910-DBCLOSE-SEC
      *
           .
       400-SYUNOU-CHK-EXT.
           EXIT.
      *****************************************************************
      *    収納明細存在チェック
      *****************************************************************
       400-SYUMEI-CHK-SEC              SECTION.
      *
      *    追加対象のデータが既に存在しないかチェック
           MOVE    ZERO                TO  FLG-SYUMEI
      *
           MOVE    SYUMEI-REC          TO  MCPDATA-REC
           MOVE    "SYUMEI-KEY"        TO  ORC-DBPATH
           PERFORM 910-DBSELECT-SEC
           IF    ( MCP-RC          NOT =   ZERO )
               MOVE    1               TO  FLG-SYUMEI
           END-IF
      *
           MOVE    "SYUMEI-KEY"        TO  ORC-DBPATH
           PERFORM 910-DBCLOSE-SEC
      *
           .
       400-SYUMEI-CHK-EXT.
           EXIT.
      *****************************************************************
      *    患者定期請求履歴存在チェック
      *****************************************************************
       400-PTTEIKIRRK-CHK-SEC          SECTION.
      *
           MOVE   ZERO                 TO  FLG-PTTEIKIRRK
           MOVE    PTTEIKIRRK-REC      TO  MCPDATA-REC
           MOVE    "PTTEIKIRRK-KEY"    TO  ORC-DBPATH
           PERFORM 910-DBSELECT-SEC
           IF    ( MCP-RC          NOT =   ZERO )
               MOVE    1               TO  FLG-PTTEIKIRRK
           END-IF
      *
           MOVE    "PTTEIKIRRK-KEY"    TO  ORC-DBPATH
           PERFORM 910-DBCLOSE-SEC
      *
           .
       400-PTTEIKIRRK-CHK-EXT.
           EXIT.
      *****************************************************************
      *    日別収納マスタ検索処理
      *****************************************************************
       400-SYUDAY-CHK-SEC              SECTION.
      *
           MOVE    ZERO                TO  FLG-SYUDAY
      *
           MOVE    SYUDAY-REC          TO  MCPDATA-REC
           MOVE    "SYUDAY-KEY"        TO  ORC-DBPATH
           PERFORM 910-DBSELECT-SEC
           IF    ( MCP-RC          NOT =   ZERO )
               MOVE    1               TO  FLG-SYUDAY
           END-IF
      *
           MOVE    "SYUDAY-KEY"        TO  ORC-DBPATH
           PERFORM 910-DBCLOSE-SEC
      *
           .
       400-SYUDAY-CHK-EXT.
           EXIT.
      *****************************************************************
      *    定期請求印刷情報存在チェック処理
      *****************************************************************
       400-TEIKIPRT-CHK-SEC            SECTION.
      *
           MOVE    ZERO                TO  FLG-TEIKIPRT
      *
           MOVE    TEIKIPRT-REC        TO  MCPDATA-REC
           MOVE    "TEIKIPRT-KEY"      TO  ORC-DBPATH
           PERFORM 910-DBSELECT-SEC
           IF    ( MCP-RC          NOT =   ZERO )
               MOVE    1               TO  FLG-TEIKIPRT
           END-IF
      *
           MOVE    "TEIKIPRT-KEY"      TO  ORC-DBPATH
           PERFORM 910-DBCLOSE-SEC
      *
           .
       400-TEIKIPRT-CHK-EXT.
           EXIT.
      *****************************************************************
      *    定期請求管理情報存在チェック処理
      *****************************************************************
       400-TEIKIKANRI-CHK-SEC          SECTION.
      *
           MOVE    ZERO                TO  FLG-TEIKIKANRI
      *
           MOVE    TEIKIKANRI-REC      TO  MCPDATA-REC
           MOVE    "TEIKIKANRI-KEY"    TO  ORC-DBPATH
           PERFORM 910-DBSELECT-SEC
           IF    ( MCP-RC          NOT =   ZERO )
               MOVE    1               TO  FLG-TEIKIKANRI
           END-IF
      *
           MOVE    "TEIKIKANRI-KEY"    TO  ORC-DBPATH
           PERFORM 910-DBCLOSE-SEC
      *
           .
       400-TEIKIKANRI-CHK-EXT.
           EXIT.
      *****************************************************************
      *    エラーファイル出力処理
      *****************************************************************
       800-TEIKIERR-WRITE-SEC          SECTION.
      *
           OPEN    OUTPUT              TEIKIERR-FILE
      *
           MOVE    WRK-TEIKIERR        TO  TEIKIERR-REC
           WRITE   TEIKIERR-REC
           CLOSE   TEIKIERR-FILE
      *
           .
       800-TEIKIERR-WRITE-EXT.
           EXIT.
      *****************************************************************
      *    エラーメッセージ編集処理
      *****************************************************************
       800-ERRMSG-EDIT-SEC             SECTION.
      *
           MOVE    SPACE               TO  ERR-HEN-ERRMSG
	                                   ERR-PTNUM
      *
      *    患者番号を取得
           MOVE    ERR-PTID            TO  WRK-PTID
           PERFORM 900-PTNUM-KEY-SEL-SEC
           IF    ( FLG-PTNUM           =   ZERO )
               MOVE    PTNUM-PTNUM     TO  ERR-PTNUM
           ELSE
               STRING "?(ID:"          DELIMITED   BY  SIZE
                      ERR-PTID         DELIMITED   BY  SIZE
                      ")"              DELIMITED   BY  SIZE
               INTO   ERR-PTNUM
               END-STRING
           END-IF
      *
           MOVE    1                   TO  WRK-LEN
           MOVE    SPACE               TO  WRK-STR
           STRING  ERR-PTNUM           DELIMITED   BY  SPACE
           INTO    WRK-STR
           WITH  POINTER WRK-LEN
           END-STRING
      *
           COMPUTE WRK-LEN =   WRK-LEN -   1
      *
           MOVE    ZERO                TO  WRK-SHO
                                           WRK-AMARI
           MOVE    SPACE               TO  WRK-KUGIRI
      *
           DIVIDE  2   INTO    WRK-LEN GIVING  WRK-SHO
           REMAINDER WRK-AMARI
           END-DIVIDE
      *
           IF    ( WRK-AMARI   =   ZERO )
               MOVE    "："            TO  WRK-KUGIRI
           ELSE
               MOVE    ":"             TO  WRK-KUGIRI
           END-IF
      *
           STRING  ERR-ERRMSG          DELIMITED   BY  SPACE
                   "［患者番号"        DELIMITED   BY  SIZE
                   WRK-KUGIRI          DELIMITED   BY  SPACE
                   ERR-PTNUM           DELIMITED   BY  SPACE
                   "］"                DELIMITED   BY  SIZE
           INTO    ERR-HEN-ERRMSG
           END-STRING
      *
           MOVE    ERR-HEN-ERRMSG      TO  WRK-TEIKIERR
           PERFORM 500-ERR-HENSYU-SEC
      *
           .
       800-ERRMSG-EDIT-EXT.
           EXIT.
      *****************************************************************
      *    マシン日付取得サブ
      *****************************************************************
       800-MCNDATE-SEC         SECTION.
      *
           CALL    "ORCSMCNDATE"   USING   ORCSMCNDATEAREA
      *
           .
      *
       800-MCNDATE-EXT.
           EXIT.
      *****************************************************************
      *    システム管理マスタ読込
      *****************************************************************
       900-SYSKANRI-KEY-SEL-SEC        SECTION.
      *
           MOVE    ZERO                TO  FLG-SYSKANRI
      *
           MOVE    "DBSELECT"          TO  MCP-FUNC
           MOVE    "SYSKANRI-KEY"      TO  ORC-DBPATH
           PERFORM 910-DBSELECT-SEC
           IF    ( MCP-RC          NOT =   ZERO )
               MOVE    1               TO  FLG-SYSKANRI
           END-IF
      *
           MOVE    "SYSKANRI-KEY"      TO  ORC-DBPATH
           PERFORM 910-DBCLOSE-SEC
      *
           .
       900-SYSKANRI-KEY-SEL-EXT.
           EXIT.
      *****************************************************************
      *    定期請求収納ファイル読込
      *****************************************************************
       910-TEIKI06-READ-SEC            SECTION.
      *
           READ    TEIKI06-FILE
           AT  END
               MOVE    1               TO  FLG-TEIKI06
           END-READ
      *
           IF    ( FLG-TEIKI06         =   ZERO )
               COMPUTE CNT-TEIKI06     =   CNT-TEIKI06 +   1
           END-IF
      *
           .
       910-TEIKI06-READ-EXT.
           EXIT.
      *****************************************************************
      *    請求取消対象収納ファイル読込
      *****************************************************************
       910-TEIKI07-READ-SEC            SECTION.
      *
           READ    TEIKI07-FILE
           AT  END
               MOVE    1               TO  FLG-TEIKI07
           END-READ
      *
           .
       910-TEIKI07-READ-EXT.
           EXIT.
      *
      *****************************************************************
      *    単独更新用日別収納ファイル読込
      *****************************************************************
       910-TEIKI08-READ-SEC            SECTION.
      *
           READ    TEIKI08-FILE
           AT  END
               MOVE    1               TO  FLG-TEIKI08
           END-READ
      *
           IF    ( FLG-TEIKI08         =   ZERO )
               COMPUTE CNT-TEIKI08     =   CNT-TEIKI08 +   1
           END-IF
      *
           .
       910-TEIKI08-READ-EXT.
           EXIT.
      *****************************************************************
      *    定期請求収納ファイル読込
      *****************************************************************
       910-TEIKI09-READ-SEC            SECTION.
      *
           READ    TEIKI09-FILE
           AT  END
               MOVE    1               TO  FLG-TEIKI09
           END-READ
      *
           IF    ( FLG-TEIKI09         =   ZERO )
               COMPUTE CNT-TEIKI09     =   CNT-TEIKI09 +   1
           END-IF
      *
           .
       910-TEIKI09-READ-EXT.
           EXIT.
      *****************************************************************
      *    定期請求管理情報削除処理
      *****************************************************************
       900-TEIKIKANRI-DEL-SEC          SECTION.
      *
           MOVE    SPACE               TO  MCPDATA-REC
           MOVE    "TEIKIKANRI-DEL1"   TO  ORC-DBPATH
           PERFORM 910-DBDELETE-SEC
           IF  (   MCP-RC          NOT =   ZERO )
               MOVE  ERR-MES02         TO  WRK-TEIKIERR
               PERFORM 500-ERR-HENSYU-SEC
           END-IF
      *
           .
       900-TEIKIKANRI-DEL-EXT.
            EXIT.
      *****************************************************************
      *    定期請求印刷情報削除処理
      *****************************************************************
       900-TEIKIPRT-DEL-SEC            SECTION.
      *
           MOVE    SPACE               TO  MCPDATA-REC
           MOVE    "TEIKIPRT-DEL1"     TO  ORC-DBPATH
           PERFORM 910-DBDELETE-SEC
           IF  (   MCP-RC          NOT =   ZERO )
               MOVE    ERR-MES03       TO  WRK-TEIKIERR
               PERFORM 500-ERR-HENSYU-SEC
           END-IF
      *
           .
       900-TEIKIPRT-DEL-EXT.
            EXIT.
      *****************************************************************
      *    患者マスター読込（主キー）
      *****************************************************************
       900-PTINF-KEY-SEL-SEC           SECTION.
      *
           MOVE    ZERO                TO  FLG-PTINF
      *
           MOVE    PTINF-REC           TO  MCPDATA-REC
           MOVE    "PTINF-KEY"         TO  ORC-DBPATH
           PERFORM 910-DBSELECT-SEC
           IF    ( MCP-RC              =   ZERO )
               MOVE    MCPDATA-REC     TO  PTINF-REC
               MOVE    ZERO            TO  FLG-PTINF
           ELSE
               MOVE    1               TO  FLG-PTINF
               INITIALIZE              PTINF-REC
           END-IF
      *
           MOVE    "DBCLOSECURSOR"     TO  MCP-FUNC
           PERFORM 910-DBCLOSE-SEC
      *
           .
       900-PTINF-KEY-SEL-EXT.
           EXIT.
      *****************************************************************
      *    入院履歴マスター読込
      *****************************************************************
       900-PTNYUINRRK-KEY-SEL-SEC      SECTION.
      *
           MOVE    ZERO                TO  FLG-PTNYUINRRK
      *
           MOVE    PTNYUINRRK-REC      TO  MCPDATA-REC
           MOVE    "PTNYUINRRK-KEY"    TO  ORC-DBPATH
           PERFORM 910-DBSELECT-SEC
           IF    ( MCP-RC              =   ZERO )
               MOVE    MCPDATA-REC     TO  PTNYUINRRK-REC
           ELSE
               MOVE    1               TO  FLG-PTNYUINRRK
           END-IF
      *
      *    カーソルクロース
           MOVE    "DBCLOSECURSOR"     TO  MCP-FUNC
           MOVE    "PTNYUINRRK-KEY"    TO  ORC-DBPATH
           PERFORM 910-DBCLOSE-SEC
      *
           .
       900-PTNYUINRRK-KEY-SEL-EXT.
           EXIT.
      *****************************************************************
      *    請求履歴情報読込
      *****************************************************************
       900-TEIKIRRK-KEY-SEL-SEC        SECTION.
      *
           MOVE    ZERO                TO  FLG-TEIKIRRK
      *
           MOVE    TEIKIRRK-REC        TO  MCPDATA-REC
           MOVE    "TEIKIRRK-KEY"      TO  ORC-DBPATH
           PERFORM 910-DBSELECT-SEC
           IF    ( MCP-RC              =   ZERO )
               MOVE    ZERO            TO  FLG-TEIKIRRK
               MOVE   MCPDATA-REC      TO  TEIKIRRK-REC
           ELSE
               MOVE    1               TO  FLG-TEIKIRRK
           END-IF
      *
           MOVE    "TEIKIRRK-KEY"      TO  ORC-DBPATH
           PERFORM 910-DBCLOSE-SEC
      *
           .
       900-TEIKIRRK-KEY-SEL-EXT.
           EXIT.
      *****************************************************************
      *    患者請求履歴情報読込
      *****************************************************************
       900-PTTEIKIRRK-KEY6-SEL-SEC     SECTION.
      *
           MOVE    ZERO                TO  FLG-PTTEIKIRRK
      *
           MOVE    PTTEIKIRRK-REC      TO  MCPDATA-REC
           MOVE    "PTTEIKIRRK-KEY6"   TO  ORC-DBPATH
           PERFORM 910-DBSELECT-SEC
           IF    ( MCP-RC              =   ZERO )
               MOVE   MCPDATA-REC      TO  PTTEIKIRRK-REC
           ELSE
               MOVE    1               TO  FLG-PTTEIKIRRK
               INITIALIZE                  PTTEIKIRRK-REC
           END-IF
      *
           MOVE    "PTTEIKIRRK-KEY6"   TO  ORC-DBPATH
           PERFORM 910-DBCLOSE-SEC
      *
           .
       900-PTTEIKIRRK-KEY6-SEL-EXT.
           EXIT.
      *****************************************************************
      *    日別収納更新処理
      *****************************************************************
       900-SYUDAY-UPD-SEC              SECTION.
      *
           MOVE    WKSYUDAY-REC        TO  SYUDAY-REC
           PERFORM 400-SYUDAY-CHK-SEC
           IF    ( FLG-SYUDAY          =   ZERO )
      *
      *        既に存在する場合はUPDATE
               MOVE    SDAY-CREYMD     TO  WRK-CREYMD
               MOVE    WKSYUDAY-REC    TO  SYUDAY-REC
      *
               PERFORM 800-MCNDATE-SEC
               MOVE    WRK-CREYMD      TO  SDAY-CREYMD
               MOVE    SMCNDATE-YMD    TO  SDAY-UPYMD
               MOVE    SMCNDATE-HMS    TO  SDAY-UPHMS
               MOVE    SYUDAY-REC      TO  MCPDATA-REC
               MOVE    "SYUDAY-KEY"    TO  ORC-DBPATH
               PERFORM 910-DBUPDATE-SEC
               IF  (   MCP-RC          NOT =   ZERO )
                   MOVE  ERR-MES10     TO  WRK-TEIKIERR
                   PERFORM 500-ERR-HENSYU-SEC
               END-IF
           ELSE
      *
      *        存在しない場合はINSERT
               MOVE    WKSYUDAY-REC    TO  SYUDAY-REC
      *
               PERFORM 800-MCNDATE-SEC
               MOVE    SMCNDATE-YMD    TO  SDAY-UPYMD
                                           SDAY-CREYMD
               MOVE    SMCNDATE-HMS    TO  SDAY-UPHMS
               MOVE    SYUDAY-REC      TO  MCPDATA-REC
               MOVE    "SYUDAY-KEY"    TO  ORC-DBPATH
               PERFORM 910-DBINSERT-SEC
               IF  (   MCP-RC          NOT =   ZERO )
                   MOVE  ERR-MES10     TO  WRK-TEIKIERR
                   PERFORM 500-ERR-HENSYU-SEC
               END-IF
           END-IF
      *
           .
       900-SYUDAY-UPD-EXT.
           EXIT.
      *****************************************************************
      *    日別収納削除処理
      *****************************************************************
       900-SYUDAY-DEL-SEC              SECTION.
      *
           MOVE    WKSYUDAY-REC    TO  MCPDATA-REC
           MOVE    "SYUDAY-KEY"    TO  ORC-DBPATH
           PERFORM 910-DBDELETE-SEC
           IF  (   MCP-RC          NOT =   ZERO )
               MOVE  ERR-MES11     TO  WRK-TEIKIERR
               PERFORM 500-ERR-HENSYU-SEC
           END-IF
      *
           .
       900-SYUDAY-DEL-EXT.
           EXIT.
      *****************************************************************
      *    患者番号マスター読込（主キー）
      *****************************************************************
       900-PTNUM-KEY-SEL-SEC           SECTION.
      *
           MOVE    ZERO                TO  FLG-PTNUM
      *
           INITIALIZE                  PTNUM-REC
           MOVE    TEIKI1P-HOSPID      TO  PTNUM-HOSPID
           MOVE    WRK-PTID            TO  PTNUM-PTID
           MOVE    PTNUM-REC           TO  MCPDATA-REC
           MOVE    "PTNUM-KEY"         TO  ORC-DBPATH
           PERFORM 910-DBSELECT-SEC
           IF    ( MCP-RC              =   ZERO )
               MOVE    MCPDATA-REC     TO  PTNUM-REC
           ELSE
               MOVE    1               TO  FLG-PTNUM
               INITIALIZE                  PTNUM-REC
           END-IF
      *
           MOVE    "PTNUM-KEY"         TO  ORC-DBPATH
           PERFORM 910-DBCLOSE-SEC
      *
           .
       900-PTNUM-KEY-SEL-EXT.
           EXIT.
      *****************************************************************
      *    ＤＢ検索処理（FHETCHも行う)
      *****************************************************************
       910-DBSELECT-SEC                SECTION.
      *
           MOVE    "DBSELECT"          TO  MCP-FUNC
           CALL    "ORCMCPSUB"         USING
                                       MCPAREA
                                       ORCMCPAREA
                                       MCPDATA-REC
           IF    ( MCP-RC          =   ZERO )
               PERFORM 910-DBFETCH-SEC
           END-IF
      *
           .
      *
       910-DBSELECT-EXT.
           EXIT.
      *****************************************************************
      *    ＤＢ検索処理（FHETCHは行わない)
      *****************************************************************
       911-DBSELECT-SEC                SECTION.
      *
           MOVE    "DBSELECT"          TO  MCP-FUNC
           CALL    "ORCMCPSUB"         USING
                                       MCPAREA
                                       ORCMCPAREA
                                       MCPDATA-REC
      *
           .
      *
       911-DBSELECT-EXT.
           EXIT.
      *****************************************************************
      *    ＤＢ読み込み処理
      *****************************************************************
       910-DBFETCH-SEC                 SECTION.
      *
           MOVE    "DBFETCH"           TO  MCP-FUNC
           CALL    "ORCMCPSUB"         USING
                                       MCPAREA
                                       ORCMCPAREA
                                       MCPDATA-REC
      *
           .
      *
       910-DBFETCH-EXT.
           EXIT.
      *****************************************************************
      *    ＤＢクローズ処理
      *****************************************************************
       910-DBCLOSE-SEC                 SECTION.
      *
           MOVE    "DBCLOSECURSOR"     TO  MCP-FUNC
           CALL    "ORCMCPSUB"         USING
                                       MCPAREA
                                       ORCMCPAREA
                                       MCPDATA-REC
      *
           .
      *
       910-DBCLOSE-EXT.
           EXIT.
      *****************************************************************
      *    ＤＢ更新処理
      *****************************************************************
       910-DBUPDATE-SEC                SECTION.
      *
           MOVE    "DBUPDATE"          TO  MCP-FUNC
           CALL    "ORCMCPSUB"         USING
                                       MCPAREA
                                       ORCMCPAREA
                                       MCPDATA-REC
      *
           .
      *
       910-DBUPDATE-EXT.
           EXIT.
      *****************************************************************
      *    ＤＢ追加処理
      *****************************************************************
       910-DBINSERT-SEC                SECTION.
      *
           MOVE    "DBINSERT"          TO  MCP-FUNC
           CALL    "ORCMCPSUB"         USING
                                       MCPAREA
                                       ORCMCPAREA
                                       MCPDATA-REC
      *
           .
      *
       910-DBINSERT-EXT.
           EXIT.
      *****************************************************************
      *    ＤＢ削除
      *****************************************************************
       910-DBDELETE-SEC                SECTION.
      *
           MOVE    "DBDELETE"          TO  MCP-FUNC
           CALL    "ORCMCPSUB"         USING
                                       MCPAREA
                                       ORCMCPAREA
                                       MCPDATA-REC
      *
           .
      *
       910-DBDELETE-EXT.
           EXIT.
      *****************************************************************
      *    ＤＢ　オープン処理
      *****************************************************************
       100-DBOPEN-SEC                SECTION.
      *
           MOVE    "DBOPEN"            TO  MCP-FUNC.
           CALL    "ORCMCPSUB"          USING
                                        MCPAREA
                                        ORCMCPAREA
                                        MCPDATA-REC.
      *
           MOVE    "DBSTART"           TO  MCP-FUNC.
           CALL    "ORCMCPSUB"          USING
                                        MCPAREA
                                        ORCMCPAREA
                                        MCPDATA-REC.
           .
       100-DBOPEN-EXT.
           EXIT.
      *

      *****************************************************************
      *    ＤＢ　クローズ処理
      *****************************************************************
       900-DBDISCONNECT-SEC            SECTION.
      *
           IF    ( FLG-ERR         =   ZERO )
               MOVE    "DBCOMMIT"          TO  MCP-FUNC
               CALL    "ORCMCPSUB"         USING
                                           MCPAREA
                                           ORCMCPAREA
                                           MCPDATA-REC
           END-IF
      *
           MOVE    "DBDISCONNECT"      TO  MCP-FUNC.
           CALL    "ORCMCPSUB"          USING
                                        MCPAREA
                                        ORCMCPAREA
                                        MCPDATA-REC.
      *
           .
       900-DBDISCONNECT-EXT.
           EXIT.
      *****************************************************************
      *    プログラム終了処理
      *****************************************************************
       990-PROGRAM-FINISH-SEC          SECTION.
      *
           STOP   RUN
      *
           .
       990-PROGRAM-FINISH-EXT.
           EXIT.

      *******************************************************************
      * Project code name "ORCA"
      * 日医標準レセプトソフト（JMA standard receipt software）
      * Copyright(C) 2002 JMA (Japan Medical Association)
      *
      * This program is part of "JMA standard receipt software".
      *
      *     This program is distributed in the hope that it will be useful
      * for further advancement in medical care, according to JMA Open
      * Source License, but WITHOUT ANY WARRANTY.
      *     Everyone is granted permission to use, copy, modify and
      * redistribute this program, but only under the conditions described
      * in the JMA Open Source License. You should have received a copy of
      * this license along with this program. If not, stop using this
      * program and contact JMA, 2-28-16 Honkomagome, Bunkyo-ku, Tokyo,
      * 113-8621, Japan.
      ********************************************************************
       IDENTIFICATION          DIVISION.
       PROGRAM-ID.             ORCHC14.
      *****************************************************************
      *  システム名        : ＯＲＣＡ
      *  サブシステム名    : 予約登録
      *  コンポーネント名  : 予約一覧表　印刷（ＨＣ１４）
      *  管理者            : 
      *  作成日付    作業者        記述
      *  01/10/26    MCC−多々納   新規作成
      *****************************************************************
      *  プログラム修正履歴
      * Maj/Min/Rev  修正者       日付      内容
      *  01.00.01    MCC-藤原     02.03.18  帳票ファイル名の変更
      *  01.00.02    NACL-多々納  03/05/19  診療内容・ドクター選択追加
      *  01.00.03    NACL-多々納  03/06/27  出力先プリンタサブ追加
      *  01.00.04    NACL-多々納  06/05/15  MONFUNC 対応 他
      *  03.05.00    NACL-多々納  07/05/07  グループ診療対応
      *  04.05.00    NACL-多々納  09/06/XX  カスタマイズ帳票対応
      *****************************************************************
      *
       ENVIRONMENT             DIVISION.
       CONFIGURATION               SECTION.
       INPUT-OUTPUT                SECTION.
       FILE-CONTROL.
      *
      *    印刷用データ
           SELECT  PRT-FILE        ASSIGN  HC01PARA
                                   FILE    STATUS  IS  STS-PARA.
      *
       DATA                    DIVISION.
       FILE                        SECTION.
      *
      *    印刷用データ
       FD  PRT-FILE.
      *    予約一覧
           COPY    "HC14.INC".
      *
       WORKING-STORAGE             SECTION.
      *
      *     印刷用データ 名称領域 
           COPY    "CPCOMMONPRT.INC".
      *
      *    フラグ領域
       01  STS-AREA.
           03  STS-PARA            PIC X(02).
      *
      *    フラグ領域
       01  FLG-AREA.
           03  FLG-END             PIC 9(01).
           03  FLG-YYK             PIC 9(01).
           03  FLG-SYSKANRI        PIC 9(01).
      *
           03  FLG-SYORI           PIC 9(01).
      *
      *    添字領域
       01  IDX-AREA.
           03  IDX                 PIC 9(04).
           03  IDY                 PIC 9(04).
           03  IDX-D               PIC 9(04).
      *    システム領域
       01  SYS-AREA.
           03  SYS-TIME            PIC 9(08).
      *
      *    一時領域
       01  WRK-AREA.
           03  WRK-SYMD.
               05  WRK-SYY         PIC 9(04).
               05  WRK-SMM         PIC 9(02).
               05  WRK-SDD         PIC 9(02).
           03  WRK-WYMD.
               05  WRK-WYY         PIC X(03).
               05  WRK-WMM         PIC 9(02).
               05  WRK-WDD         PIC 9(02).
           03  WRK-HENYMDG         PIC X(22).
      *
           03  WRK-PATH            PIC X(64).
      *
           03  WRK-PTNUM           PIC X(20).
           03  WRK-PAGE            PIC 9(03).
           03  WRK-LINE            PIC 9(04).
      *
           03  WRK-ZZ9             PIC ZZ9.
           03  WRK-ZZZ9            PIC ZZZ9.
      *
      *    ドクターテーブル
       01  TBL-DRCD-AREA.
           03  TBL-DRCD-G              OCCURS   99.
               05  TBL-DRCD            PIC X(05).
               05  TBL-DRCDNAME        PIC X(30).
      *
      *    診療内容テーブル
       01  TBL-SRYNAIYO-AREA.
           03  TBL-SRYNAIYO-G          OCCURS   99.
               05  TBL-SRYNAIYO        PIC X(02).
               05  TBL-SRYNAIYOMEI     PIC X(24).
      *
      *    診療科テーブル
       01  TBL-SRYKA-AREA.
           03  TBL-SRYKA-G             OCCURS   99.
               05  TBL-SRYKA-CD        PIC X(02).
               05  TBL-SRYKANAME       PIC X(30).
      *    連絡先テーブル
       01  TBL-RENRAK-AREA.
           03  TBL-RENRAK-G            OCCURS   9.
               05  TBL-RENRAK-CD       PIC X(01).
               05  TBL-YYKKAKNAME      PIC X(10).
      *    予約内容テーブル
       01  TBL-YYKKBN-AREA.
           03  TBL-YYKKBN-G            OCCURS   99.
               05  TBL-YYKKBN-CD       PIC X(02).
               05  TBL-YYKKBNNAME      PIC X(30).
      *
      *****************************************************************
      *    ファイルレイアウト
      *****************************************************************
      *
      *    システム管理マスタ
           COPY    "CPSYSKANRI.INC".
      *
      *    出力先プリンタ名割り当て情報
           COPY  "CPSK1031.INC".
      *
      *    診療内容情報
           COPY    "CPSK1012.INC".
      *    診療科
           COPY    "CPSK1005.INC".
      *    職員情報
           COPY    "CPSK1010.INC".
      *    予約確認
           COPY    "CPSK0051.INC".
      *    予約内容
           COPY    "CPSK1028.INC".
      *
      *    患者マスタ−
       01  PTINF-REC.
           COPY    "CPPTINF.INC".
      *
      *    予約マスター
       01  YYK-REC.
           COPY    "CPYYK.INC".
      *
      *****************************************************************
      *    サブプロ用　領域
      *****************************************************************
      *
      *   日付変換サブ
           COPY    "CPORCSDAY.INC".
           COPY    "CPORCSLNK.INC".
      *
      *   患者番号変換サブ
           COPY    "CPORCSPTID.INC".
      *
      *    印刷パラメタ
           COPY    "CPORCSMKPRT.INC".
      *
      *    オンライン帳票名・出力先プリンタ名取得パラ
           COPY    "CPORCSPRTNM.INC".
      *
      *    予約一覧パラメタ領域
           COPY    "CPORCHC14.INC".
      *
           COPY    "MCPDATA.INC".
      *
           COPY    "MCPAREA".
      *
      *
      *****************************************************************
      *    連絡　領域
      *****************************************************************
        LINKAGE                     SECTION.
      *    スパ領域
           COPY    "COMMON-SPA".
      *
           COPY    "CPONPRTDATA.INC".
      *
       PROCEDURE                   DIVISION    USING
           SPA-AREA
           ONPRTDATA
           .
      *
      *****************************************************************
      *    主　　処理
      *****************************************************************
       000-PROC-SEC                SECTION.
      *
      *    初期編集処理
           PERFORM 100-INIT-SYORI-SEC
      *    予約一覧処理
           PERFORM 200-MAIN-SYORI-SEC
      *
           .
       000-PROC-EXT.
           EXIT    PROGRAM
           .
      *
      *****************************************************************
      *    初期編集処理
      *****************************************************************
       100-INIT-SYORI-SEC             SECTION.
      *
           MOVE    ONPRTDATA-DATA      TO  ORCHC14AREA
      *
           INITIALIZE                  FLG-AREA
           INITIALIZE                  STS-AREA
           INITIALIZE                  WRK-AREA
      *
           ACCEPT  SYS-TIME            FROM    TIME
      *
      *    各ＤＢ読込
           PERFORM 100-SYORI-SEC
           .
       100-INIT-SYORI-EXT.
           EXIT.
      *
      *****************************************************************
      *    帳票印刷開始処理
      *****************************************************************
       400-SYOKI-PRINT-SEC             SECTION.
      *
      *    ファイルＯＰＥＮ
           MOVE    SPA-HOSPNUM         TO  HC01PARA-HOSPNUM
           MOVE    "HC14"              TO  HC01PARA-FORM-ID
           MOVE    SPA-TERMID          TO  HC01PARA-TERMID
           MOVE    SYS-TIME            TO  HC01PARA-SYOHMS
      *
           MOVE    WRK-PAGE            TO  HC01PARA-CNT
           OPEN    OUTPUT               PRT-FILE
      *
           .
       400-SYOKI-PRINT-EXT.
           EXIT.
      *
      *****************************************************************
      *    帳票印刷処理
      *****************************************************************
       400-PRINT-SEC                SECTION.
      *
           PERFORM 400-SYOKI-PRINT-SEC
      *
      *　　印刷用データ出力
           WRITE   HC14
      *
            CLOSE                       PRT-FILE
      *
            INITIALIZE                 ORCSMKPRTAREA
            MOVE   "HC01.sh"           TO  MKPRT-ID
            MOVE   "HC14.red"          TO  MKPRT-DIA
            MOVE   SPACE               TO  MKPRT-DEF
            MOVE   HC01PARA            TO  MKPRT-DAT
            MOVE   SYS-1031-PRTNM(18)  TO  MKPRT-PRTNM
            CALL   "ORCSMKPRT"         USING
                                       ORCSMKPRTAREA
                                       SPA-AREA
      *
           .
       400-PRINT-EXT.
           EXIT.
      *
      *****************************************************************
      *    初期編集処理
      *****************************************************************
       100-SYORI-SEC                SECTION.
      *
      *    出力先プリンタ名割り当て情報編集
           INITIALIZE                      SYS-1031-REC
           INITIALIZE                      ORCSPRTNMAREA
           MOVE    "2"                 TO  ORCSPRTNM-KBN
           MOVE    SPA-SYSYMD          TO  ORCSPRTNM-SRYYMD
           MOVE    "2"                 TO  ORCSPRTNM-NYUGAIKBN
           CALL    "ORCSPRTNM"         USING
                                           ORCSPRTNMAREA
                                           SPA-AREA
                                           SYS-1031-REC
      *    予約一覧の出力先設定がないときは、受付一覧（元の予約一覧）
           IF      SYS-1031-PRTNM(18)  =   SPACE
               MOVE    SYS-1031-PRTNM(7)   TO  SYS-1031-PRTNM(18)
           END-IF
      *
      *    診療日
           MOVE    ORCHC14-YYKYMD      TO  WRK-SYMD
           PERFORM 31012-SEIWA-HEN-SEC
      *
      *    システム管理テーブル編集
           PERFORM 1001-SYSKANRI-TBL-SEC
           .
       100-SYORI-EXT.
           EXIT.
      *
      *****************************************************************
      *    システム管理テーブル編集処理
      *****************************************************************
       1001-SYSKANRI-TBL-SEC           SECTION.
      *
           INITIALIZE                  TBL-DRCD-AREA
           INITIALIZE                  TBL-SRYNAIYO-AREA
           INITIALIZE                  TBL-SRYKA-AREA
           INITIALIZE                  TBL-RENRAK-AREA
           INITIALIZE                  TBL-YYKKBN-AREA
      *    職員情報
           MOVE    SPACE               TO  SYS-1010-REC
           MOVE    SPA-HOSPNUM         TO  SYS-1010-HOSPNUM
           MOVE    "1010"              TO  SYS-1010-KANRICD
           MOVE    ORCHC14-YYKYMD      TO  SYS-1010-STYUKYMD
           MOVE    ORCHC14-YYKYMD      TO  SYS-1010-EDYUKYMD
      *
           MOVE    SYS-1010-REC        TO  MCPDATA-REC
           MOVE    "tbl_syskanri"      TO  MCP-TABLE
           MOVE    "key2"              TO  MCP-PATHNAME
           PERFORM 910-DBSELECT-SEC
           IF      MCP-RC              =   ZERO
               MOVE    "tbl_syskanri"      TO  MCP-TABLE
               MOVE    "key2"             TO  MCP-PATHNAME
               PERFORM 901-KANRIMST-READ-SEC
           ELSE
               MOVE    1                   TO  FLG-SYSKANRI
           END-IF
           MOVE    ZERO                TO  IDX
           PERFORM UNTIL   (FLG-SYSKANRI   =   1   )
                       OR  (IDX            >=  99  )
               MOVE    MCPDATA-REC         TO  SYS-1010-REC
               IF      SYS-1010-KBNCD(1:1) =   "1"
                   ADD     1                   TO  IDX
                   MOVE    SYS-1010-KBNCD(1:5) TO  TBL-DRCD  (IDX)
                   MOVE    SYS-1010-NAME       TO  TBL-DRCDNAME (IDX)
               END-IF
               MOVE    "tbl_syskanri"      TO  MCP-TABLE
               MOVE    "key2"              TO  MCP-PATHNAME
               PERFORM 901-KANRIMST-READ-SEC
           END-PERFORM
           MOVE    "tbl_syskanri"      TO  MCP-TABLE
           MOVE    "key2"              TO  MCP-PATHNAME
           PERFORM 990-DBCLOSE-SEC
      *    診療内容
           MOVE    SPACE               TO  SYS-1012-REC
           MOVE    SPA-HOSPNUM         TO  SYS-1012-HOSPNUM
           MOVE    "1012"              TO  SYS-1012-KANRICD
           MOVE    ORCHC14-YYKYMD      TO  SYS-1012-STYUKYMD
           MOVE    ORCHC14-YYKYMD      TO  SYS-1012-EDYUKYMD
      *
           MOVE    SYS-1012-REC        TO  MCPDATA-REC
           MOVE    "tbl_syskanri"      TO  MCP-TABLE
           MOVE    "key2"              TO  MCP-PATHNAME
           PERFORM 910-DBSELECT-SEC
           IF      MCP-RC              =   ZERO
               MOVE    "tbl_syskanri"      TO  MCP-TABLE
               MOVE    "key2"             TO  MCP-PATHNAME
               PERFORM 901-KANRIMST-READ-SEC
           ELSE
               MOVE    1                   TO  FLG-SYSKANRI
           END-IF
           MOVE    ZERO                TO  IDX
           PERFORM UNTIL   (FLG-SYSKANRI   =   1   )
                       OR  (IDX            >=  99  )
               MOVE    MCPDATA-REC         TO  SYS-1012-REC
               ADD     1                   TO  IDX
               MOVE    SYS-1012-KBNCD(1:2) TO  TBL-SRYNAIYO  (IDX)
               MOVE    SYS-1012-SRYNAIYODSP    TO  TBL-SRYNAIYOMEI (IDX)
      *
               MOVE    "tbl_syskanri"      TO  MCP-TABLE
               MOVE    "key2"              TO  MCP-PATHNAME
               PERFORM 901-KANRIMST-READ-SEC
           END-PERFORM
           MOVE    "tbl_syskanri"      TO  MCP-TABLE
           MOVE    "key2"              TO  MCP-PATHNAME
           PERFORM 990-DBCLOSE-SEC
      *
      *    診療科
           MOVE    SPACE               TO  SYS-1005-REC
           MOVE    "1005"              TO  SYS-1005-KANRICD
           MOVE    ORCHC14-YYKYMD      TO  SYS-1005-STYUKYMD
           MOVE    ORCHC14-YYKYMD      TO  SYS-1005-EDYUKYMD
      *    システム管理検索
           MOVE    SPA-HOSPNUM         TO  SYS-1005-HOSPNUM
           MOVE    SYS-1005-REC        TO  MCPDATA-REC
           MOVE    "tbl_syskanri"      TO  MCP-TABLE
           MOVE    "key2"              TO  MCP-PATHNAME
           PERFORM 910-DBSELECT-SEC
           IF      MCP-RC              =   ZERO
               MOVE    "tbl_syskanri"      TO  MCP-TABLE
               MOVE    "key2"             TO  MCP-PATHNAME
               PERFORM 901-KANRIMST-READ-SEC
           ELSE
               MOVE    1                   TO  FLG-SYSKANRI
           END-IF
           MOVE    ZERO                TO  IDX
           PERFORM UNTIL   (FLG-SYSKANRI   =   1   )
                       OR  (IDX            >=  99  )
               ADD     1                   TO  IDX
               MOVE    MCPDATA-REC         TO  SYS-1005-REC
               MOVE    SYS-1005-KBNCD(1:2) TO  TBL-SRYKA-CD  (IDX)
               MOVE    SYS-1005-SRYKANAME1 TO  TBL-SRYKANAME (IDX)
      *
               MOVE    "tbl_syskanri"      TO  MCP-TABLE
               MOVE    "key2"              TO  MCP-PATHNAME
               PERFORM 901-KANRIMST-READ-SEC
           END-PERFORM
           MOVE    "tbl_syskanri"      TO  MCP-TABLE
           MOVE    "key2"              TO  MCP-PATHNAME
           PERFORM 990-DBCLOSE-SEC
      *
      *    予約確認連絡コード
           MOVE    SPACE               TO  SYS-0051-REC
           MOVE    "0051"              TO  SYS-0051-KANRICD
           MOVE    ORCHC14-YYKYMD      TO  SYS-0051-STYUKYMD
           MOVE    ORCHC14-YYKYMD      TO  SYS-0051-EDYUKYMD
      *
           MOVE    SPA-HOSPNUM         TO  SYS-0051-HOSPNUM
           MOVE    SYS-0051-REC        TO  MCPDATA-REC
           MOVE    "tbl_syskanri"      TO  MCP-TABLE
           MOVE    "key2"              TO  MCP-PATHNAME
           PERFORM 910-DBSELECT-SEC
           IF      MCP-RC              =   ZERO
               MOVE    "tbl_syskanri"      TO  MCP-TABLE
               MOVE    "key2"              TO  MCP-PATHNAME
               PERFORM 901-KANRIMST-READ-SEC
           ELSE
               MOVE    1                   TO  FLG-SYSKANRI
           END-IF
           MOVE    ZERO                TO  IDX
           PERFORM UNTIL   (FLG-SYSKANRI   =   1   )
                       OR  (IDX            >=  9   )
               ADD     1                   TO  IDX
               MOVE    MCPDATA-REC         TO  SYS-0051-REC
               MOVE    SYS-0051-KBNCD(1:2) TO  TBL-RENRAK-CD (IDX)
               MOVE    SYS-0051-YYKKAKNAME TO  TBL-YYKKAKNAME (IDX)
      *
               MOVE    "tbl_syskanri"      TO  MCP-TABLE
               MOVE    "key2"              TO  MCP-PATHNAME
               PERFORM 901-KANRIMST-READ-SEC
           END-PERFORM
           MOVE    "tbl_syskanri"      TO  MCP-TABLE
           MOVE    "key2"              TO  MCP-PATHNAME
           PERFORM 990-DBCLOSE-SEC
      *
      *    予約内容コード
           MOVE    SPACE               TO  SYS-1028-REC
           MOVE    SPA-HOSPNUM         TO  SYS-1028-HOSPNUM
           MOVE    "1028"              TO  SYS-1028-KANRICD
           MOVE    ORCHC14-YYKYMD      TO  SYS-1028-STYUKYMD
           MOVE    ORCHC14-YYKYMD      TO  SYS-1028-EDYUKYMD
      *
           MOVE    SYS-1028-REC        TO  MCPDATA-REC
           MOVE    "tbl_syskanri"      TO  MCP-TABLE
           MOVE    "key2"              TO  MCP-PATHNAME
           PERFORM 910-DBSELECT-SEC
           IF      MCP-RC              =   ZERO
               MOVE    "tbl_syskanri"      TO  MCP-TABLE
               MOVE    "key2"              TO  MCP-PATHNAME
               PERFORM 901-KANRIMST-READ-SEC
           ELSE
               MOVE    1                   TO  FLG-SYSKANRI
           END-IF
           MOVE    ZERO                TO  IDX
           PERFORM UNTIL   (FLG-SYSKANRI   =   1   )
                       OR  (IDX            >=  99  )
               ADD     1                   TO  IDX
               MOVE    MCPDATA-REC         TO  SYS-1028-REC
               MOVE    SYS-1028-KBNCD(1:2) TO  TBL-YYKKBN-CD (IDX)
               MOVE    SYS-1028-YYKKBNNAME
                                           TO  TBL-YYKKBNNAME (IDX)
      *
               MOVE    "tbl_syskanri"      TO  MCP-TABLE
               MOVE    "key2"              TO  MCP-PATHNAME
               PERFORM 901-KANRIMST-READ-SEC
           END-PERFORM
           MOVE    "tbl_syskanri"      TO  MCP-TABLE
           MOVE    "key2"              TO  MCP-PATHNAME
           PERFORM 990-DBCLOSE-SEC
      *
           .
       1001-SYSKANRI-TBL-EXT.
           EXIT.
      *
      *****************************************************************
      *    予約一覧処理
      *****************************************************************
       200-MAIN-SYORI-SEC              SECTION.
      *
           MOVE    ZERO                TO  WRK-PAGE
           MOVE    ZERO                TO  WRK-LINE
      *
      *    予約一覧編集
           INITIALIZE                  YYK-KEY
           INITIALIZE                  YYK-REC
           MOVE    SPA-HOSPNUM         TO  YYK-HOSPNUM
           MOVE    ORCHC14-YYKYMD      TO  YYK-YYKYMD
           MOVE    ORCHC14-SRYNAIYO    TO  YYK-SRYNAIYO
           MOVE    ORCHC14-DRCD-G      TO  YYK-DRCD
           IF     (ORCHC14-SRYNAIYO    =   "00"  )  OR
                  (ORCHC14-DRCD        =   "9999")
               MOVE    "key5"          TO  WRK-PATH
           ELSE
               MOVE    "key3"          TO  WRK-PATH
           END-IF
      *
           MOVE    YYK-REC             TO  MCPDATA-REC
           MOVE    "tbl_yyk"           TO  MCP-TABLE
           MOVE    WRK-PATH            TO  MCP-PATHNAME
           PERFORM 910-DBSELECT-SEC
           IF      MCP-RC              =   ZERO
               MOVE    "tbl_yyk"           TO  MCP-TABLE
               MOVE    WRK-PATH            TO  MCP-PATHNAME
               PERFORM 900-YOYAKU-READ-SEC
           ELSE
               MOVE    1               TO  FLG-YYK
           END-IF
           MOVE    ZERO                TO  IDX
           INITIALIZE                  HC14
           PERFORM
                   UNTIL       FLG-YYK     =   1
               MOVE    ZERO                TO  FLG-SYORI
               IF     (ORCHC14-SRYNAIYO    =   "00"  )  OR
                      (ORCHC14-DRCD        =   "9999")
                   IF      ORCHC14-SRYNAIYO    NOT =   "00"
                       IF      ORCHC14-SRYNAIYO    =  YYK-SRYNAIYO
                           MOVE    1               TO  FLG-SYORI
                       END-IF
                   END-IF
                   IF      ORCHC14-DRCD        NOT =   "9999"
                       IF      ORCHC14-DRCD-G      =  YYK-DRCD
                           MOVE    1               TO  FLG-SYORI
                       END-IF
                   END-IF
                   IF     (ORCHC14-SRYNAIYO    =   "00"  )  AND
                          (ORCHC14-DRCD        =   "9999")
                       MOVE    1               TO  FLG-SYORI
                   END-IF
               ELSE
                   MOVE    1               TO  FLG-SYORI
               END-IF
      *        キーデータは対象外
               IF     (YYK-YYKID           =   ZERO )
                   MOVE    ZERO                TO  FLG-SYORI
               END-IF
      *
               IF      FLG-SYORI           =   1
                   IF      (ORCHC14-KBN        =   "1"     )  OR
                          ((ORCHC14-KBN        =   "2"  ) AND
                           (YYK-RAIINZUMI      =   SPACE ))
                       ADD     1               TO  IDX
                       IF      IDX             >   34
      *                    帳票印刷開始処理
                           PERFORM 200-HC14-PRINT-SEC
                           ADD     1               TO  IDX
                       END-IF
      *                明細編集処理
                       PERFORM 300-HC14-HEN-SEC
                   END-IF
               END-IF
      *
               MOVE    "tbl_yyk"           TO  MCP-TABLE
               MOVE    WRK-PATH            TO  MCP-PATHNAME
               PERFORM 900-YOYAKU-READ-SEC
           END-PERFORM
      *    カーソルクロース
           MOVE    "tbl_yyk"           TO  MCP-TABLE
           MOVE    WRK-PATH            TO  MCP-PATHNAME
           PERFORM 990-DBCLOSE-SEC
      *
           IF      IDX             >   ZERO
      *        帳票印刷開始処理
               PERFORM 200-HC14-PRINT-SEC
           END-IF
      *
           .
       200-MAIN-SYORI-EXT.
           EXIT.
      *****************************************************************
      *    帳票編集処理
      *****************************************************************
       200-HC14-PRINT-SEC                SECTION.
      *
           ADD     1                   TO  WRK-PAGE
           MOVE    WRK-PAGE            TO  WRK-ZZ9
           MOVE    WRK-ZZ9             TO  HC14-PAGE
      *
           IF      ORCHC14-KBN         =   "1"
               MOVE    "予約一覧表"        TO  HC14-TITLE
           ELSE
               MOVE    "未院一覧表"        TO  HC14-TITLE
           END-IF
      *    診療日
           MOVE    WRK-HENYMDG         TO  HC14-SRYYMD
      *
           PERFORM 400-PRINT-SEC
      *
           INITIALIZE                  HC14
           MOVE    ZERO                TO  IDX
           .
       200-HC14-PRINT-EXT.
           EXIT.
      *
      *****************************************************************
      *    帳票編集処理
      *****************************************************************
       300-HC14-HEN-SEC                SECTION.
      *
      *    連番号
           ADD     1                   TO  WRK-LINE
           MOVE    WRK-LINE            TO  WRK-ZZ9
           MOVE    WRK-ZZ9             TO  HC14-NUM   (IDX)
      *    患者番号
           IF      YYK-PTID            =   ZERO
               MOVE    SPACE               TO  HC14-PTNUM  (IDX)
           ELSE
               PERFORM 3001-KNJNO-HENSYU-SEC
               MOVE    WRK-PTNUM           TO  HC14-PTNUM  (IDX)
           END-IF
      *    患者名称
           MOVE    YYK-NAME            TO  HC14-NAME   (IDX)
      *    予約時間
           MOVE    YYK-YYKTIME(1:2)    TO  HC14-YYKTIME (IDX)(1:2)
           MOVE    ":"                 TO  HC14-YYKTIME (IDX)(3:1)
           MOVE    YYK-YYKTIME(3:2)    TO  HC14-YYKTIME (IDX)(4:2)
      *    ドクター
           PERFORM VARYING     IDX-D   FROM    1   BY  1
                   UNTIL      (IDX-D   >   99  )
                          OR  (TBL-DRCD (IDX-D)    =   SPACE)
               IF     YYK-DRCD             =   TBL-DRCD (IDX-D)
                   MOVE    TBL-DRCDNAME (IDX-D)
                                           TO  HC14-DRNAME (IDX)
                   MOVE    99              TO  IDX-D
               END-IF
           END-PERFORM
      *    診療内容をセット
           PERFORM VARYING     IDX-D   FROM    1   BY  1
                   UNTIL      (IDX-D   >   99  )
                          OR  (TBL-SRYNAIYO (IDX-D)    =   SPACE)
               IF     YYK-SRYNAIYO         =   TBL-SRYNAIYO (IDX-D)
                   MOVE    TBL-SRYNAIYOMEI (IDX-D)
                                           TO  HC14-SRYNAIYO (IDX)
                   MOVE    99              TO  IDX-D
               END-IF
           END-PERFORM
      *
      *    来院済み
           IF      YYK-RAIINZUMI       =   "1"
               MOVE    "済"            TO  HC14-RAIIN  (IDX)
           END-IF
      *
      *    診療科
           PERFORM VARYING     IDX-D   FROM    1   BY  1
                   UNTIL      (IDX-D   >   99  )
                          OR  (TBL-SRYKA-CD (IDX-D)    =   SPACE)
               IF     YYK-SRYKA            =   TBL-SRYKA-CD (IDX-D)
                   MOVE    TBL-SRYKANAME (IDX-D)
                                           TO  HC14-SRYKA  (IDX)
                   MOVE    99              TO  IDX-D
               END-IF
           END-PERFORM
      *
      *    予約確認連絡コード
           PERFORM VARYING     IDX-D   FROM    1   BY  1
                   UNTIL      (IDX-D   >   9   )
                          OR  (TBL-RENRAK-CD (IDX-D)   =   SPACE)
               IF     YYK-KAKREN            =   TBL-RENRAK-CD (IDX-D)
                   MOVE    TBL-YYKKAKNAME (IDX-D)
                                           TO  HC14-RENRAKU  (IDX)
                   MOVE    9               TO  IDX-D
               END-IF
           END-PERFORM
      *
      *    予約内容コード
           PERFORM VARYING     IDX-D   FROM    1   BY  1
                   UNTIL      (IDX-D   >   99   )
                          OR  (TBL-YYKKBN-CD (IDX-D)   =   SPACE)
               IF     YYK-YYKKBN            =   TBL-YYKKBN-CD (IDX-D)
                   MOVE    TBL-YYKKBNNAME (IDX-D)
                                           TO  HC14-YOYAKUNAIY (IDX)
                   MOVE    99              TO  IDX-D
               END-IF
           END-PERFORM
      *
           .
       310-HC14-HEN-EXT.
           EXIT.
      *
      *****************************************************************
      *    患者マスタ編集処理
      *****************************************************************
       3001-KNJNO-HENSYU-SEC           SECTION.
      *
      *    患者番号変換より、患者IDより患者NOを取得する
      *
           INITIALIZE                      ORCSPTIDAREA
           MOVE    SPA-HOSPNUM         TO  SPTID-HOSPNUM
           MOVE    YYK-PTID            TO  SPTID-PTID
      *
           CALL    "ORCSPTID"          USING  ORCSPTIDAREA
                                              SPA-AREA
           IF      SPTID-RC        NOT =   ZERO
               MOVE    SPACE           TO  WRK-PTNUM
           ELSE
               MOVE    SPTID-PTNUM     TO  WRK-PTNUM
           END-IF
      *
           .
       3001-KNJNO-HENSYU-EXT.
           EXIT.
      *
      *****************************************************************
      *    西暦日本語変換処理
      *****************************************************************
       31012-SEIWA-HEN-SEC        SECTION.
      *
           INITIALIZE                      STS-AREA-DAY
           INITIALIZE                      LNK-DAY2-AREA
           MOVE    "21"                TO  LNK-DAY2-IRAI
           MOVE    WRK-SYMD            TO  LNK-DAY2-YMD
           CALL    "ORCSDAY"           USING   STS-AREA-DAY
                                               LNK-DAY2-AREA
           MOVE    LNK-DAY2-EDTYMD3    TO  WRK-HENYMDG
           .
       31012-SEIWA-HEN-EXT.
           EXIT.
      *****************************************************************
      *    システム管理マスタ読み込み処理
      *****************************************************************
       900-SYSKANRI-READ-SEC         SECTION.
      *
           MOVE    "tbl_syskanri"      TO  MCP-TABLE
           MOVE    "key10"             TO  MCP-PATHNAME
           PERFORM 910-DBSELECT-SEC
           IF       MCP-RC             =   ZERO
               MOVE    "tbl_syskanri"      TO  MCP-TABLE
               MOVE    "key10"             TO  MCP-PATHNAME
               PERFORM 901-KANRIMST-READ-SEC
               IF      MCP-RC              =   ZERO
                   MOVE    MCPDATA-REC     TO  SYSKANRI-REC
               END-IF
           ELSE
               MOVE    1              TO  FLG-SYSKANRI
           END-IF
           MOVE    "tbl_syskanri"      TO  MCP-TABLE
           MOVE    "key10"             TO  MCP-PATHNAME
           PERFORM 990-DBCLOSE-SEC
           .
       900-SYSKANRI-READ-EXT.
           EXIT.
      *****************************************************************
      *    システム管理マスタ読み込み処理
      *****************************************************************
       901-KANRIMST-READ-SEC         SECTION.
      *
           PERFORM 920-DBFETCH-SEC
           IF      MCP-RC              =   ZERO
               MOVE    ZERO            TO  FLG-SYSKANRI
           ELSE
               MOVE    1               TO  FLG-SYSKANRI
           END-IF
      *
           .
       901-KANRIMST-READ-EXT.
           EXIT.
      *
      *****************************************************************
      *    予約マスター読込
      *****************************************************************
       900-YOYAKU-READ-SEC         SECTION.
      *
           PERFORM 920-DBFETCH-SEC
           IF      MCP-RC              =   ZERO
               MOVE    ZERO                TO  FLG-YYK
               MOVE    MCPDATA-REC         TO  YYK-REC
           ELSE
               MOVE    1                   TO  FLG-YYK
           END-IF
           .
      *
       900-YOYAKU-READ-EXT.
           EXIT.
      *****************************************************************
      *    テーブル検索処理
      *****************************************************************
       910-DBSELECT-SEC                SECTION.
      *
           MOVE    "DBSELECT"      TO  MCP-FUNC
           CALL    "ORCDBMAIN"     USING
                                       MCPAREA
                                       MCPDATA-REC
                                       SPA-AREA
      *
           .
       910-DBSELECT-EXT.
           EXIT.
      *****************************************************************
      *    ＤＢ読み込み処理
      *****************************************************************
       920-DBFETCH-SEC                 SECTION.
      *
           MOVE    "DBFETCH"       TO  MCP-FUNC
           CALL    "ORCDBMAIN"     USING
                                       MCPAREA
                                       MCPDATA-REC
                                       SPA-AREA
      *
           .
       920-DBFETCH-EXT.
           EXIT.
      *
      *****************************************************************
      *    ＤＢクロース処理
      *****************************************************************
       990-DBCLOSE-SEC         SECTION.
      *
           MOVE    "DBCLOSECURSOR"     TO  MCP-FUNC
           CALL    "ORCDBMAIN"         USING
                                           MCPAREA
                                           MCPDATA-REC
                                           SPA-AREA
      *
           .
       990-DBCLOSE-EXT.
           EXIT.
      *
      *****************************************************************
      *    終了　　処理
      *****************************************************************
       300-END-SEC                 SECTION.
      *
           .
       300-END-EXT.
           EXIT.
      *

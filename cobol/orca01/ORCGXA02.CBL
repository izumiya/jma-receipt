      *******************************************************************
      * Project code name "ORCA"
      * 日医標準レセプトソフト（JMA standard receipt software）
      * Copyright(C) 2002 JMA (Japan Medical Association)
      *
      * This program is part of "JMA standard receipt software".
      *
      *     This program is distributed in the hope that it will be useful
      * for further advancement in medical care, according to JMA Open
      * Source License, but WITHOUT ANY WARRANTY.
      *     Everyone is granted permission to use, copy, modify and
      * redistribute this program, but only under the conditions described
      * in the JMA Open Source License. You should have received a copy of
      * this license along with this program. If not, stop using this
      * program and contact JMA, 2-28-16 Honkomagome, Bunkyo-ku, Tokyo,
      * 113-8621, Japan.
      ********************************************************************
       IDENTIFICATION              DIVISION.
       PROGRAM-ID.                 ORCGXA02.
      *****************************************************************
      *  システム名        : ＯＲＣＡ
      *  サブシステム名    : 再印刷(オンライン帳票)
      *  コンポーネント名  : 再印刷指定（ＸＡ０２）
      *  管理者            : 
      *  作成日付    作業者        記述
      *  02/07/00    NACL−竹田    新規作成
      *****************************************************************
      *  プログラム修正履歴
      * Maj/Min/Rev  修正者       日付      内容
      *****************************************************************
      *
       ENVIRONMENT                 DIVISION.
       CONFIGURATION               SECTION.
       INPUT-OUTPUT                SECTION.
       FILE-CONTROL.
      *
      *    エラーファイル
           SELECT  RECEERR-FILE    ASSIGN  RECEERR
                                   FILE    STATUS  IS  STS-RECEERR.
      *
       DATA                    DIVISION.
       FILE                        SECTION.
      *
      *    エラーファイル
       FD  RECEERR-FILE.
       01  RECEERR-REC             PIC X(200). 
      *
       WORKING-STORAGE             SECTION.
      *
      *     レセプト明細ファイル シェル領域 
      **** COPY    "CPCOMMONSHELL1.INC".
      *
      *    エラーファイル 名称領域 
           COPY    "CPCOMMONDAT2.INC"
                                   REPLACING  //RECE01PARA//
                                   BY         //RECEERR//.
           03  FILLER              PIC X(04)   VALUE   ".dat".
      *
      *    スパ領域
           COPY    "COMMON-SPA".
      *
      *    再印刷処理共通パラメタ
           COPY    "XA02COMMON-SPA".
      *
       01  SPA-XA02.
           03  SPA-XA02-AREA.
               05  SPA-GMN-PAGE        PIC 9(05).
               05  SPA-GMN-MAX         PIC 9(03).
               05  SPA-GMN-CUR         PIC 9(02).
               05  SPA-GMN-YMD         PIC X(08).
      *
               05  SPA-GMN-AREA.
                   07  SPA-GMN-TITLE1              PIC X(08).
                   07  SPA-GMN-TITLE2              PIC X(10).
                   07  SPA-GMN-TBL.
                       09  SPA-GMN-TBL-NEXT        PIC 9(01).
                       09  SPA-GMN-OCC             OCCURS  300.
                           11  SPA-GMN-TNO         PIC 9(03).
                           11  SPA-GMN-TTBL-GROUP  PIC X(14).
                           11  SPA-GMN-TRENNUM     PIC 9(04).
                           11  SPA-GMN-TSHORI-RENNUM
                                                   PIC 9(04).
                           11  SPA-GMN-TPAGE       PIC 9(05).
                           11  SPA-GMN-TSRYYM      PIC X(06).
                           11  SPA-GMN-GYOUMU      PIC X(12).
                           11  SPA-GMN-TSKYYMD     PIC X(09).
                           11  SPA-GMN-LPTNUM
                                                   PIC X(20).
                           11  SPA-GMN-LPTNAME     PIC X(50).
                           11  SPA-GMN-TPRT-TITLE  PIC X(60).
                   07  SPA-GMN-BODY.        
                       09  SPA-GMN-SELNUM          PIC 9(03).
                       09  SPA-GMN-BODY1.
                           11  SPA-GMN-TBL-GROUP   PIC X(14).
                           11  SPA-GMN-TBL-UUID    PIC X(36).
                           11  SPA-GMN-RENNUM      PIC 9(04).
                           11  SPA-GMN-SHORI-RENNUM
                                                   PIC 9(04).
                           11  SPA-GMN-STPAGE      PIC 9(05).
                           11  SPA-GMN-EDPAGE      PIC 9(05).
                       09  SPA-GMN-BODY2.
                           11  SPA-GMN-GYOMU-COMB  PIC X(35).
                           11  SPA-GMN-G-COUNT     PIC S9(09)  BINARY.
                           11  SPA-GMN-G-ITEM      PIC X(35)
                                                   OCCURS  100.
                   07  SPA-GMN-PTNUM               PIC X(20).
                   07  SPA-GMN-PTNAME              PIC X(50).
                   07  SPA-GMN-SRYYMD              PIC X(10).
      *                 
               05  SPA-NAI-AREA.
                   07  SPA-NAI-SELNUM              PIC 9(03).      
                   07  SPA-NAI-TBL.
                       09  SPA-NAI-OCC             OCCURS  300.
                           11  SPA-NAI-TTBL-KEY    PIC X(08).
                           11  SPA-NAI-TTBL-GROUP  PIC X(14).
                           11  SPA-NAI-TTBL-UUID   PIC X(36).
                           11  SPA-NAI-TPRTFLG     PIC X(01).
                           11  SPA-NAI-LPTID       PIC 9(10).
                   07  SPA-NAI-BODY2.
                       09  SPA-NAI-GYOMU-COMB.
                           11  SPA-NAI-GYOMU-CD    PIC X(02).
                           11  SPA-NAI-GYOMU-FIL   PIC X(01).
                           11  SPA-NAI-GYOMU-NAME  PIC X(32).
                       09  SPA-NAI-G-COUNT         PIC S9(09)  BINARY.
                       09  SPA-NAI-G-ITEM          OCCURS  100.
                           11  SPA-NAI-ITEM-CD    PIC X(02).
                           11  SPA-NAI-ITEM-FIL   PIC X(01).
                           11  SPA-NAI-ITEM-NAME  PIC X(32).
      *
      *                     
               05  SPA-WRK-TBL-KEY                 PIC X(08).            
      *
      *   
           COPY    "ENUM-VALUE".
      *
      *   
           COPY    "CMJOBNAME.INC".
      *
           COPY    "CPLOCKCONST.INC".
      *    ファイルデイレクトリ位置指定サブ
           COPY  "CPMKPASS.INC".
      *
      *    フラグ領域
       01  STS-AREA.
           03  STS-RECEERR         PIC X(02).
      *
      *    カウント領域
       01  CNT-AREA.
           03  CNT-PRTKANRI        PIC 9(05).
      *
      *    フラグ領域
       01  FLG-AREA.
           03  FLG-SYSKANRI                        PIC 9(01).
           03  FLG-PRTKANRI                        PIC 9(01).
           03  FLG-PRTKANRI2                       PIC 9(01).
           03  FLG-END                             PIC 9(01).
           03  FLG-LIST-ROW                        PIC 9(01).
           03  FLG-LDMAP                           PIC 9(01).
           03  FLG-READ-SKIP                       PIC 9(01).
           03  FLG-PTINF                           PIC 9(01).
      *
      *    システム領域
       01  SYS-AREA.
           03  SYS-YMD.
               05  SYS-YY          PIC 9(02).
               05  SYS-MM          PIC 9(02).
               05  SYS-DD          PIC 9(02).
           03  WRK-SYSYMD.
               05  WRK-SYSYY       PIC 9(04).
               05  WRK-SYSMM       PIC 9(02).
               05  WRK-SYSDD       PIC 9(02).
           03  SYS-TIME            PIC X(06).
      *
      *    添字領域
       01  IDX-AREA.
           03  IDX                 PIC 9(04).
           03  IDY                 PIC 9(04).
           03  IDZ                 PIC 9(04).
      *
      *    一時領域
       01  WRK-AREA.
           03  WRK-PRTKANRI-TBL-GROUP
                                   PIC X(14).
           03  WRK-PRTKANRI-RENNUM PIC 9(04).
           03  WRK-RECEERR         PIC X(200).
           03  WRK-XAIDMSG         PIC X(70).
           03  WRK-SYMD.
               05  WRK-SYY         PIC 9(04).
               05  WRK-SMM         PIC 9(02).
               05  WRK-SDD         PIC 9(02).
           03  WRK-WYMD.
               05  WRK-WGO         PIC X(01).
               05  WRK-WYY         PIC 9(02).
               05  WRK-WMM         PIC 9(02).
               05  WRK-WDD         PIC 9(02).
           03  WRK-PAGE            PIC ZZZZZZZZZ9.   
           03  WRK-MOTOPG          PIC X(08).
           03  WRK-XA02-MOTOPG     PIC X(08).
           03  WRK-SELNUM          PIC 9(03). 
      *
           03  WRK-ZZ              PIC ZZ9.
           03  WRK-YMD             PIC X(10).
           03  WRK-ZOGENPTN        PIC X(01).
           03  WRK-ZOGEN           PIC S9(05).
           03  WRK-GYOMU-COMB-G.
               05  WRK-GYOMU-CD    PIC X(02).
               05  WRK-GYOMU-FIL   PIC X(01).
               05  WRK-GYOMU-NAME  PIC X(32).
      *  
           03  WRK-MCP-PATHNAME    PIC X(64).
      *  
           03  WRK-STPAGE          PIC 9(05).
           03  WRK-EDPAGE-G.
               05  WRK-EDPAGE      PIC 9(05).
      *
       01  WRK-HEN-AREA.
           03  WRK-HENYMDG         PIC X(09).
           03  WRK-HENYMD.
               05  WRK-HGO         PIC X(01).
               05  WRK-HYY         PIC 9(02).
               05  FILLER          PIC X(01)   VALUE   ".".
               05  WRK-HMM         PIC 9(02).
               05  FILLER          PIC X(01)   VALUE   ".".
               05  WRK-HDD         PIC 9(02).
           03  WRK-HENTIME.
               05  WRK-HH          PIC X(02).
               05  FILLER          PIC X(01)   VALUE   ".".
               05  WRK-MM          PIC X(02).
               05  FILLER          PIC X(01)   VALUE   ".".
               05  WRK-SS          PIC X(02).
      *
       01  WRK-ROW.
           05  WRK-LIST-ROW        PIC S9(9)   BINARY.
      *                             
       01  WRK-SHELL-NAME.
           05  FILLER             PIC X(16) VALUE
                                             "scripts/allways/".
           05  WRK-SHELL-SHELL    PIC X(20).         
      *
           COPY    "CPSHELLTBL.INC".
      *     
      *    COPY    "ORCA-DBPATH".
      *
       01  WRK-LNK-AREA.
           COPY    "CPORCSPRTLNK.INC".   
      *     
      *    プレビュー用情報
           COPY    "CPXC01LNK.INC".
      *              
      *
       01  WRK-CONS-AREA.
      *    システム管理ＤＢ（３００３）のキー値  
           03  WRK-CONS-3003-KBNCD PIC X(08)   VALUE   "001".
           03  WRK-CONS-3003-KBNCD-LIKE        PIC X(08)
                                               VALUE   "001%".
      *    印刷管理ＤＢのキー値  
           03  WRK-CONS-PRTKANRI-TBL-KEY
                                   PIC X(08)   VALUE   "REPRINT".
           03  WRK-CONS-PRTKANRI-TBL-GROUP
                                   PIC X(14)   VALUE   SPACE.
      *    ジョブ管理ＤＢのキー値  
           03  WRK-CONS-JOB-SHELLID
                                   PIC X(08)   VALUE   "REPRINT".
           03  WRK-CONS-JOB-JOBID  PIC 9(07)   VALUE   ZERO.
      *
           03  WRK-CONS-LINE-MAX   PIC 9(03)   VALUE   100.
      *
           03  WRK-CONS-TBL-MAX    PIC 9(03)   VALUE   300.
      *
           03  WRK-CONS-TBL-LIMIT  PIC 9(03)   VALUE   50.
      *
      **** COPY    "XC01COMMON-SPA".
      *****************************************************************
      *    ファイルレイアウト
      *****************************************************************
      *
      *    システム管理マスタ
           COPY    "CPSYSKANRI.INC".
      *
           COPY    "CPSK1031.INC".
      *
           COPY    "CPSK3003.INC".
      *
       01  PRTKANRI-REC.
           COPY    "CPPRTKANRI.INC".
      *
       01  PRTDATA-REC.
           COPY    "CPPRTDATA.INC".          
      *
      *    ジョブ管理マスタ
       01  JOBKANRI-REC.
           COPY    "CPJOBKANRI.INC".
      *
      *    LDMAP情報（詳細）
           COPY  "CPLDMAP.INC".
      *    患者マスタ−
       01  PTINF-REC.
           COPY    "CPPTINF.INC".
      *
      *****************************************************************
      *    サブプロ用　領域
      *****************************************************************
      *
      *   日付変換サブ
           COPY    "CPORCSDAY.INC".
           COPY    "CPORCSLNK.INC".
      *   日付変換サブ
           COPY    "CPORCSGDAY.INC".
      *
      *    ＤＢ検索
           COPY    "MCPDATA.INC".
           COPY    "CPORCMCP.INC".
      *
      *   ジョブ管理ＤＢ制御サブ
           COPY    "CPORCSJOBKANRI.INC".
      *
      *   患者番号変換サブ
           COPY    "CPORCSPTID.INC".
      *****************************************************************
      *    連絡　領域
      *****************************************************************
        LINKAGE                     SECTION.
      *
           COPY    "MCPAREA".
           COPY    "ORCA-SPA".
      *
           COPY    "LINKAREA". 
      *
       01  SCRAREA.
           COPY    "XA01.INC".
           COPY    "XA02.INC".
           COPY    "XA99.INC".
           COPY    "XAERR.INC".
           COPY    "XAID1.INC".
           COPY    "XAID2.INC".
      *
      *
       PROCEDURE                   DIVISION    USING
           MCPAREA
           SPAAREA
           LINKAREA
           SCRAREA.
      *     
      *****************************************************************
      *    主　　処理
      *****************************************************************
       000-PROC-SEC                SECTION.
      *
           INITIALIZE                  FLG-AREA
           INITIALIZE                  WRK-AREA
           INITIALIZE                  IDX-AREA
           INITIALIZE                  CNT-AREA
      *
           MOVE    SPA-COMMON      TO  SPA-AREA
           MOVE    SPA-FREE        TO  SPA-XA02
           MOVE    SPA-WORK        TO  SPA-XA02KYOUTU 
      *
           MOVE    SPACE           TO  SPA-ERRCD
           MOVE    ZERO            TO  FLG-END
      *
           EVALUATE    MCP-STATUS      ALSO    MCP-EVENT
               WHEN    "LINK"          ALSO    ANY
                   PERFORM 100-INIT-SEC
      *    画面遷移
               WHEN     "PUTG"           ALSO   "CLICKED"
                   PERFORM 200-GMNSENI
      *    入力
               WHEN      OTHER
                   PERFORM 200-ENTRY
           END-EVALUATE.
      *
      *    画面遷移がない時、画面表示へ
           IF      FLG-END             =   ZERO
               PERFORM 500-SET-SCREEN
           END-IF
      *
           MOVE    SPA-XA02KYOUTU  TO  SPA-WORK  
           MOVE    SPA-AREA        TO  SPA-COMMON
           MOVE    SPA-XA02        TO  SPA-FREE
      *
           .
           EXIT    PROGRAM.
           .
      *****************************************************************
      *    初期　処理
      *****************************************************************
       100-INIT-SEC                   SECTION.
      *
      *    エラー画面より
           IF      SPA-MOTOPG          =   "XAERR"
               MOVE    SPACE               TO  SPA-MOTOPG
               PERFORM 5001-MAPCUR-SEC
           ELSE
      *        初期画面セット
               PERFORM 300-SCREEN-SEC
               IF      FLG-END             =   1
                   GO  TO  100-INIT-EXT
               END-IF
      *        画面編集
               PERFORM 500-GMNHEN-SEC
               IF      SPA-ERRCD       NOT =   SPACE
                   PERFORM 510-ERRSET-SEC
                   GO  TO  100-INIT-EXT
               END-IF
           END-IF
      *
      *****MOVE    SPACE               TO  LINKAREA
      *
           MOVE    SPACE               TO  MCP-PUTTYPE
           MOVE    "XA02"              TO  MCP-WINDOW
      *
           PERFORM 900-PUT-WINDOW.
      *
           MOVE    1                   TO  FLG-END
           .
       100-INIT-EXT.
           EXIT.
      *
      *****************************************************************
      *    初期画面処理
      *****************************************************************
       300-SCREEN-SEC              SECTION.
      *
           MOVE    SPACE           TO  WRK-MOTOPG
      *
           EVALUATE    SPA-MOTOPG
      *    作表処理
               WHEN    "XA99" 
                   PERFORM 340-XA99-SET-SEC
                   GO      TO      300-SCREEN-EXT
               WHEN    "XAID1"
                   PERFORM 330-XAID1-SET-SEC
                   GO      TO      300-SCREEN-EXT
               WHEN    "XAID2"
                   PERFORM 330-XAID2-SET-SEC
                   GO      TO      300-SCREEN-EXT
           END-EVALUATE
      *
      *    他のＬＤより
           IF      LINKAREA        NOT =   SPACE
               MOVE    LNK-KYOUTU          TO  SPA-KYOUTU
               MOVE    SPA-MOTOPG          TO  WRK-MOTOPG
      *
      *        元の画面へ戻す
               EVALUATE    WRK-MOTOPG
               WHEN    "XB01"
                   INITIALIZE              SPA-XA02-AREA
                                           SPA-XA02KYOUTU
                   MOVE    SPA-WORK        TO  LNK-XC01-AREA
                   MOVE    LNK-XC01-MOTOPG TO  SPA-XA02-MOTOPG
                   MOVE    LNK-XC01-TBL-UUID
                                           TO  SPA-GMN-TBL-UUID
                   MOVE    LNK-XC01-GYOUMU-CD
                                           TO  SPA-NAI-GYOMU-CD
                   MOVE    LNK-XC01-YMD    TO  SPA-GMN-YMD
                   PERFORM 310-SPASET-SEC
                   GO  TO  300-SCREEN-EXT
      *
               WHEN    "XC01"
                   INITIALIZE              SPA-XA02-AREA
                                           SPA-XA02KYOUTU
                   MOVE    SPA-WORK        TO  LNK-XC01-AREA
                   MOVE    LNK-XC01-MOTOPG TO  SPA-XA02-MOTOPG
                   MOVE    LNK-XC01-TBL-UUID
                                           TO  SPA-GMN-TBL-UUID
                   MOVE    LNK-XC01-GYOUMU-CD
                                           TO  SPA-NAI-GYOMU-CD
                   MOVE    LNK-XC01-YMD    TO  SPA-GMN-YMD
      *-----------> take rin st
                   MOVE    ZERO            TO  SPA-NAI-GYOMU-CD
                   MOVE    SPA-SYSYMD      TO  SPA-GMN-YMD
      *-----------> take rin ed
                   PERFORM 310-SPASET-SEC
                   GO  TO  300-SCREEN-EXT
               WHEN    "M01"
               WHEN    "M01N"
                   INITIALIZE              SPA-XA02-AREA
                                           SPA-XA02KYOUTU
      *            MOVE    SPA-WORK        TO  LNK-XC01-AREA
                   MOVE    SPA-SYSYMD      TO  SPA-GMN-YMD
      *            MOVE    LNK-XC01-MOTOPG TO  SPA-XA02-MOTOPG
                   MOVE    ZERO            TO  SPA-NAI-GYOMU-CD
                   PERFORM 310-SPASET-SEC
                   MOVE    WRK-MOTOPG      TO  SPA-XA02-MOTOPG
                   GO  TO  300-SCREEN-EXT
               END-EVALUATE
           END-IF
      *
      *
           INITIALIZE              SPA-XA02-AREA
                                   SPA-XA02KYOUTU
      *
      *
           PERFORM 310-SPASET-SEC
      *
           MOVE    WRK-MOTOPG              TO  SPA-XA02-MOTOPG
      *
           .
       300-SCREEN-EXT.
           EXIT.
      *
      *****************************************************************
      *    スパ初期編集処理
      *****************************************************************
       310-SPASET-SEC              SECTION.
      *
           MOVE    1                      TO  SPA-GMN-PAGE
      *
           PERFORM 470-EDIT-SRYYMD-SEC
      *
           EVALUATE    SPA-PRTKANRI-TBL-KEY
               WHEN    "SOKATU"
                   MOVE    "診療年月"     TO  SPA-GMN-TITLE1
                   MOVE    "請求年月日"   TO  SPA-GMN-TITLE2
               WHEN    "DAILY"
               WHEN    "MONTHLY"
                   MOVE    SPACE          TO  SPA-GMN-TITLE1
                   MOVE    "作成年月日"   TO  SPA-GMN-TITLE2
               WHEN    "RECEPT2"
                   MOVE    "診療年月"     TO  SPA-GMN-TITLE1
                   MOVE    "作成年月日"   TO  SPA-GMN-TITLE2
               WHEN    OTHER
                   MOVE    "業務名"       TO  SPA-GMN-TITLE1
                   MOVE    "患者番号"     TO  SPA-GMN-TITLE2
           END-EVALUATE
      *
      *                                 
           MOVE    1                   TO  FLG-LIST-ROW
      *                                 
           MOVE    ZERO                TO  SPA-GMN-CUR
      *                                 
           MOVE   "00 全業務"          TO  SPA-GMN-GYOMU-COMB
                                           SPA-NAI-G-ITEM(1)
                                           SPA-GMN-G-ITEM(1)
           MOVE    1                   TO  SPA-NAI-G-COUNT
                                           SPA-GMN-G-COUNT
      *
grpsys     MOVE    SPA-HOSPNUM         TO  LDMAP-HOSPNUM
grpsys*    MOVE    SPA-HOSPNUM         TO  -HOSPNUM
           MOVE    TBL-LDMAP           TO  MCPDATA-REC
           MOVE    "DBSELECT"          TO  MCP-FUNC
           MOVE    "tbl_ldmap"         TO  MCP-TABLE
           MOVE    "key4"
                                       TO  MCP-PATHNAME
grpsys     CALL    "ORCDBMAIN"         USING   MCPAREA
                                               MCPDATA-REC
                                               SPA-AREA
           IF      MCP-RC              =   ZERO
               MOVE    "tbl_ldmap"         TO  MCP-TABLE
               MOVE    "key4"
                                           TO  MCP-PATHNAME
               PERFORM 900-TBL-LDMAP-READ-SEC
               MOVE    MCPDATA-REC     TO  TBL-LDMAP
           ELSE
               INITIALIZE                  TBL-LDMAP
               MOVE    1               TO  FLG-LDMAP
           END-IF
      *
           MOVE    ZERO                TO  IDX
           PERFORM             UNTIL   FLG-LDMAP   =   1
                               OR      IDX         >=
                                       WRK-CONS-LINE-MAX
               MOVE    MCPDATA-REC        TO  TBL-LDMAP
               ADD     1              TO  IDX
               ADD     1              TO  SPA-NAI-G-COUNT
                                          SPA-GMN-G-COUNT
      *
               MOVE    LDMAP-LDMSG
                                      TO
                        SPA-NAI-G-ITEM(SPA-NAI-G-COUNT)
                        SPA-GMN-G-ITEM(SPA-NAI-G-COUNT)
      *
               MOVE    "tbl_ldmap"         TO  MCP-TABLE
               MOVE    "key4"
                                           TO  MCP-PATHNAME
               PERFORM 900-TBL-LDMAP-READ-SEC
           END-PERFORM
      *
      *    カーソルクロース
           MOVE    "DBCLOSECURSOR"     TO  MCP-FUNC
           MOVE    "tbl_ldmap"         TO  MCP-TABLE
           MOVE    "key4"
                                       TO  MCP-PATHNAME
grpsys     CALL    "ORCDBMAIN"         USING   MCPAREA
                                               MCPDATA-REC
                                               SPA-AREA
      *
               ADD     1              TO  SPA-NAI-G-COUNT
                                          SPA-GMN-G-COUNT
      *
           MOVE    "99 その他"        TO
                                      SPA-NAI-G-ITEM(SPA-NAI-G-COUNT)
                                      SPA-GMN-G-ITEM(SPA-NAI-G-COUNT)
           MOVE    SPA-NAI-GYOMU-CD    TO  WRK-GYOMU-CD
           PERFORM 20801-GYOUMU-CD-CHK-SEC
           MOVE   WRK-GYOMU-COMB-G     TO  SPA-NAI-GYOMU-COMB
      *
           PERFORM 3102-SPA-LIST-HEN-SEC
      *
           .
       310-SPASET-EXT.
           EXIT.
      *
      *****************************************************************
      *     患者情報取得
      *****************************************************************
       3101-SPA-PTINFO-SEC          SECTION.
           INITIALIZE                      PRTDATA-REC
           MOVE    PRTKANRI-RENNUM     TO  PRTDATA-RENNUM
           MOVE    PRTKANRI-SHORI-RENNUM
                                       TO  PRTDATA-SHORI-RENNUM
           MOVE    PRTKANRI-TBL-GROUP  TO  PRTDATA-TBL-GROUP
           MOVE    PRTKANRI-TBL-UUID   TO  PRTDATA-TBL-UUID
           MOVE    PRTKANRI-TBL-KEY    TO  PRTDATA-TBL-KEY
           MOVE    1                   TO  PRTDATA-PAGE
      *
grpsys     MOVE    SPA-HOSPNUM         TO  PRTDATA-HOSPNUM
grpsys*    MOVE    SPA-HOSPNUM         TO  -HOSPNUM
           MOVE    PRTDATA-REC         TO  MCPDATA-REC
           MOVE    "DBSELECT"          TO  MCP-FUNC
           MOVE    "tbl_prtdata"       TO  MCP-TABLE
           MOVE    "key"
                                       TO  MCP-PATHNAME
grpsys     CALL    "ORCDBMAIN"         USING   MCPAREA
                                               MCPDATA-REC
                                               SPA-AREA
           IF      MCP-RC              =   ZERO
                   PERFORM 31011-PTINFO-GET-SEC
           ELSE
                   DISPLAY "XA02 PRT FETCH ERR !!"
           END-IF
      *    カーソルクロース
           MOVE    "DBCLOSECURSOR"     TO  MCP-FUNC
           MOVE    "tbl_prtdata"       TO  MCP-TABLE
           MOVE    "key"
                                       TO  MCP-PATHNAME
grpsys     CALL    "ORCDBMAIN"         USING   MCPAREA
                                               MCPDATA-REC
                                               SPA-AREA
           .
       3101-SPA-PTINFO-EXT.
           EXIT.
      *
      *****************************************************************
      *    患者情報取得 その2
      *****************************************************************
       31011-PTINFO-GET-SEC         SECTION.
           MOVE    "DBFETCH"           TO  MCP-FUNC
           MOVE    "tbl_prtdata"       TO  MCP-TABLE
           MOVE    "key"
                                       TO  MCP-PATHNAME
grpsys     CALL    "ORCDBMAIN"         USING   MCPAREA
                                               MCPDATA-REC
                                               SPA-AREA
           MOVE    MCPDATA-REC         TO  PRTDATA-REC
           IF      MCP-RC        NOT   =   ZERO
                   GO  TO              31011-PTINFO-GET-EXT
           END-IF
      *
           MOVE    PRTDATA-PTID        TO  SPA-NAI-LPTID  (IDX)
           INITIALIZE                      ORCSPTIDAREA
           MOVE    SPA-HOSPNUM          TO  SPTID-HOSPNUM
           MOVE    PRTDATA-PTID        TO  SPTID-PTID
           CALL    "ORCSPTID"      USING   ORCSPTIDAREA
                                           SPA-AREA
      *
           IF      SPTID-RC        NOT =   ZERO
                   MOVE    SPACE           TO  SPA-GMN-LPTNUM  (IDX)
           ELSE
                   MOVE    SPTID-PTNUM     TO  SPA-GMN-LPTNUM  (IDX)
           END-IF
      *
           MOVE    SPA-HOSPNUM          TO  PTINF-HOSPNUM
           MOVE    PRTDATA-PTID        TO  PTINF-PTID
           PERFORM 900-PTINF-READ-SEC
           MOVE    PTINF-NAME          TO  SPA-GMN-LPTNAME (IDX)
      *    IF      FLG-PTINF               =   ZERO
      *    END-IF
           .
       31011-PTINFO-GET-EXT.
           EXIT.
      *
      *****************************************************************
      *    コラムリストの編集
      *****************************************************************
       3102-SPA-LIST-HEN-SEC           SECTION.
      *
           IF      SPA-NAI-GYOMU-CD    =   ZERO
                   MOVE "key8"         TO  WRK-MCP-PATHNAME 
           ELSE
                   MOVE "key9"         TO  WRK-MCP-PATHNAME 
           END-IF
      *
           INITIALIZE                  PRTKANRI-REC
           MOVE    SPA-PRTKANRI-TBL-KEY
                                       TO  PRTKANRI-TBL-KEY
           MOVE    SPA-GMN-YMD         TO  PRTKANRI-CREYMD
           MOVE    SPA-NAI-GYOMU-CD    TO  PRTKANRI-GYOUMU-CD
           COMPUTE PRTKANRI-LIMIT
               =   WRK-CONS-TBL-LIMIT  +   1
           COMPUTE PRTKANRI-OFFSET
               =   WRK-CONS-TBL-LIMIT
               * ( SPA-GMN-PAGE        -   1 )
grpsys     MOVE    SPA-HOSPNUM         TO  PRTKANRI-HOSPNUM
grpsys*    MOVE    SPA-HOSPNUM         TO  -HOSPNUM
           MOVE    PRTKANRI-REC        TO  MCPDATA-REC
           MOVE    "DBSELECT"          TO  MCP-FUNC
           MOVE    "tbl_prtkanri"      TO  MCP-TABLE
           MOVE    WRK-MCP-PATHNAME
                                       TO  MCP-PATHNAME
grpsys     CALL    "ORCDBMAIN"         USING   MCPAREA
                                               MCPDATA-REC
                                               SPA-AREA
           IF      MCP-RC             =   ZERO
               PERFORM 900-PRTKANRI-FET-SEC
           ELSE 
               MOVE    1                   TO  FLG-PRTKANRI
           END-IF
      *
           MOVE    ZERO                TO  CNT-PRTKANRI
           MOVE    ZERO                TO  IDX
           PERFORM UNTIL   FLG-PRTKANRI    =   1
                   OR      IDX             =   WRK-CONS-TBL-MAX
                   OR      CNT-PRTKANRI   >=   WRK-CONS-TBL-LIMIT
      *
               DISPLAY "CNT-PRTKANRI =" CNT-PRTKANRI
      *
               COMPUTE CNT-PRTKANRI
                   =   CNT-PRTKANRI    +   1
      *
               MOVE    ZERO            TO  FLG-PRTKANRI2
               MOVE    SPA-PRTKANRI-TBL-KEY
                                       TO  PRTKANRI-TBL-KEY
grpsys         MOVE    SPA-HOSPNUM     TO  PRTKANRI-HOSPNUM
grpsys*        MOVE    SPA-HOSPNUM         TO  -HOSPNUM
               MOVE    PRTKANRI-REC    TO  MCPDATA-REC
               MOVE    "tbl_prtkanri"  TO  MCP-TABLE
               MOVE    "key2"          TO  MCP-PATHNAME
               MOVE    "DBSELECT"      TO  MCP-FUNC
grpsys         CALL    "ORCDBMAIN"         USING   MCPAREA
                                                   MCPDATA-REC
                                                   SPA-AREA
               IF    ( MCP-RC          =   ZERO )
                   PERFORM 900-PRTKANRI-KEY2-FET-SEC
               ELSE
                   MOVE    1           TO  FLG-PRTKANRI2
               END-IF
      *
               MOVE    SPACE           TO  WRK-PRTKANRI-TBL-GROUP
               PERFORM UNTIL ( FLG-PRTKANRI2   =   1 )
                   OR      IDX                 =   WRK-CONS-TBL-MAX
                   ADD     1                   TO  IDX
                   MOVE    IDX                 TO  SPA-GMN-TNO    (IDX)
                   MOVE    PRTKANRI-TBL-KEY    TO
                                               SPA-NAI-TTBL-KEY   (IDX)
                   MOVE    PRTKANRI-TBL-GROUP  TO
                                               SPA-NAI-TTBL-GROUP (IDX)
                   MOVE    PRTKANRI-TBL-UUID   TO
                                           SPA-NAI-TTBL-UUID  (IDX)
                   IF      WRK-PRTKANRI-TBL-GROUP
                                           =   SPACE
                       MOVE    PRTKANRI-TBL-GROUP  TO
                                               SPA-GMN-TTBL-GROUP (IDX)
                                               WRK-PRTKANRI-TBL-GROUP
                       IF      PRTKANRI-SRYYM  NOT =   SPACE   AND ZERO
                           MOVE    PRTKANRI-SRYYM  TO  WRK-SYMD (1:6)
                           MOVE    "01"            TO  WRK-SYMD (7:2)
                           PERFORM 5002-HIZUKE-HEN-SEC
                           MOVE    WRK-HENYMDG(1:6)
                                               TO  SPA-GMN-TSRYYM (IDX)
                       END-IF
                       IF      PRTKANRI-SKYYMD NOT =   SPACE   AND ZERO
                           MOVE    PRTKANRI-SKYYMD TO  WRK-SYMD 
                           PERFORM 5002-HIZUKE-HEN-SEC
                           MOVE    WRK-HENYMDG TO  SPA-GMN-TSKYYMD(IDX)
                       END-IF
      *
                       SET     TBLLOCK-IDX             TO  1
                       SEARCH  ORC-LOCKCONST-TX    VARYING  TBLLOCK-IDX
                       AT   END
                           MOVE    "その他"        TO  SPA-GMN-GYOUMU
                                                       (IDX)
                       WHEN   PRTKANRI-GYOUMU-CD   =
                              ORC-LOCKCONST-GNUM (TBLLOCK-IDX)
                              MOVE    ORC-LOCKCONST-NAME (TBLLOCK-IDX)
                                                   TO  SPA-GMN-GYOUMU
                                                       (IDX)
                       END-SEARCH
      *
      *
                   END-IF                                 
                   MOVE    PRTKANRI-RENNUM     TO  SPA-GMN-TRENNUM (IDX)
                   MOVE    PRTKANRI-SHORI-RENNUM
                                           TO  SPA-GMN-TSHORI-RENNUM
                                                                  (IDX)
                   MOVE    PRTKANRI-PAGE       TO  SPA-GMN-TPAGE  (IDX)
                   MOVE    PRTKANRI-TITLE      TO  SPA-GMN-TPRT-TITLE
                                                                  (IDX)
                   MOVE    PRTKANRI-PRTFLG     TO  SPA-NAI-TPRTFLG(IDX)
      *
                   PERFORM 3101-SPA-PTINFO-SEC
      *
                   PERFORM 900-PRTKANRI-KEY2-FET-SEC
      *
               END-PERFORM
      *
      *
               MOVE    "DBCLOSECURSOR"     TO  MCP-FUNC
               MOVE    "tbl_prtkanri"      TO  MCP-TABLE
               MOVE    "key2"              TO  MCP-PATHNAME
grpsys         CALL    "ORCDBMAIN"         USING   MCPAREA
                                                   MCPDATA-REC
                                                   SPA-AREA
      *
               PERFORM 900-PRTKANRI-FET-SEC
           END-PERFORM
      *
           MOVE    "DBCLOSECURSOR"     TO  MCP-FUNC
           MOVE    "tbl_prtkanri"      TO  MCP-TABLE
           MOVE    WRK-MCP-PATHNAME
                                       TO  MCP-PATHNAME
grpsys     CALL    "ORCDBMAIN"         USING   MCPAREA
                                               MCPDATA-REC
                                               SPA-AREA
      *                                 
           MOVE    IDX                 TO  SPA-GMN-MAX
      *                                 
           IF    ( FLG-PRTKANRI    =   ZERO )
               MOVE    1           TO  SPA-GMN-TBL-NEXT
           END-IF
      *                                 
           .
       3102-SPA-LIST-HEN-EXT.
           EXIT.
      *
      *****************************************************************
      *    確認画面（ＣＩＤ１）ＯＫ処理
      *****************************************************************
       330-XAID1-SET-SEC            SECTION.
      *
           IF      SPA-XAID1-FLG    =   "OK"
               EVALUATE    SPA-XAIDCD
                   WHEN    "6666"
                       MOVE    "JOIN"          TO  MCP-PUTTYPE
                       PERFORM 210-BACK-SYORI-SEC
                   WHEN    "1002"
                   WHEN    "1003"
                       PERFORM 430-DELETE-SEC
                   WHEN    "1001"
      *************WHEN    "1004"
                       PERFORM 460-PRINT-SEC
               END-EVALUATE        
           END-IF
      *
           MOVE    1               TO  SPA-GMN-CUR
      *
           MOVE    SPACE           TO  SPA-XAID1-FLG
                                       SPA-XA99-FLG
           MOVE    SPACE           TO  SPA-XAIDCD
           MOVE    SPACE           TO  SPA-XAIDCD2      
           .
       330-XAID1-SET-EXT.
           EXIT.
      *
      *****************************************************************
      *    確認画面２ ＯＫ処理
      *****************************************************************
       330-XAID2-SET-SEC            SECTION.
      *
           IF      SPA-XAID1-FLG    NOT =   SPACE
               EVALUATE    SPA-XAIDCD2
                   WHEN    "1001"
      *************WHEN    "1004"
                   WHEN    "1005"
                       PERFORM 460-PRINT-SEC
               END-EVALUATE
           ELSE        
               MOVE    SPACE           TO  SPA-XAID1-FLG
           END-IF
      *
           MOVE    SPACE           TO  SPA-XA99-FLG
           MOVE    SPACE           TO  SPA-XAIDCD
           MOVE    SPACE           TO  SPA-XAIDCD2      
           .
       330-XAID2-SET-EXT.
           EXIT.
      *
      *****************************************************************
      *    処理表示画面戻り処理
      *****************************************************************
       340-XA99-SET-SEC            SECTION.
      *
           IF      SPA-XA99-FLG     =   "OK"
               PERFORM 4501-NEXT-GMN-SEC
           END-IF    
      *
           MOVE    SPACE           TO  SPA-XAID1-FLG
                                       SPA-XA99-FLG
           MOVE    SPACE           TO  SPA-XAIDCD
           MOVE    SPACE           TO  SPA-XAIDCD2      
           .
       340-XA99-SET-EXT.
           EXIT.
      *
      *****************************************************************
      *    プレビュー処理
      *****************************************************************
       4501-NEXT-GMN-SEC            SECTION.       
      *
      *****MOVE    SPACE               TO  LINKAREA
      *****MOVE    SPA-KYOUTU          TO  LNK-COMMON
      *****MOVE    SPA-XA02            TO  LNK-SCRSPA
      *
      *    画面移行用のパラメタ変更
           MOVE    "XA02"              TO  SPA-MOTOPG
           MOVE    "XC01"              TO  SPA-SAKIPG
      *
           MOVE    SPA-GMN-RENNUM      TO  LNK-XC01-RENNUM
           MOVE    SPA-GMN-TBL-GROUP   TO  LNK-XC01-TBL-GROUP
           MOVE    SPA-GMN-TBL-UUID    TO  LNK-XC01-TBL-UUID
           MOVE    SPA-GMN-SHORI-RENNUM
                                       TO  LNK-XC01-SHORI-RENNUM
           MOVE    SPA-GMN-STPAGE      TO  LNK-XC01-ST-PAGE
           MOVE    SPA-GMN-EDPAGE      TO  LNK-XC01-ED-PAGE
           MOVE    SPA-XA02-MOTOPG     TO  LNK-XC01-MOTOPG
           MOVE    SPACE               TO  LNK-XC01-MODE
           MOVE    SPA-GMN-YMD         TO  LNK-XC01-YMD
           MOVE    SPA-NAI-GYOMU-CD    TO  LNK-XC01-GYOUMU-CD
      *          
           MOVE    LNK-XC01-AREA       TO  SPA-WORK
           MOVE    SPA-KYOUTU          TO  LNK-KYOUTU
      *
           MOVE   "JOIN"               TO  MCP-PUTTYPE
           MOVE   "XC01   "            TO  MCP-WINDOW
      *
           PERFORM 900-PUT-WINDOW.
      *
           MOVE    1                   TO  FLG-END
      *
           .
       4501-NEXT-GMN-EXT.
           EXIT.
      *****************************************************************
      *    画面遷移処理
      *****************************************************************
       200-GMNSENI                   SECTION.
      *
           EVALUATE    MCP-EVENT       ALSO    MCP-WIDGET
      *     終了
               WHEN    "CLICKED"       ALSO    "B01"
                   MOVE   "CHANGE"         TO  MCP-PUTTYPE
                   PERFORM 210-BACK
      *     クリア
               WHEN    "CLICKED"       ALSO    "B02"
                   PERFORM 420-CLEAR-SEC
      *     削除
               WHEN    "CLICKED"       ALSO    "B03"
                   PERFORM 430-DELETE-GID-SEC
      *     情報削除
               WHEN    "CLICKED"       ALSO    "B11"
                   PERFORM 490-JYOUHOU-DEL-SEC
      *     前日
               WHEN    "CLICKED"       ALSO    "B06"
                   PERFORM 440-BEFORE-SEC
      *     次日
               WHEN    "CLICKED"       ALSO    "B07"
                   PERFORM 450-NEXT-SEC
      *     前頁
               WHEN    "CLICKED"       ALSO    "B06S"
                   PERFORM 440-BACK-PAGE-SEC
      *     次頁
               WHEN    "CLICKED"       ALSO    "B07S"
                   PERFORM 450-NEXT-PAGE-SEC
      *     一覧更新
               WHEN    "CLICKED"       ALSO    "B11S"
                   PERFORM 470-REFRESH-SEC
      *     印刷
               WHEN    "CLICKED"       ALSO    "B12"
                   PERFORM 460-PRINT-GID-SEC
      *     処理確認
               WHEN    "CLICKED"       ALSO    "B13"
                   PERFORM 490-KAKUNIN-SEC
               WHEN    "CLICKED"       ALSO    "B09"
                   MOVE    "8888"          TO  SPA-ERRCD
           END-EVALUATE
      *
           .
       200-GMNSENI-EXT.
           EXIT.
      *
      *****************************************************************
      *    会話　処理
      *****************************************************************
       200-ENTRY                   SECTION.
      *
           EVALUATE    MCP-EVENT       ALSO    MCP-WIDGET
      *
               WHEN    "ACTIVATE"      ALSO    "SELNUM"
                   PERFORM 201-SELNUM-SEC
               WHEN    "ACTIVATE"      ALSO    "RENNUM"
                   PERFORM 203-RENNUM-SEC
               WHEN    "ACTIVATE"      ALSO    "SHORI_RENNUM"
                   PERFORM 204-SHORI-RENNUM-SEC
               WHEN    "ACTIVATE"      ALSO    "STPAGE"
                   PERFORM 205-STPAGE-SEC
               WHEN    "ACTIVATE"      ALSO    "EDPAGE"
                   PERFORM 206-EDPAGE-SEC
               WHEN    "ACTIVATE"      ALSO    "PTNUM"
                   PERFORM 207-PTNUM-SEC
               WHEN    "ACTIVATE"      ALSO    "GYOMUCD"
                   PERFORM 208-GYOMU-SEC
      *        行選択
               WHEN    ANY             ALSO    "LIST"
                   PERFORM 203-SELECT-SEC
           END-EVALUATE
           .
       200-ENTRY-EXT.
           EXIT.
      *
      *****************************************************************
      *    選択番号入力処理
      *****************************************************************
       201-SELNUM-SEC                 SECTION.
      *
           MOVE    1                   TO  SPA-GMN-CUR
      *
           INITIALIZE                  SPA-GMN-BODY
      *      
           MOVE    XA02-SELNUM         TO  SPA-GMN-SELNUM
      *     
           IF      SPA-GMN-SELNUM      =   ZERO
               CONTINUE
           ELSE
               IF      SPA-GMN-SELNUM  >   SPA-GMN-MAX
                   MOVE    "0001"          TO  SPA-ERRCD
               ELSE
                   MOVE    SPA-GMN-SELNUM  TO  SPA-NAI-SELNUM     
                   PERFORM 2011-SELECT-HENSYU-SEC
               END-IF    
           END-IF
      *
           IF      SPA-ERRCD           =   SPACE
               MOVE    3                   TO  SPA-GMN-CUR
           END-IF    
           .
       201-SELNUM-EXT.
           EXIT.
      *
      *****************************************************************
      *    患者検索処理
      *****************************************************************
       2011-SELECT-HENSYU-SEC        SECTION.
      *
           MOVE    SPA-GMN-SELNUM      TO  IDY
      *     
           MOVE    SPA-NAI-TTBL-GROUP  (IDY)   TO  SPA-GMN-TBL-GROUP
           MOVE    SPA-NAI-TTBL-UUID   (IDY)   TO  SPA-GMN-TBL-UUID 
           MOVE    SPA-GMN-TRENNUM     (IDY)   TO  SPA-GMN-RENNUM
           MOVE    SPA-GMN-TSHORI-RENNUM (IDY) TO
                                               SPA-GMN-SHORI-RENNUM
           MOVE    1                           TO  SPA-GMN-STPAGE
           MOVE    SPA-GMN-TPAGE       (IDY)   TO  SPA-GMN-EDPAGE 
           MOVE    SPA-GMN-LPTNUM      (IDY)   TO  SPA-GMN-PTNUM  
           MOVE    SPA-GMN-LPTNAME     (IDY)   TO  SPA-GMN-PTNAME 
           .
       2011-SELECT-HENSYU-EXT.
           EXIT.
           .
      *     
      *****************************************************************
      *    連番入力処理
      *****************************************************************
       203-RENNUM-SEC               SECTION.
      *
           MOVE    3                   TO  SPA-GMN-CUR
      *     
           MOVE    XA02-RENNUM         TO  SPA-GMN-RENNUM
      *     
           MOVE    ZERO                TO  SPA-GMN-STPAGE
                                           SPA-GMN-EDPAGE
                                           SPA-GMN-SELNUM
                                           SPA-NAI-SELNUM      
           IF      SPA-GMN-RENNUM      =   ZERO
               MOVE    ZERO                TO  SPA-GMN-SHORI-RENNUM
               MOVE    99                  TO  SPA-GMN-CUR
               GO  TO  203-RENNUM-EXT                                
           END-IF
      *     
           IF      SPA-GMN-TBL-GROUP   =   SPACE
               MOVE    "0003"              TO  SPA-ERRCD
           ELSE
               MOVE    ZERO                TO  IDY
               PERFORM VARYING IDX FROM    1   BY  1
                       UNTIL   IDX >       SPA-GMN-MAX
                       OR      SPA-GMN-TBL-GROUP
                                           >   SPA-NAI-TTBL-GROUP (IDX)
                   IF      SPA-GMN-TBL-GROUP
                                           =   SPA-NAI-TTBL-GROUP (IDX)
                   AND     SPA-GMN-RENNUM  =   SPA-GMN-TRENNUM    (IDX)
                       MOVE    IDX             TO  IDY
                       IF      SPA-GMN-SHORI-RENNUM
                                           NOT =   ZERO
                       AND     SPA-GMN-SHORI-RENNUM
                                       =   SPA-GMN-TSHORI-RENNUM(IDX)
                           MOVE    IDX             TO  SPA-GMN-SELNUM
                                                       SPA-NAI-SELNUM
                           MOVE    1               TO  SPA-GMN-STPAGE
                           MOVE    SPA-GMN-TPAGE (IDX)
                                                   TO  SPA-GMN-EDPAGE                                 
                           MOVE    SPA-GMN-MAX     TO  IDX
                       END-IF    
                   END-IF
               END-PERFORM
               IF      IDY                 =   ZERO
                   MOVE    "0006"              TO  SPA-ERRCD
               END-IF
           END-IF        
      *
           IF      SPA-ERRCD           =   SPACE
               MOVE    4                   TO  SPA-GMN-CUR
           END-IF
     *
           .
       203-RENNUM-EXT.
           EXIT.
      *     
      *****************************************************************
      *    処理内連番入力処理
      *****************************************************************
       204-SHORI-RENNUM-SEC               SECTION.
      *
           MOVE    4                   TO  SPA-GMN-CUR
      *     
           MOVE    XA02-SHORI-RENNUM   TO  SPA-GMN-SHORI-RENNUM
      *
           MOVE    ZERO                TO  SPA-GMN-SELNUM
                                           SPA-NAI-SELNUM      
      *
           IF      SPA-GMN-SHORI-RENNUM    =   ZERO
               MOVE    ZERO                TO  SPA-GMN-STPAGE
                                               SPA-GMN-EDPAGE
               MOVE    99                  TO  SPA-GMN-CUR
               GO  TO  204-SHORI-RENNUM-EXT                                
           END-IF
      *     
           IF      SPA-GMN-TBL-GROUP   =   SPACE
           OR      SPA-GMN-RENNUM      =   ZERO
               MOVE    "0003"              TO  SPA-ERRCD
           ELSE
               MOVE    ZERO                TO  IDY
               PERFORM VARYING IDX FROM    1   BY  1
                       UNTIL   IDX >       SPA-GMN-MAX
                   IF      SPA-GMN-TBL-GROUP
                                       =   SPA-NAI-TTBL-GROUP (IDX)
                   AND     SPA-GMN-RENNUM
                                       =   SPA-GMN-TRENNUM    (IDX)
                   AND     SPA-GMN-SHORI-RENNUM
                                       =   SPA-GMN-TSHORI-RENNUM(IDX)
                       MOVE    IDX             TO  IDY
                       MOVE    SPA-GMN-MAX     TO  IDX
                   END-IF
               END-PERFORM
               IF      IDY                 =   ZERO
                   MOVE    "0006"              TO  SPA-ERRCD
               ELSE
                   MOVE    IDY                 TO  SPA-GMN-SELNUM
                                                   SPA-NAI-SELNUM
                   MOVE    1                   TO  SPA-GMN-STPAGE
                   MOVE    SPA-GMN-TPAGE (IDY) TO  SPA-GMN-EDPAGE                                 
               END-IF
           END-IF        
      *
           IF      SPA-ERRCD           =   SPACE
               MOVE    5                   TO  SPA-GMN-CUR
           ELSE    
               MOVE    ZERO                TO  SPA-GMN-STPAGE
                                               SPA-GMN-EDPAGE
           END-IF                                                  
      *
           .
       204-SHORI-RENNUM-EXT.
           EXIT.
      *     
      *****************************************************************
      *    開始頁入力処理
      *****************************************************************
       205-STPAGE-SEC               SECTION.
      *
           MOVE    5                   TO  SPA-GMN-CUR
      *     
           MOVE    XA02-STPAGE         TO  SPA-GMN-STPAGE
      *
           IF      SPA-GMN-STPAGE      >   ZERO
               IF      SPA-GMN-TBL-GROUP       =   SPACE
               OR      SPA-GMN-RENNUM          =   ZERO
               OR      SPA-GMN-SHORI-RENNUM    =   ZERO
               OR      SPA-GMN-SELNUM          =   ZERO
                   MOVE    "0003"              TO  SPA-ERRCD
               ELSE    
                   IF      SPA-GMN-STPAGE      >
                                       SPA-GMN-TPAGE (SPA-GMN-SELNUM) 
                       MOVE    "0002"              TO  SPA-ERRCD
                   END-IF                                    
               END-IF
           END-IF                                                  
      *
      *
           IF      SPA-ERRCD           =   SPACE
               MOVE    6                   TO  SPA-GMN-CUR
           END-IF    
      *
           .
       205-STPAGE-EXT.
           EXIT.
      *     
      *****************************************************************
      *    終了頁入力処理
      *****************************************************************
       206-EDPAGE-SEC               SECTION.
      *
           MOVE    6                   TO  SPA-GMN-CUR
      *     
           MOVE    XA02-EDPAGE         TO  SPA-GMN-EDPAGE
      *
           IF      SPA-GMN-EDPAGE      >   ZERO
               IF      SPA-GMN-TBL-GROUP       =   SPACE
               OR      SPA-GMN-RENNUM          =   ZERO
               OR      SPA-GMN-SHORI-RENNUM    =   ZERO
               OR      SPA-GMN-SELNUM          =   ZERO
                   MOVE    "0003"              TO  SPA-ERRCD
               ELSE    
                   IF      SPA-GMN-EDPAGE      >
                                       SPA-GMN-TPAGE (SPA-GMN-SELNUM) 
                       MOVE    "0002"              TO  SPA-ERRCD
                   ELSE    
                       IF      SPA-GMN-EDPAGE      <   SPA-GMN-STPAGE 
                           MOVE    "0002"              TO  SPA-ERRCD
                       END-IF
                   END-IF                                        
               END-IF
           END-IF                                                  
      *
      *
           IF      SPA-ERRCD           =   SPACE
               MOVE    99                  TO  SPA-GMN-CUR
           END-IF    
      *
           .
       206-EDPAGE-EXT.
           EXIT.
      *
      *****************************************************************
      *    患者番号入力処理
      *****************************************************************
       207-PTNUM-SEC                SECTION.
      *
           MOVE    "8888"              TO  SPA-ERRCD
      *
           .
       207-PTNUM-EXT.
           EXIT.
      *
      *****************************************************************
      *    業務コード入力処理
      *****************************************************************
       208-GYOMU-SEC                SECTION.
      *
           MOVE    1                   TO  SPA-GMN-CUR
           MOVE    XA02-G-VALUE        TO  WRK-GYOMU-COMB-G
           PERFORM 20801-GYOUMU-CD-CHK-SEC
           IF      WRK-GYOMU-NAME      =  SPACE
               MOVE    "0007"          TO  SPA-ERRCD
           ELSE
               INITIALIZE              SPA-GMN-BODY
                                       SPA-GMN-TBL
                                       SPA-GMN-PTNUM
                                       SPA-GMN-PTNAME
                                       SPA-NAI-SELNUM
                                       SPA-NAI-TBL
               MOVE    1   TO          SPA-GMN-PAGE
               MOVE    WRK-GYOMU-COMB-G    TO  SPA-NAI-GYOMU-COMB
               PERFORM 3102-SPA-LIST-HEN-SEC
           END-IF
      *
           .
       208-GYOMU-EXT.
           EXIT.
      *
      *****************************************************************
      *    業務コード入力チェック処理
      *****************************************************************
       20801-GYOUMU-CD-CHK-SEC         SECTION.
      *
           MOVE    SPACE               TO  WRK-GYOMU-FIL
                                           WRK-GYOMU-NAME
      *
           IF      WRK-GYOMU-CD        =   SPACE
               CONTINUE
           ELSE
               PERFORM VARYING IDX FROM   1   BY  1
                       UNTIL   IDX >      SPA-NAI-G-COUNT
                   IF  WRK-GYOMU-CD    =  SPA-NAI-ITEM-CD(IDX)
                       MOVE    SPA-NAI-ITEM-FIL(IDX)
                                       TO  WRK-GYOMU-FIL
                       MOVE    SPA-NAI-ITEM-NAME(IDX)
                                       TO  WRK-GYOMU-NAME
                   END-IF
               END-PERFORM
           END-IF
      *
           .
       20801-GYOUMU-CD-CHK-EXT.
           EXIT.
      *
      *****************************************************************
      *    行選択処理
      *****************************************************************
       203-SELECT-SEC               SECTION.
      *          
      *    行選択
           MOVE    ZERO                TO  WRK-SELNUM
      *     
           PERFORM VARYING     IDX     FROM    1   BY  1
                   UNTIL       IDX     >   SPA-GMN-MAX
               IF      XA02-SELECT (IDX)   =   "T"
                   IF      IDX             =   SPA-NAI-SELNUM
                       CONTINUE
                   ELSE        
                       MOVE    IDX             TO  WRK-SELNUM
                       MOVE    SPA-GMN-MAX     TO  IDX
                   END-IF
               END-IF    
           END-PERFORM
      *
           IF      WRK-SELNUM          >   ZERO
               MOVE    WRK-SELNUM          TO  SPA-NAI-SELNUM
           END-IF
      *
           INITIALIZE                  SPA-GMN-BODY
      *
           MOVE    SPA-NAI-SELNUM      TO  SPA-GMN-SELNUM
      *                                 
           PERFORM 2011-SELECT-HENSYU-SEC
      *
           MOVE    99                  TO  SPA-GMN-CUR         
           .
       203-SELECT-EXT.
           EXIT.
      *
      *
      *****************************************************************
      *    戻る　処理
      *****************************************************************
       210-BACK                    SECTION.
      *        
           MOVE    SPACE               TO  SYS-3003-REC 
           INITIALIZE                      SYS-3003-REC
           MOVE    "3003"              TO  SYS-3003-KANRICD
      *    MOVE    WRK-CONS-3003-KBNCD TO  SYS-3003-KBNCD
           MOVE    WRK-CONS-3003-KBNCD-LIKE
                                       TO  SYS-3003-KBNCD
      *    MOVE    SPA-SYSYMD          TO  ORC-DBYMD
           MOVE    SPA-SYSYMD          TO  SYS-3003-STYUKYMD
                                           SYS-3003-EDYUKYMD
grpsys     MOVE    SPA-HOSPNUM         TO  SYS-3003-HOSPNUM
grpsys*    MOVE    SPA-HOSPNUM         TO  -HOSPNUM
           MOVE    SYS-3003-REC        TO  MCPDATA-REC
           PERFORM 900-SYSKANRI-INV-SEC
           IF      FLG-SYSKANRI        =   ZERO
               MOVE    MCPDATA-REC         TO  SYS-3003-REC 
               PERFORM VARYING IDX FROM    1   BY  1
                       UNTIL   IDX >       SYS-3003-JOBID
      *            ジョブ管理チェック処理
                   MOVE    "CHK"           TO  SJOBKANRI-MODE
                   INITIALIZE                  JOBKANRI-REC
                   MOVE    IDX             TO  JOB-JOBID
                   MOVE    WRK-CONS-JOB-SHELLID
                                           TO  JOB-SHELLID
                   MOVE    SPA-HOSPNUM     TO  JOB-HOSPNUM
                   CALL    "ORCSJOB"       USING
                                           ORCSJOBKANRIAREA  
                                           JOBKANRI-REC
                   IF      SJOBKANRI-RETURN
                                           =   ZERO
                       IF      JOB-ENDYMD      =   SPACE
      *********        AND     JOB-TERMID      =   SPA-TERMID
                           MOVE    "6666"          TO  SPA-XAIDCD
                           GO  TO  210-BACK-EXT
                       END-IF
                   END-IF
               END-PERFORM
           END-IF
      *
           PERFORM 210-BACK-SYORI-SEC         
           .
       210-BACK-EXT.
           EXIT.
      *
      *
      *****************************************************************
      *    戻る　処理
      *****************************************************************
       210-BACK-SYORI-SEC              SECTION.
      *
           DISPLAY "back moto = "    SPA-XA02-MOTOPG 
           MOVE    SPA-XA02-MOTOPG     TO  SPA-SAKIPG
           MOVE    "XA02"              TO  SPA-MOTOPG
      *
           MOVE    SPACE               TO  SPA-WORK 
           INITIALIZE                  SPA-KYOTUKEY
                                       SPA-KYOTU-SET
           MOVE    SPA-KYOUTU          TO  LNK-KYOUTU
      *
           MOVE    SPA-SAKIPG          TO  MCP-WINDOW
      *
           PERFORM 900-PUT-WINDOW
      *
           MOVE    1                   TO  FLG-END
      *
           .
       210-BACK-SYORI-EXT.
           EXIT.
      *
      *****************************************************************
      *    クリア処理
      *****************************************************************
       420-CLEAR-SEC                SECTION.
      *
           INITIALIZE               SPA-GMN-BODY
                                    SPA-NAI-SELNUM
      *
           MOVE    1                TO  SPA-GMN-CUR        
      *
           .
       420-CLEAR-EXT.
           EXIT.
      *
      *****************************************************************
      *    削除処理
      *****************************************************************
       430-DELETE-GID-SEC                SECTION.
      *
           IF      SPA-GMN-TBL-GROUP   =   SPACE
               MOVE    "0001"              TO  SPA-ERRCD
           END-IF
      *
           IF      SPA-ERRCD       NOT =   SPACE                 
               GO  TO  430-DELETE-GID-EXT               
           END-IF
      *         
      *    帳票管理ＤＢのチェック
           INITIALIZE                      PRTKANRI-REC
           MOVE    SPA-PRTKANRI-TBL-KEY
                                       TO  PRTKANRI-TBL-KEY
           MOVE    SPA-GMN-TBL-GROUP   TO  PRTKANRI-TBL-GROUP
           MOVE    SPA-GMN-TBL-UUID    TO  PRTKANRI-TBL-UUID 
           MOVE    SPA-GMN-SHORI-RENNUM
                                       TO  PRTKANRI-SHORI-RENNUM
           MOVE    SPA-GMN-RENNUM      TO  PRTKANRI-RENNUM
grpsys     MOVE    SPA-HOSPNUM         TO  PRTKANRI-HOSPNUM
grpsys*    MOVE    SPA-HOSPNUM         TO  -HOSPNUM
           MOVE    PRTKANRI-REC        TO  MCPDATA-REC
           MOVE    "DBSELECT"          TO  MCP-FUNC
           MOVE    "tbl_prtkanri"      TO  MCP-TABLE
           MOVE    "key2"
                                       TO  MCP-PATHNAME
grpsys     CALL    "ORCDBMAIN"         USING   MCPAREA
                                               MCPDATA-REC
                                               SPA-AREA
           IF      MCP-RC             =   ZERO
               PERFORM 900-PRTKANRI-FET2-SEC
           ELSE
               MOVE    1                   TO  FLG-PRTKANRI
           END-IF
      *     
      *    指定のグループが存在しないとき
           IF      FLG-PRTKANRI        =   1
               MOVE    "0005"              TO  SPA-ERRCD
               GO  TO  430-DELETE-GID-EXT    
           ELSE
               MOVE    PRTKANRI-TBL-KEY    TO  SPA-WRK-TBL-KEY    
           END-IF
      *     
      *    ガイドメッセージセット
               MOVE    "1002"              TO  SPA-XAIDCD
      *
      *    未処理の帳票があるとき
           PERFORM                     UNTIL   FLG-PRTKANRI    =   1
               IF      PRTKANRI-PRTFLG         =   SPACE
                   MOVE    "1003"          TO  SPA-XAIDCD
                   MOVE    1               TO  FLG-PRTKANRI
               END-IF
               PERFORM 900-PRTKANRI-FET2-SEC
           END-PERFORM
      *                  
           MOVE    "DBCLOSECURSOR"     TO  MCP-FUNC
           MOVE    "tbl_prtkanri"      TO  MCP-TABLE
           MOVE    "key2"
                                       TO  MCP-PATHNAME
grpsys     CALL    "ORCDBMAIN"         USING   MCPAREA
                                               MCPDATA-REC
                                               SPA-AREA
           .
       430-DELETE-GID-EXT.
           EXIT.
      *
      *****************************************************************
      *    削除処理
      *****************************************************************
       430-DELETE-SEC                SECTION.
      *         
           INITIALIZE                      PRTKANRI-REC
           MOVE    SPA-WRK-TBL-KEY     TO  PRTKANRI-TBL-KEY
           MOVE    SPA-GMN-TBL-GROUP   TO  PRTKANRI-TBL-GROUP
           MOVE    SPA-GMN-TBL-UUID    TO  PRTKANRI-TBL-UUID 
           MOVE    SPA-GMN-SHORI-RENNUM
                                       TO  PRTKANRI-SHORI-RENNUM
           MOVE    SPA-GMN-RENNUM      TO  PRTKANRI-RENNUM
grpsys     MOVE    SPA-HOSPNUM         TO  PRTKANRI-HOSPNUM
grpsys*    MOVE    SPA-HOSPNUM         TO  -HOSPNUM
           MOVE    PRTKANRI-REC        TO  MCPDATA-REC
           MOVE    "DBDELETE"          TO  MCP-FUNC
           MOVE    "tbl_prtkanri"      TO  MCP-TABLE
           MOVE    "del14"
                                       TO  MCP-PATHNAME
grpsys     CALL    "ORCDBMAIN"         USING   MCPAREA
                                               MCPDATA-REC
                                               SPA-AREA
      *
           IF      MCP-RC         NOT  =   ZERO
               MOVE    "1002"              TO  SPA-ERRCD
               GO  TO  430-DELETE-EXT
           END-IF
      *         
           INITIALIZE                      PRTDATA-REC
           MOVE    SPA-WRK-TBL-KEY     TO  PRTDATA-TBL-KEY
           MOVE    SPA-GMN-TBL-GROUP   TO  PRTDATA-TBL-GROUP
           MOVE    SPA-GMN-TBL-UUID    TO  PRTDATA-TBL-UUID
           MOVE    SPA-GMN-SHORI-RENNUM
                                       TO  PRTKANRI-SHORI-RENNUM
           MOVE    SPA-GMN-RENNUM      TO  PRTKANRI-RENNUM
grpsys     MOVE    SPA-HOSPNUM         TO  PRTDATA-HOSPNUM
grpsys*    MOVE    SPA-HOSPNUM         TO  -HOSPNUM
           MOVE    PRTDATA-REC         TO  MCPDATA-REC
           MOVE    "DBDELETE"          TO  MCP-FUNC
           MOVE    "tbl_prtdata"       TO  MCP-TABLE
           MOVE    "del14"
                                       TO  MCP-PATHNAME
grpsys     CALL    "ORCDBMAIN"         USING   MCPAREA
                                               MCPDATA-REC
                                               SPA-AREA
      *
           IF      MCP-RC         NOT  =   ZERO
               MOVE    "1003"              TO  SPA-ERRCD
               GO  TO  430-DELETE-EXT
           END-IF
      *
      *    PERFORM 310-SPASET-SEC
           PERFORM 470-REFRESH-SEC
           .
       430-DELETE-EXT.
           EXIT.
      *
      *****************************************************************
      *    前日処理
      *****************************************************************
       440-BEFORE-SEC                  SECTION.
      *
      *
           INITIALIZE              SPA-GMN-BODY
                                   SPA-GMN-TBL
                                   SPA-GMN-PTNUM
                                   SPA-GMN-PTNAME
                                   SPA-NAI-SELNUM
                                   SPA-NAI-TBL
           MOVE    1   TO          SPA-GMN-PAGE
           MOVE    SPA-GMN-YMD         TO  WRK-YMD
           MOVE    "1"                 TO  WRK-ZOGENPTN
           MOVE    -1                  TO  WRK-ZOGEN
           PERFORM 800-CALENDAR-SEC
           MOVE    LNK-DAY6-KEISAN     TO  SPA-GMN-YMD
           PERFORM 3102-SPA-LIST-HEN-SEC
      *
           PERFORM 470-EDIT-SRYYMD-SEC
      *
           .
       440-BEFORE-EXT.
           EXIT.
      *
      *****************************************************************
      *    次日処理
      *****************************************************************
       450-NEXT-SEC                    SECTION.
      *
      *
           INITIALIZE              SPA-GMN-BODY
                                   SPA-GMN-TBL
                                   SPA-GMN-PTNUM
                                   SPA-GMN-PTNAME
                                   SPA-NAI-SELNUM
                                   SPA-NAI-TBL
           MOVE    1   TO          SPA-GMN-PAGE
           MOVE    SPA-GMN-YMD         TO  WRK-YMD
           MOVE    "1"                 TO  WRK-ZOGENPTN
           MOVE    1                   TO  WRK-ZOGEN
           PERFORM 800-CALENDAR-SEC
           MOVE    LNK-DAY6-KEISAN     TO  SPA-GMN-YMD
           PERFORM 3102-SPA-LIST-HEN-SEC
      *
           PERFORM 470-EDIT-SRYYMD-SEC
      *
           .
       450-NEXT-EXT.
           EXIT.
      *****************************************************************
      *    前頁処理
      *****************************************************************
       440-BACK-PAGE-SEC               SECTION.
      *
           IF    ( SPA-GMN-PAGE    >   1 )
               INITIALIZE              SPA-GMN-BODY
                                       SPA-GMN-TBL
                                       SPA-GMN-PTNUM
                                       SPA-GMN-PTNAME
                                       SPA-NAI-SELNUM
                                       SPA-NAI-TBL
               COMPUTE     SPA-GMN-PAGE
                   =       SPA-GMN-PAGE
                   -       1
               PERFORM 3102-SPA-LIST-HEN-SEC
           END-IF
      *
           .
       440-BACK-PAGE-EXT.
           EXIT.
      *
      *****************************************************************
      *    次頁処理
      *****************************************************************
       450-NEXT-PAGE-SEC           SECTION.
      *
           IF    ( SPA-GMN-TBL-NEXT    =   1 )
               INITIALIZE              SPA-GMN-BODY
                                       SPA-GMN-TBL
                                       SPA-GMN-PTNUM
                                       SPA-GMN-PTNAME
                                       SPA-NAI-SELNUM
                                       SPA-NAI-TBL
               COMPUTE     SPA-GMN-PAGE
                   =       SPA-GMN-PAGE
                   +       1
               PERFORM 3102-SPA-LIST-HEN-SEC
           END-IF
      *
           .
       450-NEXT-PAGE-EXT.
           EXIT.
      *
      *****************************************************************
      *    リフレッシュ処理
      *****************************************************************
       470-REFRESH-SEC                 SECTION.
      *
      *
           INITIALIZE              SPA-GMN-BODY
                                   SPA-GMN-TBL
                                   SPA-GMN-PTNUM
                                   SPA-GMN-PTNAME
                                   SPA-NAI-SELNUM
                                   SPA-NAI-TBL
      *                            SPA-GMN-PAGE
           PERFORM 3102-SPA-LIST-HEN-SEC
      *
           .
       470-REFRESH-EXT.
           EXIT.
      *
      *****************************************************************
      *    診療日編集処理
      *****************************************************************
       470-EDIT-SRYYMD-SEC             SECTION.
      *
           MOVE    SPA-GMN-YMD     TO     WRK-SYMD
           PERFORM 5002-HIZUKE-HEN-SEC
           MOVE    WRK-HENYMDG     TO     SPA-GMN-SRYYMD
      *
           .
       470-EDIT-SRYYMD-EXT.
           EXIT.
      *****************************************************************
      *    情報削除画面へ
      *****************************************************************
       490-JYOUHOU-DEL-SEC             SECTION.
      *
      *    MOVE    SPACE               TO  LINKAREA
      *    MOVE    SPA-KYOUTU          TO  LNK-COMMON
      *    MOVE    SPA-XA02            TO  LNK-SCRSPA
      *
      *    画面移行用のパラメタ変更
           MOVE    "XA02"              TO  SPA-MOTOPG
           MOVE    "XB01"              TO  SPA-SAKIPG
      *
           MOVE    WRK-CONS-JOB-SHELLID
                                       TO  SPA-JOB-SHELLID
      *
           MOVE    SPA-XA02KYOUTU      TO  SPA-WORK
           MOVE    SPA-GMN-RENNUM      TO  LNK-XC01-RENNUM
           MOVE    SPA-GMN-TBL-GROUP   TO  LNK-XC01-TBL-GROUP
           MOVE    SPA-GMN-TBL-UUID    TO  LNK-XC01-TBL-UUID
           MOVE    SPA-GMN-SHORI-RENNUM
                                       TO  LNK-XC01-SHORI-RENNUM
           MOVE    SPA-GMN-STPAGE      TO  LNK-XC01-ST-PAGE
           MOVE    SPA-GMN-EDPAGE      TO  LNK-XC01-ED-PAGE
           MOVE    SPA-XA02-MOTOPG     TO  LNK-XC01-MOTOPG
           MOVE    SPACE               TO  LNK-XC01-MODE
           MOVE    SPA-GMN-YMD         TO  LNK-XC01-YMD
           MOVE    SPA-NAI-GYOMU-CD    TO  LNK-XC01-GYOUMU-CD
      *          
           MOVE    LNK-XC01-AREA       TO  SPA-WORK
           MOVE    SPA-KYOUTU          TO  LNK-KYOUTU
      *
           MOVE   "CHANGE"             TO  MCP-PUTTYPE
           MOVE   "XB01   "            TO  MCP-WINDOW
      *
           PERFORM 900-PUT-WINDOW.
      *
           MOVE    1                   TO  FLG-END
      *
           .
       490-JYOUHOU-DEL-EXT.
           EXIT.
      *****************************************************************
      *    作表処理
      *****************************************************************
       460-PRINT-GID-SEC                SECTION.
      *
      *    ジョブ管理チェック        
           MOVE    SPACE               TO  SYS-3003-REC 
           INITIALIZE                      SYS-3003-REC
           MOVE    "3003"              TO  SYS-3003-KANRICD
      *    MOVE    WRK-CONS-3003-KBNCD TO  SYS-3003-KBNCD
           MOVE    WRK-CONS-3003-KBNCD-LIKE
                                       TO  SYS-3003-KBNCD
      *    MOVE    SPA-SYSYMD          TO  ORC-DBYMD 
           MOVE    SPA-SYSYMD          TO  SYS-3003-STYUKYMD
                                           SYS-3003-EDYUKYMD
grpsys     MOVE    SPA-HOSPNUM         TO  SYS-3003-HOSPNUM
grpsys*    MOVE    SPA-HOSPNUM         TO  -HOSPNUM
           MOVE    SYS-3003-REC        TO  MCPDATA-REC
           PERFORM 900-SYSKANRI-INV-SEC
           IF      FLG-SYSKANRI        =   ZERO
               MOVE    MCPDATA-REC         TO  SYS-3003-REC 
               PERFORM VARYING IDX FROM    1   BY  1
                       UNTIL   IDX >       SYS-3003-JOBID
      *            ジョブ管理チェック処理
                   MOVE    "CHK"           TO  SJOBKANRI-MODE
                   INITIALIZE                  JOBKANRI-REC
                   MOVE    IDX             TO  JOB-JOBID
                   MOVE    WRK-CONS-JOB-SHELLID
                                           TO  JOB-SHELLID
                   MOVE    SPA-HOSPNUM     TO  JOB-HOSPNUM
                   CALL    "ORCSJOB"       USING
                                           ORCSJOBKANRIAREA  
                                           JOBKANRI-REC
                   IF      SJOBKANRI-RETURN
                                           =   ZERO
                       IF      JOB-ENDYMD      =   SPACE
                           MOVE    "7777"          TO  SPA-ERRCD
                           MOVE    SPACE           TO  WRK-RECEERR
                           STRING  "処理中です【"  DELIMITED  BY  SIZE
                                   JOB-SHELLMSG    DELIMITED  BY  SPACE
                                   "】"            DELIMITED  BY  SIZE      
                                                   INTO  WRK-RECEERR
                           MOVE    SYS-3003-JOBID  TO  IDX
                       END-IF
                   END-IF    
               END-PERFORM
           END-IF
      *
           IF      SPA-ERRCD       NOT =   SPACE                 
               GO  TO  460-PRINT-GID-EXT               
           END-IF
      *
           IF      SPA-ERRCD       NOT =   SPACE                 
               GO  TO  460-PRINT-GID-EXT               
           END-IF
      *     
           IF      SPA-GMN-TBL-GROUP   =   SPACE
               MOVE    "0001"              TO  SPA-ERRCD
           ELSE
               IF      SPA-GMN-SELNUM  =   ZERO
                   CONTINUE
               ELSE
                   IF      SPA-GMN-STPAGE      >
                                   SPA-GMN-TPAGE (SPA-GMN-SELNUM) 
                   OR      SPA-GMN-EDPAGE      >
                                   SPA-GMN-TPAGE (SPA-GMN-SELNUM) 
                       MOVE    "0002"              TO  SPA-ERRCD
                   ELSE    
                       IF      SPA-GMN-EDPAGE      <   SPA-GMN-STPAGE 
                           MOVE    "0002"              TO  SPA-ERRCD
                       END-IF
                   END-IF
               END-IF            
           END-IF                                
      *
           IF      SPA-ERRCD       NOT =   SPACE                 
               GO  TO  460-PRINT-GID-EXT               
           END-IF
      *         
      *    帳票管理ＤＢのチェック
           INITIALIZE                      PRTKANRI-REC
           MOVE    SPA-PRTKANRI-TBL-KEY
                                       TO  PRTKANRI-TBL-KEY
           MOVE    SPA-GMN-TBL-GROUP   TO  PRTKANRI-TBL-GROUP
           MOVE    SPA-GMN-TBL-UUID    TO  PRTKANRI-TBL-UUID
grpsys     MOVE    SPA-HOSPNUM         TO  PRTKANRI-HOSPNUM
grpsys*    MOVE    SPA-HOSPNUM         TO  -HOSPNUM
           MOVE    PRTKANRI-REC        TO  MCPDATA-REC
           MOVE    "DBSELECT"          TO  MCP-FUNC
           MOVE    "tbl_prtkanri"      TO  MCP-TABLE
           MOVE    "key2"
                                       TO  MCP-PATHNAME
grpsys     CALL    "ORCDBMAIN"         USING   MCPAREA
                                               MCPDATA-REC
                                               SPA-AREA
           IF      MCP-RC             =   ZERO
               PERFORM 900-PRTKANRI-FET2-SEC
           ELSE
               MOVE    1                   TO  FLG-PRTKANRI
           END-IF
      *     
      *    指定のグループが存在しないとき
           IF      FLG-PRTKANRI        =   1
               MOVE    "0005"              TO  SPA-ERRCD
               GO  TO  460-PRINT-GID-EXT   
           ELSE
               MOVE    PRTKANRI-TBL-KEY    TO  SPA-WRK-TBL-KEY    
           END-IF
      *     
      *    ガイドメッセージセット
           IF     MCP-WIDGET           =   "B12"
                  MOVE    "1001"       TO  SPA-XAIDCD2
           ELSE
                  MOVE    "1005"       TO  SPA-XAIDCD2
           END-IF
      *
      *    未処理の帳票があるとき
      *****PERFORM                     UNTIL   FLG-PRTKANRI    =   1
      *        IF      PRTKANRI-PRTFLG         =   SPACE
      *            MOVE    "1004"          TO  SPA-XAIDCD2
      *            MOVE    1               TO  FLG-PRTKANRI
      *        END-IF
      *        PERFORM 900-PRTKANRI-FET2-SEC
      *****END-PERFORM                   
      *                 
           MOVE    "DBCLOSECURSOR"     TO  MCP-FUNC
           MOVE    "tbl_prtkanri"      TO  MCP-TABLE
           MOVE    "key2"
                                       TO  MCP-PATHNAME
grpsys     CALL    "ORCDBMAIN"         USING   MCPAREA
                                               MCPDATA-REC
                                               SPA-AREA
           .
       460-PRINT-GID-EXT.
           EXIT.
      *
      *****************************************************************
      *    印刷編集処理
      *****************************************************************
       460-PRINT-SEC                   SECTION.
      * 
      *    ＪＯＢＩＤ（３００４）削除処理
           MOVE    SPACE               TO  SYS-3003-REC 
           INITIALIZE                      SYS-3003-REC
           MOVE    "3003"              TO  SYS-3003-KANRICD
      *    MOVE    WRK-CONS-3003-KBNCD TO  SYS-3003-KBNCD
           MOVE    WRK-CONS-3003-KBNCD-LIKE
                                       TO  SYS-3003-KBNCD
      *    MOVE    SPA-SYSYMD          TO  ORC-DBYMD 
           MOVE    SPA-SYSYMD          TO  SYS-3003-STYUKYMD
                                           SYS-3003-EDYUKYMD
grpsys     MOVE    SPA-HOSPNUM         TO  SYS-3003-HOSPNUM
grpsys*    MOVE    SPA-HOSPNUM         TO  -HOSPNUM
           MOVE    SYS-3003-REC        TO  MCPDATA-REC
           PERFORM 900-SYSKANRI-INV-SEC
           IF      FLG-SYSKANRI        =   ZERO
               MOVE    MCPDATA-REC         TO  SYS-3003-REC
grpsys*        MOVE    SPA-HOSPNUM         TO  -HOSPNUM
               MOVE    SYS-3003-REC        TO  MCPDATA-REC
               MOVE    "DBDELETE"          TO  MCP-FUNC
               MOVE    "tbl_syskanri"      TO  MCP-TABLE
               MOVE    "key"
                                           TO  MCP-PATHNAME
grpsys         CALL    "ORCDBMAIN"         USING   MCPAREA
                                                   MCPDATA-REC
                                                   SPA-AREA
               IF      MCP-RC              =   ZERO
                   CONTINUE
               ELSE                                 
                   MOVE    "1005"              TO  SPA-ERRCD
                   GO  TO  460-PRINT-EXT
               END-IF
           END-IF                 
      * 
      *    ジョブ管理削除処理
           MOVE    "ALL"           TO  SJOBKANRI-MODE
           INITIALIZE                  JOBKANRI-REC
           MOVE    WRK-CONS-JOB-SHELLID
                                   TO  JOB-SHELLID
           MOVE    SPA-HOSPNUM     TO  JOB-HOSPNUM
           CALL    "ORCSJOB"       USING   ORCSJOBKANRIAREA  
                                           JOBKANRI-REC
           IF      SJOBKANRI-RETURN    =   ZERO
               CONTINUE
           ELSE
               MOVE    "1004"              TO  SPA-ERRCD
               GO  TO  460-PRINT-EXT
           END-IF             
      *
           ACCEPT  SYS-YMD         FROM    DATE
           COMPUTE WRK-SYSYY       =   SYS-YY      +   2000
           MOVE    SYS-MM              TO  WRK-SYSMM
           MOVE    SYS-DD              TO  WRK-SYSDD
           ACCEPT  SYS-TIME            FROM    TIME
      *
           MOVE    ZERO                TO  WRK-CONS-JOB-JOBID     
                                           WRK-PRTKANRI-RENNUM
           MOVE    SPACE               TO  WRK-CONS-PRTKANRI-TBL-GROUP                                
      *     
           MOVE    "RECEERR"           TO  RECEERR-FILE-ID
           MOVE    SPA-TERMID          TO  RECEERR-TERMID
      *
      *    印刷処理
      *
           PERFORM 45011-JOBKANRI-HENSYU-SEC
      *                                   
      *****MOVE    RECEERR             TO  SHELL-ERRFILE
      *
           MOVE    SPA-GMN-STPAGE      TO  WRK-STPAGE
           IF      SPA-GMN-EDPAGE      =   ZERO
               MOVE    ALL "9"             TO  WRK-EDPAGE-G
           ELSE
               MOVE    SPA-GMN-EDPAGE      TO  WRK-EDPAGE
           END-IF                            
      *             
           MOVE    SPACE               TO  SHELLTBL
      *****MOVE    "/usr/local/orca/scripts/allways/print.sh"
      *****                            TO  SHELLTBL-NAME
           MOVE    "print_prv.sh"          TO  WRK-SHELL-SHELL
      *    ファイルデイレクトリ位置指定
           INITIALIZE                  ORCSMKPASSAREA
           MOVE    WRK-SHELL-NAME      TO  MKPASS-IN-01
           CALL    "ORCSMKPASS"        USING   ORCSMKPASSAREA
           MOVE    MKPASS-OT-01        TO  SHELLTBL-NAME
      *     
           MOVE    SPACE               TO  LNK-PRTKANRI-AREA
           INITIALIZE                      LNK-PRTKANRI-AREA
           MOVE    SPA-WRK-TBL-KEY     TO  LNK-PRTKANRI-TBL-KEY  
           MOVE    SPA-GMN-TBL-GROUP   TO  LNK-PRTKANRI-TBL-GROUP
           MOVE    SPA-GMN-TBL-UUID    TO  LNK-PRTKANRI-TBL-UUID
           MOVE    SPA-GMN-RENNUM      TO  LNK-PRTKANRI-RENNUM
           MOVE    SPA-GMN-SHORI-RENNUM
                                       TO  LNK-PRTKANRI-SHORI-RENNUM
           MOVE    "print_prv.sh"      TO  LNK-PRTKANRI-SHELLID
           MOVE    SPA-TERMID          TO  LNK-PRTKANRI-TERMID
           MOVE    SPA-OPID            TO  LNK-PRTKANRI-OPID
           MOVE    LNK-PRTKANRI-RENNUM TO  SHELLTBL-ARG1       
           MOVE    LNK-PRTKANRI-TBL-KEY
                                       TO  SHELLTBL-ARG2     
           MOVE    LNK-PRTKANRI-TBL-GROUP
                                       TO  SHELLTBL-ARG3       
           MOVE    LNK-PRTKANRI-SHORI-RENNUM
                                       TO  SHELLTBL-ARG4       
           MOVE    LNK-PRTKANRI-SRYYM  TO  SHELLTBL-ARG5       
           MOVE    LNK-PRTKANRI-SKYYMD TO  SHELLTBL-ARG6       
           MOVE    LNK-PRTKANRI-SHELLID
                                       TO  SHELLTBL-ARG7       
           MOVE    LNK-PRTKANRI-PRIORITY
                                       TO  SHELLTBL-ARG8      
           MOVE    LNK-PRTKANRI-TERMID TO  SHELLTBL-ARG9       
           MOVE    LNK-PRTKANRI-OPID   TO  SHELLTBL-ARG10      
           MOVE    LNK-PRTKANRI-PRTNM  TO  SHELLTBL-ARG11      
      *                 
      *                 
           MOVE    RECEERR             TO  SHELLTBL-ARG12
           MOVE    WRK-CONS-JOB-JOBID  TO  SHELLTBL-ARG13
           MOVE    WRK-CONS-JOB-SHELLID
                                       TO  SHELLTBL-ARG14
           MOVE    WRK-STPAGE          TO  SHELLTBL-ARG15
           MOVE    WRK-EDPAGE          TO  SHELLTBL-ARG16
           MOVE    SPA-HOSPNUM         TO  SHELLTBL-ARG18
           MOVE    LNK-PRTKANRI-TBL-UUID 
                                       TO  SHELLTBL-ARG19      
           MOVE    LNK-PRTKANRI-GYOUMU-CD
                                       TO  SHELLTBL-ARG20      
           EVALUATE    SPA-XAID1-FLG
               WHEN    "PV"
                   MOVE    "3"             TO  SHELLTBL-ARG17
               WHEN    "NO"
                   MOVE    "3"             TO  SHELLTBL-ARG17
               WHEN    "OK"
                   MOVE    "2"             TO  SHELLTBL-ARG17
           END-EVALUATE
grpsys*    MOVE    SPA-HOSPNUM         TO  -HOSPNUM
           MOVE    SHELLTBL            TO  MCPDATA-REC
           MOVE    "SHELL"             TO  MCP-FUNC
           MOVE    "shell"             TO  MCP-TABLE
           MOVE    "shell"
                                       TO  MCP-PATHNAME
grpsys     CALL    "ORCDBMAIN"         USING   MCPAREA
                                               MCPDATA-REC
                                               SPA-AREA
      *
      *    ＪＯＢＩＤ更新処理
      *
           PERFORM 45012-JOBID-UPD-SEC 
      *     
      *    情報確認画面を表示                                                   
           PERFORM 490-KAKUNIN-SEC
           .
       460-PRINT-EXT.
           EXIT.
      *
      *****************************************************************
      *    ジョブ開始セット処理
      *****************************************************************
       45011-JOBKANRI-HENSYU-SEC       SECTION.
      *
           ADD     1               TO  WRK-CONS-JOB-JOBID
      *       
           MOVE    "DEL"           TO  SJOBKANRI-MODE
           INITIALIZE                  JOBKANRI-REC
           MOVE    WRK-CONS-JOB-JOBID
                                   TO  JOB-JOBID
           MOVE    WRK-CONS-JOB-SHELLID
                                   TO  JOB-SHELLID
           MOVE    SPA-HOSPNUM     TO  JOB-HOSPNUM
           CALL    "ORCSJOB"       USING
                                   ORCSJOBKANRIAREA 
                                   JOBKANRI-REC
      *
           MOVE    "JBS"           TO  SJOBKANRI-MODE
           INITIALIZE                  JOBKANRI-REC
           MOVE    WRK-CONS-JOB-JOBID
                                   TO  JOB-JOBID
           MOVE    WRK-CONS-JOB-SHELLID
                                   TO  JOB-SHELLID
           MOVE    SPA-TERMID      TO  JOB-TERMID                        
           MOVE    SPA-HOSPNUM     TO  JOB-HOSPNUM
           CALL    "ORCSJOB"       USING
                                   ORCSJOBKANRIAREA 
                                   JOBKANRI-REC
      *
           .
       45011-JOBKANRI-HENSYU-EXT.
           EXIT.
      *
      *****************************************************************
      *    ＪＯＢＩＤ更新処理
      *****************************************************************
       45012-JOBID-UPD-SEC    SECTION.
      *
           MOVE    SPACE               TO  SYS-3003-REC 
           INITIALIZE                      SYS-3003-REC
           MOVE    "3003"              TO  SYS-3003-KANRICD
      *    MOVE    WRK-CONS-3003-KBNCD TO  SYS-3003-KBNCD
           MOVE    WRK-CONS-3003-KBNCD-LIKE
                                       TO  SYS-3003-KBNCD
      *    MOVE    SPA-SYSYMD          TO  ORC-DBYMD 
           MOVE    SPA-SYSYMD          TO  SYS-3003-STYUKYMD
                                           SYS-3003-EDYUKYMD
grpsys     MOVE    SPA-HOSPNUM         TO  SYS-3003-HOSPNUM
grpsys*    MOVE    SPA-HOSPNUM         TO  -HOSPNUM
           MOVE    SYS-3003-REC        TO  MCPDATA-REC
           PERFORM 900-SYSKANRI-INV-SEC
      *       
           MOVE    SPACE               TO  SYS-3003-REC 
           INITIALIZE                      SYS-3003-REC
           MOVE    "3003"              TO  SYS-3003-KANRICD
           MOVE    WRK-CONS-3003-KBNCD TO  SYS-3003-KBNCD
           MOVE    ZERO                TO  SYS-3003-STYUKYMD
           MOVE    ALL "9"             TO  SYS-3003-EDYUKYMD
           MOVE    WRK-CONS-PRTKANRI-TBL-KEY
                                       TO  SYS-3003-TBL-KEY
           MOVE    WRK-CONS-PRTKANRI-TBL-GROUP
                                       TO  SYS-3003-TBL-GROUP
           MOVE    WRK-CONS-JOB-JOBID  TO  SYS-3003-JOBID                                      
      *
grpsys     MOVE    SPA-HOSPNUM         TO  SYS-3003-HOSPNUM
grpsys*    MOVE    SPA-HOSPNUM         TO  -HOSPNUM
           MOVE    SYS-3003-REC        TO  MCPDATA-REC
           IF      FLG-SYSKANRI        =   ZERO     
               MOVE    "DBUPDATE"          TO  MCP-FUNC
           ELSE
               MOVE    "DBINSERT"          TO  MCP-FUNC
           END-IF         
           MOVE    "tbl_syskanri"      TO  MCP-TABLE
           MOVE    "key"
                                       TO  MCP-PATHNAME
grpsys     CALL    "ORCDBMAIN"         USING   MCPAREA
                                               MCPDATA-REC
                                               SPA-AREA
           IF      MCP-RC          NOT =   ZERO
               DISPLAY "SYSKANRI 3003  ERR KEY = ["
                                  MCP-RC  " " SYS-3003-KEY  "]"   
      *****    MOVE    "1010"          TO  SPA-ERRCD
           END-IF
      *
           .
       45012-JOBID-UPD-EXT.
           EXIT.
      *
      *****************************************************************
      *    処理確認処理
      *****************************************************************
       490-KAKUNIN-SEC            SECTION.
      *
           MOVE    SPACE               TO  XA99
           MOVE    ZERO                TO  XA99-COUNT
           MOVE    WIDGET-INSENSITIVE  TO  XA99-B12-STATE
      *        
           MOVE    SPACE               TO  SYS-3003-REC 
           INITIALIZE                      SYS-3003-REC
           MOVE    "3003"              TO  SYS-3003-KANRICD
      *    MOVE    WRK-CONS-3003-KBNCD TO  SYS-3003-KBNCD
           MOVE    WRK-CONS-3003-KBNCD-LIKE
                                       TO  SYS-3003-KBNCD
      *    MOVE    SPA-SYSYMD          TO  ORC-DBYMD 
           MOVE    SPA-SYSYMD          TO  SYS-3003-STYUKYMD
                                           SYS-3003-EDYUKYMD
grpsys     MOVE    SPA-HOSPNUM         TO  SYS-3003-HOSPNUM
grpsys*    MOVE    SPA-HOSPNUM         TO  -HOSPNUM
           MOVE    SYS-3003-REC        TO  MCPDATA-REC
           PERFORM 900-SYSKANRI-INV-SEC
           IF      FLG-SYSKANRI        =   ZERO
               MOVE    MCPDATA-REC         TO  SYS-3003-REC 
               PERFORM VARYING IDX FROM    1   BY  1
                       UNTIL   IDX >       SYS-3003-JOBID
      *            ジョブ管理チェック処理
                   MOVE    "CHK"           TO  SJOBKANRI-MODE
                   INITIALIZE                  JOBKANRI-REC
                   MOVE    IDX             TO  JOB-JOBID
                   MOVE    WRK-CONS-JOB-SHELLID
                                           TO  JOB-SHELLID
                   MOVE    SPA-HOSPNUM     TO  JOB-HOSPNUM
                   CALL    "ORCSJOB"       USING
                                           ORCSJOBKANRIAREA  
                                           JOBKANRI-REC 
                   IF      SJOBKANRI-RETURN    =   ZERO
                       MOVE    JOB-JOBID       TO  XA99-RENNUM   (IDX)
                       MOVE    JOB-SHELLMSG    TO  XA99-SHORINM  (IDX)
                       MOVE    JOB-STARTTIME(1:2)
                                               TO  WRK-HH
                       MOVE    JOB-STARTTIME(3:2)
                                               TO  WRK-MM
                       MOVE    JOB-STARTTIME(5:2)
                                               TO  WRK-SS
                       MOVE    WRK-HENTIME     TO  XA99-STARTTIME(IDX)
                       MOVE    JOB-ENDTIME(1:2)
                                               TO  WRK-HH
                       MOVE    JOB-ENDTIME(3:2)
                                               TO  WRK-MM
                       MOVE    JOB-ENDTIME(5:2)
                                               TO  WRK-SS
                       MOVE    WRK-HENTIME     TO  XA99-ENDTIME  (IDX)
                       MOVE    JOB-UPDCNT      TO  WRK-PAGE
                       MOVE    WRK-PAGE        TO  XA99-PAGE     (IDX)
                       MOVE    JOB-YOBI        TO  XA99-ERRMSG   (IDX)
                       IF      JOB-ENDYMD      =   SPACE
                           MOVE    "処理中です"    TO  XA99-MSG
                       ELSE
                           IF      JOB-ERRCD       =   SPACE
                               CONTINUE
                           ELSE
                               STRING  JOB-YOBI    DELIMITED  BY  SPACE
                                       "【"        DELIMITED  BY  SIZE
                                      JOB-SHELLMSG DELIMITED  BY  SPACE
                                      "】"         DELIMITED  BY  SIZE      
                                                   INTO  XA99-MSG
                           END-IF                          
                       END-IF    
                       MOVE    IDX                     TO  XA99-COUNT    
                   END-IF
               END-PERFORM
           END-IF
           MOVE    "B01"                TO  MCP-WIDGET
           IF      XA99-MSG              =   SPACE
               IF      XA99-COUNT            >   ZERO    
                   MOVE    "処理は正常に終了しました"
                                                TO  XA99-MSG
                   IF      SPA-XAID1-FLG    =   "PV"
                       MOVE    WIDGET-NORMAL    TO  XA99-B12-STATE
                       MOVE    "B12"            TO  MCP-WIDGET
                   ELSE
                       MOVE    "B01"            TO  MCP-WIDGET
                   END-IF
               ELSE
                   MOVE    "該当する処理はありません"
                                                TO  XA99-MSG
               END-IF                                 
           END-IF                                 
      *
           MOVE    "XA02"                TO  SPA-MOTOPG
           MOVE    "XA99"                TO  SPA-SAKIPG
      *
           MOVE    "NEW"                TO  MCP-PUTTYPE
           MOVE    "XA99"                TO  MCP-WINDOW
      *
           PERFORM 900-PUT-WINDOW
      *
           MOVE    1                    TO  FLG-END              
           .
       490-KAKUNIN-EXT.
           EXIT.
      *
      *****************************************************************
      *    和暦西暦変換編集処理
      *****************************************************************
       5002-HIZUKE-HEN-SEC          SECTION.
      *
           IF      WRK-SYMD            =   ALL "9"  OR   SPACE
               MOVE    SPACE               TO  WRK-HGO
               MOVE    WRK-SYMD(3:2)       TO  WRK-HYY
               MOVE    WRK-SYMD(5:2)       TO  WRK-HMM
               MOVE    WRK-SYMD(7:2)       TO  WRK-HDD
               MOVE    WRK-HENYMD          TO  WRK-HENYMDG
           ELSE
               INITIALIZE                      STS-AREA-DAY
               INITIALIZE                      LNK-DAY2-AREA
               MOVE    "21"                TO  LNK-DAY2-IRAI
               MOVE    WRK-SYMD            TO  LNK-DAY2-YMD
               CALL    "ORCSDAY"            USING   STS-AREA-DAY
                                                   LNK-DAY2-AREA
               MOVE    LNK-DAY2-EDTYMD1    TO  WRK-HENYMDG
           END-IF
      *
           .
       5002-HIZUKE-HEN-EXT.
           EXIT.
      *
      *****************************************************************
      *    自画面編集処理
      *****************************************************************
       500-SET-SCREEN              SECTION.
      *
           PERFORM 500-GMNHEN-SEC
      *
           IF      SPA-ERRCD       NOT =   SPACE
               PERFORM 510-ERRSET-SEC
               GO  TO  500-SET-SCREEN-EXT
           END-IF
      *
           IF      SPA-XAIDCD       NOT =   SPACE
               PERFORM 520-TIDSET-SEC
               GO  TO  500-SET-SCREEN-EXT
           END-IF
      *
           IF      SPA-XAIDCD2      NOT =   SPACE
               PERFORM 530-XAID2SET-SEC
               GO  TO  500-SET-SCREEN-EXT
           END-IF
      *
      **** MOVE    SPACE               TO  LINKAREA
      *
           MOVE    "CURRENT"           TO  MCP-PUTTYPE.
           MOVE    "XA02    "          TO  MCP-WINDOW.
      *
           PERFORM 900-PUT-WINDOW.
           .
       500-SET-SCREEN-EXT.
           EXIT.
      *
      *****************************************************************
      *    画面内容編集処理
      *****************************************************************
       500-GMNHEN-SEC              SECTION.
      *
           MOVE    XA02-ROW            TO  WRK-LIST-ROW
      *
           INITIALIZE  XA02
      *
           EVALUATE    FLG-LIST-ROW
           WHEN    1
               MOVE    ZERO            TO  XA02-ROW
               MOVE    ZERO            TO  XA02-ROWATTR
           WHEN    OTHER
               MOVE    WRK-LIST-ROW    TO  XA02-ROW
               MOVE    ZERO            TO  XA02-ROWATTR
           END-EVALUATE
      *
           MOVE    SPA-GMN-TITLE1      TO  XA02-HEAD1
           MOVE    SPA-GMN-TITLE2      TO  XA02-HEAD2
      *     
           MOVE    SPA-GMN-SELNUM      TO  XA02-SELNUM
           MOVE    SPA-GMN-TBL-GROUP   TO  XA02-TBL-GROUP
           MOVE    SPA-GMN-RENNUM      TO  XA02-RENNUM
           MOVE    SPA-GMN-SHORI-RENNUM 
                                       TO  XA02-SHORI-RENNUM
           MOVE    SPA-GMN-STPAGE      TO  XA02-STPAGE
           MOVE    SPA-GMN-EDPAGE      TO  XA02-EDPAGE                            
      *
           PERFORM VARYING     IDX     FROM    1   BY  1
                   UNTIL       IDX             >   SPA-GMN-MAX
      *
               MOVE    SPA-GMN-TNO       (IDX) TO  WRK-ZZ
               MOVE    WRK-ZZ                  TO  XA02-TNO    (IDX)
               IF      SPA-GMN-TTBL-GROUP(IDX) NOT = SPACE
               MOVE    SPA-GMN-TTBL-GROUP(IDX)(1:8)
                                               TO  WRK-SYMD
               PERFORM 5002-HIZUKE-HEN-SEC
               MOVE    WRK-HENYMDG     TO  XA02-SAKU-YMD(IDX)
               MOVE    SPA-GMN-TTBL-GROUP(IDX)(9:2)
                                       TO  XA02-SAKU-TIME(IDX)(1:2)
               MOVE    ":" 
                                       TO  XA02-SAKU-TIME(IDX)(3:1)
               MOVE    SPA-GMN-TTBL-GROUP(IDX)(11:2)
                                       TO  XA02-SAKU-TIME(IDX)(4:2)
               MOVE    ":" 
                                       TO  XA02-SAKU-TIME(IDX)(6:1)
               MOVE    SPA-GMN-TTBL-GROUP(IDX)(13:2)
                                       TO  XA02-SAKU-TIME(IDX)(7:2)
               END-IF
               MOVE    SPA-GMN-TRENNUM   (IDX) TO  XA02-TRENNUM   (IDX)
               MOVE    SPA-GMN-TSHORI-RENNUM(IDX)
                                               TO
                                               XA02-TSHORI-RENNUM (IDX)
               MOVE    SPA-GMN-TPAGE     (IDX) TO  WRK-PAGE
               MOVE    WRK-PAGE(6:5)           TO  XA02-TPAGE     (IDX)
      *
               MOVE    SPA-GMN-GYOUMU    (IDX) TO  XA02-GYOMUNM   (IDX)
      *
               MOVE    SPA-GMN-TSKYYMD   (IDX) TO  XA02-TSKYYMD   (IDX)
               MOVE    SPA-GMN-LPTNUM (IDX)    TO
                                               XA02-LPTNUM  (IDX)
               MOVE    SPA-GMN-TPRT-TITLE(IDX) TO  XA02-TPRT-TITLE(IDX)
               MOVE    SPA-GMN-LPTNAME   (IDX) TO  XA02-PTNAME   (IDX)
      *       
               IF      IDX             =   SPA-NAI-SELNUM
                   MOVE    "T"             TO  XA02-SELECT (IDX)
               ELSE
                   MOVE    "F"             TO  XA02-SELECT (IDX)
               END-IF
           END-PERFORM
      *
           MOVE    SPA-GMN-MAX         TO  XA02-COUNT
      *
           MOVE    SPA-NAI-GYOMU-COMB  TO  XA02-G-VALUE
      *    MOVE    SPA-GMN-G-COUNT     TO  XA02-G-COUNT
           MOVE    SPA-NAI-G-COUNT     TO  XA02-G-COUNT
           PERFORM VARYING     IDX     FROM    1   BY  1
                   UNTIL       IDX             >   SPA-NAI-G-COUNT
      *        MOVE    SPA-GMN-G-ITEM(IDX)     TO  XA02-G-ITEM(IDX)
               MOVE    SPA-NAI-G-ITEM(IDX)     TO  XA02-G-ITEM(IDX)
           END-PERFORM
           MOVE    SPA-GMN-PTNUM       TO  XA02-PTNUM 
           MOVE    "blue"              TO  XA02-PTNAME-STYLE
           MOVE    SPA-GMN-PTNAME      TO  XA02-PTNAME-VALUE
      *
           MOVE    SPA-GMN-SRYYMD      TO  XA02-SRYYMD
      *
           MOVE    WIDGET-NORMAL       TO  XA02-B06S-STATE
                                           XA02-B07S-STATE
      *
           IF    ( SPA-GMN-PAGE        =   1 )
               MOVE    WIDGET-INSENSITIVE
                                       TO  XA02-B06S-STATE
           END-IF
      *
           IF    ( SPA-GMN-TBL-NEXT    =   ZERO )
               MOVE    WIDGET-INSENSITIVE
                                       TO  XA02-B07S-STATE
           END-IF
      *
           PERFORM 5001-MAPCUR-SEC
      *
           .
       500-GMNHEN-EXT.
           EXIT.
      *
      *****************************************************************
      *    画面カーソルセット処理
      *****************************************************************
       5001-MAPCUR-SEC             SECTION.
      *
           EVALUATE    SPA-GMN-CUR
               WHEN    ZERO
                   MOVE    "GYOMUCD"      TO  MCP-WIDGET
               WHEN    1
                   MOVE    "SELNUM"       TO  MCP-WIDGET
               WHEN    3
                   MOVE    "RENNUM"       TO  MCP-WIDGET
               WHEN    4
                   MOVE    "SHORI_RENNUM" TO  MCP-WIDGET
               WHEN    5
                   MOVE    "STPAGE"       TO  MCP-WIDGET
               WHEN    6
                   MOVE    "EDPAGE"       TO  MCP-WIDGET
               WHEN    99
                   MOVE    "B12"          TO  MCP-WIDGET
           END-EVALUATE
          .
       5001-MAPCUR-EXT.
           EXIT.
      *
      *****************************************************************
      *    エラーメッセージ表示理
      *****************************************************************
       510-ERRSET-SEC              SECTION.
           MOVE    SPACE               TO  SPA-ERRMSG
      *
           EVALUATE    SPA-ERRCD
               WHEN    "0001"
                   MOVE    "該当の選択番号はありません" 
                                               TO  SPA-ERRMSG
               WHEN    "0002"
                   MOVE    "頁の入力が違います" 
                                               TO  SPA-ERRMSG
               WHEN    "0003"
                   MOVE    "未入力の項目があります" 
                                               TO  SPA-ERRMSG
               WHEN    "0004"
                   MOVE    "頁の入力が違います" 
                                               TO  SPA-ERRMSG
               WHEN    "0005"
                   MOVE    "該当の帳票はありません" 
                                               TO  SPA-ERRMSG
               WHEN    "0006"
                   MOVE    "該当の帳票はありません" 
                                               TO  SPA-ERRMSG
               WHEN    "0007"
                   MOVE    "業務コードの入力に誤りがあります" 
                                               TO  SPA-ERRMSG
               WHEN    "1001"
                   MOVE    "帳票管理ＤＢが削除できませんでした"
                                               TO  SPA-ERRMSG
               WHEN    "1002"
                   MOVE    "帳票ＤＢが削除できませんでした"
                                               TO  SPA-ERRMSG
               WHEN    "1004"
                   MOVE    "ジョブ管理ＤＢが削除できませんでした"
                                               TO  SPA-ERRMSG
               WHEN    "1005"
                   MOVE
                   "システム管理ＤＢ（３００３）が削除できませんでした"
                                               TO  SPA-ERRMSG
               WHEN    "7777"
                   MOVE    WRK-RECEERR         TO  SPA-ERRMSG
               WHEN    "9999"
                   MOVE    WRK-RECEERR         TO  SPA-ERRMSG
               WHEN    "8888"
                   MOVE
                   "　　開　　発　　中!!(しばらくお待ち下さい)"
                                               TO  SPA-ERRMSG
           END-EVALUATE
      *
           MOVE    SPACE                TO  XAERR
           MOVE    SPA-ERRCD            TO  XAERR-ERRCODE
           MOVE    SPA-ERRMSG           TO  XAERR-ERRMSG
           MOVE    "B01"                TO  MCP-WIDGET
      *
           MOVE    "XA02"                TO  SPA-MOTOPG
           MOVE    "XAERR"               TO  SPA-SAKIPG
      *
           MOVE    "NEW"                 TO  MCP-PUTTYPE
           MOVE    "XAERR"               TO  MCP-WINDOW
      *
           PERFORM 900-PUT-WINDOW
      *
           MOVE    1                    TO  FLG-END              
      *
           .
       510-ERRMSG-EXT.
           EXIT.
      *
      *****************************************************************
      *    エラーメッセージ表示理
      *****************************************************************
       520-TIDSET-SEC              SECTION.
      *
           MOVE    SPACE               TO  WRK-XAIDMSG
      *
           EVALUATE    SPA-XAIDCD
               WHEN    "1002"
                   MOVE    "該当のグループの削除処理を行います"
                                       TO  WRK-XAIDMSG
               WHEN    "1003"
                   MOVE
                   "処理中の可能性がありますが削除処理を行いますか"
                                       TO  WRK-XAIDMSG
               WHEN    "1005"
                   MOVE    "プレビュー処理を行います"
                                       TO  WRK-XAIDMSG
               WHEN    "6666"
                   MOVE    "印刷処理中です"
                                            TO  WRK-XAIDMSG
               WHEN    "8888"
                   MOVE    "処理が正常終了しました"
                                            TO  WRK-XAIDMSG
           END-EVALUATE
      *
           MOVE    SPACE                TO  XAID1
           MOVE    SPA-XAIDCD           TO  XAID1-ID1CODE
           MOVE    WRK-XAIDMSG          TO  XAID1-ID1MSG
           MOVE    "B12"                TO  MCP-WIDGET
      *
           MOVE    "XA02"               TO  SPA-MOTOPG
           MOVE    "XAID1"              TO  SPA-SAKIPG
      *
           MOVE    "NEW"                TO  MCP-PUTTYPE
           MOVE    "XAID1"              TO  MCP-WINDOW
      *
           PERFORM 900-PUT-WINDOW
      *
           MOVE    1                    TO  FLG-END              
      *
           .
       520-TIDSET-EXT.
           EXIT.
      *
      *****************************************************************
      *    ガイドメッセージ３表示処理
      *****************************************************************
       530-XAID2SET-SEC              SECTION.
      *
           MOVE    SPACE               TO  WRK-XAIDMSG
      *
           EVALUATE    SPA-XAIDCD2
               WHEN    "1001"
                   MOVE    "印刷処理を行います"
                                       TO  WRK-XAIDMSG
      *********WHEN    "1004"
      **           MOVE
      **           "処理中の可能性がありますが印刷処理を行いますか"
      *********                        TO  WRK-XAIDMSG
           END-EVALUATE
      *
           MOVE    SPACE                TO  XAID2
           MOVE    SPA-XAIDCD2          TO  XAID2-ID1CODE
           MOVE    WRK-XAIDMSG          TO  XAID2-ID1MSG
           MOVE    "B12"                TO  MCP-WIDGET
      *
           MOVE    "XA02"               TO  SPA-MOTOPG
           MOVE    "XAID2"              TO  SPA-SAKIPG
      *
           MOVE    "NEW"                TO  MCP-PUTTYPE
           MOVE    "XAID2"              TO  MCP-WINDOW
      *
           PERFORM 900-PUT-WINDOW
      *
           MOVE    1                    TO  FLG-END              
      *
           .
       530-XAID2SET-EXT.
           EXIT.
      *
      *****************************************************************
      *    カレンダー処理
      *****************************************************************
       800-CALENDAR-SEC                SECTION.
      *
      *    増減パターン 1:日／2:月／3:年
      *
           INITIALIZE                  STS-AREA-DAY
           INITIALIZE                  LNK-DAY6-AREA
           MOVE    "61"                TO  LNK-DAY6-IRAI
           MOVE    WRK-YMD             TO  LNK-DAY6-KIJUN
           MOVE    WRK-ZOGENPTN        TO  LNK-DAY6-ZOGENPTN
           MOVE    WRK-ZOGEN           TO  LNK-DAY6-ZOGEN
           CALL    "ORCSDAY"           USING   STS-AREA-DAY
                                               LNK-DAY6-AREA
      *    MOVE    LNK-DAY6-KEISAN     TO  WRK-KEISANYMD
      *
           .
       800-CALENDAR-EXT.
           EXIT.
      *****************************************************************
      *     印刷管理ＤＢ読み込み（ｋｅｙ２）
      *****************************************************************
       900-PRTKANRI-FET2-SEC            SECTION.
      *
           MOVE    "DBFETCH"           TO  MCP-FUNC
           MOVE    "tbl_prtkanri"      TO  MCP-TABLE
           MOVE    "key2"
                                       TO  MCP-PATHNAME
grpsys     CALL    "ORCDBMAIN"         USING   MCPAREA
                                               MCPDATA-REC
                                               SPA-AREA
      *                                  
           IF      MCP-RC              =   ZERO
               MOVE    MCPDATA-REC         TO  PRTKANRI-REC
               IF      SPA-GMN-RENNUM      =   ZERO
                   CONTINUE
               ELSE
                   IF      SPA-GMN-RENNUM   NOT = 
                                           PRTKANRI-RENNUM
                       GO  TO  900-PRTKANRI-FET2-SEC
                   END-IF
               END-IF                                             
               IF      SPA-GMN-SHORI-RENNUM   =   ZERO
                   CONTINUE
               ELSE
                   IF      SPA-GMN-SHORI-RENNUM   NOT = 
                                           PRTKANRI-SHORI-RENNUM
                       GO  TO  900-PRTKANRI-FET2-SEC
                   END-IF
               END-IF                                             
               MOVE    ZERO                TO  FLG-PRTKANRI
           ELSE
               MOVE    1                   TO  FLG-PRTKANRI
           END-IF
           .
       900-PRTKANRI-FET2-EXT.
           EXIT.
      *
      *****************************************************************
      *     印刷管理ＤＢ読み込み
      *****************************************************************
       900-PRTKANRI-FET-SEC            SECTION.
      *
           MOVE    "DBFETCH"           TO  MCP-FUNC
           MOVE    "tbl_prtkanri"      TO  MCP-TABLE
           MOVE    WRK-MCP-PATHNAME
                                       TO  MCP-PATHNAME
grpsys     CALL    "ORCDBMAIN"         USING   MCPAREA
                                               MCPDATA-REC
                                               SPA-AREA
      *                                  
           IF      MCP-RC              =   ZERO
               MOVE    MCPDATA-REC         TO  PRTKANRI-REC
               MOVE    ZERO                TO  FLG-PRTKANRI
           ELSE
               MOVE    1                   TO  FLG-PRTKANRI
           END-IF
           .
       900-PRTKANRI-FET-EXT.
           EXIT.
      *****************************************************************
      *     印刷管理ＤＢ読み込み
      *****************************************************************
       900-PRTKANRI-KEY2-FET-SEC           SECTION.
      *
           MOVE    ZERO                TO  FLG-PRTKANRI2
      *
           MOVE    "DBFETCH"           TO  MCP-FUNC
           MOVE    "tbl_prtkanri"      TO  MCP-TABLE
           MOVE    "key2"              TO  MCP-PATHNAME
grpsys     CALL    "ORCDBMAIN"         USING   MCPAREA
                                               MCPDATA-REC
                                               SPA-AREA
      *                                  
           IF      MCP-RC              =   ZERO
               MOVE    MCPDATA-REC         TO  PRTKANRI-REC
               MOVE    ZERO                TO  FLG-PRTKANRI2
           ELSE
               MOVE    1                   TO  FLG-PRTKANRI2
           END-IF
           .
       900-PRTKANRI-KEY2-FET-EXT.
           EXIT.
      *
      *****************************************************************
      *    管理マスター読込（キー）
      *****************************************************************
       900-SYSKANRI-INV-SEC         SECTION.
      *
           MOVE    "DBSELECT"          TO  MCP-FUNC
           MOVE    "tbl_syskanri"      TO  MCP-TABLE
           MOVE    "key"
                                       TO  MCP-PATHNAME
grpsys     CALL    "ORCDBMAIN"         USING   MCPAREA
                                               MCPDATA-REC
                                               SPA-AREA
           IF      MCP-RC              =   ZERO
               MOVE    "DBFETCH"           TO  MCP-FUNC
               MOVE    "tbl_syskanri"      TO  MCP-TABLE
               MOVE    "key"
                                           TO  MCP-PATHNAME
grpsys         CALL    "ORCDBMAIN"         USING   MCPAREA
                                                   MCPDATA-REC
                                                   SPA-AREA
               IF      MCP-RC              =   ZERO
                   MOVE    ZERO                TO  FLG-SYSKANRI
               ELSE
                   MOVE    1                   TO  FLG-SYSKANRI
               END-IF
           ELSE
               MOVE    1                   TO  FLG-SYSKANRI
           END-IF
      *
           MOVE    "DBCLOSECURSOR"     TO  MCP-FUNC
           MOVE    "tbl_syskanri"      TO  MCP-TABLE
           MOVE    "key"
                                       TO  MCP-PATHNAME
grpsys     CALL    "ORCDBMAIN"         USING   MCPAREA
                                               MCPDATA-REC
                                               SPA-AREA
      *
           .
       900-SYSKANRI-INV-EXT.
           EXIT.
      *
      *****************************************************************
      *    排他制御マスター読込（キー）
      *****************************************************************
       900-TBL-LDMAP-READ-SEC        SECTION.
      *
           MOVE    1                   TO  FLG-READ-SKIP
      *
           PERFORM UNTIL     ( FLG-READ-SKIP   =   ZERO )
                   OR        ( FLG-LDMAP   NOT =   ZERO )
               MOVE    "DBFETCH"           TO  MCP-FUNC
               MOVE    "tbl_ldmap"         TO  MCP-TABLE
               MOVE    "key4"
                                           TO  MCP-PATHNAME
grpsys         CALL    "ORCDBMAIN"         USING   MCPAREA
                                                   MCPDATA-REC
                                                   SPA-AREA
               IF    ( MCP-RC              =   ZERO )
                     MOVE    ZERO          TO  FLG-LDMAP
                     MOVE    MCPDATA-REC   TO  TBL-LDMAP
      *
      *            無床診療所の場合、入院情報は読み飛ばす
                   IF    ( SPA-BEDSU       <=  ZERO )
                     AND ( LDMAP-GROUPNUM   =  2 )
                       CONTINUE
                   ELSE
                       MOVE    ZERO        TO  FLG-READ-SKIP
                   END-IF
               ELSE
                   MOVE    1               TO  FLG-LDMAP
               END-IF
           END-PERFORM
      *
           .
       900-TBL-LDMAP-READ-EXT.
           EXIT.
      *
      *****************************************************************
      *    患者マスター読込処理
      *****************************************************************
       900-PTINF-READ-SEC         SECTION.
      *
grpsys*    MOVE    SPA-HOSPNUM         TO  -HOSPNUM
           MOVE    PTINF-REC           TO  MCPDATA-REC
           MOVE    "DBSELECT"          TO  MCP-FUNC
           MOVE    "tbl_ptinf"         TO  MCP-TABLE
           MOVE    "key"
                                       TO  MCP-PATHNAME
grpsys     CALL    "ORCDBMAIN"         USING   MCPAREA
                                               MCPDATA-REC
                                               SPA-AREA
           IF      MCP-RC              =   ZERO
               MOVE    "DBFETCH"           TO  MCP-FUNC
               MOVE    "tbl_ptinf"         TO  MCP-TABLE
               MOVE    "key"
                                       TO  MCP-PATHNAME
grpsys         CALL    "ORCDBMAIN"         USING   MCPAREA
                                                   MCPDATA-REC
                                                   SPA-AREA
               IF      MCP-RC              =   ZERO
                   MOVE    MCPDATA-REC         TO  PTINF-REC
                   MOVE    ZERO                TO  FLG-PTINF
               ELSE
                   MOVE    1                   TO  FLG-PTINF
                   INITIALIZE                  PTINF-REC
               END-IF
           ELSE
               MOVE    1                   TO  FLG-PTINF
               INITIALIZE                  PTINF-REC
           END-IF
      *
           MOVE    "tbl_ptinf"         TO  MCP-TABLE
           MOVE    "key"
                                       TO  MCP-PATHNAME
           PERFORM 990-DBCLOSE-SEC
      *
           .
       900-PTINF-READ-EXT.
           EXIT.
      *
      *****************************************************************
      *    ＤＢクローズ処理
      *****************************************************************
       990-DBCLOSE-SEC         SECTION.
      *
           MOVE    "DBCLOSECURSOR"     TO  MCP-FUNC
grpsys     CALL    "ORCDBMAIN"         USING   MCPAREA
                                               MCPDATA-REC
                                               SPA-AREA
      *
           .
       990-DBCLOSE-EXT.
           EXIT.
      *
      *****************************************************************
      *    ＰＵＴ　処理
      *****************************************************************
       900-PUT-WINDOW              SECTION.
      *
           MOVE   "PUTWINDOW"                   TO  MCP-FUNC.
grpsys     CALL    "ORCDBMAIN"         USING   MCPAREA
                                               MCPDATA-REC
                                               SPA-AREA
      *
           .
       900-PUT-WINDOW-EXT.
           EXIT.

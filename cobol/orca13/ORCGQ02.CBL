      *******************************************************************
      * Project code name "ORCA"
      * 日医標準レセプトソフト（JMA standard receipt software）
      * Copyright(C) 2002 JMA (Japan Medical Association)
      *
      * This program is part of "JMA standard receipt software".
      *
      *     This program is distributed in the hope that it will be useful
      * for further advancement in medical care, according to JMA Open
      * Source License, but WITHOUT ANY WARRANTY.
      *     Everyone is granted permission to use, copy, modify and
      * redistribute this program, but only under the conditions described
      * in the JMA Open Source License. You should have received a copy of
      * this license along with this program. If not, stop using this
      * program and contact JMA, 2-28-16 Honkomagome, Bunkyo-ku, Tokyo,
      * 113-8621, Japan.
      ********************************************************************
       IDENTIFICATION                  DIVISION.
       PROGRAM-ID.                     ORCGQ02.
      *****************************************************************
      *  システム名        : ＯＲＣＡ
      *  サブシステム名    : 患者照会
      *  コンポーネント名  : 検索画面（Ｑ０２）
      *  管理者            : 
      *  作成日付    作業者        記述
      *  02/03/19    MCC−太田　   新規作成
      *****************************************************************
      *  プログラム修正履歴
      * Maj/Min/Rev  修正者       日付      内容
      * 01.00.01     MCC-森脇     02/06/04+-患者番号の追加
      *              MCC-太田     02/06/06+-最終受診日の検索条件を
      *                                   | 範囲指定式に変更
      *                                   +-総件数取得を行う判定方法変更
      *                                   +-確認画面遷移時の編集を変更
      * 01.00.02     MCC-太田     02/06/26+-ジョブ管理ＤＢ追加
      *                                   +-画面ＱＩＤ１を
      *                                   | Ｑ１００に名称変更
      *                                   +-新たにＱＩＤ１を追加
      *                                   +-削除後の保険情報も抽出対象
      *                                   | としていた不具合を修正
      *                                   +-リストに年齢追加
      *                                   +患者の病名をコードからでなく
      *                                   |名称から行うよう修正
      *                                   +診療行為を診療年月を考慮し
      *                                   |て検索を行うよう修正
      *                                   +保険者番号追加
      *                                   +保険の種類追加
      *                                   +保険（公費）入力時、
      *                                   |同時に診療年月日が入力されて
      *                                   |いた場合はビューを検索する
      *                                   |よう修正
      *                                   +受診履歴は基本情報のみ
      *                                   |条件入力された場合に限り
      *                                   |検索を行う
      *                                   +テスト患者区分を条件に追加
      * 01.00.03     MCC-太田     02/09/12+パスをサブプロから取得
      * 01.00.04     NACL-太田    03/05/26+老人一割、２割を追加
      * 01.00.05     NACL-太田    03/06/10+一括入力対応
      * 01.00.06     NACL-太田    03/09/11+コラムリストに郵便番号を追加
      * 01.00.07     NACL-太田    04/02/09+コラムリスト編集修正
      * 01.00.08     NACL-太田    04/08/05+保険を追加
      * 03.05.00     NACL-太田    07.05.23 グループ診療対応
      *****************************************************************
      *
       ENVIRONMENT                 DIVISION.
       CONFIGURATION               SECTION.
       INPUT-OUTPUT                SECTION.
       FILE-CONTROL.
      *
      *    パラメータファイル
           SELECT  BQ01-FILE   ASSIGN  PRFNAME
                                   ORGANIZATION    IS  SEQUENTIAL.
      *
       DATA                            DIVISION.
       FILE                            SECTION.
      *
      *    パラメータファイル
       FD  BQ01-FILE.
           COPY    "CPBQ01.INC".
      *
       WORKING-STORAGE                 SECTION.
      *
      *    スパ領域
           COPY    "COMMON-SPA".
      *
      *    画面用ＳＰＡ
           COPY    "Q01SCR-SPA".
      *
      *    コンスタント値
           COPY    "ENUM-VALUE".
      *
      *    パラメータファイル名
           COPY    "CPCOMMONDAT3.INC"
                                   REPLACING  //TMPFLPARA//
                                   BY         //PRFNAME//.
      *
      *    エラーファイル 名称領域
           COPY    "CPERRFL.INC".
      *
      *    フラグ領域
       01  FLG-AREA.
           03  FLG-END                             PIC 9(01).
           03  FLG-EOF                             PIC 9(01).
           03  FLG-PTINF                           PIC 9(01).
           03  FLG-JYOKEN                          PIC 9(01).
           03  FLG-JYOKEN-AND                      PIC 9(01).
           03  FLG-ENTRYCHK                        PIC 9(01).
           03  FLG-JOBEND                          PIC 9(01).
           03  FLG-HKNKOHSEL                       PIC 9(01).
           03  FLG-GMN-INIT                        PIC 9(01).
      *
      *    添字領域
       01  IDX-AREA.
           03  IDX1                                PIC 9(05).
           03  IDX2                                PIC 9(05).
           03  IDX3                                PIC 9(05).
           03  IDX-LEN                             PIC 9(05).
           03  IDX-GMN                             PIC 9(05).
      *
      *    カウント領域
       01  CNT-AREA.
           03  CNT-TTL-PTINF                       PIC 9(05).
           03  CNT-JYOKEN                          PIC 9(05).
      *
      *    一時領域
       01  WRK-AREA.
           03  WRK-STR                             PIC X(20000).
           03  WRK-STR-LEN                         PIC 9(05).
           03  WRK-STR-PNT                         PIC 9(05).
           03  WRK-PTINF-ADR-LEN                   PIC 9(05).
           03  WRK-PTINF-BANTI-LEN                 PIC 9(05).
           03  WRK-SQL-PNT                         PIC 9(05).
           03  WRK-SQL                             PIC X(20000).
           03  WRK-SQL-COUNT                       PIC X(20000).
           03  WRK-EDPTNUM                         PIC X(20).
           03  WRK-ZZZZ9                           PIC ZZZZ9.
           03  WRK-TOTAL                           PIC ZZZ,ZZZ,ZZ9.
           03  WRK-JYOKEN-PNT                      PIC 9(05).
           03  WRK-JYOKEN-TBL.
               05  WRK-JYOKEN                      PIC X(01)
                                                   OCCURS  5.
           03  WRK-NUM                             PIC 9(10).
           03  WRK-KEKKALST-NO-ST                  PIC 9(05).
           03  WRK-KEKKALST-NO-ED                  PIC 9(05).
           03  WRK-QIDMSG                          PIC X(80).
           03  WRK-KANACHK-MAE-INPUT               PIC X(400).
           03  WRK-PROC-FLG                        PIC 9(1).
           03  WRK-SHORIERR                        PIC X(200).
           03  WRK-CALC-YMD1.
               05  WRK-CALC-YY1                    PIC 9(04).
               05  WRK-CALC-MM1                    PIC 9(02).
               05  WRK-CALC-DD1                    PIC 9(02).
      *
           03  WRK-CALC-YMD2.
               05  WRK-CALC-YY2                    PIC 9(04).
               05  WRK-CALC-MM2                    PIC 9(02).
               05  WRK-CALC-DD2                    PIC 9(02).
           03  WRK-AGE-YY                          PIC S9(04).
           03  WRK-AGE-MM                          PIC S9(02).
           03  WRK-AGE-G.
               05  WRK-AGE-Z                       PIC ---9.
               05  WRK-AGE-MEI                     PIC X(04).
           03  WRK-KJYNYMD                         PIC 9(08).
           03  WRK-MCP-WIDGET                      PIC X(64).
           03  WRK-JOBID                           PIC 9(07).
      *
       01  WRK-ROW-AREA.
           03  WRK-KEKKALST-ROW                    PIC S9(9)   BINARY.
      *
       01  WRK-WIDGET-AREA.
           03  WRK-WIDGET                          PIC X(20).
      *
           03  WRK-WIDGET-R1   REDEFINES   WRK-WIDGET.
               05  WRK-WIDGET-R1-DAMMY1            PIC X(01).
               05  WRK-WIDGET1                     PIC 9(01).
               05  WRK-WIDGET-R1-DAMMY2            PIC X(18).
      *
           03  WRK-WIDGET-R2   REDEFINES   WRK-WIDGET.
               05  WRK-WIDGET-R2-DAMMY1            PIC X(02).
               05  WRK-WIDGET2                     PIC 9(01).
               05  WRK-WIDGET-R2-DAMMY2            PIC X(17).
      *
           03  WRK-WIDGET-R3   REDEFINES   WRK-WIDGET.
               05  WRK-WIDGET-R3-DAMMY1            PIC X(03).
               05  WRK-WIDGET3                     PIC 9(01).
               05  WRK-WIDGET-R3-DAMMY2            PIC X(16).
      *
           03  WRK-WIDGET-R4   REDEFINES   WRK-WIDGET.
               05  WRK-WIDGET-R4-DAMMY1            PIC X(04).
               05  WRK-WIDGET4                     PIC 9(01).
               05  WRK-WIDGET-R4-DAMMY2            PIC X(15).
      *
           03  WRK-WIDGET-R5   REDEFINES   WRK-WIDGET.
               05  WRK-WIDGET-R5-DAMMY1            PIC X(05).
               05  WRK-WIDGET5                     PIC 9(01).
               05  WRK-WIDGET-R5-DAMMY2            PIC X(14).
      *
           03  WRK-WIDGET-R6   REDEFINES   WRK-WIDGET.
               05  WRK-WIDGET-R6-DAMMY1            PIC X(06).
               05  WRK-WIDGET6                     PIC 9(01).
               05  WRK-WIDGET-R6-DAMMY2            PIC X(13).
      *
           03  WRK-WIDGET-R7   REDEFINES   WRK-WIDGET.
               05  WRK-WIDGET-R7-DAMMY1            PIC X(07).
               05  WRK-WIDGET7                     PIC 9(01).
               05  WRK-WIDGET-R7-DAMMY2            PIC X(12).
      *
           03  WRK-WIDGET-R8   REDEFINES   WRK-WIDGET.
               05  WRK-WIDGET-R8-DAMMY1            PIC X(08).
               05  WRK-WIDGET8                     PIC 9(01).
               05  WRK-WIDGET-R8-DAMMY2            PIC X(11).
      *
      *    固定値領域
       01  CONST-AREA.
           03  CONST-JYOKENMEI-AREA.
               05  FILLER          PIC X(10)   VALUE   "基本情報".
               05  FILLER          PIC X(10)   VALUE   "保険".
               05  FILLER          PIC X(10)   VALUE   "公費".
               05  FILLER          PIC X(10)   VALUE   "病名".
               05  FILLER          PIC X(10)   VALUE   "診療行為".
           03  CONST-JYOKENMEI-R   REDEFINES   CONST-JYOKENMEI-AREA.
               05  CONST-JYOKENMEI PIC X(10)   OCCURS  5.
           03  CONST-SHELNAME      PIC X(100)
               VALUE   "scripts/allways/ORCBQ01.sh".
           03  CONST-SHELNAME2     PIC X(100)
               VALUE   "scripts/allways/ORCBQ02.sh".
           03  CONST-LINE                          PIC 9(05)
               VALUE   30.
           03  CONST-JOBID                         PIC 9(07)
               VALUE   1.
           03  CONST-JOBID2                        PIC 9(07)
               VALUE   2.
           03  CONST-JOB-SHELLID                   PIC X(08)
               VALUE   "ORCBQ01".
           03  CONST-RECE-MAX      PIC 9(05)   VALUE 500.
           03  CONST-LINE-MAX      PIC 9(05)   VALUE 200.
      *
      *****************************************************************
      *    ファイルレイアウト
      *****************************************************************
      *
      *    患者マスタ（照会用コピー句）
       01  QUERY-PTINF-REC.
           COPY    "CPQPTINF.INC".
      *
      *    シェルテーブル
           COPY    "CPSHELLTBL.INC".
      *
      *    ジョブ管理マスタ
       01  JOBKANRI-REC.
           COPY    "CPJOBKANRI.INC".
      *
      *****************************************************************
      *    サブプロ用　領域
      *****************************************************************
      *
      *    画面日付変換サブ
           COPY    "CPORCSGDAY.INC".
      *
      *    ＤＢ検索
           COPY    "MCPDATA.INC".
      *
      *    患者番号変換サブ
           COPY    "CPORCSPTID.INC".
      *
      *    照会用サブ
           COPY    "CPQSUB01.INC".
           COPY    "CPQSUB02.INC".
      *
      *    患者一覧印刷パラメタ
           COPY    "CPORCHC28.INC".
      *
      *    全角チェックパラメタ
           COPY    "CPORCSKANACHK.INC".
      *
      *   ジョブ管理ＤＢ制御サブ
           COPY    "CPORCSJOBKANRI.INC".
      *
      *    フィアルデイレクトリ位置指定サブ
           COPY  "CPMKPASS.INC".
      *
      *    マシン日付取得サブ
           COPY    "CPORCSMCNDATE.INC".
      *
      *    最終受診日取得
           COPY    "CPORCSLASTYMD.INC".
      *
      *****************************************************************
      *    連絡領域
      *****************************************************************
       LINKAGE                     SECTION.
      *
           COPY    "MCPAREA".
           COPY    "ORCA-SPA".
           COPY    "LINKAREA".
      *
       01  SCRAREA.
           COPY    "Q01.INC".
           COPY    "Q02.INC".
           COPY    "Q03.INC".
           COPY    "Q04.INC".
           COPY    "Q96.INC".
           COPY    "Q97.INC".
           COPY    "Q98.INC".
           COPY    "Q99.INC".
           COPY    "Q100.INC".
           COPY    "QERR.INC".
           COPY    "QID1.INC".
      *
       PROCEDURE                                 DIVISION    USING
                                                 MCPAREA
                                                 SPAAREA
                                                 LINKAREA
                                                 SCRAREA.
      *
      *****************************************************************
      *    主　　処理
      *****************************************************************
       000-PROC-SEC                SECTION.
      *
           INITIALIZE                  FLG-AREA
                                       IDX-AREA
                                       CNT-AREA
                                       WRK-AREA
      *
           MOVE    SPA-COMMON      TO  SPA-AREA
           MOVE    SPA-FREE        TO  SPA-Q01FREE
      *
           MOVE    SPACE           TO  SPA-ERRCD
           MOVE    ZERO            TO  FLG-END
      *
           EVALUATE    MCP-STATUS      ALSO    MCP-EVENT
               WHEN    "LINK"          ALSO    ANY
                   PERFORM 100-INIT-SEC
      *    画面遷移
               WHEN     "PUTG"           ALSO   "CLICKED"
                   PERFORM 200-GMNSENI
      *    入力
               WHEN      OTHER
                   PERFORM 200-ENTRY
           END-EVALUATE.
      *
      *    画面遷移がない時、画面表示へ
           IF  ( FLG-END           =   ZERO )
               PERFORM 500-SET-SCREEN
           END-IF
      *
           MOVE    SPA-AREA        TO  SPA-COMMON
           MOVE    SPA-Q01FREE     TO  SPA-FREE
      *
           .
           EXIT    PROGRAM
           .
      *****************************************************************
      *    初期　処理
      *****************************************************************
       100-INIT-SEC                SECTION.
      *
           INITIALIZE                  FLG-AREA
                                       IDX-AREA
                                       CNT-AREA
                                       WRK-AREA
      *
      *    エラー画面より
           IF  ( SPA-MOTOPG            =   "QERR" )
               MOVE    SPACE           TO  SPA-MOTOPG
               PERFORM 5001-MAPCUR-SEC
           ELSE
      *        初期画面セット
               PERFORM 300-SCREEN-SEC
               IF    ( FLG-END         =   1 )
                   GO  TO  100-INIT-EXT
               END-IF
      **** 暫定 ****
               IF  ( SPA-ERRCD                 =   "0001" )
      *            MOVE    "条件に該当する患者は存在しませんでした"
      *                            TO  SPA-GMN-Q02-KEKKALST-SRYACT (16)
                   MOVE    "該当患者は存在しませんでした"
                                   TO  SPA-GMN-Q02-KEKKALST-NAME (16)
                   MOVE    16      TO  SPA-GMN-Q02-KEKKA-MAX
               END-IF
      *
               IF  ( SPA-ERRCD                 =   "9999" )
                   MOVE    "検索条件が多すぎます"
                                   TO  SPA-GMN-Q02-KEKKALST-SRYACT (16)
                   MOVE    16      TO  SPA-GMN-Q02-KEKKA-MAX
               END-IF
      **************
      *        画面編集
               PERFORM 500-GMNHEN-SEC
           END-IF
      *
           MOVE    SPACE               TO  LINKAREA
      *
           MOVE    SPACE               TO  MCP-PUTTYPE
           MOVE    "Q02"               TO  MCP-WINDOW
      *
           PERFORM 900-PUT-WINDOW
      *
           MOVE    1                   TO  FLG-END
           .
       100-INIT-EXT.
           EXIT.
      *
      *****************************************************************
      *    初期画面処理
      *****************************************************************
       300-SCREEN-SEC              SECTION.
      *
           MOVE    1               TO  SPA-GMN-Q02-CUR
      *
      *    他のＬＤより
           IF  ( LINKAREA      NOT =   SPACE )
               MOVE    LNK-KYOUTU          TO  SPA-KYOUTU
           END-IF
      *
           EVALUATE    SPA-MOTOPG
      *    処理確認画面
           WHEN    "Q97"
               GO  TO      300-SCREEN-EXT
      *    出力指示画面
           WHEN    "Q100"
               PERFORM 3001-Q100SPASET-SEC
               GO  TO      300-SCREEN-EXT
      *    明細書連携確認画面
           WHEN    "Q03"
               PERFORM 3001-Q03SPASET-SEC
               GO  TO      300-SCREEN-EXT
      *    確認メッセージ画面
           WHEN    "QID1"
               PERFORM 3001-QID1SPASET-SEC
               GO  TO      300-SCREEN-EXT
      *    情報削除画面
           WHEN    "XB01"
               MOVE    LNK-KYOUTU          TO  SPA-KYOUTU
               MOVE    LNK-SCRSPA          TO  SPA-Q01FREE
               GO  TO      300-SCREEN-EXT
      *    予約登録
           WHEN    "Y01"
      *    受付一覧
           WHEN    "U01"
               MOVE    LNK-KYOUTU          TO  SPA-KYOUTU
               MOVE    LNK-SCRSPA          TO  SPA-Q01FREE
      *    患者登録
           WHEN    "P02"
      *
      *        退避しておいたＳＰＡの内容を元に戻す
               INITIALIZE                  QSUB01-LNK
               MOVE    SPA-TERMID      TO  QSUB01-TERMID
               MOVE    "I"             TO  QSUB01-KBN
               CALL    "ORCGQSUB01"    USING   QSUB01-LNK
      *
               MOVE    QSUB01-SPA-AREA TO  SPA-Q01FREE
      *
               GO      TO      300-SCREEN-EXT
           END-EVALUATE
      *
           MOVE    SPACE               TO  SPA-Q00-QID-AREA
      *
           PERFORM 310-SPASET-SEC
      *
           .
       300-SCREEN-EXT.
           EXIT.
      *
      *****************************************************************
      *    ＱＩＤ１戻りスパ編集処理
      *****************************************************************
       3001-QID1SPASET-SEC             SECTION.
      *
      *
           IF    ( SPA-QID1-FLG    =   "OK" )
               CONTINUE
           ELSE
               INITIALIZE  SPA-Q00-QID-AREA
               GO  TO  3001-QID1SPASET-EXT
           END-IF
      *
           EVALUATE    SPA-QIDCD
           WHEN    "6666"
               MOVE    "JOIN"      TO  MCP-PUTTYPE
               PERFORM 210-BACK-SYORI-SEC
      *
           END-EVALUATE
      *
           INITIALIZE  SPA-Q00-QID-AREA
           .
      *
       3001-QID1SPASET-EXT.
           EXIT.
      *****************************************************************
      *    Ｑ１００戻りスパ編集処理
      *****************************************************************
       3001-Q100SPASET-SEC             SECTION.
      *
           IF  ( SPA-Q02-Q100-RTOK-FLG =   ZERO )
               GO  TO  3001-Q100SPASET-EXT
           END-IF
      *
           MOVE    SPA-HOSPNUM         TO  PRFNAME-HOSPNUM
           MOVE    "ORC13001"          TO  PRFNAME-FILE-ID
           MOVE    1                   TO  PRFNAME-SYOKBN1
           MOVE    SPA-TERMID          TO  PRFNAME-TERMID
      *
           PERFORM 800-MCNDATE-SEC
      *
           OPEN    OUTPUT              BQ01-FILE
      *
           INITIALIZE                  BQ01
      *
           MOVE    SPA-Q02-Q100-PROC-FLG
                                       TO  BQ01-PROC-FLG
           MOVE    SPA-Q02-Q100-FILENAME
                                       TO  BQ01-CSVFILENAME
           MOVE    SPA-GMN-Q02-HYODAI  TO  BQ01-HYODAI
           MOVE    SPA-GMN-Q02-JYOKEN1 TO  BQ01-JYOKEN1
           MOVE    SPA-GMN-Q02-JYOKEN2 TO  BQ01-JYOKEN2
           MOVE    SPA-Q02-Q100-STPAGE
                                       TO  BQ01-STPAGE
           MOVE    SPA-Q02-Q100-EDPAGE
                                       TO  BQ01-EDPAGE
           MOVE    SPA-Q02-Q100-STKNS  TO  BQ01-STKNS
           MOVE    SPA-Q02-Q100-EDKNS  TO  BQ01-EDKNS
      *
      *    年齢
           IF  (   SPA-Q01-AGE-CALC-KBN    =   ZERO )
               MOVE    SPA-SYSYMD      TO  BQ01-KJYNYMD
           ELSE
               MOVE    SPA-Q01-KJYNYMD TO  BQ01-KJYNYMD
           END-IF
      *
      *     PERFORM 3101-SQL-EDIT-SEC
           INITIALIZE  QSUB02-LNK
           CALL    "ORCGQSUB02"    USING
                                   SPA-AREA
                                   SPA-Q01FREE
                                   QSUB02-LNK
      *
           MOVE    QSUB02-SQL      TO  BQ01-SQL
      *
           WRITE   BQ01
      *
           CLOSE   BQ01-FILE
      *
      *    ジョブ開始セット処理
           MOVE    CONST-JOBID     TO  WRK-JOBID
           PERFORM 900-JOBKANRI-HENSYU-SEC
      *
      *    印刷用スクリプト処理
      *
      *    フィアルデイレクトリ位置指定
           INITIALIZE              ORCSMKPASSAREA
           MOVE    CONST-SHELNAME  TO  MKPASS-IN-01
           CALL    "ORCSMKPASS"    USING   ORCSMKPASSAREA
           MOVE    MKPASS-OT-01    TO  SHELLTBL-NAME
      *
           MOVE    SPA-Q02-Q100-PROC-FLG
                                   TO  SHELLTBL-ARG1
           MOVE    SPA-Q02-Q100-FILENAME
                                   TO  SHELLTBL-ARG2
           MOVE    PRFNAME         TO  SHELLTBL-ARG3
           MOVE    SPA-TERMID      TO  SHELLTBL-ARG4
           MOVE    SPA-SYSYMD      TO  SHELLTBL-ARG5
           MOVE    SMCNDATE-HMS    TO  SHELLTBL-ARG6
           MOVE    SPA-HOSPNUM     TO  SHELLTBL-ARG7
           MOVE    SPA-STAFFCD     TO  SHELLTBL-ARG8
           MOVE    CONST-JOBID     TO  SHELLTBL-ARG9
           MOVE    CONST-JOB-SHELLID
                                   TO  SHELLTBL-ARG10
      *
           MOVE    SHELLTBL        TO  MCPDATA-REC
           MOVE    "SHELL"         TO  MCP-FUNC
           MOVE    "shell"         TO  MCP-TABLE
           MOVE    "shell"         TO  MCP-PATHNAME
           CALL    "ORCDBMAIN"         USING   MCPAREA
                                               MCPDATA-REC
                                               SPA-AREA
      *
           PERFORM 490-KAKUNIN-SEC
      *
           .
       3001-Q100SPASET-EXT.
           EXIT.
      *****************************************************************
      *    Ｑ０３戻りスパ編集処理
      *****************************************************************
       3001-Q03SPASET-SEC             SECTION.
      *
           IF  ( SPA-Q02-Q03-RTOK-FLG =   ZERO )
               GO  TO  3001-Q03SPASET-EXT
           END-IF
      *
           MOVE    SPA-HOSPNUM         TO  PRFNAME-HOSPNUM
           MOVE    "OR013002"          TO  PRFNAME-FILE-ID
           MOVE    2                   TO  PRFNAME-SYOKBN1
           MOVE    SPA-TERMID          TO  PRFNAME-TERMID
      *
           PERFORM 800-MCNDATE-SEC
      *
           MOVE    SPA-HOSPNUM         TO  ERRFLPARA-HOSPNUM
           MOVE    "OR013ERR"          TO  ERRFLPARA-FILE-ID
           MOVE    2                   TO  ERRFLPARA-SYOKBN1
           MOVE    SPA-TERMID          TO  ERRFLPARA-TERMID
      *
           OPEN    OUTPUT              BQ01-FILE
      *
           INITIALIZE                  BQ01
      *
           MOVE    SPA-GMN-Q02-HYODAI  TO  BQ01-HYODAI
           MOVE    SPA-GMN-Q02-JYOKEN1 TO  BQ01-JYOKEN1
           MOVE    SPA-GMN-Q02-JYOKEN2 TO  BQ01-JYOKEN2
      *
           MOVE    SPA-SYSYMD          TO  BQ01-KJYNYMD
           MOVE    SPA-Q02-Q03-SRYYM   TO  BQ01-SRYYM
           MOVE    SPA-Q02-Q03-NYUGAIKBN TO BQ01-NYUGAIKBN
      *
      *     PERFORM 3101-SQL-EDIT-SEC
           INITIALIZE  QSUB02-LNK
           CALL    "ORCGQSUB02"    USING
                                   SPA-AREA
                                   SPA-Q01FREE
                                   QSUB02-LNK
      *
           MOVE    QSUB02-SQL      TO  BQ01-SQL
      *
           WRITE   BQ01
      *
           CLOSE   BQ01-FILE
      *
      *    ジョブ開始セット処理
           MOVE    CONST-JOBID2    TO  WRK-JOBID
           PERFORM 900-JOBKANRI-HENSYU-SEC
      *
      *    印刷用スクリプト処理
      *
      *    フィアルデイレクトリ位置指定
           INITIALIZE              ORCSMKPASSAREA
           MOVE    CONST-SHELNAME2 TO  MKPASS-IN-01
           CALL    "ORCSMKPASS"    USING   ORCSMKPASSAREA
           MOVE    MKPASS-OT-01    TO  SHELLTBL-NAME
      *
           MOVE    PRFNAME         TO  SHELLTBL-ARG1
           MOVE    SPA-TERMID      TO  SHELLTBL-ARG2
           MOVE    SPA-SYSYMD      TO  SHELLTBL-ARG3
           MOVE    SMCNDATE-HMS    TO  SHELLTBL-ARG4
           MOVE    SPA-HOSPNUM     TO  SHELLTBL-ARG5
           MOVE    SPA-OPID        TO  SHELLTBL-ARG6
           MOVE    ERRFLPARA       TO  SHELLTBL-ARG7
           MOVE    CONST-JOBID2    TO  SHELLTBL-ARG8
      *    IDは検索結果印刷に併せる
           MOVE    CONST-JOB-SHELLID
                                   TO  SHELLTBL-ARG9
      *
           MOVE    SHELLTBL        TO  MCPDATA-REC
           MOVE    "SHELL"         TO  MCP-FUNC
           MOVE    "shell"         TO  MCP-TABLE
           MOVE    "shell"         TO  MCP-PATHNAME
           CALL    "ORCDBMAIN"         USING   MCPAREA
                                               MCPDATA-REC
                                               SPA-AREA
      *
           PERFORM 490-KAKUNIN-SEC
      *
      *
           .
       3001-Q03SPASET-EXT.
           EXIT.
      *
      *****************************************************************
      *    スパ初期編集処理
      *****************************************************************
       310-SPASET-SEC              SECTION.
      *
      *
           MOVE    SPACE           TO  SPA-Q02
      *
           INITIALIZE                  SPA-Q02
      *
           MOVE    ZERO            TO  SPA-GMN-Q02-TOTAL-GET-FLG
      *    画面編集処理
           PERFORM 310-Q02-GMN-EDIT-SEC
      *
      *    カーソルキー制御項目
           MOVE   1                TO  SPA-GMN-Q02-CUR
      *
           .
       310-SPASET-EXT.
           EXIT.
      *
      *****************************************************************
      *    画面編集処理
      *****************************************************************
       310-Q02-GMN-EDIT-SEC            SECTION.
      *
      *
           MOVE   1                TO  FLG-GMN-INIT
      *
           MOVE    SPACE           TO  SPA-GMN-Q02-KEKKATBL
                                       SPA-GMN-Q02-PTINF-AREA
      *
           INITIALIZE                  SPA-GMN-Q02-KEKKATBL
                                       SPA-GMN-Q02-PTINF-AREA
      *
      *    画面条件編集処理
           PERFORM 3101-JYOKEN-EDIT-SEC
      *
      *
      *    ＳＱＬ文編集処理
      *    PERFORM 3101-SQL-EDIT-SEC
           INITIALIZE              QSUB02-LNK
           MOVE    1               TO  QSUB02-LIMITCONTROL
           COMPUTE QSUB02-OFFSET
               =   SPA-GMN-Q02-SKIP-KNS
           COMPUTE QSUB02-LIMIT
               =   CONST-LINE-MAX  +   1
           CALL    "ORCGQSUB02"    USING
                                   SPA-AREA
                                   SPA-Q01FREE
                                   QSUB02-LNK
      *
           MOVE    QSUB02-SQL      TO  WRK-SQL
           MOVE    QSUB02-SQL-CNT  TO  WRK-SQL-COUNT
      *
           DISPLAY "WRK-SQL  = " WRK-SQL
      *+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
      *    暫定 組み立てたＳＱＬが１０００バイトを超えた場合
           MOVE     20000          TO   WRK-STR-LEN
           MOVE     WRK-SQL        TO   WRK-STR
      *
      *
           PERFORM 900-STR-LEN-GET-SEC
           IF  ( WRK-STR-LEN       >    15000 )
      *
      *        ＳＱＬ解析限界値オーバー
               MOVE    "9999"      TO  SPA-ERRCD
               GO  TO  310-Q02-GMN-EDIT-EXT
           END-IF
      *+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
      *    患者マスタ検索処理
           PERFORM 900-QUERY-PTINF-KEY-SEL-SEC
      *
           PERFORM VARYING IDX-GMN FROM    1   BY  1
               UNTIL   (   IDX-GMN     >   CONST-LINE-MAX )
                OR     (   FLG-PTINF   =   1   )
      *
      *        番号
               COMPUTE WRK-NUM     =   SPA-GMN-Q02-SKIP-KNS
                                   +   IDX-GMN
               MOVE    WRK-NUM     TO  WRK-ZZZZ9
               MOVE    WRK-ZZZZ9   TO  SPA-GMN-Q02-KEKKALST-NO
                                                           (IDX-GMN)
      *
      *        患者番号
               MOVE    QUERY-PTINF-PTNUM 
                                   TO  SPA-GMN-Q02-KEKKALST-PTNUM
                                                           (IDX-GMN)
      *
      *        氏名
               MOVE    QUERY-PTINF-NAME
                                   TO  SPA-GMN-Q02-KEKKALST-NAME
                                                           (IDX-GMN)
      *        性別
               EVALUATE    QUERY-PTINF-SEX
               WHEN    "1"
                   MOVE    "男"    TO  SPA-GMN-Q02-KEKKALST-SEX
                                                           (IDX-GMN)
               WHEN    "2"
                   MOVE    "女"    TO  SPA-GMN-Q02-KEKKALST-SEX
                                                           (IDX-GMN)
               END-EVALUATE
      *
      *        誕生日
               INITIALIZE              ORCSGDAYAREA
      *
               MOVE    QUERY-PTINF-BIRTHDAY
                                   TO  SGDAY-INDAY
               CALL    "ORCSGDAY"      USING
                                       ORCSGDAYAREA
               IF  ( SGDAY-RC          =   ZERO )
                   MOVE    SGDAY-OTDAY
                               TO  SPA-GMN-Q02-KEKKALST-BIRTH (IDX-GMN)
               ELSE
                   MOVE    SPACE
                               TO  SPA-GMN-Q02-KEKKALST-BIRTH (IDX-GMN)
               END-IF
      *
      *        年齢
               IF  (   SPA-Q01-AGE-CALC-KBN    =   ZERO )
                   MOVE    SPA-SYSYMD      TO  WRK-KJYNYMD
               ELSE
                   MOVE    SPA-Q01-KJYNYMD TO  WRK-KJYNYMD
               END-IF
      *
               IF  (   QUERY-PTINF-BIRTHDAY    <   WRK-KJYNYMD )
                   MOVE    QUERY-PTINF-BIRTHDAY
                                           TO  WRK-CALC-YMD1
                   MOVE    WRK-KJYNYMD     TO  WRK-CALC-YMD2
                   PERFORM 3101-AGE-CALC-SEC
               ELSE
                   MOVE    WRK-KJYNYMD     TO  WRK-CALC-YMD1
                   MOVE    QUERY-PTINF-BIRTHDAY
                                           TO  WRK-CALC-YMD2
                   PERFORM 3101-AGE-CALC-SEC
                   COMPUTE WRK-AGE-YY   =   WRK-AGE-YY
                                           *   -1
                   COMPUTE WRK-AGE-MM   =   WRK-AGE-MM
                                           *   -1
               END-IF
      *
               IF    ( WRK-AGE-YY          =   ZERO )
                   MOVE    WRK-AGE-MM      TO  WRK-AGE-Z
                   MOVE    "ヶ月"          TO  WRK-AGE-MEI
               ELSE
                   MOVE    WRK-AGE-YY      TO  WRK-AGE-Z
                   MOVE    "歳"            TO  WRK-AGE-MEI
               END-IF
      *
               MOVE    WRK-AGE-G           TO  SPA-GMN-Q02-KEKKALST-AGE
                                                               (IDX-GMN)
      *
      *        前回来院日編集
               INITIALIZE                  ORCSLASTYMDAREA
               MOVE    QUERY-PTINF-PTID    TO  ORCSLASTYMD-PTID
               CALL    "ORCSLASTYMD"       USING
                                           ORCSLASTYMDAREA
                                           SPA-AREA
               MOVE    ORCSLASTYMD-HKNCOMBI-MEI
                                           TO  SPA-GMN-Q02-KEKKALST-HKN
                                                               (IDX-GMN)
      *
      *        電話番号
               MOVE    QUERY-PTINF-HOME-TEL1
                                           TO  SPA-GMN-Q02-KEKKALST-TEL
                                                               (IDX-GMN)
      *        最終受診日
               IF    ( ORCSLASTYMD-BRMNUM  NOT =   SPACE )
                   MOVE    ORCSLASTYMD-BRMNUM
                                       TO  SPA-GMN-Q02-KEKKALST-LASTYMD
                                                               (IDX-GMN)
               ELSE
                   MOVE    ORCSLASTYMD-LASTYMDW
                                       TO  SPA-GMN-Q02-KEKKALST-LASTYMD
                                                               (IDX-GMN)
               END-IF
      *
      *        郵便番号
               IF    ( QUERY-PTINF-HOME-POST   NOT =   SPACE )
                   MOVE    QUERY-PTINF-HOME-POST (1:3)
                                           TO  SPA-GMN-Q02-KEKKALST-POST
                                                          (IDX-GMN)(1:3)
                   MOVE    "-"             TO  SPA-GMN-Q02-KEKKALST-POST
                                                          (IDX-GMN)(4:1)
                   MOVE    QUERY-PTINF-HOME-POST (4:4)
                                           TO  SPA-GMN-Q02-KEKKALST-POST
                                                          (IDX-GMN)(5:4)
                   
               END-IF
      *
      *        診療内容（住所を表示）
               MOVE    QUERY-PTINF-HOME-ADRS
                                       TO  WRK-STR
      *
      *        住所の項目長を求める
               MOVE    1               TO  WRK-STR-LEN
               MOVE    SPACE           TO  WRK-STR
               STRING  QUERY-PTINF-HOME-ADRS   DELIMITED   BY  SIZE
               INTO    WRK-STR
               WITH POINTER WRK-STR-LEN
               END-STRING
               COMPUTE WRK-STR-LEN =   WRK-STR-LEN -   1
      *
      *        住所の実際の文字列長を求める
               MOVE    QUERY-PTINF-HOME-ADRS TO  WRK-STR
               PERFORM 900-STR-LEN-GET-SEC
               MOVE    WRK-STR-LEN           TO  WRK-PTINF-ADR-LEN
      *
      *        番地の項目長を求める
               MOVE    1               TO  WRK-STR-LEN
               MOVE    SPACE           TO  WRK-STR
               STRING  QUERY-PTINF-HOME-BANTI  DELIMITED   BY  SIZE
               INTO    WRK-STR
               WITH POINTER WRK-STR-LEN
               END-STRING
               COMPUTE WRK-STR-LEN =   WRK-STR-LEN -   1
      *
      *        番地の実際の文字列長を求める
               MOVE    QUERY-PTINF-HOME-BANTI  TO  WRK-STR
               PERFORM 900-STR-LEN-GET-SEC
               MOVE    WRK-STR-LEN         TO  WRK-PTINF-BANTI-LEN
      *
               MOVE    1                   TO  WRK-STR-PNT
               IF  ( WRK-PTINF-ADR-LEN     >   0)
                   STRING  QUERY-PTINF-HOME-ADRS 
                                   (1: WRK-PTINF-ADR-LEN)
                                           DELIMITED   BY  SIZE
                           "　"            DELIMITED   BY  SIZE
                   INTO    SPA-GMN-Q02-KEKKALST-SRYACT (IDX-GMN)
                   WITH    POINTER WRK-STR-PNT
                   END-STRING
               END-IF
      *
               IF  ( WRK-PTINF-BANTI-LEN   >   0)
                   STRING  QUERY-PTINF-HOME-BANTI 
                                   (1: WRK-PTINF-BANTI-LEN)
                                           DELIMITED   BY  SIZE
                   INTO    SPA-GMN-Q02-KEKKALST-SRYACT  (IDX-GMN)
                   WITH    POINTER WRK-STR-PNT
                   END-STRING
               END-IF
      *
               MOVE    IDX-GMN     TO  SPA-GMN-Q02-KEKKA-MAX
      *
      *        患者ＩＤ
               MOVE    QUERY-PTINF-PTID
                                   TO  SPA-GMN-Q02-PTINF-PTID (IDX-GMN)
      *
      *        患者マスタ読込処理
               PERFORM 900-QUERY-PTINF-KEY-FET-SEC
      *
           END-PERFORM
      *
      *    患者マスタクローズ処理
           MOVE    "query_ptinf"       TO  MCP-TABLE
           MOVE    "key2"              TO  MCP-PATHNAME
           PERFORM 910-DBCLOSECURSOR-SEC
      *
      *    総件数編集
           IF  ( SPA-GMN-Q02-TOTAL-GET-FLG =   ZERO )
               MOVE    1              TO   SPA-GMN-Q02-TOTAL-GET-FLG
               PERFORM 3101-TOTAL-KNS-EDIT-SEC
           END-IF
      *
           MOVE   FLG-PTINF           TO  SPA-GMN-Q02-FLG-PTINF
      *    前頁ボタン活性編集
           IF  ( SPA-GMN-Q02-SKIP-KNS      >   0 )
               MOVE    WIDGET-NORMAL       TO  SPA-GMN-Q02-B06-STATE
           ELSE
               MOVE    WIDGET-INSENSITIVE  TO  SPA-GMN-Q02-B06-STATE
           END-IF
      *    次頁ボタン活性編集
           IF  ( SPA-GMN-Q02-FLG-PTINF     =   0 )
               MOVE    WIDGET-NORMAL       TO  SPA-GMN-Q02-B07-STATE
           ELSE
               MOVE    WIDGET-INSENSITIVE  TO  SPA-GMN-Q02-B07-STATE
           END-IF
      *
           IF    ( SPA-GMN-Q02-SKIP-KNS     =   ZERO )
            AND  ( SPA-GMN-Q02-KEKKA-MAX    =   ZERO )
      *
      *        該当なしエラー
               MOVE    "0001"      TO  SPA-ERRCD
      *
           END-IF
      *
           .
       310-Q02-GMN-EDIT-EXT.
           EXIT.
      *****************************************************************
      *    年齢計算処理
      *****************************************************************
       3101-AGE-CALC-SEC               SECTION.
      *
           COMPUTE WRK-AGE-YY          =   WRK-CALC-YY2
                                       -   WRK-CALC-YY1
           IF    ( WRK-CALC-YMD1(5:4)  >   WRK-CALC-YMD2(5:4) )
               COMPUTE WRK-AGE-YY      =   WRK-AGE-YY
                                       -   1
               COMPUTE WRK-AGE-MM      =  (WRK-CALC-MM2    +   12 )
                                       -   WRK-CALC-MM1
           ELSE
               COMPUTE WRK-AGE-MM      =   WRK-CALC-MM2
                                       -   WRK-CALC-MM1
           END-IF
           IF    ( WRK-CALC-DD1        >   WRK-CALC-DD2 )
               COMPUTE WRK-AGE-MM      =   WRK-AGE-MM
                                       -   1
           END-IF
           .
      *
       3101-AGE-CALC-EXT.
           EXIT.
      *****************************************************************
      *    条件編集処理
      *****************************************************************
       3101-JYOKEN-EDIT-SEC            SECTION.
      *
           MOVE    SPACE           TO  SPA-GMN-Q02-JYOKEN1
                                       WRK-JYOKEN-TBL
           MOVE    1               TO  WRK-JYOKEN-PNT
      *
           IF  ( SPA-Q01-GTBL (1)  =   "1" )
            OR ( SPA-Q01-GTBL (2)  =   "1" )
            OR ( SPA-Q01-GTBL (3)  =   "1" )
            OR ( SPA-Q01-GTBL (7)  =   "1" )
            OR ( SPA-Q01-GTBL (8)  =   "1" )
               MOVE    "1"         TO  WRK-JYOKEN (1)
           END-IF
      *
           IF  ( SPA-Q01-GTBL (4)  =   "1" )
               MOVE    "1"         TO  WRK-JYOKEN (4)
           END-IF
      *
           IF  ( SPA-Q01-GTBL (5)  =   "1" )
               MOVE    "1"         TO  WRK-JYOKEN (2)
           END-IF
      *
           IF  ( SPA-Q01-GTBL (6)  =   "1" )
               MOVE    "1"         TO  WRK-JYOKEN (3)
           END-IF
      *
           IF  ( SPA-Q01-GTBL (9)  =   "1" )
               MOVE    "1"         TO  WRK-JYOKEN (5)
           END-IF
      *
           PERFORM VARYING IDX1    FROM    1   BY  1
               UNTIL   (   IDX1    >   5 )
      *
               IF   ( WRK-JYOKEN (IDX1)        =   "1" )
                   IF   ( SPA-GMN-Q02-JYOKEN1  =   SPACE )
                       STRING  CONST-JYOKENMEI (IDX1)
                                                   DELIMITED   BY  SPACE
                       INTO    SPA-GMN-Q02-JYOKEN1
                       WITH    POINTER     WRK-JYOKEN-PNT
                       END-STRING
                   ELSE
                       STRING  "、"                DELIMITED   BY  SIZE
                               CONST-JYOKENMEI (IDX1)
                                                   DELIMITED   BY  SPACE
                       INTO    SPA-GMN-Q02-JYOKEN1
                       WITH    POINTER     WRK-JYOKEN-PNT
                       END-STRING
                   END-IF
               END-IF
      *
           END-PERFORM
      *
           .
      *
       3101-JYOKEN-EDIT-EXT.
           EXIT.
      *****************************************************************
      *    総件数編集処理
      *****************************************************************
       3101-TOTAL-KNS-EDIT-SEC             SECTION.
      *
           IF      ( FLG-PTINF         =   "1" )
               COMPUTE WRK-NUM =   IDX-GMN     -   1
               MOVE    WRK-NUM         TO  WRK-TOTAL
                                           SPA-GMN-Q02-TOTAL-NUM
           ELSE
      *        検索結果件数を取得
      *        INITIALIZE                  MCPAREA
               MOVE    "query_ptinf"       TO  MCP-TABLE
               MOVE    "key3"              TO  MCP-PATHNAME
               MOVE    SPACE               TO  QUERY-PTINF-REC
               MOVE    WRK-SQL-COUNT       TO  QUERY-PTINF-SQL
               MOVE    QUERY-PTINF-REC     TO  MCPDATA-REC
               PERFORM 911-DBSELECT-SEC
               IF  (   MCP-RC          NOT =   ZERO    )
                   MOVE    SPACE       TO  SPA-GMN-Q02-TOTAL
                   GO  TO  3101-TOTAL-KNS-EDIT-EXT
               END-IF
      *
               MOVE    MCPDATA-REC     TO  QUERY-PTINF-REC
               MOVE    QUERY-PTINF-TOTAL
                                       TO  WRK-TOTAL
                                           SPA-GMN-Q02-TOTAL-NUM
           END-IF
      *
      *    左詰で編集を行う
           PERFORM VARYING IDX1    FROM    1   BY  1
               UNTIL ( IDX1    >   11 )
                OR   ( WRK-TOTAL (IDX1 : 1)    NOT =   SPACE )
      *
               CONTINUE
      *
           END-PERFORM
      *
           IF  ( IDX1         <=   11 )
               MOVE    SPACE   TO  SPA-GMN-Q02-TOTAL
               STRING  "総件数："          DELIMITED   BY    SIZE
                       WRK-TOTAL (IDX1 : ) DELIMITED   BY    SIZE
               INTO    SPA-GMN-Q02-TOTAL
               END-STRING
           END-IF
      *
           .
      *
       3101-TOTAL-KNS-EDIT-EXT.
           EXIT.
      *****************************************************************
      *    画面遷移処理
      *****************************************************************
       200-GMNSENI                           SECTION.
      *
           MOVE        MCP-WIDGET      TO      WRK-MCP-WIDGET
      *
           EVALUATE    MCP-EVENT       ALSO    MCP-WIDGET
      *    戻る
               WHEN    "CLICKED"       ALSO    "B01"
                   PERFORM 210-BACK
      *    クリア
               WHEN    "CLICKED"       ALSO    "B02"
                   PERFORM 480-CLEAR-SEC
      *    患者登録へ
               WHEN    "CLICKED"       ALSO    "B05"
                   PERFORM 480-KANJYA-SEC
      *    前頁
               WHEN    "CLICKED"       ALSO    "B06"
                   PERFORM 210-MAE-PAGE-SEC
      *    次頁
               WHEN    "CLICKED"       ALSO    "B07"
                   PERFORM 210-TUGI-PAGE-SEC
      *    明細書連携
               WHEN    "CLICKED"       ALSO    "B09"
                   PERFORM 480-Q03-SENI-SEC
      *    情報削除
               WHEN    "CLICKED"       ALSO    "B10"
                   PERFORM 480-JYOUHOU-DEL-SEC
      *    ＣＳＶ確認
               WHEN    "CLICKED"       ALSO    "B11"
                   PERFORM 480-CSV-KAKUNIN-SEC
      *    印刷確認
               WHEN    "CLICKED"       ALSO    "B12"
                   PERFORM 480-PRINT-KAKUNIN-SEC
      *    処理確認画面
               WHEN    "CLICKED"       ALSO    "B24"
                   PERFORM 490-KAKUNIN-SEC
           END-EVALUATE
      *
           .
      *
       200-GMNSENI-EXT.
           EXIT.
      *****************************************************************
      *    会話　処理
      *****************************************************************
       200-ENTRY                             SECTION.
      *
           MOVE        MCP-WIDGET      TO      WRK-MCP-WIDGET
      *
           EVALUATE    MCP-EVENT       ALSO    MCP-WIDGET
      *    行選択
           WHEN    ANY             ALSO    "KEKKALST"
               PERFORM 4102-KEKKALSTSEL-SEC
           WHEN    OTHER
      *
      *        入力個所のセット
               PERFORM 400-GMN-INPUT-SEC
      *        入力チェック処理
               PERFORM 410-INPUT-CHK-SEC
           END-EVALUATE
      *
           .
      *
       200-ENTRY-EXT.
           EXIT.
      *
      *****************************************************************
      *    入力個所のセット処理
      *****************************************************************
       400-GMN-INPUT-SEC           SECTION.
      *
           MOVE    ZERO        TO      SPA-GMN-Q02-CUR
      *
           .
      *
       400-GMN-INPUT-EXT.
           EXIT.
      *****************************************************************
      *    入力チェック処理
      *****************************************************************
       410-INPUT-CHK-SEC           SECTION.
      *
      *    画面をＳＰＡにセット
           PERFORM 4101-GMN-SPA-SET-SEC
      *
      *    表題チェック
           PERFORM 4100-HYODAI-CHK-SEC
           IF    ( SPA-ERRCD   NOT =   SPACE )
               GO  TO  410-INPUT-CHK-EXT
           END-IF
      *
      *    条件上段チェック
           PERFORM 4101-JYOKEN1-CHK-SEC
           IF    ( SPA-ERRCD   NOT =   SPACE )
               GO  TO  410-INPUT-CHK-EXT
           END-IF
      *
      *    条件下段チェック
           PERFORM 4102-JYOKEN2-CHK-SEC
           IF    ( SPA-ERRCD   NOT =   SPACE )
               GO  TO  410-INPUT-CHK-EXT
           END-IF
      *
      *    選択番号チェック
           PERFORM 4102-SELUM-CHK-SEC
           IF    ( SPA-ERRCD   NOT =   SPACE )
               GO  TO  410-INPUT-CHK-EXT
           END-IF
      *
           .
      *
       410-INPUT-CHK-EXT.
           EXIT.
      *****************************************************************
      *    画面ＳＰＡ編集処理
      *****************************************************************
       4101-GMN-SPA-SET-SEC        SECTION.
      *
      *    表題
           MOVE    Q02-HYODAI      TO  SPA-GMN-Q02-HYODAI
      *    条件１
           MOVE    Q02-JYOKEN1     TO  SPA-GMN-Q02-JYOKEN1
      *    条件２
           MOVE    Q02-JYOKEN2     TO  SPA-GMN-Q02-JYOKEN2
      *    選択番号
           MOVE    Q02-SELNUM      TO  SPA-GMN-Q02-SELNUM
      *
           .
       4101-GMN-SPA-SET-EXT.
           EXIT.
      *****************************************************************
      *    表題チェック
      *****************************************************************
       4100-HYODAI-CHK-SEC         SECTION.
      *
      *    全角半角混在チェック
           MOVE    SPA-GMN-Q02-HYODAI
                                       TO  WRK-KANACHK-MAE-INPUT
      *
           PERFORM     800-ENRTRY-CHK-SEC
           IF  ( FLG-ENTRYCHK      NOT =   ZERO )
               MOVE    1               TO  SPA-GMN-Q02-CUR
               MOVE    "0003"          TO  SPA-ERRCD
               GO  TO  4100-HYODAI-CHK-EXIT
           END-IF
      *
           .
       4100-HYODAI-CHK-EXIT.
           EXIT.
      *****************************************************************
      *    条件上段チェック
      *****************************************************************
       4101-JYOKEN1-CHK-SEC        SECTION.
      *
      *    全角半角混在チェック
           MOVE    SPA-GMN-Q02-JYOKEN1
                                       TO  WRK-KANACHK-MAE-INPUT
      *
           PERFORM     800-ENRTRY-CHK-SEC
           IF  ( FLG-ENTRYCHK      NOT =   ZERO )
               MOVE    2               TO  SPA-GMN-Q02-CUR
               MOVE    "0003"          TO  SPA-ERRCD
               GO  TO  4101-JYOKEN1-CHK-EXT
           END-IF
      *
           .
       4101-JYOKEN1-CHK-EXT.
           EXIT.
      *****************************************************************
      *    条件下段チェック
      *****************************************************************
       4102-JYOKEN2-CHK-SEC        SECTION.
      *
      *    全角半角混在チェック
           MOVE    SPA-GMN-Q02-JYOKEN2
                                       TO  WRK-KANACHK-MAE-INPUT
      *
           PERFORM     800-ENRTRY-CHK-SEC
           IF  ( FLG-ENTRYCHK      NOT =   ZERO )
               MOVE    3               TO  SPA-GMN-Q02-CUR
               MOVE    "0003"          TO  SPA-ERRCD
               GO  TO  4102-JYOKEN2-CHK-EXT
           END-IF
      *
           .
       4102-JYOKEN2-CHK-EXT.
           EXIT.
      *****************************************************************
      *    選択番号チェック
      *****************************************************************
       4102-SELUM-CHK-SEC          SECTION.
      *
      *    未入力時はチェックを行わない
           IF  ( SPA-GMN-Q02-SELNUM    =   ZERO )
               GO  TO  4102-SELUM-CHK-EXT
           END-IF
      *
      *    選択番号範囲チェック
           COMPUTE WRK-KEKKALST-NO-ST  =   SPA-GMN-Q02-SKIP-KNS
                                       +   1
      *
           COMPUTE WRK-KEKKALST-NO-ED  =   SPA-GMN-Q02-SKIP-KNS
                                       +   SPA-GMN-Q02-KEKKA-MAX
      *
           IF      ( SPA-GMN-Q02-SELNUM   >=   WRK-KEKKALST-NO-ST )
            AND    ( SPA-GMN-Q02-SELNUM   <=   WRK-KEKKALST-NO-ED )
      *
               COMPUTE SPA-GMN-Q02-KEKKA-SEL
                   =   SPA-GMN-Q02-SELNUM  -  SPA-GMN-Q02-SKIP-KNS
      *
           ELSE
               MOVE    4               TO  SPA-GMN-Q02-CUR
               MOVE    "0002"          TO  SPA-ERRCD
               GO  TO  4102-SELUM-CHK-EXT
           END-IF
      *
           .
       4102-SELUM-CHK-EXT.
           EXIT.
      *****************************************************************
      *    行選択処理
      *****************************************************************
       4102-KEKKALSTSEL-SEC        SECTION.
      *
      *    画面をＳＰＡにセット
           PERFORM 4101-GMN-SPA-SET-SEC
      *
           PERFORM VARYING     IDX1    FROM    1   BY  1
                   UNTIL     ( IDX1    >       CONST-LINE-MAX )
               IF      (  Q02-KEKKALST-SELECT (IDX1)  =   "T"  )
                   COMPUTE SPA-GMN-Q02-SELNUM
                                   =   IDX1
                                   +   SPA-GMN-Q02-SKIP-KNS
                   MOVE    IDX1            TO  SPA-GMN-Q02-KEKKA-SEL
                   MOVE    CONST-LINE-MAX  TO  IDX1
               END-IF
           END-PERFORM
      *
           .
       4102-KEKKALSTSEL-EXT.
           EXIT.
      *
      *****************************************************************
      *    戻る　処理
      *****************************************************************
       210-BACK                        SECTION.
      *
      *    ジョブ管理チェック処理
           PERFORM 900-JOBKANRI-CHK-SEC
           IF    ( FLG-JOBEND   =   ZERO )
               MOVE    "6666"          TO  SPA-QIDCD
               STRING  "処理中です【"  DELIMITED  BY  SIZE
                       JOB-SHELLMSG    DELIMITED  BY  SPACE
                       "】"            DELIMITED  BY  SIZE
               INTO  WRK-SHORIERR
               END-STRING
               GO  TO  210-BACK-EXT
           END-IF
      *
           MOVE    "CHANGE"            TO  MCP-PUTTYPE
           PERFORM 210-BACK-SYORI-SEC
      *
           .
       210-BACK-EXT.
           EXIT.
      *
      *****************************************************************
      *    戻る　処理
      *****************************************************************
       210-BACK-SYORI-SEC              SECTION.
      * 
           MOVE    "Q01"               TO  SPA-SAKIPG
           MOVE    "Q02"               TO  SPA-MOTOPG
      *
           MOVE    SPA-SAKIPG          TO  MCP-WINDOW
      *
           PERFORM 900-PUT-WINDOW
      *
           MOVE    1                   TO  FLG-END
      *
           .
       210-BACK-SYORI-EXT.
           EXIT.
      *
      *****************************************************************
      *    前頁　処理
      *****************************************************************
       210-MAE-PAGE-SEC                SECTION.
      *
      *    画面をＳＰＡにセット
           PERFORM 4101-GMN-SPA-SET-SEC
      *
           IF  ( SPA-GMN-Q02-SKIP-KNS      >   0 )
               COMPUTE SPA-GMN-Q02-SKIP-KNS    =   SPA-GMN-Q02-SKIP-KNS
                                               -   CONST-LINE-MAX
               PERFORM 310-Q02-GMN-EDIT-SEC
               MOVE    ZERO            TO  SPA-GMN-Q02-SELNUM
           END-IF
      *
           .
       210-MAE-PAGE-EXT.
           EXIT.
      *****************************************************************
      *    次頁　処理
      *****************************************************************
       210-TUGI-PAGE-SEC               SECTION.
      *
      *    画面をＳＰＡにセット
           PERFORM 4101-GMN-SPA-SET-SEC
      *
           IF  ( SPA-GMN-Q02-FLG-PTINF     =   0 )
               COMPUTE SPA-GMN-Q02-SKIP-KNS    =   SPA-GMN-Q02-SKIP-KNS
                                               +   CONST-LINE-MAX
               PERFORM 310-Q02-GMN-EDIT-SEC
               MOVE    ZERO            TO  SPA-GMN-Q02-SELNUM
           END-IF
      *
           .
       210-TUGI-PAGE-EXT.
           EXIT.
      *****************************************************************
      *    情報削除へ
      *****************************************************************
       480-JYOUHOU-DEL-SEC             SECTION.
      *
      *    画面をＳＰＡにセット
           PERFORM 4101-GMN-SPA-SET-SEC
      *
           MOVE    SPACE               TO  LINKAREA
           MOVE    SPA-KYOUTU          TO  LNK-COMMON
           MOVE    SPA-Q01FREE         TO  LNK-SCRSPA
      *
           INITIALIZE                  SPA-KYOTUKEY
      *    画面移行用のパラメタ変更
           MOVE    "Q02"               TO  SPA-MOTOPG
           MOVE    "XB01"              TO  SPA-SAKIPG
      *
           MOVE    CONST-JOB-SHELLID
                                       TO  SPA-JOB-SHELLID
      *
           MOVE    SPA-KYOUTU          TO  LNK-KYOUTU
      *
           MOVE   "CHANGE"             TO  MCP-PUTTYPE
           MOVE   "XB01"               TO  MCP-WINDOW
      *
           PERFORM 900-PUT-WINDOW
      *
           MOVE    1                   TO  FLG-END
      *
           .
       480-JYOUHOU-DEL-EXT.
           EXIT.
      *****************************************************************
      *    予約登録へ
      *****************************************************************
       480-YOYAKU-SEC              SECTION.
      *
      *    戻り時、画面を元へ戻す
           MOVE    SPACE               TO  LINKAREA
           MOVE    SPA-KYOUTU          TO  LNK-COMMON
           MOVE    SPA-Q01FREE         TO  LNK-SCRSPA
      *
           INITIALIZE                  SPA-KYOTUKEY
      *    画面移行用のパラメタ変更
           MOVE    "Q02"               TO  SPA-MOTOPG
           MOVE    "Y01"               TO  SPA-SAKIPG
           MOVE    "Q02"               TO  SPA-MOTOPG3
           MOVE    SPA-KYOUTU          TO  LNK-KYOUTU
      *
           MOVE   "CHANGE"             TO  MCP-PUTTYPE
           MOVE   "Y01    "            TO  MCP-WINDOW
      *
           PERFORM 900-PUT-WINDOW
      *
           MOVE    1                   TO  FLG-END
           .
       480-YOYAKU-EXT.
           EXIT.
      *
      *****************************************************************
      *    クリア処理
      *****************************************************************
       480-CLEAR-SEC               SECTION.
      *
           INITIALIZE              SPA-GMN-Q02-HYODAI
                                   SPA-GMN-Q02-JYOKEN1
                                   SPA-GMN-Q02-JYOKEN2
                                   SPA-GMN-Q02-SELNUM
                                   SPA-GMN-Q02-KEKKA-SEL
      *
           MOVE    1               TO  SPA-GMN-Q02-CUR
      *
           .
       480-CLEAR-EXT.
           EXIT.
      *****************************************************************
      *    患者登録へ
      *****************************************************************
       480-KANJYA-SEC             SECTION.
      *
      *    入力時チェック処理
           PERFORM 410-INPUT-CHK-SEC
           IF  ( SPA-ERRCD         NOT =   SPACE )
               GO  TO  480-KANJYA-EXT
           END-IF
      *
      *    画面遷移時のチェック処理
           PERFORM 4801-GAMEN-SENIJI-CHK-SEC
           IF  ( SPA-ERRCD         NOT =   SPACE )
               GO  TO  480-KANJYA-EXT
           END-IF
      *
           COMPUTE IDX1    =   SPA-GMN-Q02-SELNUM
                           -   SPA-GMN-Q02-SKIP-KNS
      *
           IF  SPA-GMN-Q02-KEKKALST-PTNUM (IDX1)  =  SPACE
               MOVE    "1002"          TO  SPA-ERRCD
               GO  TO  480-KANJYA-EXT
           END-IF
      *
      *    ＳＰＡの内容を退避
           INITIALIZE                  QSUB01-LNK
           MOVE    SPA-TERMID          TO  QSUB01-TERMID
           MOVE    "O"                 TO  QSUB01-KBN
           MOVE    SPA-Q01FREE         TO  QSUB01-SPA-AREA
      *
           CALL "ORCGQSUB01"           USING   QSUB01-LNK
      *
           INITIALIZE                  SPA-KYOTUKEY
      *
           MOVE    SPA-GMN-Q02-PTINF-PTID (IDX1)
                                       TO  SPA-PTID
           MOVE  SPA-GMN-Q02-KEKKALST-PTNUM (IDX1)
                                       TO  SPA-PTNUM
      *
      *    受付と患者検索以外の元をセット
           MOVE    "Q02"               TO  SPA-MOTOPG
           MOVE    "P02"               TO  SPA-SAKIPG
      *
           MOVE    SPACE               TO  LINKAREA
           MOVE    SPA-KYOUTU          TO  LNK-KYOUTU
           MOVE    SPA-KYOUTU          TO  LNK-COMMON
           MOVE    SPA-Q01FREE         TO  LNK-SCRSPA
      *
           MOVE   "CHANGE"             TO  MCP-PUTTYPE.
           MOVE   "P02"                TO  MCP-WINDOW.
      *
           PERFORM 900-PUT-WINDOW
      *
           MOVE    1                   TO  FLG-END
      *
           .
       480-KANJYA-EXT.
           EXIT.
      *
      *****************************************************************
      *    受付一覧へ
      *****************************************************************
       490-UKEICHI-SEC             SECTION.
      *
      *    戻り時、画面を元へ戻す
           MOVE    SPACE               TO  LINKAREA
           MOVE    SPA-KYOUTU          TO  LNK-COMMON
           MOVE    SPA-Q01FREE         TO  LNK-SCRSPA
      *    画面移行用のパラメタ変更
           MOVE    "Q02"               TO  SPA-MOTOPG
           MOVE    "U01"               TO  SPA-SAKIPG
      *
           INITIALIZE                  SPA-KYOTUKEY
           MOVE    SPA-KYOUTU          TO  LNK-KYOUTU
      *
           MOVE   "CHANGE"             TO  MCP-PUTTYPE
           MOVE   "U01"                TO  MCP-WINDOW
      *
           PERFORM 900-PUT-WINDOW
      *
           MOVE    1                   TO  FLG-END
      *
           .
       490-UKEICHI-EXT.
           EXIT.
      *
      *****************************************************************
      *    明細書連携確認画面
      *****************************************************************
       480-Q03-SENI-SEC                SECTION.
      *
           PERFORM 410-INPUT-CHK-SEC
           IF    ( SPA-ERRCD    NOT =  SPACE )
               GO  TO  480-Q03-SENI-EXT
           END-IF
      *
      *    ジョブ管理チェック処理
           PERFORM 900-JOBKANRI-CHK-SEC
           IF    ( FLG-JOBEND   =   ZERO )
               MOVE    "7777"          TO  SPA-ERRCD
               MOVE    SPACE           TO  WRK-SHORIERR
               STRING  "処理中です【"  DELIMITED  BY  SIZE
                       JOB-SHELLMSG    DELIMITED  BY  SPACE
                       "】"            DELIMITED  BY  SIZE
               INTO  WRK-SHORIERR
               END-STRING
               GO  TO  480-Q03-SENI-EXT
           END-IF
      *
           MOVE    ZERO            TO  WRK-PROC-FLG
      *
           PERFORM 4801-Q03-SENI-SEC
      *
           .
       480-Q03-SENI-EXT.
           EXIT.
      *****************************************************************
      *    明細書連携確認画面
      *****************************************************************
       4801-Q03-SENI-SEC               SECTION.
      *
           MOVE    SPACE           TO  SPA-Q02-Q03-SENI-AREA
           INITIALIZE                  SPA-Q02-Q03-SENI-AREA
      *
           MOVE    SPA-Q01-NYUGAIKBN
                                   TO  SPA-Q02-Q03-NYUGAIKBN
           EVALUATE    TRUE
           WHEN  ( SPA-Q01-EDSRYYMD    NOT =   SPACE )
               MOVE    SPA-Q01-EDSRYYMD (1 : 6)
                                   TO  SPA-Q02-Q03-SRYYM
           WHEN  ( SPA-Q01-STSRYYMD    NOT =   SPACE )
               MOVE    SPA-Q01-STSRYYMD (1 : 6)
                                   TO  SPA-Q02-Q03-SRYYM
           WHEN    OTHER
               MOVE    SPA-SYSYMD (1:6)
                                   TO  SPA-Q02-Q03-SRYYM
           END-EVALUATE
      *
           MOVE    SPACE           TO  SPA-MOTOPG
           MOVE    SPA-AREA        TO  SPA-COMMON
           MOVE    SPA-Q01FREE     TO  SPA-FREE
      *
           MOVE    "LINK"          TO  MCP-STATUS
           MOVE    SPACE           TO  MCP-EVENT
      *
           CALL    "ORCGQ03"      USING
                                   MCPAREA
                                   SPAAREA
                                   LINKAREA
                                   SCRAREA
      *
      *
           MOVE    SPA-COMMON      TO  SPA-AREA
           MOVE    SPA-FREE        TO  SPA-Q01FREE
      *
           MOVE  "B12"             TO  MCP-WIDGET
      *
           MOVE  "Q02"             TO  SPA-MOTOPG
           MOVE  "Q03"            TO  SPA-SAKIPG
      *
           MOVE  "NEW"             TO  MCP-PUTTYPE
           MOVE  "Q03"            TO  MCP-WINDOW
      *
           PERFORM 900-PUT-WINDOW
      *
           MOVE  1                 TO  FLG-END
      *
           .
       4801-Q03-SENI-EXT.
           EXIT.
      *****************************************************************
      *    印刷確認
      *****************************************************************
       480-CSV-KAKUNIN-SEC             SECTION.
      *
      *    画面をＳＰＡにセット
           PERFORM 4101-GMN-SPA-SET-SEC
      *
      *    ジョブ管理チェック処理
           PERFORM 900-JOBKANRI-CHK-SEC
           IF    ( FLG-JOBEND   =   ZERO )
               MOVE    "7777"          TO  SPA-ERRCD
               MOVE    SPACE           TO  WRK-SHORIERR
               STRING  "処理中です【"  DELIMITED  BY  SIZE
                       JOB-SHELLMSG    DELIMITED  BY  SPACE
                       "】"            DELIMITED  BY  SIZE
               INTO  WRK-SHORIERR
               END-STRING
               GO  TO  480-CSV-KAKUNIN-EXT
           END-IF
      *
           MOVE    ZERO            TO  WRK-PROC-FLG
      *
           PERFORM 480-OUT-SIJI-CALL-SEC
      *
           .
       480-CSV-KAKUNIN-EXT.
           EXIT.
      *
      *****************************************************************
      *    印刷確認
      *****************************************************************
       480-PRINT-KAKUNIN-SEC           SECTION.
      *
      *    画面をＳＰＡにセット
           PERFORM 4101-GMN-SPA-SET-SEC
      *
      *    ジョブ管理チェック処理
           PERFORM 900-JOBKANRI-CHK-SEC
           IF    ( FLG-JOBEND   =   ZERO )
               MOVE    "7777"          TO  SPA-ERRCD
               MOVE    SPACE           TO  WRK-SHORIERR
               STRING  "処理中です【"  DELIMITED  BY  SIZE
                       JOB-SHELLMSG    DELIMITED  BY  SPACE
                       "】"            DELIMITED  BY  SIZE
               INTO  WRK-SHORIERR
               END-STRING
               GO  TO  480-PRINT-KAKUNIN-EXT
           END-IF
      *
      *    表題全角半角混在チェック
           MOVE    SPA-GMN-Q02-HYODAI  TO  WRK-KANACHK-MAE-INPUT
      *
           PERFORM     800-ENRTRY-CHK-SEC
           IF  ( FLG-ENTRYCHK      NOT =   ZERO )
               MOVE    "0003"          TO  SPA-ERRCD
               MOVE    1               TO  SPA-GMN-Q02-CUR
               GO  TO  480-PRINT-KAKUNIN-EXT
           END-IF
      *
      *
      *    条件１全角半角混在チェック
           MOVE    SPA-GMN-Q02-JYOKEN1 TO  WRK-KANACHK-MAE-INPUT
      *
           PERFORM     800-ENRTRY-CHK-SEC
           IF  ( FLG-ENTRYCHK      NOT =   ZERO )
               MOVE    "0003"          TO  SPA-ERRCD
               MOVE    2               TO  SPA-GMN-Q02-CUR
               GO  TO  480-PRINT-KAKUNIN-EXT
           END-IF
      *
      *
      *    条件２全角半角混在チェック
           MOVE    SPA-GMN-Q02-JYOKEN2 TO  WRK-KANACHK-MAE-INPUT
      *
           PERFORM     800-ENRTRY-CHK-SEC
           IF  ( FLG-ENTRYCHK      NOT =   ZERO )
               MOVE    "0003"          TO  SPA-ERRCD
               MOVE    3               TO  SPA-GMN-Q02-CUR
               GO  TO  480-PRINT-KAKUNIN-EXT
           END-IF
      *
      *
           MOVE    1               TO  WRK-PROC-FLG
      *
           PERFORM 480-OUT-SIJI-CALL-SEC
      *
           .
       480-PRINT-KAKUNIN-EXT.
           EXIT.
      *
      *****************************************************************
      *    処理確認画面
      *****************************************************************
       490-KAKUNIN-SEC                 SECTION.
      *
      *    画面をＳＰＡにセット
           PERFORM 4101-GMN-SPA-SET-SEC
      *
           MOVE    SPACE           TO  SPA-MOTOPG
           MOVE    SPA-AREA        TO  SPA-COMMON
      *
           MOVE    "LINK"          TO  MCP-STATUS
           MOVE    SPACE           TO  MCP-EVENT
      *
           CALL    "ORCGQ97"       USING
                                   MCPAREA
                                   SPAAREA
                                   LINKAREA
                                   SCRAREA
      *
           MOVE    SPA-COMMON      TO  SPA-AREA
      *
      *
           MOVE    "B01"           TO  MCP-WIDGET
      *
           MOVE    "Q02"           TO  SPA-MOTOPG
           MOVE    "Q97"           TO  SPA-SAKIPG
      *
           MOVE    "NEW"           TO  MCP-PUTTYPE
           MOVE    "Q97"           TO  MCP-WINDOW
      *
           PERFORM 900-PUT-WINDOW
      *
           MOVE    1               TO  FLG-END
      *
           .
       490-KAKUNIN-EXT.
           EXIT.
      *
      *****************************************************************
      *    出力指示画面呼出処理
      *****************************************************************
       480-OUT-SIJI-CALL-SEC           SECTION.
      *
           MOVE    SPACE           TO  SPA-Q02-Q100-SENI-AREA
           INITIALIZE                  SPA-Q02-Q100-SENI-AREA
      *
           MOVE    WRK-PROC-FLG    TO  SPA-Q02-Q100-PROC-FLG
           MOVE    1               TO  SPA-Q02-Q100-STNUM
      *
           IF  ( WRK-PROC-FLG      =   ZERO )
               MOVE    SPA-GMN-Q02-TOTAL-NUM
                                   TO  SPA-Q02-Q100-EDNUM
           ELSE
               COMPUTE SPA-Q02-Q100-EDNUM
                   = ( SPA-GMN-Q02-TOTAL-NUM   +   CONST-LINE  -   1 )
                   /   CONST-LINE
           END-IF
      *
           MOVE    SPACE           TO  SPA-MOTOPG
           MOVE    SPA-AREA        TO  SPA-COMMON
           MOVE    SPA-Q01FREE     TO  SPA-FREE
      *
           MOVE    "LINK"          TO  MCP-STATUS
           MOVE    SPACE           TO  MCP-EVENT
      *
           CALL    "ORCGQ100"      USING
                                   MCPAREA
                                   SPAAREA
                                   LINKAREA
                                   SCRAREA
      *
      *
           MOVE    SPA-COMMON      TO  SPA-AREA
           MOVE    SPA-FREE        TO  SPA-Q01FREE
      *
           MOVE  "B12"             TO  MCP-WIDGET
      *
           MOVE  "Q02"             TO  SPA-MOTOPG
           MOVE  "Q100"            TO  SPA-SAKIPG
      *
           MOVE  "NEW"             TO  MCP-PUTTYPE
           MOVE  "Q100"            TO  MCP-WINDOW
      *
           PERFORM 900-PUT-WINDOW
      *
           MOVE  1                 TO  FLG-END
           .
      *
       480-OUT-SIJI-CALL-EXT.
           EXIT.
      *****************************************************************
      *    画面遷移時のチェック処理
      *****************************************************************
       4801-GAMEN-SENIJI-CHK-SEC   SECTION.
      *
      *    選択番号チェック
      *    １．未入力チェック
           IF  ( SPA-GMN-Q02-SELNUM    =   ZERO )
               MOVE    "1001"      TO  SPA-ERRCD
               GO  TO  4801-GAMEN-SENIJI-CHK-EXT
           END-IF
      *
      *    ２．範囲チェック
           COMPUTE WRK-KEKKALST-NO-ST  =   SPA-GMN-Q02-SKIP-KNS
                                       +   1
      *
           COMPUTE WRK-KEKKALST-NO-ED  =   SPA-GMN-Q02-SKIP-KNS
                                       +   SPA-GMN-Q02-KEKKA-MAX
      *
           IF      ( SPA-GMN-Q02-SELNUM   >=   WRK-KEKKALST-NO-ST )
            AND    ( SPA-GMN-Q02-SELNUM   <=   WRK-KEKKALST-NO-ED )
                   CONTINUE
           ELSE
               MOVE    "0002"          TO  SPA-ERRCD
               GO  TO  4801-GAMEN-SENIJI-CHK-EXT
           END-IF
           .
      *
       4801-GAMEN-SENIJI-CHK-EXT.
           EXIT.
     *****************************************************************
      *    自画面編集処理
      *****************************************************************
       500-SET-SCREEN              SECTION.
      *
           PERFORM 500-GMNHEN-SEC
      *
           IF      SPA-ERRCD       NOT =   SPACE
               PERFORM 510-ERRSET-SEC
               GO  TO  500-SET-SCREEN-EXT
           END-IF
      *
           IF      SPA-QIDCD       NOT =   SPACE
               PERFORM 520-QIDSET-SEC
               GO  TO  500-SET-SCREEN-EXT
           END-IF
      *
           MOVE    SPACE               TO  LINKAREA
      *
           MOVE    "CURRENT"           TO  MCP-PUTTYPE.
           MOVE    "Q02"               TO  MCP-WINDOW.
      *
           PERFORM 900-PUT-WINDOW.
           .
       500-SET-SCREEN-EXT.
           EXIT.
      *
      *****************************************************************
      *    画面編集処理
      *****************************************************************
       500-GMNHEN-SEC                  SECTION.
      *
           IF    ( FLG-GMN-INIT            =   1 )
               MOVE    ZERO                TO  Q02-KEKKALST-ROW
               MOVE    ZERO                TO  Q02-KEKKALST-ROWATTR
           ELSE
               MOVE    ZERO                TO  Q02-KEKKALST-ROWATTR
           END-IF
      *
           MOVE    WIDGET-NORMAL           TO  Q02-B10-STATE
                                               Q02-B24-STATE
                                               Q02-B09-STATE
      *    検索結果件数がゼロ件でない場合はボタンを活性にする
           IF  ( SPA-GMN-Q02-TOTAL-NUM     >   ZERO )
               MOVE    WIDGET-NORMAL       TO  Q02-B05-STATE
                                               Q02-B11-STATE
                                               Q02-B12-STATE
               MOVE    SPA-GMN-Q02-B06-STATE
                                           TO  Q02-B06-STATE
               MOVE    SPA-GMN-Q02-B07-STATE
                                           TO  Q02-B07-STATE
           ELSE
               MOVE    WIDGET-INSENSITIVE  TO  Q02-B05-STATE
                                               Q02-B06-STATE
                                               Q02-B07-STATE
                                               Q02-B09-STATE
                                               Q02-B11-STATE
                                               Q02-B12-STATE
           END-IF
      *
           IF  ( SPA-GMN-Q02-TOTAL-NUM     >   CONST-RECE-MAX )
               MOVE    WIDGET-INSENSITIVE  TO  Q02-B09-STATE
           END-IF
      *
           IF    ( SPA-GSRAUTH-12          =   "1" )
               MOVE    WIDGET-NORMAL       TO  Q02-B05-STATE
           ELSE
               MOVE    WIDGET-INSENSITIVE  TO  Q02-B05-STATE
           END-IF
      *
      *    表題
           MOVE    SPA-GMN-Q02-HYODAI      TO  Q02-HYODAI
      *    条件上段
           MOVE    SPA-GMN-Q02-JYOKEN1     TO  Q02-JYOKEN1
      *    条件下段
           MOVE    SPA-GMN-Q02-JYOKEN2     TO  Q02-JYOKEN2
      *    検索件数
           MOVE    SPA-GMN-Q02-KEKKA-MAX
                                   TO  Q02-KEKKALST-COUNT
      *    検索結果
           PERFORM VARYING IDX1    FROM    1   BY  1
                   UNTIL ( IDX1    >       SPA-GMN-Q02-KEKKA-MAX)
      *        連番
               MOVE    SPA-GMN-Q02-KEKKALST-NO   (IDX1)
                                   TO  Q02-KEKKALST-NO (IDX1)
      *
      *        患者番号
               MOVE    SPA-GMN-Q02-KEKKALST-PTNUM   (IDX1)
                                   TO  Q02-KEKKALST-PTNUM (IDX1)
      *
      *        氏名
               MOVE    SPA-GMN-Q02-KEKKALST-NAME (IDX1)
                                   TO  Q02-KEKKALST-NAME (IDX1)
      *        性別
               MOVE    SPA-GMN-Q02-KEKKALST-SEX (IDX1)
                                   TO  Q02-KEKKALST-SEX (IDX1)
      *        誕生日
               MOVE    SPA-GMN-Q02-KEKKALST-BIRTH (IDX1)
                                   TO  Q02-KEKKALST-BIRTH (IDX1)
      *        年齢
               MOVE    SPA-GMN-Q02-KEKKALST-AGE (IDX1)
                                   TO  Q02-KEKKALST-AGE (IDX1)
      *        保険組合せ
               MOVE    SPA-GMN-Q02-KEKKALST-HKN (IDX1)
                                   TO  Q02-KEKKALST-HKN (IDX1)
      *        最終受診日
               MOVE    SPA-GMN-Q02-KEKKALST-LASTYMD (IDX1)
                                   TO  Q02-KEKKALST-LASTYMD (IDX1)
      *        電話番号
               MOVE    SPA-GMN-Q02-KEKKALST-TEL (IDX1)
                                   TO  Q02-KEKKALST-TEL (IDX1)
      *        郵便番号
               MOVE    SPA-GMN-Q02-KEKKALST-POST (IDX1)
                                   TO  Q02-KEKKALST-POST (IDX1)
      *        診療内容
               MOVE    SPA-GMN-Q02-KEKKALST-SRYACT (IDX1)
                                   TO  Q02-KEKKALST-SRYACTNAIYO (IDX1)
      *
               IF      ( SPA-GMN-Q02-KEKKA-SEL =   IDX1 )
                   MOVE    "T"     TO  Q02-KEKKALST-SELECT (IDX1)
               ELSE
                   MOVE    "F"     TO  Q02-KEKKALST-SELECT (IDX1)
               END-IF
      *
           END-PERFORM
      *
      *    総件数
           MOVE    SPA-GMN-Q02-TOTAL   TO  Q02-TOTAL
      *
      *    選択番号
           MOVE    SPA-GMN-Q02-SELNUM  TO  Q02-SELNUM
      *
           PERFORM 5001-MAPCUR-SEC
      *
           .
       500-GMNHEN-EXT.
           EXIT.
      *
      *****************************************************************
      *    画面カーソルセット処理
      *****************************************************************
       5001-MAPCUR-SEC             SECTION.
      *
           IF     (SPA-ERRCD           =   SPACE)  AND
                  (SPA-GMN-Q02-CUR     =   ZERO )
               PERFORM 5011-INPUT-CUR-SEC
           END-IF
      *
           EVALUATE    SPA-GMN-Q02-CUR
      *
      *    表題
           WHEN   001
               MOVE  "HYODAI"      TO  MCP-WIDGET
      *
      *    条件上段
           WHEN   002
               MOVE  "JYOKEN1"     TO  MCP-WIDGET
      *
      *    条件下段
           WHEN   003
               MOVE  "JYOKEN2"     TO  MCP-WIDGET
      *
      *    選択番号
           WHEN   004
               MOVE  "SELNUM"      TO  MCP-WIDGET
           END-EVALUATE
      *
           .
       5001-MAPCUR-EXT.
           EXIT.
      *
      *****************************************************************
      *    入力個所のセット処理
      *****************************************************************
       5011-INPUT-CUR-SEC          SECTION.
      *
           EVALUATE    WRK-MCP-WIDGET
      *
      *    表題
           WHEN    "HYODAI"
               MOVE    001         TO  SPA-GMN-Q02-CUR
      *
      *    条件上段
           WHEN    "JYOKEN1"
               MOVE    002         TO  SPA-GMN-Q02-CUR
      *
      *    条件下段
           WHEN    "JYOKEN2"
               MOVE    003         TO  SPA-GMN-Q02-CUR
      *
      *    選択番号
           WHEN    "SELNUM"
               MOVE    004         TO  SPA-GMN-Q02-CUR
           END-EVALUATE
      *
      *    次カーソル位置を設定
           EVALUATE    WRK-MCP-WIDGET
           WHEN    "SELNUM"
               MOVE    001         TO  SPA-GMN-Q02-CUR
           WHEN    OTHER
               COMPUTE SPA-GMN-Q02-CUR =   SPA-GMN-Q02-CUR +   1
           END-EVALUATE
      *
           .
      *
       5011-INPUT-CUR-EXT.
           EXIT.
      *****************************************************************
      *    エラーメッセージ表示理
      *****************************************************************
       510-ERRSET-SEC              SECTION.
      *
           MOVE    SPACE               TO  SPA-ERRMSG
      *
           EVALUATE    SPA-ERRCD
           WHEN    "0001"
               MOVE    "条件に該当する患者は存在しませんでした"
                                       TO  SPA-ERRMSG
           WHEN    "0002"
               MOVE    "範囲外の番号が入力されています"
                                       TO  SPA-ERRMSG
           WHEN    "0003"
               MOVE    "全角文字でのみ入力してください"
                                       TO  SPA-ERRMSG
           WHEN    "1001"
               MOVE    "選択番号が未入力です"
                                       TO  SPA-ERRMSG
           WHEN    "1002"
               MOVE    "患者番号が未登録のため遷移できません"
                                       TO  SPA-ERRMSG
           WHEN    "7777"
               MOVE    WRK-SHORIERR    TO  SPA-ERRMSG
           END-EVALUATE
      *
           MOVE    SPACE                TO  QERR
           MOVE    SPA-ERRCD            TO  QERR-ERRCODE
           MOVE    SPA-ERRMSG           TO  QERR-ERRMSG
           MOVE    "B01"                TO  MCP-WIDGET
      *
           MOVE    "Q02"                TO  SPA-MOTOPG
           MOVE    "QERR"               TO  SPA-SAKIPG
      *
           MOVE    "NEW"                TO  MCP-PUTTYPE
           MOVE    "QERR"               TO  MCP-WINDOW
      *
           PERFORM 900-PUT-WINDOW
      *
           MOVE    1                    TO  FLG-END
      *
           .
       510-ERRSET-EXT.
           EXIT.
      *
      *****************************************************************
      *    確認メッセージ表示処理
      *****************************************************************
       520-QIDSET-SEC              SECTION.
      *
           MOVE    SPACE               TO  WRK-QIDMSG
      *
           EVALUATE    SPA-QIDCD
           WHEN    "6666"
               MOVE    WRK-SHORIERR    TO  WRK-QIDMSG
           END-EVALUATE
      *
           MOVE    SPACE               TO  QID1
           MOVE    SPA-QIDCD           TO  QID1-ID1CODE
           MOVE    WRK-QIDMSG          TO  QID1-ID1MSG
           MOVE    "B12"               TO  MCP-WIDGET
      *
           MOVE    "Q02"               TO  SPA-MOTOPG
           MOVE    "QID1"              TO  SPA-SAKIPG
      *
           MOVE    "NEW"               TO  MCP-PUTTYPE.
           MOVE    "QID1"              TO  MCP-WINDOW.
      *
           PERFORM 900-PUT-WINDOW.
      *
           MOVE    1                   TO  FLG-END
      *
           .
       520-QIDSET-EXT.
           EXIT.
      *****************************************************************
      *    全角半角混在チェック
      *****************************************************************
       800-ENRTRY-CHK-SEC              SECTION.
      *
           MOVE    ZERO            TO  FLG-ENTRYCHK
      *
           IF  ( WRK-KANACHK-MAE-INPUT
                                   =   SPACE )
               GO  TO  800-ENRTRY-CHK-EXT
           END-IF
      *
           MOVE    SPACE           TO  ORCSKANACHKAREA
           INITIALIZE                  ORCSKANACHKAREA
           MOVE    "1"             TO  KANACHK-SYORI
           MOVE    WRK-KANACHK-MAE-INPUT
                                   TO  KANACHK-MAE-INPUT
           CALL    "ORCSKANACHK"   USING   ORCSKANACHKAREA
           IF      ( KANACHK-RC    NOT =   ZERO )
               MOVE    1           TO  FLG-ENTRYCHK
               GO  TO  800-ENRTRY-CHK-EXT
           END-IF
      *
      *    全角でない場合はエラーとする。
           IF      ( KANACHK-SYUBETU   NOT =   2 )
               MOVE    1           TO  FLG-ENTRYCHK
           END-IF
      *
           .
       800-ENRTRY-CHK-EXT.
           EXIT.
      *****************************************************************
      *    マシン日付取得サブ
      *****************************************************************
       800-MCNDATE-SEC         SECTION.
      *
           CALL    "ORCSMCNDATE"   USING   ORCSMCNDATEAREA
      *
           .
      *
       800-MCNDATE-EXT.
           EXIT.
      *****************************************************************
      *    文字列長取得処理
      *****************************************************************
       900-STR-LEN-GET-SEC                 SECTION.
      *
           PERFORM VARYING IDX-LEN FROM    WRK-STR-LEN BY  -1
               UNTIL   ( IDX-LEN   <   1)
                OR     ( WRK-STR (IDX-LEN : 1) NOT =   SPACE)
      *
               CONTINUE
           END-PERFORM
      *
           MOVE    IDX-LEN     TO  WRK-STR-LEN
      *
           .
       900-STR-LEN-GET-EXT.
           EXIT.
      *****************************************************************
      *    患者マスタ検索処理
      *****************************************************************
       900-QUERY-PTINF-KEY-SEL-SEC     SECTION.
      *
      *    INITIALIZE                  MCPAREA
           MOVE    "query_ptinf"       TO  MCP-TABLE
           MOVE    "key2"              TO  MCP-PATHNAME
           MOVE    SPACE               TO  QUERY-PTINF-REC
           MOVE    WRK-SQL             TO  QUERY-PTINF-SQL
           MOVE    QUERY-PTINF-REC     TO  MCPDATA-REC
           PERFORM 910-DBSELECT-SEC
           IF    ( MCP-RC              =   ZERO )
               MOVE    ZERO            TO  FLG-PTINF
               MOVE    MCPDATA-REC     TO  QUERY-PTINF-REC
           ELSE
               MOVE    1               TO  FLG-PTINF
           END-IF
      *
           .
       900-QUERY-PTINF-KEY-SEL-EXT.
           EXIT.
      *****************************************************************
      *    患者マスタ読込処理
      *****************************************************************
       900-QUERY-PTINF-KEY-FET-SEC     SECTION.
      *
      *    INITIALIZE              MCPAREA
           MOVE    "query_ptinf"       TO  MCP-TABLE
           MOVE    "key2"              TO  MCP-PATHNAME
           PERFORM 910-DBFETCH-SEC
           IF    ( MCP-RC              =   ZERO )
               MOVE    ZERO            TO  FLG-PTINF
               MOVE    MCPDATA-REC     TO  QUERY-PTINF-REC
           ELSE
               MOVE    1               TO  FLG-PTINF
           END-IF
      *
           .
       900-QUERY-PTINF-KEY-FET-EXT.
           EXIT.
      *****************************************************************
      *    ＤＢ検索処理
      *****************************************************************
       910-DBSELECT-SEC               SECTION.
      *
           MOVE    "DBSELECT"      TO  MCP-FUNC
           CALL    "ORCDBMAIN"         USING   MCPAREA
                                               MCPDATA-REC
                                               SPA-AREA
           IF    ( MCP-RC          =   ZERO )
               PERFORM 910-DBFETCH-SEC
           END-IF
      *
           .
      *
       910-DBSELECT-EXT.
           EXIT.
      *****************************************************************
      *    ＤＢ検索処理
      *****************************************************************
       911-DBSELECT-SEC               SECTION.
      *
           MOVE    "DBSELECT"      TO  MCP-FUNC
           CALL    "ORCDBMAIN"         USING   MCPAREA
                                               MCPDATA-REC
                                               SPA-AREA
      *
           .
      *
       911-DBSELECT-EXT.
           EXIT.
      *****************************************************************
      *    ＤＢ読み込み処理
      *****************************************************************
       910-DBFETCH-SEC                 SECTION.
      *
           MOVE    "DBFETCH"       TO  MCP-FUNC
           CALL    "ORCDBMAIN"         USING   MCPAREA
                                               MCPDATA-REC
                                               SPA-AREA
      *
           .
      *
       910-DBFETCH-EXT.
           EXIT.
      *****************************************************************
      *    ＤＢクローズ処理
      *****************************************************************
       910-DBCLOSECURSOR-SEC                SECTION.
      *
           MOVE    "DBCLOSECURSOR" TO  MCP-FUNC
           CALL    "ORCDBMAIN"         USING   MCPAREA
                                               MCPDATA-REC
                                               SPA-AREA
      *
           .
      *
       910-DBCLOSECURSOR-EXT.
           EXIT.
      *****************************************************************
      *    ジョブ開始セット処理
      *****************************************************************
       900-JOBKANRI-HENSYU-SEC         SECTION.
      *
           MOVE    "DEL"           TO  SJOBKANRI-MODE
           INITIALIZE                  JOBKANRI-REC
           MOVE    CONST-JOBID     TO  JOB-JOBID
           MOVE    CONST-JOB-SHELLID
                                   TO  JOB-SHELLID
           MOVE    SPA-HOSPNUM     TO  JOB-HOSPNUM
           CALL    "ORCSJOB"       USING
                                   ORCSJOBKANRIAREA 
                                   JOBKANRI-REC
                                   SPA-AREA
      *
           MOVE    "DEL"           TO  SJOBKANRI-MODE
           INITIALIZE                  JOBKANRI-REC
           MOVE    CONST-JOBID2     TO  JOB-JOBID
           MOVE    CONST-JOB-SHELLID
                                   TO  JOB-SHELLID
           MOVE    SPA-HOSPNUM     TO  JOB-HOSPNUM
           CALL    "ORCSJOB"       USING
                                   ORCSJOBKANRIAREA 
                                   JOBKANRI-REC
                                   SPA-AREA
      *
           MOVE    "JBS"           TO  SJOBKANRI-MODE
           INITIALIZE                  JOBKANRI-REC
           MOVE    WRK-JOBID       TO  JOB-JOBID
           MOVE    CONST-JOB-SHELLID
                                   TO  JOB-SHELLID
           MOVE    SPA-TERMID      TO  JOB-TERMID
           MOVE    SPA-HOSPNUM     TO  JOB-HOSPNUM
           CALL    "ORCSJOB"       USING
                                   ORCSJOBKANRIAREA 
                                   JOBKANRI-REC
                                   SPA-AREA
      *
           .
       900-JOBKANRI-HENSYU-EXT.
           EXIT.
      *****************************************************************
      *    ジョブ管理チェック処理
      *****************************************************************
       900-JOBKANRI-CHK-SEC            SECTION.
      *
           MOVE    ZERO            TO  FLG-JOBEND
      *
      *    ジョブ管理チェック処理
           MOVE    "CHK"           TO  SJOBKANRI-MODE
           INITIALIZE                  JOBKANRI-REC
           MOVE    CONST-JOBID     TO  JOB-JOBID
           MOVE    CONST-JOB-SHELLID
                                   TO  JOB-SHELLID
           MOVE    SPA-HOSPNUM     TO  JOB-HOSPNUM
           CALL    "ORCSJOB"       USING
                                       ORCSJOBKANRIAREA
                                       JOBKANRI-REC
                                       SPA-AREA
           IF    ( SJOBKANRI-RETURN    =   ZERO )
               IF    ( JOB-ENDYMD      =   SPACE )
                   MOVE    ZERO        TO  FLG-JOBEND
               ELSE
                   MOVE    1           TO  FLG-JOBEND
               END-IF
           ELSE
                   MOVE    2           TO  FLG-JOBEND
           END-IF
      *
           IF    ( FLG-JOBEND      =   ZERO )
               CONTINUE
           ELSE
      *        ジョブ管理チェック処理
               MOVE    "CHK"           TO  SJOBKANRI-MODE
               INITIALIZE                  JOBKANRI-REC
               MOVE    CONST-JOBID2    TO  JOB-JOBID
               MOVE    CONST-JOB-SHELLID
                                       TO  JOB-SHELLID
               MOVE    SPA-HOSPNUM     TO  JOB-HOSPNUM
               CALL    "ORCSJOB"       USING
                                           ORCSJOBKANRIAREA
                                           JOBKANRI-REC
                                           SPA-AREA
               IF    ( SJOBKANRI-RETURN    =   ZERO )
                   IF    ( JOB-ENDYMD      =   SPACE )
                       MOVE    ZERO    TO  FLG-JOBEND
                   ELSE
                       MOVE    1       TO  FLG-JOBEND
                   END-IF
               ELSE
                       MOVE    2       TO  FLG-JOBEND
               END-IF
           END-IF
      *
           .
       900-JOBKANRI-CHK-EXT.
           EXIT.
      *****************************************************************
      *    ＰＵＴ　処理
      *****************************************************************
       900-PUT-WINDOW              SECTION.
      *
           MOVE    "PUTWINDOW"     TO  MCP-FUNC.
           CALL    "ORCDBMAIN"         USING   MCPAREA
                                               MCPDATA-REC
                                               SPA-AREA
      *
           .
       900-PUT-WINDOW-EXT.
           EXIT.

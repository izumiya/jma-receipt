      *******************************************************************
      * Project code name "ORCA"
      * 日医標準レセプトソフト（JMA standard receipt software）
      * Copyright(C) 2002 JMA (Japan Medical Association)
      *
      * This program is part of "JMA standard receipt software".
      *
      *     This program is distributed in the hope that it will be useful
      * for further advancement in medical care, according to JMA Open
      * Source License, but WITHOUT ANY WARRANTY.
      *     Everyone is granted permission to use, copy, modify and
      * redistribute this program, but only under the conditions described
      * in the JMA Open Source License. You should have received a copy of
      * this license along with this program. If not, stop using this
      * program and contact JMA, 2-28-16 Honkomagome, Bunkyo-ku, Tokyo,
      * 113-8621, Japan.
      ********************************************************************
       IDENTIFICATION              DIVISION.
       PROGRAM-ID.                 ORCGI44SUB7.
      *****************************************************************
      *  システム名        : ＯＲＣＡ
      *  サブシステム名    : 入院行為入力
      *  コンポーネント名  : 画面入力コード編集変換処理
      *  管理者            : 
      *  作成日付    作業者        記述
      *  02.09.19    ひさなが      新規作成
      *****************************************************************
      *  プログラム修正履歴
      * Maj/Min/Rev  修正者       日付      内容
      *
      *****************************************************************
      *
      *
       ENVIRONMENT                 DIVISION.
       CONFIGURATION               SECTION.
       INPUT-OUTPUT                SECTION.
       FILE-CONTROL.
      *
      *
       DATA                        DIVISION.
      *FILE                        SECTION.
      *
       WORKING-STORAGE             SECTION.
      *
      *    フラグ領域
       01  FLG-AREA.
           03  FLG-END             PIC 9(01).
           03  FLG-INPUTCD         PIC 9(01).
      *
           03  FLG-KANJI           PIC 9(01).
           03  FLG-KANA            PIC 9(01).
           03  FLG-SUJI            PIC 9(01).
           03  FLG-KAI             PIC 9(01).
           03  FLG-HI              PIC 9(01).
      *
      *    添字領域
       01  IDX-AREA.
           03  IDX                 PIC 9(04).
           03  IDX2                PIC 9(04).
           03  IDX3                PIC 9(04).
           03  IDY                 PIC 9(04).
           03  IDH1                PIC 9(04).
           03  IDH2                PIC 9(04).
      *
           03  IDX-ZAI             PIC 9(02).
           03  IDX-D1              PIC 9(02).
           03  IDX-D2              PIC 9(02).
           03  IDX-Z               PIC 9(02).
      *
           03  MAP-IDX             PIC 9(04).
           03  GMN-IDX             PIC 9(04).
      *
      *    一時領域
       01  WRK-AREA.
           03  WRK-INPUTCD         PIC X(22).
           03  WRK-INPUTCD2        PIC X(22).
           03  WRK-INPUTCD3        PIC X(22).
      *
           03  WRK-CHK-FLG         PIC X(01).
      *
           03  WRK-STR             PIC 9(02).
           03  WRK-END             PIC 9(02).
      *
           03  WRK-SRYYMD.
               05  WRK-SRYYY       PIC 9(04).
               05  WRK-SRYMM       PIC 9(02).
               05  WRK-SRYDD       PIC 9(02).
      *
           03  WRK-ICD-CDSYU       PIC X(01).
      *    パス名
           03  WRK-PATH            PIC X(64).
      *
       01  WRK-NYUMOJI-AREA.
           03  WRK-HI              PIC 9(02).
           03  WRK-NISUU           PIC X(20).
           03  WRK-MAE-CD          PIC X(01).
      *
       01  WRK-MOJI-AREA.
           03  FLG-SP              PIC 9(02).
           03  FLG-AST             PIC 9(02).
           03  FLG-JKNKSN          PIC 9(01).
           03  WRK-IDX-FT          PIC 9(02).
           03  WRK-CD-MCNT         PIC 9(02).
           03  WRK-ACCT-MCNT       PIC 9(02).
           03  WRK-SUR-MCNT        PIC 9(02).
           03  WRK-MCNT1           PIC 9(02).
           03  WRK-MCNT2           PIC 9(02).
           03  WRK-MCNT3           PIC 9(02).
           03  WRK-MCNT4           PIC 9(02).
           03  WRK-CD              PIC X(22).
           03  WRK-KAI             PIC X(20).
           03  WRK-SUR             PIC X(20).
           03  WRK-MOJI1           PIC X(10).
           03  WRK-MOJI2           PIC X(10).
           03  WRK-MOJI3           PIC X(10).
           03  WRK-MOJI4           PIC X(10).
           03  IDM                 PIC 9(04).
           03  IDMS                PIC 9(04).
           03  WRK-HENKAN-MAE      PIC X(22).
           03  WRK-HENKAN-ATO      PIC X(22).
           03  IDX-D               PIC 9(04).
           03  WRK-BUNGSU          PIC 9(08).
           03  WRK-SURYO           PIC 9(07)V9(03).
           03  WRK-SURYO-R         REDEFINES   WRK-SURYO.
               05  WRK-SURYO-S1    PIC 9(07).
               05  WRK-SURYO-S2    PIC 9(03).
      *    入院期間
       01  TBL-NYUIN-KAISU-AREA.
           03  TBL-NYUIN-OCC           OCCURS   10.
               05  TBL-HI              PIC 9(02).
               05  TBL-SEL             PIC X(01).
      *
      *****************************************************************
      *    サブプロ用　領域
      *****************************************************************
      *
      *   日付変換サブ
           COPY    "CPORCSDAY.INC".
           COPY    "CPORCSLNK.INC".
      *
      *    画面日付変換サブ
           COPY    "CPORCSGDAY.INC".
      *
      *    数字変換領域
           COPY    "CPORCSNUM.INC".
      *
      *    ＤＢ検索
           COPY    "MCPDATA.INC".
           COPY    "CPORCMCP.INC".
      *
           COPY    "MCPAREA".
      *
      *    入力コードマスタ−
       01  INPUTCD-REC.
           COPY    "CPINPUTCD.INC".
      *
      *****************************************************************
      *    連絡　領域
      *****************************************************************
        LINKAGE                     SECTION.
      *
      *    スパ領域
           COPY    "COMMON-SPA".
      *    パラメタ
           COPY    "CPORCGI4G.INC".
      *
      *    ＳＰＡパラメタ
       01  SPA-SCR-REC.
           COPY    "I4SCR-NYU.INC".
      *
       PROCEDURE                   DIVISION    USING
           SPA-AREA
           ORCGI4GAREA
           SPA-SCR-REC
           .
      *
      *****************************************************************
      *    主　　処理
      *****************************************************************
       000-PROC-SEC                SECTION.
      *
           MOVE    SPACE               TO  SPA-ERRMSG
           INITIALIZE                      WRK-MOJI-AREA
           INITIALIZE                      WRK-AREA
      *
           PERFORM 100-INPUTCD-SYORI-SEC
      *
           . 
       000-PROC-EXT.
      *
           EXIT    PROGRAM
           .
      *
      *****************************************************************
      *    全角、半角変更編集処理
      *****************************************************************
       100-INPUTCD-SYORI-SEC            SECTION.
      *
      *
           INITIALIZE                      LNK-GI4G-OUTAREA
      *
      *    '_'をなくす
           INSPECT LNK-GI4G-INPUTCD
                   TALLYING    LNK-GI4G-JODOU  FOR  ALL "_"
                   REPLACING   ALL "_"  BY  " "
      *
      *    １桁目全角、半角変更
           PERFORM 100-MAE-HANKAKU-SEC
           IF      WRK-INPUTCD     NOT =   SPACE
               MOVE    WRK-INPUTCD         TO  LNK-GI4G-INPUTCD
               GO      TO          100-INPUTCD-SYORI-EXT
           END-IF
           IF      SPA-ERRCD       NOT =   SPACE
               GO      TO          100-INPUTCD-SYORI-EXT
           END-IF
      *
      *    入力コードの判定
           PERFORM 200-INPUTCD-CHK-SEC
           IF      SPA-ERRCD       NOT =   SPACE
               GO      TO          100-INPUTCD-SYORI-EXT
           END-IF
      *
           .
       100-INPUTCD-SYORI-EXT.
           EXIT
           .
      *
      *****************************************************************
      *    全角、半角変更編集処理
      *****************************************************************
       100-MAE-HANKAKU-SEC            SECTION.
      *
           MOVE    SPACE              TO  WRK-INPUTCD2
           MOVE    SPACE              TO  WRK-INPUTCD
      *    漢字変換
           MOVE    1                   TO  IDH1
           MOVE    ZERO                TO  IDH2
           PERFORM
                   UNTIL       IDH1    >   22
             IF      LNK-GI4G-INPUTCD(IDH1:1)
                                          >=  X"A1"
               EVALUATE    LNK-GI4G-INPUTCD(IDH1:2)
                   WHEN    "−"
                       ADD     1           TO  IDH2
                       MOVE    "-"         TO  WRK-INPUTCD2(IDH2:1)
                   WHEN    "＋"
                       ADD     1           TO  IDH2
                       MOVE    "+"         TO  WRK-INPUTCD2 (IDH2:1)
                   WHEN    "！"
                       ADD     1           TO  IDH2
                       MOVE    "!"         TO  WRK-INPUTCD2 (IDH2:1)
                   WHEN    "／"
                       ADD     1           TO  IDH2
                       MOVE    "/"         TO  WRK-INPUTCD2 (IDH2:1)
                   WHEN    "　"
                       ADD     1           TO  IDH2
                       MOVE    SPACE       TO  WRK-INPUTCD2 (IDH2:1)
                   WHEN    OTHER
                       ADD     1           TO  IDH2
                       MOVE    LNK-GI4G-INPUTCD(IDH1:2)
                                           TO  WRK-INPUTCD2 (IDH2:2)
                       ADD     1           TO  IDH2
               END-EVALUATE
               ADD     2                   TO  IDH1
             ELSE
               ADD     1                   TO  IDH2
               MOVE    LNK-GI4G-INPUTCD(IDH1:1)
                                           TO  WRK-INPUTCD2 (IDH2:1)
               ADD     1                   TO  IDH1
             END-IF
           END-PERFORM
      *
      *    ’＋’、’−’のチェック
           MOVE    SPACE               TO  WRK-CHK-FLG
           MOVE    SPACE               TO  WRK-INPUTCD3
           MOVE    ZERO                TO  IDX2
           PERFORM VARYING     IDX     FROM    1   BY  1
                   UNTIL       IDX    >   22
               IF      WRK-INPUTCD2(IDX:1)     =   "+"  OR  "-"
                   COMPUTE IDX3                =   IDX  +  1
                   IF     (WRK-CHK-FLG         =   SPACE)  AND
                          (IDX2                =   ZERO   OR
                           IDX                 =   22     OR
                           WRK-INPUTCD2(IDX3:) =   SPACE )
                       MOVE    WRK-INPUTCD2(IDX:1) TO  WRK-CHK-FLG
                   END-IF
               ELSE
                   ADD     1                   TO  IDX2
                   MOVE    WRK-INPUTCD2(IDX:1) TO  WRK-INPUTCD3
                                                             (IDX2:1)
      *            １，２桁目以外の '/'は、空白とする
                   IF     (WRK-INPUTCD2(IDX:1)     =   "/" )  AND
                          (IDX                 NOT =   1  AND   2)
                       IF      WRK-INPUTCD2(1:1)   =   "*"
                           CONTINUE
                       ELSE
                           MOVE    SPACE       TO  WRK-INPUTCD2(IDX:1)
                           MOVE    SPACE       TO  WRK-INPUTCD3(IDX2:1)
                       END-IF
                   END-IF
               END-IF
           END-PERFORM
      *
           MOVE    WRK-INPUTCD2        TO  LNK-GI4G-INPUTCD
      *
      *    − 入力エラー
           IF      SPA-ERRCD       NOT =   SPACE
               MOVE    LNK-GI4G-INPUTCD
                                       TO  WRK-INPUTCD
               GO      TO      100-MAE-HANKAKU-EXT
           END-IF
      *
      *    １桁目へ−，＋編集
           IF      WRK-CHK-FLG     NOT =   SPACE
               MOVE    WRK-CHK-FLG         TO  WRK-INPUTCD(1:1)
               MOVE    WRK-INPUTCD3        TO  WRK-INPUTCD(2:)
           END-IF
      *
      *
           IF      LNK-GI4G-INPUTCD(1:2)   =   "!!"
               MOVE    LNK-GI4G-INPUTCD
                                       TO  WRK-INPUTCD
           END-IF
           IF      LNK-GI4G-INPUTCD(1:1)   =   "!"  OR
                                               " "  OR
                                               "/"  OR
                                               "-"  OR
                                               "+"
               MOVE    LNK-GI4G-INPUTCD
                                       TO  WRK-INPUTCD
           END-IF
      *
           .
       100-MAE-HANKAKU-EXT.
           EXIT.
      *
      *****************************************************************
      *    入力コードの判定処理
      *****************************************************************
       200-INPUTCD-CHK-SEC    SECTION.
      *
           MOVE    LNK-GI4G-INPUTCD    TO  WRK-INPUTCD
      *
      *    １桁が数字で２桁目が空白の判定を全角チェック
           IF     (LNK-GI4G-INPUTCD(1:1)   >=  X"A1"  )  AND
                  (LNK-GI4G-INPUTCD(3:1)   =   " "    )  AND
                  (LNK-GI4G-INPUTCD(4:)    NOT =   SPACE)
               MOVE    LNK-GI4G-INPUTCD    TO  WRK-HENKAN-MAE
               MOVE    1                   TO  WRK-STR
               MOVE    2                   TO  WRK-END
               PERFORM 2010-MOJI-HENKAN-SEC
               MOVE    SPACE               TO  WRK-INPUTCD
               MOVE    WRK-HENKAN-ATO      TO  WRK-INPUTCD(1:1)
               MOVE    LNK-GI4G-INPUTCD(3:)
                                           TO  WRK-INPUTCD(2:)
           END-IF
      *
      *    １桁が’．’の時、種別入力とする
           IF      LNK-GI4G-INPUTCD(1:2)   =   "．" 
               MOVE    LNK-GI4G-INPUTCD    TO  WRK-HENKAN-MAE
               MOVE    1                   TO  WRK-STR
               MOVE    22                  TO  WRK-END
               PERFORM 2010-MOJI-HENKAN-SEC
               MOVE    WRK-HENKAN-ATO      TO  LNK-GI4G-INPUTCD
           END-IF
      *    診療種別
           IF      LNK-GI4G-INPUTCD(1:1)   =   "."
               MOVE    4                   TO  WRK-CD-MCNT
               MOVE    1                   TO  WRK-IDX-FT
      *
      *        数量以下の変換
               PERFORM 300-SURYOU-SYORI-SEC
               GO      TO      200-INPUTCD-CHK-EXT
           END-IF
      *
      *    入力コード文字列調査処理
           PERFORM 2020-INPUTCD-CHOSA-SEC
           MOVE    WRK-INPUTCD         TO  LNK-GI4G-INPUTCD
           MOVE    WRK-CD              TO  LNK-GI4G-INCD
           IF      SPA-ERRCD       NOT =   SPACE
               GO      TO      200-INPUTCD-CHK-EXT
           END-IF
      *
      *    入力コード変更時、点数マスタの決定
           IF      LNK-GI4G-NYURYOKU   =   1
               PERFORM 2030-INPUTCD-KENSAKU-SEC
               IF     (LNK-GI4G-KENSAKU    NOT =   SPACE )  OR
                      (SPA-ERRCD           NOT =   SPACE )
                   GO      TO      200-INPUTCD-CHK-EXT
               END-IF
           END-IF
      *
      *    数量以下の変換
           PERFORM 300-SURYOU-SYORI-SEC
           .
       200-INPUTCD-CHK-EXT.
           EXIT.
      *
      *****************************************************************
      *    入力コード文字列調査処理
      *****************************************************************
       2020-INPUTCD-CHOSA-SEC       SECTION.
      *
      *--- 文字列調査の開始位置　取得
           IF  (WRK-INPUTCD(1:1)       IS  NUMERIC) AND
               (WRK-INPUTCD(2:1)       =   SPACE  ) AND
               (WRK-INPUTCD(3:1)   NOT =   SPACE  )
      *---     時間加算
               MOVE    WRK-INPUTCD(1:1)
                                       TO  LNK-GI4G-JIKAN
      *---     時間特例チェック
               IF      LNK-GI4G-JIKAN      =   4
                   IF      LNK-GI4G-JIKANTOKU  =   1
                       CONTINUE
                   ELSE
                       MOVE    "0069"          TO  SPA-ERRCD
                       GO      TO      2020-INPUTCD-CHOSA-EXT
                   END-IF
               END-IF
               MOVE    LNK-GI4G-JIKAN  TO  FLG-JKNKSN
               MOVE    3               TO  WRK-IDX-FT
           ELSE
               MOVE    ZERO            TO  LNK-GI4G-JIKAN
               MOVE    LNK-GI4G-JIKAN  TO  FLG-JKNKSN
               MOVE    1               TO  WRK-IDX-FT
           END-IF
      *
           MOVE    ZERO                TO  FLG-KANJI
           MOVE    ZERO                TO  FLG-KANA
           MOVE    ZERO                TO  FLG-SUJI
           MOVE    ZERO                TO  WRK-CD-MCNT
           MOVE    SPACE               TO  WRK-CD
           PERFORM VARYING IDX     FROM    WRK-IDX-FT   BY  1
                   UNTIL  (IDX         >   22     )  OR
                          (WRK-INPUTCD(IDX:1)
                                       =   SPACE  OR  "*" )
      *      全角の＊を半角の*とする
             IF      WRK-INPUTCD(IDX:2)    =  "＊" OR "　"
                 MOVE    22                TO  IDX
             ELSE
      *
               ADD     1                   TO  WRK-CD-MCNT
               MOVE    WRK-INPUTCD(IDX:1)
                                           TO  WRK-CD (WRK-CD-MCNT:1)
      *
               IF      WRK-INPUTCD (IDX:1)
                                      >=  X"A1"
                   MOVE    1               TO  FLG-KANJI
               ELSE
                   IF     (WRK-INPUTCD (IDX:1)
                                          IS  ALPHABETIC )  OR
                          (WRK-INPUTCD (IDX:1)
                                          IS  NUMERIC    )  OR
                          (WRK-INPUTCD (IDX:1)
                                          =   "."  OR  "/" )
                       MOVE    1              TO  FLG-SUJI
                   ELSE
                       MOVE    1              TO  FLG-KANA
                   END-IF
               END-IF
             END-IF
           END-PERFORM
      *
      *    コード変更の有無
           MOVE    LNK-GI4G-GMN-IDX    TO  GMN-IDX
           IF      SPA-COM-ICD-INPUTCD(GMN-IDX)
                                           =   SPACE
      *        入力コードが無い時、点数コードと判定する
               IF      SPA-COM-INPUTCD(GMN-IDX)
                                           =   WRK-CD
                   MOVE    ZERO                TO  LNK-GI4G-NYURYOKU
               ELSE
                   MOVE    1                   TO  LNK-GI4G-NYURYOKU
               END-IF
           ELSE
      *        入力コードがある時、入力コードと判定する
               IF     (SPA-COM-ICD-INPUTCD(GMN-IDX)
                                           =   WRK-CD  )  OR
                      (SPA-COM-INPUTCD(GMN-IDX)
                                           =   WRK-CD  )
                   MOVE    ZERO                TO  LNK-GI4G-NYURYOKU
               ELSE
                   MOVE    1                   TO  LNK-GI4G-NYURYOKU
               END-IF
           END-IF
      *
      *    半角カナは入力不可
           IF      FLG-KANA            =   1
               MOVE    "0005"          TO  SPA-ERRCD
           END-IF
      *    半角、全角混在は入力不可
           IF     (FLG-KANJI           =   1  )  AND
                  (FLG-SUJI            =   1  )
               MOVE    "0005"          TO  SPA-ERRCD
           END-IF
           MOVE    WRK-CD-MCNT         TO  LNK-GI4G-INCDCNT
           .
       2020-INPUTCD-CHOSA-EXT.
           EXIT.
      *
      *****************************************************************
      *    漢字変換処理
      *****************************************************************
       2010-MOJI-HENKAN-SEC    SECTION.
      *    漢字変換
           MOVE    SPACE               TO  WRK-HENKAN-ATO
           MOVE    WRK-STR             TO  IDH1
           MOVE    ZERO                TO  IDH2
           PERFORM
                   UNTIL       IDH1    >   WRK-END
             IF      WRK-HENKAN-MAE(IDH1:1)
                                          >=  X"A1"
               EVALUATE    WRK-HENKAN-MAE(IDH1:2)
                   WHEN    "０"
                       ADD     1           TO  IDH2
                       MOVE    "0"         TO  WRK-HENKAN-ATO (IDH2:1)
                   WHEN    "１"
                       ADD     1           TO  IDH2
                       MOVE    "1"         TO  WRK-HENKAN-ATO (IDH2:1)
                   WHEN    "２"
                       ADD     1           TO  IDH2
                       MOVE    "2"         TO  WRK-HENKAN-ATO (IDH2:1)
                   WHEN    "３"
                       ADD     1           TO  IDH2
                       MOVE    "3"         TO  WRK-HENKAN-ATO (IDH2:1)
                   WHEN    "４"
                       ADD     1           TO  IDH2
                       MOVE    "4"         TO  WRK-HENKAN-ATO (IDH2:1)
                   WHEN    "５"
                       ADD     1           TO  IDH2
                       MOVE    "5"         TO  WRK-HENKAN-ATO (IDH2:1)
                   WHEN    "６"
                       ADD     1           TO  IDH2
                       MOVE    "6"         TO  WRK-HENKAN-ATO (IDH2:1)
                   WHEN    "７"
                       ADD     1           TO  IDH2
                       MOVE    "7"         TO  WRK-HENKAN-ATO (IDH2:1)
                   WHEN    "８"
                       ADD     1           TO  IDH2
                       MOVE    "8"         TO  WRK-HENKAN-ATO (IDH2:1)
                   WHEN    "９"
                       ADD     1           TO  IDH2
                       MOVE    "9"         TO  WRK-HENKAN-ATO (IDH2:1)
                   WHEN    "．"
                       ADD     1           TO  IDH2
                       MOVE    "."         TO  WRK-HENKAN-ATO (IDH2:1)
                   WHEN    "＊"
                       ADD     1           TO  IDH2
                       MOVE    "*"         TO  WRK-HENKAN-ATO (IDH2:1)
                   WHEN    "　"
                       ADD     1           TO  IDH2
                       MOVE    SPACE       TO  WRK-HENKAN-ATO (IDH2:1)
                   WHEN    OTHER
                       ADD     1           TO  IDH2
                       MOVE    WRK-HENKAN-MAE(IDH1:2)
                                           TO  WRK-HENKAN-ATO (IDH2:2)
                       ADD     1           TO  IDH2
               END-EVALUATE
               ADD     2                   TO  IDH1
             ELSE
               ADD     1                   TO  IDH2
               MOVE    WRK-HENKAN-MAE(IDH1:1)
                                           TO  WRK-HENKAN-ATO (IDH2:1)
               ADD     1                   TO  IDH1
             END-IF
           END-PERFORM
           .
       2010-MOJI-HENKAN-EXT.
           EXIT.
      *
      *****************************************************************
      *    入力文字を数字変換処理
      *****************************************************************
       2030-INPUTCD-KENSAKU-SEC        SECTION.
      *
      *    セットの時、時間外がないこと
           IF     (WRK-CD(1:1)         =   "S"  )   AND
                  (FLG-JKNKSN      NOT =   ZERO )
               MOVE    "0010"              TO  SPA-ERRCD
               GO  TO                 2030-INPUTCD-KENSAKU-EXT
           END-IF
      *
           MOVE    SPACE               TO  WRK-ICD-CDSYU
           IF      FLG-KANJI           =   1
      *        全角のコードチェック
               MOVE    "J"             TO  WRK-ICD-CDSYU
           ELSE
      *        診療コードチェック
               IF      WRK-CD(1:1)         =   "P"  OR  "S"
                   MOVE    "6"             TO  WRK-ICD-CDSYU
               ELSE
                   IF      WRK-CD(1:WRK-CD-MCNT)   NUMERIC
                       EVALUATE    WRK-CD-MCNT
                           WHEN    9
      *                    診療行為コード入力
                               MOVE    WRK-CD(1:WRK-CD-MCNT)
                                               TO  LNK-GI4G-INCD
      *                    短縮３桁
                           WHEN    3
                               MOVE    "6"         TO  WRK-ICD-CDSYU
      *                    ４桁
                           WHEN    4
                               MOVE    "4"         TO  WRK-ICD-CDSYU
      *                    ５桁
                           WHEN    5
                               MOVE    "5"         TO  WRK-ICD-CDSYU
      *                    ６桁
                           WHEN    6
                               MOVE    "6"         TO  WRK-ICD-CDSYU
      * 
                           WHEN    OTHER
                               MOVE    "A"         TO  WRK-ICD-CDSYU
                       END-EVALUATE
                   ELSE
                       MOVE    "A"         TO  WRK-ICD-CDSYU
                   END-IF
               END-IF
           END-IF
      *
      *    入力コード存在チェック
           IF     WRK-ICD-CDSYU    NOT =   SPACE
      *
               MOVE    SPACE               TO  INPUTCD-REC
               MOVE    SPA-HOSPID          TO  ICD-HOSPID
               MOVE    WRK-ICD-CDSYU       TO  ICD-CDSYU
               MOVE    WRK-CD              TO  ICD-INPUTCD
      *
               PERFORM 930-INPUTCD-READ-SEC
      *        入力コードなし
               IF      FLG-INPUTCD     NOT =   ZERO
                   IF      WRK-ICD-CDSYU       =   "4" OR "5" OR "6"
                       MOVE    "0050"          TO  SPA-ERRCD
                       GO  TO                  2030-INPUTCD-KENSAKU-EXT
                   END-IF
               END-IF
      *
      *        全桁対象がある時
               IF     (WRK-CD              =  ICD-INPUTCD )  AND
                      (ICD-SRYCD(1:1)      =   "S"  OR  "P")
      *
      *            セットの時、時間外がないこと
                   IF      FLG-JKNKSN      NOT =   ZERO
                       MOVE    "0010"              TO  SPA-ERRCD
                       GO  TO                  2030-INPUTCD-KENSAKU-EXT
                   END-IF
      *
                   MOVE    LNK-GI4G-INPUTCD    TO  WRK-INPUTCD
                   MOVE    ICD-SRYCD(1:6)      TO  LNK-GI4G-INPUTCD
                                                               (1:6)
                   COMPUTE IDY                 =   WRK-CD-MCNT   +  1
                   MOVE    WRK-INPUTCD(IDY:)   TO  LNK-GI4G-INPUTCD
                                                                (7:)
               ELSE
                   IF     (WRK-CD              =   ICD-INPUTCD )
      *                点数コード決定
                       MOVE    ICD-SRYCD           TO  LNK-GI4G-INCD
                   ELSE
      *                一覧表示へ
                       MOVE    "1"                 TO  LNK-GI4G-KENSAKU
                   END-IF
               END-IF
      *
               MOVE    WRK-ICD-CDSYU       TO  LNK-GI4G-CDSYU
      *
           END-IF
      *
           .
       2030-INPUTCD-KENSAKU-EXT.
           EXIT.
      *
      *****************************************************************
      *    入力文字を数字変換処理
      *****************************************************************
       300-SURYOU-SYORI-SEC        SECTION.
      *
      *    数量、回数の調査処理
           PERFORM 2030-MOJI-SET-SEC
      *
           .
       300-SURYOU-SYORI-EXT.
           EXIT.
      *
      *****************************************************************
      *    入力文字を数字変換処理
      *****************************************************************
       2030-MOJI-SET-SEC        SECTION.
      *
           MOVE    ZERO                TO  FLG-AST
           MOVE    ZERO                TO  FLG-SP
           MOVE    ZERO                TO  WRK-SUR-MCNT
           MOVE    ZERO                TO  WRK-ACCT-MCNT
           MOVE    ZERO                TO  WRK-MCNT1
           MOVE    ZERO                TO  WRK-MCNT2
           MOVE    ZERO                TO  WRK-MCNT3
           MOVE    ZERO                TO  WRK-MCNT4
           MOVE    SPACE               TO  WRK-CD
           MOVE    SPACE               TO  WRK-SUR
           MOVE    SPACE               TO  WRK-KAI
           MOVE    SPACE               TO  WRK-MOJI1
           MOVE    SPACE               TO  WRK-MOJI2
           MOVE    SPACE               TO  WRK-MOJI3
           MOVE    SPACE               TO  WRK-MOJI4
      *
      *    コード以外の全角数字を半角とする
           COMPUTE IDX-D               =   WRK-CD-MCNT
                                       +   WRK-IDX-FT
           MOVE    LNK-GI4G-INPUTCD(IDX-D:)
                                       TO  WRK-HENKAN-MAE
           MOVE    1                   TO  WRK-STR
           MOVE    20                  TO  WRK-END
           PERFORM 2010-MOJI-HENKAN-SEC
           MOVE    WRK-HENKAN-ATO      TO  LNK-GI4G-INPUTCD(IDX-D:)
      *
           MOVE    ZERO                TO  WRK-CD-MCNT
           PERFORM VARYING     IDX     FROM    WRK-IDX-FT   BY  1
                   UNTIL       IDX         >   22
      *
               EVALUATE    LNK-GI4G-INPUTCD(IDX:1)
                   WHEN    "*"
                       MOVE    1               TO  FLG-AST
                   WHEN    SPACE
                       ADD     1               TO  FLG-SP
                   WHEN    OTHER
      *---             診療行為コード編集
                       IF     (FLG-SP          =   ZERO )  AND
                              (FLG-AST         =   ZERO )
                           ADD     1               TO  WRK-CD-MCNT
                           MOVE    LNK-GI4G-INPUTCD(IDX:1)
                                                   TO  WRK-CD
                                                       (WRK-CD-MCNT:1)
                       END-IF
      *---             数量編集
                       IF     (FLG-SP          =   1    )  AND
                              (FLG-AST         =   ZERO )
                           ADD     1               TO  WRK-SUR-MCNT
                           MOVE    LNK-GI4G-INPUTCD(IDX:1)
                                                   TO  WRK-SUR
                                                       (WRK-SUR-MCNT:1)
                       END-IF
      *---             回数編集
                       IF      FLG-AST         =   1
                           ADD     1               TO  WRK-ACCT-MCNT
                           MOVE    LNK-GI4G-INPUTCD (IDX:1)
                                                   TO  WRK-KAI
                                                      (WRK-ACCT-MCNT:1)
                       END-IF
      *---             その他値
                       IF     (FLG-SP          =   1    )  AND
                              (FLG-AST         =   0    )
                           ADD     1               TO  WRK-MCNT1
                           MOVE    LNK-GI4G-INPUTCD(IDX:1)
                                                   TO  WRK-MOJI1
                                                          (WRK-MCNT1:1)
                       END-IF
                       IF     (FLG-SP          =   2    )  AND
                              (FLG-AST         =   0    )
                           ADD     1               TO  WRK-MCNT2
                           MOVE    LNK-GI4G-INPUTCD(IDX:1)
                                                   TO  WRK-MOJI2
                                                          (WRK-MCNT2:1)
                       END-IF
                       IF     (FLG-SP          =   3    )  AND
                              (FLG-AST         =   0    )
                           ADD     1               TO  WRK-MCNT3
                           MOVE    LNK-GI4G-INPUTCD(IDX:1)
                                                   TO  WRK-MOJI3
                                                          (WRK-MCNT3:1)
                       END-IF
                       IF     (FLG-SP          =   4    )  AND
                              (FLG-AST         =   0    )
                           ADD     1               TO  WRK-MCNT4
                           MOVE    LNK-GI4G-INPUTCD(IDX:1)
                                                   TO  WRK-MOJI4
                                                          (WRK-MCNT4:1)
                       END-IF
                   END-EVALUATE
           END-PERFORM
      *
      *    数量
           MOVE    ZERO                TO  WRK-SURYO-R
      *    数量　数字変換
           INITIALIZE                      ORCSNUMAREA
           MOVE    WRK-SUR             TO  SNUM-INX
           CALL    "ORCSNUM"           USING
                                       ORCSNUMAREA
           IF     (SNUM-RC         NOT =   ZERO  )   OR
                  (SNUM-MINKBN         =   "1"   )   OR
                  (SNUM-SEISUU         >   7     )   OR
                  (SNUM-SYOSUU         >   3     )
               MOVE    "0012"              TO  SPA-ERRCD
           ELSE
               MOVE    SNUM-SEISUU         TO  LNK-GI4G-SURYOFLG
               MOVE    WRK-SUR-MCNT        TO  LNK-GI4G-SURYOCNT
               MOVE    SNUM-OUTNUM         TO  LNK-GI4G-SURYO
      *
      *        数量にゼロ入力した時、行削除とする
               IF     (WRK-SUR-MCNT        >   ZERO  )  AND
                      (LNK-GI4G-SURYO      =   ZERO  )
                   MOVE    " "             TO  LNK-GI4G-INPUTCD
               END-IF
           END-IF
      *
      *    回数（＊があるが、回数が入力されてない時、＊削除する）
           IF     (FLG-AST             =   1    )  AND
                  (WRK-ACCT-MCNT       =   ZERO )
               INSPECT LNK-GI4G-INPUTCD
                       REPLACING   FIRST "*"  BY  " "
               MOVE    1                   TO  LNK-GI4G-KAISUFLG
           ELSE
               MOVE    ZERO                TO  LNK-GI4G-KAISUFLG
           END-IF
           MOVE    WRK-ACCT-MCNT       TO  LNK-GI4G-KAISUCNT
      *
      *    回数
      *    回数　数字変換
           MOVE    ZERO                TO  LNK-GI4G-KAISU
           INITIALIZE                      ORCSNUMAREA
           MOVE    WRK-KAI             TO  SNUM-INX
           CALL    "ORCSNUM"           USING
                                       ORCSNUMAREA
           IF     (SNUM-RC         NOT =   ZERO  )   OR
                  (SNUM-MINKBN         =   "1"   )   OR
                  (SNUM-SEISUU         >   4     )   OR
                  (SNUM-SYOSUU         >   ZERO  )
               MOVE    "0012"              TO  SPA-ERRCD
           ELSE
               MOVE    SNUM-OUTNUM         TO  LNK-GI4G-KAISU
           END-IF
      *
      *    フィルムの時、分画数
           MOVE    ZERO                TO  WRK-BUNGSU
           IF     (WRK-MCNT2           >   ZERO  )  AND
                  (SPA-COM-INPUTCD(GMN-IDX)(1:1)    NOT =   "8")
      *
               MOVE    ZERO                TO  WRK-BUNGSU
               INITIALIZE                      ORCSNUMAREA
               MOVE    WRK-MOJI2(1:WRK-MCNT2)
                                           TO  SNUM-INX
               CALL    "ORCSNUM"           USING
                                           ORCSNUMAREA
               IF     (SNUM-RC         NOT =   ZERO  )   OR
                      (SNUM-MINKBN         =   "1"   )
                   MOVE    "0012"              TO  SPA-ERRCD
               ELSE
                   MOVE    SNUM-OUTNUM         TO  LNK-GI4G-BUNGSU
               END-IF
               MOVE    SPACE              TO  WRK-MOJI2
               MOVE    ZERO               TO  WRK-MCNT2
           END-IF
      *
      *    コメント値
           IF      WRK-MCNT1          =   ZERO   AND
                   WRK-MCNT2          >   ZERO
               MOVE    WRK-MOJI2          TO  WRK-MOJI1
               MOVE    SPACE              TO  WRK-MOJI2
               MOVE    ZERO               TO  WRK-MCNT2
           END-IF
           IF      WRK-MCNT2          =   ZERO   AND
                   WRK-MCNT3          >   ZERO
               MOVE    WRK-MOJI3          TO  WRK-MOJI2
               MOVE    SPACE              TO  WRK-MOJI3
               MOVE    ZERO               TO  WRK-MCNT3
           END-IF
           IF      WRK-MCNT3          =   ZERO   AND
                   WRK-MCNT4          >   ZERO
               MOVE    WRK-MOJI4          TO  WRK-MOJI3
               MOVE    SPACE              TO  WRK-MOJI4
               MOVE    ZERO               TO  WRK-MCNT4
           END-IF
      *
           MOVE    WRK-MOJI1           TO  LNK-GI4G-MOJI1
           MOVE    WRK-MOJI2           TO  LNK-GI4G-MOJI2
           MOVE    WRK-MOJI3           TO  LNK-GI4G-MOJI3
           MOVE    WRK-MOJI4           TO  LNK-GI4G-MOJI4
           MOVE    WRK-MCNT1           TO  LNK-GI4G-MCNT1
           MOVE    WRK-MCNT2           TO  LNK-GI4G-MCNT2
           MOVE    WRK-MCNT3           TO  LNK-GI4G-MCNT3
           MOVE    WRK-MCNT4           TO  LNK-GI4G-MCNT4
           .
       4101121-MOJI-SET-EXT.
           EXIT.
      *
      *****************************************************************
      *    入力コードマスター読込
      *****************************************************************
       930-INPUTCD-READ-SEC         SECTION.
      *
           IF      WRK-CD-MCNT         =   20
               MOVE    "INPUTCD-KEY2"      TO  WRK-PATH
           ELSE
               MOVE    "INPUTCD-KEY"       TO  WRK-PATH
           END-IF
      *
           MOVE    INPUTCD-REC         TO  MCPDATA-REC
           MOVE    "DBSELECT"          TO  MCP-FUNC
           MOVE    WRK-PATH            TO  ORC-DBPATH
           CALL    "ORCMCPSUB"          USING
                                        MCPAREA
                                        ORCMCPAREA
                                        MCPDATA-REC
           IF      MCP-RC              =   ZERO
               MOVE    "DBFETCH"           TO  MCP-FUNC
               MOVE    WRK-PATH            TO  ORC-DBPATH
               CALL    "ORCMCPSUB"          USING
                                            MCPAREA
                                            ORCMCPAREA
                                            MCPDATA-REC
               IF      MCP-RC              =   ZERO
                   MOVE    MCPDATA-REC         TO  INPUTCD-REC
                   MOVE    ZERO                TO  FLG-INPUTCD
               ELSE
                   INITIALIZE                  INPUTCD-REC
                   MOVE    1                   TO  FLG-INPUTCD
               END-IF
           ELSE
               INITIALIZE                  INPUTCD-REC
               MOVE    1                   TO  FLG-INPUTCD
           END-IF
      *
           MOVE    "DBCLOSE"           TO  MCP-FUNC
           MOVE    WRK-PATH            TO  ORC-DBPATH
           CALL    "ORCMCPSUB"          USING
                                        MCPAREA
                                        ORCMCPAREA
                                        MCPDATA-REC
      *
           .
       920-INPUTCD-NEXT-EXT.
           EXIT.
      *

      *******************************************************************
      * Project code name "ORCA"
      * 日医標準レセプトソフト（JMA standard receipt software）
      * Copyright(C) 2002 JMA (Japan Medical Association)
      *
      * This program is part of "JMA standard receipt software".
      *
      *     This program is distributed in the hope that it will be useful
      * for further advancement in medical care, according to JMA Open
      * Source License, but WITHOUT ANY WARRANTY.
      *     Everyone is granted permission to use, copy, modify and
      * redistribute this program, but only under the conditions described
      * in the JMA Open Source License. You should have received a copy of
      * this license along with this program. If not, stop using this
      * program and contact JMA, 2-28-16 Honkomagome, Bunkyo-ku, Tokyo,
      * 113-8621, Japan.
      ********************************************************************
       IDENTIFICATION          DIVISION.
       PROGRAM-ID.             ORCHCM43V02.
      *****************************************************************
      *  システム名        : ＯＲＣＡ
      *  サブシステム名    : 患者登録
      *  コンポーネント名  : 外来月別請求書兼領収書　印刷（ＨＣＭ４３Ｖ０２）
      *                      自費１０項目対応
      *  管理者            : 
      *  作成日付    作業者        記述
      *  05/07/21    NACL−小豆沢  新規作成
      *****************************************************************
      *  プログラム修正履歴
      * Maj/Min/Rev  修正者       日付      内容
      *****************************************************************
      *
       ENVIRONMENT             DIVISION.
       CONFIGURATION               SECTION.
       INPUT-OUTPUT                SECTION.
       FILE-CONTROL.
      *
       DATA                    DIVISION.
       FILE                        SECTION.
      *
       WORKING-STORAGE             SECTION.
      *
      *    帳票
           COPY    "HCM43V02A.INC"   REPLACING  //HCM43V02A//
                                  BY         //HCM43//.
      *
      *    フラグ領域
       01  STS-AREA.
           03  STS-PARA            PIC X(02).
      *
      *    フラグ領域
       01  FLG-AREA.
           03  FLG-END             PIC 9(01).
           03  FLG-PTINF           PIC 9(01).
           03  FLG-SYUNOU          PIC 9(01).
           03  FLG-HKNCOMBI        PIC 9(01).
           03  FLG-PTRSIINF        PIC 9(01).
           03  FLG-SYSKANRI        PIC 9(01).
           03  FLG-PRINT           PIC 9(01).
      *
      *    添字領域
       01  IDX-AREA.
           03  IDX-JIHI            PIC 9(05).
           03  IDX-SYU             PIC 9(05).
           03  IDX1                PIC 9(05).
           03  IDX2                PIC 9(05).
      *
      *    一時領域
       01  WRK-AREA.
           03  WRK-SRYKA-MAX       PIC 9(05).
           03  WRK-SRYKA-PNT       PIC 9(05).
           03  WRK-SRYYMD          PIC X(08).
      *
           03  WRK-SYMD.
               05  WRK-SYY         PIC 9(04).
               05  WRK-SMM         PIC 9(02).
               05  WRK-SDD         PIC 9(02).
           03  WRK-HENYMDG         PIC X(22).
      *
           03  WRK-HKNCOMBIMEI     PIC X(40).
      *
           03  WRK-MISYUMONEY      PIC S9(11).
           03  WRK-GOKTEN          PIC S9(11).
      *
           03  WRK-DENPNUM         PIC 9(07).
           03  WRK-SRYKA           PIC X(02).
           03  WRK-HKNCOMBI        PIC X(04).
      *
           03  WRK-WARI            PIC 9(02)V9.
           03  WRK-Z6              PIC ZZZ.
           03  WRK-Z62             PIC 9.9.
      *
           03  WRK-NO              PIC Z(7).
           03  WRK-Z72-G.
               05  WRK-FIL2        PIC X(18).
           03  WRK-Z72             PIC S9(07).
           03  WRK-Z72-H           PIC Z,ZZZ,ZZZ.
           03  WRK-Z72-HS          REDEFINES   WRK-Z72-H
                                   PIC -,---,---.
      *    自費集計
           03  WRK-JIHI-REC.
               05  WRK-JIHI        PIC S9(07)   OCCURS  10.
           03  WRK-JIHI-TOTAL      PIC S9(08).
           03  WRK-JIHIMEI-REC.
               05  WRK-JIHIMEI     PIC X(20)   OCCURS  10.
      *
           03  WRK-SRYKA-MEI       PIC X(30).
      *
       01  WKSYUNOU-REC.
           COPY    "CPSYUNOU.INC"     REPLACING //SYU-//
                                      BY        //WKSYU-//.
      *
      *****************************************************************
      *    ファイルレイアウト
      *****************************************************************
      *
           COPY    "CPSYSKANRI.INC".
      *
      *    医療機関情報−基本情報 
           COPY    "CPSK1001.INC".
      *    医療機関情報−所在地、連絡先 
           COPY    "CPSK1002.INC".
      *
           COPY    "CPSK1005.INC".
      *
           COPY    "CPSK1013.INC".
      *
      *    労災医療機関情報
           COPY    "CPSK4001.INC".
      *
      *    患者マスタ
       01  PTINF-REC.
           COPY    "CPPTINF.INC".
      *
      *    収納
       01  SYUNOU-REC.
           COPY    "CPSYUNOU.INC".
      *
      *    保険組合せ
       01  HKNCOMBI-REC.
           COPY    "CPHKNCOMBI.INC".
      *
      *    患者労災保険情報
       01  PTRSIINF-REC.
           COPY    "CPPTRSIINF.INC".
      *
      *****************************************************************
      *    サブプロ用　領域
      *****************************************************************
      *
      *   日付変換サブ
           COPY    "CPORCSDAY.INC".
           COPY    "CPORCSLNK.INC".
      *
      *    全角チェックパラメタ
           COPY    "CPORCSKANACHK.INC".
      *
      *    請求書収納パラメタ
           COPY    "CPORCHCM43S01.INC".
      *
       01  LNKSYU-REC.
         02    LNKSYU-OCC             OCCURS  50.
           COPY    "CPSYUNOU.INC"     REPLACING //SYU-//
                                      BY        //LNKSYU-//.
      *
           COPY    "MCPDATA.INC".
           COPY    "ORCA-DBPATH".
      *
      *
           COPY    "MCPAREA".
      *
      *****************************************************************
      *    連絡　領域
      *****************************************************************
        LINKAGE                     SECTION.
      *
      *    スパ領域
           COPY    "COMMON-SPA".
      *
           COPY    "CPORCSPRT.INC".
      *
      *    請求書パラメタ
           COPY    "CPORCHCM43.INC".
      *
       PROCEDURE                   DIVISION    USING
           SPA-AREA
           ORCSPRTAREA
           ORCHCM43AREA
           .
      *
      *****************************************************************
      *    主　　処理
      *****************************************************************
       000-PROC-SEC                SECTION.
      *
           INITIALIZE                  FLG-AREA
           INITIALIZE                  STS-AREA
           INITIALIZE                  WRK-AREA
      *
      *    各ＤＢ読込
           PERFORM 100-SYORI-SEC
      *
           PERFORM 200-MAIN-SEC
      *
           .
       000-PROC-EXT.
           EXIT    PROGRAM
           .
      *
      *****************************************************************
      *    初期編集処理
      *****************************************************************
       100-SYORI-SEC                SECTION.
      *
           MOVE    ORCHCM43-STSRYYM    TO  WRK-SRYYMD
           MOVE    "01"                TO  WRK-SRYYMD (7:)
      *
      *    医療機関情報−所在地、連絡先
           INITIALIZE                      SYS-1001-REC
           MOVE    "1001"              TO  SYS-1001-KANRICD
           MOVE    "*"                 TO  SYS-1001-KBNCD
           MOVE    WRK-SRYYMD          TO  SYS-1001-STYUKYMD
                                           SYS-1001-EDYUKYMD
           MOVE    SYS-1001-REC        TO  MCPDATA-REC
           PERFORM 900-SYSKANRI-KEY10-SEL-SEC
           MOVE    SYSKANRI-REC        TO  SYS-1001-REC
      *
      *    医療機関情報−所在地、連絡先
           INITIALIZE                      SYS-1002-REC
           MOVE    "1002"              TO  SYS-1002-KANRICD
           MOVE    "*"                 TO  SYS-1002-KBNCD
           MOVE    WRK-SRYYMD          TO  SYS-1002-STYUKYMD
                                           SYS-1002-EDYUKYMD
           MOVE    SYS-1002-REC        TO  MCPDATA-REC
           PERFORM 900-SYSKANRI-KEY10-SEL-SEC
           MOVE    SYSKANRI-REC        TO  SYS-1002-REC
      *
      *    労災医療機関情報編集
           INITIALIZE                      SYS-4001-REC
           MOVE    "4001"              TO  SYS-4001-KANRICD
           MOVE    "*"                 TO  SYS-4001-KBNCD
           MOVE    WRK-SRYYMD          TO  SYS-4001-STYUKYMD
                                           SYS-4001-EDYUKYMD
           MOVE    SYS-4001-REC        TO  MCPDATA-REC
           PERFORM 900-SYSKANRI-KEY10-SEL-SEC
           MOVE    SYSKANRI-REC        TO  SYS-4001-REC
      *
      *    自費名称編集
           PERFORM 1002-JIHIMSG-HEN-SEC
      *
      *    患者情報
           PERFORM 900-PTINF-KEY-SEC
      *
           .
       100-SYORI-EXT.
           EXIT.
      *****************************************************************
      *    自費名称編集処理
      *****************************************************************
       1002-JIHIMSG-HEN-SEC             SECTION.
      *
           INITIALIZE                      SYS-1013-REC
           MOVE    "1013"              TO  SYS-1013-KANRICD
           MOVE    WRK-SRYYMD          TO  SYS-1013-STYUKYMD
                                           SYS-1013-EDYUKYMD
           MOVE    SYS-1013-REC        TO  MCPDATA-REC
           PERFORM 900-SYSKANRI-KEY2-SEL-SEC
      *
           INITIALIZE                  WRK-JIHIMEI-REC
      *
           PERFORM UNTIL ( FLG-SYSKANRI    =   1 )
      *
               MOVE    MCPDATA-REC         TO  SYS-1013-REC
      *
               IF    ( SPA-NYUGAIKBN       =   SYS-1013-KBNCD(1:1) )
                   EVALUATE    SYS-1013-KBNCD(2:1)
                   WHEN    "1"
                       MOVE    SYS-1013-JIHINAME   TO  WRK-JIHIMEI  (1)
                   WHEN    "2"
                       MOVE    SYS-1013-JIHINAME   TO  WRK-JIHIMEI  (2)
                   WHEN    "3"
                       MOVE    SYS-1013-JIHINAME   TO  WRK-JIHIMEI  (3)
                   WHEN    "4"
                       MOVE    SYS-1013-JIHINAME   TO  WRK-JIHIMEI  (4)
                   WHEN    "5"
                       MOVE    SYS-1013-JIHINAME   TO  WRK-JIHIMEI  (5)
                   WHEN    "6"
                       MOVE    SYS-1013-JIHINAME   TO  WRK-JIHIMEI  (6)
                   WHEN    "7"
                       MOVE    SYS-1013-JIHINAME   TO  WRK-JIHIMEI  (7)
                   WHEN    "8"
                       MOVE    SYS-1013-JIHINAME   TO  WRK-JIHIMEI  (8)
                   WHEN    "9"
                       MOVE    SYS-1013-JIHINAME   TO  WRK-JIHIMEI  (9)
                   WHEN    "0"
                       MOVE    SYS-1013-JIHINAME   TO  WRK-JIHIMEI (10)
                   END-EVALUATE
               END-IF
      *
               PERFORM 900-SYSKANRI-KEY2-FET-SEC
      *
           END-PERFORM
      *
           MOVE    PATH-TBL-SYSKANRI-KEY2
                                       TO  MCP-PATH
           PERFORM 910-DBCLOSECURSOR-SEC
      *
      *
           .
       1002-JIHIMSG-HEN-EXT.
           EXIT.
      *****************************************************************
      *   主処理
      *****************************************************************
       200-MAIN-SEC                    SECTION.
      *
           INITIALIZE                  HCM43S01-AREA
      *
      *    収納マスタまとめ計算
           CALL    "ORCHCM43S02"   USING
                                   SPA-AREA
                                   ORCHCM43AREA
                                   HCM43S01-AREA
                                   LNKSYU-REC
      *
           PERFORM VARYING IDX-SYU     FROM    1   BY  1
                   UNTIL ( IDX-SYU     >   HCM43S01-GRP-MAX )
      *
               MOVE    LNKSYU-OCC (IDX-SYU)
                                       TO  SYUNOU-REC
      *
               MOVE    ZERO            TO  FLG-PRINT
               IF    ( ORCHCM43-ZEROSKYFLG =   "1" )
                   IF    ( SYU-SKYMONEY    >   ZERO )
                       MOVE    1       TO  FLG-PRINT
                   END-IF
               ELSE
                       MOVE    1       TO  FLG-PRINT
               END-IF
      *
               IF    ( FLG-PRINT       =   1 )
      *            診療科名編集
                   MOVE    1           TO  WRK-SRYKA-PNT
                   MOVE    SPACE       TO  WRK-SRYKA-MEI
                   PERFORM 2021-GRP-SRYKAMEI-HEN-SEC
      *
      *            保険組合せ
                   IF    ( HCM43S01-GRP-HKNCOMBI-MAX (IDX-SYU)
                                       >       1 )
                       MOVE    "複数保険使用"  TO  WRK-HKNCOMBIMEI
                       MOVE    ZERO            TO  SYU-PTFTNRATE
                   ELSE
                       MOVE    HCM43S01-GRP-HKNCOMBI (IDX-SYU 1)
                                               TO  SYU-HKNCOMBINUM
                       MOVE    HCM43S01-GRP-PTFTNRATE (IDX-SYU 1)
                                               TO  SYU-PTFTNRATE
                       PERFORM 1001-HKNCOMBI-HEN-SEC
                   END-IF
      *
      *            帳票編集処理
                   PERFORM 300-HCM43-HEN-SEC
      *
      *            帳票印刷処理
                   PERFORM 400-PRINT-SEC
               END-IF
      *
           END-PERFORM
      *
           .
       200-MAIN-EXT.
           EXIT.
      *****************************************************************
      *    保険組合せ名称編集処理
      *****************************************************************
       1001-HKNCOMBI-HEN-SEC             SECTION.
      *
      *    保険組合せマスター検索
           INITIALIZE                      HKNCOMBI-REC
           MOVE    SPA-HOSPID          TO  COMB-HOSPID
           MOVE    SPA-PTID            TO  COMB-PTID
           MOVE    SYU-HKNCOMBINUM     TO  COMB-HKNCOMBINUM
           PERFORM 900-HKNCOMBI-KEY-SEL-SEC
           IF    ( FLG-HKNCOMBI        =   1 )
                DISPLAY "*** HKNCOMBI  ERR:"   COMB-KEY "."
           END-IF
      *
           PERFORM 10011-HKNCOMBIMEI-HEN-SEC
      *
           IF    ( COMB-HKNNUM         =   "971" OR "973" )
      *
      *        患者労災保険情報検索
               INITIALIZE                      PTRSIINF-REC
               MOVE    SPA-HOSPID          TO  PTRSI-HOSPID
               MOVE    SPA-PTID            TO  PTRSI-PTID
               MOVE    COMB-HKNID          TO  PTRSI-HKNID
               PERFORM 900-PTRSIINF-KEY-SEC
           ELSE
               INITIALIZE                      PTRSIINF-REC
           END-IF
      *
           .
       1001-HKNCOMBI-HEN-EXT.
           EXIT.
      *****************************************************************
      *    保険組合せ名称編集処理
      *****************************************************************
       10011-HKNCOMBIMEI-HEN-SEC             SECTION.
      *
           MOVE    SPACE               TO  WRK-HKNCOMBIMEI
      *
           STRING  COMB-SYU-TANSEIDONAME   DELIMITED  BY  SPACE
                   "　"                    DELIMITED  BY  SIZE
                   COMB-KOH1-TANSEIDONAME  DELIMITED  BY  SPACE
                   "　"                    DELIMITED  BY  SIZE
                   COMB-KOH2-TANSEIDONAME  DELIMITED  BY  SPACE
                   "　"                    DELIMITED  BY  SIZE
                   COMB-KOH3-TANSEIDONAME  DELIMITED  BY  SPACE
                   "　"                    DELIMITED  BY  SIZE
                   COMB-KOH4-TANSEIDONAME  DELIMITED  BY  SPACE
                         INTO              WRK-HKNCOMBIMEI
           END-STRING
      *
           .
       10011-HKNCOMBIMEI-HEN-EXT.
           EXIT.
      *
      *****************************************************************
      *    診療科名編集処理
      *****************************************************************
       1000-SRYKAMEI-HEN-SEC                SECTION.
      *
      *    診療科
           INITIALIZE                      SYS-1005-REC
           MOVE    "1005"              TO  SYS-1005-KANRICD
           MOVE    WRK-SRYKA           TO  SYS-1005-KBNCD
           MOVE    WRK-SRYYMD          TO  SYS-1005-STYUKYMD
                                           SYS-1005-EDYUKYMD
           MOVE    SYS-1005-REC        TO  MCPDATA-REC
           PERFORM 900-SYSKANRI-KEY10-SEL-SEC
      *
           IF    ( FLG-SYSKANRI        =   ZERO )
      *
               MOVE    MCPDATA-REC     TO  SYS-1005-REC
               IF    ( WRK-SRYKA-MEI   =   SPACE )
                   STRING  SYS-1005-SRYKANAME2
                                               DELIMITED  BY  SPACE
                   INTO            WRK-SRYKA-MEI
                   WITH    POINTER WRK-SRYKA-PNT
                   END-STRING
               ELSE
                   IF    ( WRK-SRYKA-MEI(19:2) NOT =   SPACE )
                       CONTINUE
                   ELSE
                       STRING  "、"            DELIMITED  BY  SIZE
                               SYS-1005-SRYKANAME2
                                               DELIMITED  BY  SPACE
                       INTO            WRK-SRYKA-MEI
                       WITH    POINTER WRK-SRYKA-PNT
                       END-STRING
                   END-IF
               END-IF
           END-IF
      *
           .
       1000-SRYKAMEI-HEN-EXT.
           EXIT.
      *
      *****************************************************************
      *    帳票編集処理
      *****************************************************************
       300-HCM43-HEN-SEC               SECTION.
      *
           INITIALIZE                  HCM43
      *
      *    伝票番号
      *    MOVE    WRK-DENPNUM         TO  WRK-NO
      *    MOVE    WRK-NO              TO  HCM43-NO
      *
      *    診療科
           MOVE    WRK-SRYKA-MEI(1:20) TO  HCM43-MIDASI
      *
      *    患者番号
           MOVE    SPA-PTNUM           TO  HCM43-KNJNO
      *
      *    患者名称
           MOVE    PTINF-NAME          TO  HCM43-KNJNAMW
      *
      *    保険種別
           MOVE    WRK-HKNCOMBIMEI     TO  HCM43-HOKEN
      *
      *    負担割合
           IF    ( SYU-PTFTNRATE   >   ZERO )
               COMPUTE WRK-WARI    =   SYU-PTFTNRATE   /   10
               IF    ( SYU-PTFTNRATE(3:1)  =   ZERO )
                   MOVE    WRK-WARI    TO  WRK-Z6
                   MOVE    WRK-Z6      TO  HCM43-FUTAN
               ELSE
                   MOVE    WRK-WARI    TO  WRK-Z62
                   MOVE    WRK-Z62     TO  HCM43-FUTAN
               END-IF
           END-IF
      *
           MOVE    "診療年月"          TO  HCM43-LBLKKN
      *
      *    診療年月
           MOVE    WRK-SRYYMD          TO  WRK-SYMD
           PERFORM 31012-SEIWA-HEN-SEC
           MOVE    WRK-HENYMDG(1:16)   TO  HCM43-SRYYM
      *
      *    発行日
           MOVE    ORCHCM43-HAKYMD     TO  WRK-SYMD
           PERFORM 31012-SEIWA-HEN-SEC
           MOVE    WRK-HENYMDG         TO  HCM43-HAKYMD
      *
      *    医療機関住所
           MOVE    SYS-1002-ADRS       TO  HCM43-IRYOADRS
      *    医療機関名
           MOVE    SYS-1001-HOSPNAME   TO  HCM43-IRYOMEI
      *    医療機関ＴＥＬ
           MOVE    SYS-1002-TEL        TO  HCM43-IRYOTEL
      *
      *    自費名称編集
           PERFORM 3102-JIHIMSG-HEN-SEC
      *
           MOVE    SPACE               TO  WRK-Z72-G
      *
      *    診療科
           MOVE    SYU-HKNTEN (01)     TO  WRK-Z72
           PERFORM 800-HENKAN-SEC
           MOVE    WRK-Z72-G           TO  HCM43-HKNRYO1
      *
      *    指導科
           MOVE    SYU-HKNTEN (02)     TO  WRK-Z72
           PERFORM 800-HENKAN-SEC
           MOVE    WRK-Z72-G           TO  HCM43-HKNRYO2
      *
      *    在宅科
           MOVE    SYU-HKNTEN (03)     TO  WRK-Z72
           PERFORM 800-HENKAN-SEC
           MOVE    WRK-Z72-G           TO  HCM43-HKNRYO3
      *
      *    投薬科
           MOVE    SYU-HKNTEN (04)     TO  WRK-Z72
           PERFORM 800-HENKAN-SEC
           MOVE    WRK-Z72-G           TO  HCM43-HKNRYO4
      *
      *    注射科
           MOVE    SYU-HKNTEN (05)     TO  WRK-Z72
           PERFORM 800-HENKAN-SEC
           MOVE    WRK-Z72-G           TO  HCM43-HKNRYO5
      *
      *    処置科
           MOVE    SYU-HKNTEN (06)     TO  WRK-Z72
           PERFORM 800-HENKAN-SEC
           MOVE    WRK-Z72-G           TO  HCM43-HKNRYO6
      *
      *    手術科
           MOVE    SYU-HKNTEN (07)     TO  WRK-Z72
           PERFORM 800-HENKAN-SEC
           MOVE    WRK-Z72-G           TO  HCM43-HKNRYO7
      *
      *    麻酔科
           MOVE    SYU-HKNTEN (08)     TO  WRK-Z72
           PERFORM 800-HENKAN-SEC
           MOVE    WRK-Z72-G           TO  HCM43-HKNRYO8
      *
      *    検査科
           MOVE    SYU-HKNTEN (09)     TO  WRK-Z72
           PERFORM 800-HENKAN-SEC
           MOVE    WRK-Z72-G           TO  HCM43-HKNRYO9
      *
      *    Ｘ線科
           MOVE    SYU-HKNTEN (10)     TO  WRK-Z72
           PERFORM 800-HENKAN-SEC
           MOVE    WRK-Z72-G           TO  HCM43-HKNRYO10
      *
      *    リハビリ
           MOVE    SYU-HKNTEN (11)     TO  WRK-Z72
           PERFORM 800-HENKAN-SEC
           MOVE    WRK-Z72-G           TO  HCM43-HKNRYO11
      *
      *    精神専門
           MOVE    SYU-HKNTEN (12)     TO  WRK-Z72
           PERFORM 800-HENKAN-SEC
           MOVE    WRK-Z72-G           TO  HCM43-HKNRYO12
      *
      *    放射線治療
           MOVE    SYU-HKNTEN (13)     TO  WRK-Z72
           PERFORM 800-HENKAN-SEC
           MOVE    WRK-Z72-G           TO  HCM43-HKNRYO13
      *
      *    入院料
           MOVE    SYU-HKNTEN (14)     TO  WRK-Z72
           PERFORM 800-HENKAN-SEC
           MOVE    WRK-Z72-G           TO  HCM43-HKNRYO14
      *
      *    療養担当手当
           MOVE    SYU-HKNTEN (15)     TO  WRK-Z72
           PERFORM 800-HENKAN-SEC
           MOVE    WRK-Z72-G           TO  HCM43-HKNRYO15
      *
           MOVE    SYU-TOTAL-HKNTEN    TO  WRK-Z72
           PERFORM 800-HENKAN-SEC
           MOVE    WRK-Z72-G           TO  HCM43-GOKTEN
      *
      *    労災・自賠で患者請求が無い場合、点数表示なし
           IF    ( COMB-HKNNUM             =    "971"  AND
                   SYS-4001-RSIIRYOKBN     =    "1" )  OR
                 ( COMB-HKNNUM             =    "973"  AND
                   SYU-PTFTNRATE           =    ZERO )
               MOVE    SPACE               TO  HCM43-HKNRYO1
               MOVE    SPACE               TO  HCM43-HKNRYO2
               MOVE    SPACE               TO  HCM43-HKNRYO3
               MOVE    SPACE               TO  HCM43-HKNRYO4
               MOVE    SPACE               TO  HCM43-HKNRYO5
               MOVE    SPACE               TO  HCM43-HKNRYO6
               MOVE    SPACE               TO  HCM43-HKNRYO7
               MOVE    SPACE               TO  HCM43-HKNRYO8
               MOVE    SPACE               TO  HCM43-HKNRYO9
               MOVE    SPACE               TO  HCM43-HKNRYO10
               MOVE    SPACE               TO  HCM43-HKNRYO11
               MOVE    SPACE               TO  HCM43-HKNRYO12
               MOVE    SPACE               TO  HCM43-HKNRYO13
               MOVE    SPACE               TO  HCM43-HKNRYO14
               MOVE    SPACE               TO  HCM43-HKNRYO15
               MOVE    ALL "*"             TO  HCM43-GOKTEN
           END-IF
      *
      *    保険適用分
           MOVE    SYU-HKNTEKMONEY     TO  WRK-Z72
           PERFORM 800-HENKAN-SEC
           MOVE    WRK-Z72-G           TO  HCM43-HOKENBUN
      *
      *    保険適用外編集
           PERFORM VARYING IDX-JIHI    FROM    1   BY  1
                   UNTIL ( IDX-JIHI >  15 )
      *
               MOVE    ZERO      TO    WRK-Z72
               COMPUTE WRK-Z72   = SYU-TGMONEY(IDX-JIHI)  +
                                   SYU-TGMONEY-TAX(IDX-JIHI)
               PERFORM 800-HENKAN-SEC
               MOVE    WRK-Z72-G TO    HCM43-JIHIBUN(IDX-JIHI)
      *
           END-PERFORM
      *
      *    保険適用外消費税（再掲）
           MOVE    SYU-TGMONEY-TAX-SAI TO  WRK-Z72
           PERFORM 800-HENKAN-SEC
           MOVE    WRK-Z72-G           TO  HCM43-HOKENZEI
      *
           IF    ( COMB-HKNNUM             =    "971"
                                           OR   "973" )
      *        労災・自賠の時、点数表示なし
               EVALUATE    TRUE
               WHEN      ( COMB-HKNNUM             =    "971" )
                   AND   ( SYS-4001-RSIIRYOKBN NOT =   "1" )
      *
               WHEN      ( COMB-HKNNUM             =    "973" )
                   AND   ( PTRSI-HKNKBN        =    "4" ) 
                   AND   ( SYU-PTFTNRATE   NOT =   ZERO  )
      *
                   COMPUTE WRK-GOKTEN      =    SYU-HKNTEKMONEY
                                           +    SYU-RSISHOSHIN-MONEY
                                           +    SYU-RSISAISHIN-MONEY
                                           +    SYU-RSISDO-MONEY
                                           +    SYU-RSIETC-MONEY
                   MOVE    WRK-GOKTEN      TO  WRK-Z72
                   PERFORM 800-HENKAN-SEC
                   MOVE    WRK-Z72-G       TO  HCM43-HOKENBUN
               WHEN    OTHER
                   MOVE    SPACE           TO  HCM43-HOKENBUN
               END-EVALUATE
           END-IF
      *
      *    労災・自賠の診察料等の記載
           IF   (SYU-RSI-TOTAL     NOT =   ZERO) AND
                (SYS-4001-RSIPRTKBN    =   "2")
               EVALUATE    COMB-HKNNUM
                   WHEN    "971"
                       MOVE  "（内労災診察等）"  TO HCM43-RSIMIDASI
                   WHEN    "973"
                       MOVE  "（内自賠診察等）"  TO HCM43-RSIMIDASI
               END-EVALUATE
               MOVE    SYU-RSI-TOTAL          TO  WRK-Z72
               PERFORM 800-HENKAN-SEC
               MOVE    WRK-Z72-G              TO  HCM43-RSIBUN
               MOVE    "円"                   TO  HCM43-RSIEN
           ELSE
               MOVE    SPACE                  TO  HCM43-RSIMIDASI
               MOVE    SPACE                  TO  HCM43-RSIBUN
               MOVE    SPACE                  TO  HCM43-RSIEN
           END-IF
      *
      *    保険適用外分
           MOVE    ZERO                TO  WRK-GOKTEN
           COMPUTE WRK-GOKTEN          =   SYU-TOTAL-TGMONEY
                                       +   SYU-TOTAL-TGMONEY-TAX
           MOVE    WRK-GOKTEN          TO  WRK-Z72
           PERFORM 800-HENKAN-SEC
           MOVE    WRK-Z72-G           TO  HCM43-HOKENGAI
      *
      *    自費１から１０
           MOVE    ZERO                TO  WRK-JIHI-REC
           PERFORM VARYING IDX2     FROM    1   BY  1
                   UNTIL ( IDX2     >  10 )
               COMPUTE WRK-JIHI (IDX2)     =   SYU-JIHI-TAXNASI (IDX2)
                                           +   SYU-JIHI-TAXARI  (IDX2)
               MOVE    WRK-JIHI (IDX2)  TO  WRK-Z72
               PERFORM 800-HENKAN-SEC
               MOVE    WRK-Z72-G       TO  HCM43-JIHI(IDX2)
           END-PERFORM
      *
      *    自費計
           COMPUTE WRK-JIHI-TOTAL      =   SYU-JIHI-TOTAL
                                       +   SYU-JIHI-TOTAL-TAX
      *
      *    自費消費税
           MOVE    SYU-JIHI-TAX        TO  WRK-Z72
           PERFORM 800-HENKAN-SEC
           MOVE    WRK-Z72-G           TO  HCM43-JIHI(12)
      *
      *    自費合計
           COMPUTE WRK-GOKTEN          =   WRK-JIHI-TOTAL
                                       +   SYU-JIHI-TAX
           MOVE    WRK-GOKTEN          TO  WRK-Z72
           PERFORM 800-HENKAN-SEC
           MOVE    WRK-Z72-G           TO  HCM43-JIHI(11)
      *
      *    薬剤一部負担
           MOVE    SYU-YKZ-FTN         TO  WRK-Z72
           PERFORM 800-HENKAN-SEC
           MOVE    WRK-Z72-G           TO  HCM43-YAKUZAI
      *
      *    老人一部負担金
           MOVE    SYU-RJN-FTN         TO  WRK-Z72
           PERFORM 800-HENKAN-SEC
           MOVE    WRK-Z72-G           TO  HCM43-ROUJIN
      *
      *    公費一部負担金
           MOVE    SYU-KOH-FTN         TO  WRK-Z72
           PERFORM 800-HENKAN-SEC
           MOVE    WRK-Z72-G           TO  HCM43-KOUHI
      *
      *    調整金
           MOVE    SYU-CHOSEI          TO  WRK-Z72
           PERFORM 800-HENKAN-SEC
           MOVE    WRK-Z72-G           TO  HCM43-CHOSEI
      *
      *    今回請求額
           MOVE    SYU-SKYMONEY        TO  WRK-Z72
           PERFORM 800-HENKAN-SEC
           MOVE    WRK-Z72-G           TO  HCM43-KONSEIKYU
      *
      *    今回請求額消費税（再掲）
           MOVE    SYU-SKYMONEY-TAX-SAI
                                       TO  WRK-Z72
           PERFORM 800-HENKAN-SEC
           MOVE    WRK-Z72-G           TO  HCM43-SEIKYUZEI
      *
      *    今回入金額
           MOVE    SYU-NYUKIN-TOTAL    TO  WRK-Z72
           PERFORM 800-HENKAN-SEC
           MOVE    WRK-Z72-G           TO  HCM43-KONNYUKIN
      *
      *    前回未収額計算
           MOVE    ZERO                TO  WRK-MISYUMONEY
           IF    ( IDX-SYU =   1 )
               PERFORM 3001-MISYU-SEC
           END-IF
      *
           MOVE    WRK-MISYUMONEY      TO  WRK-Z72
           PERFORM 800-HENKAN-SEC
           MOVE    WRK-Z72-G           TO  HCM43-ZENMISYU
      *
      *    合計請求額
           COMPUTE WRK-GOKTEN          =   SYU-SKYMONEY
                                       +   WRK-MISYUMONEY
           MOVE    WRK-GOKTEN          TO  WRK-Z72
           PERFORM 800-HENKAN-SEC
           MOVE    WRK-Z72-G           TO  HCM43-GOUKEIGAK
      *
      *    見出し
           PERFORM 3001-MEISAI-HEN-SEC
      *
           .
       300-HCM43-HEN-EXT.
           EXIT.
      *
      *****************************************************************
      *   明細見出し編集処理
      *****************************************************************
       3001-MEISAI-HEN-SEC                SECTION.
      *
           MOVE    "初 ・ 再 診 料"
                                       TO  HCM43-MEISAI (1)
           MOVE    "医 学 管 理 等"
                                       TO  HCM43-MEISAI (2)
           MOVE    "在　宅　医　療"
                                       TO  HCM43-MEISAI (3)
           MOVE    "投　　　　　薬"
                                       TO  HCM43-MEISAI (4)
           MOVE    "注　　　　　射"
                                       TO  HCM43-MEISAI (5)
           MOVE    "処　　　　　置"
                                       TO  HCM43-MEISAI (6)
           MOVE    "手　　　　　術"
                                       TO  HCM43-MEISAI (7)
           MOVE    "麻　　　　　酔"
                                       TO  HCM43-MEISAI (8)
           MOVE    "検　　　　　査"
                                       TO  HCM43-MEISAI (9)
           MOVE    "画　像　診　断"
                                       TO  HCM43-MEISAI (10)
           MOVE    "リハビリテーション"
                                       TO  HCM43-MEISAI (11)
           MOVE    "精神科専門療法"
                                       TO  HCM43-MEISAI (12)
           MOVE    "放 射 線 治 療"
                                       TO  HCM43-MEISAI (13)
           MOVE    "入　院　料　等"
                                       TO  HCM43-MEISAI (14)
           MOVE    "療養担当手当"
                                       TO  HCM43-MEISAI (15)
      *    明細削除
      *    療養担当手当て
           IF    ( SYU-HKNTEN (15)     =   ZERO )
               MOVE    SPACE               TO  HCM43-MEISAI (15)
           END-IF
      *
           .
       3001-MEISAI-HEN-EXT.
           EXIT.
      *****************************************************************
      *    前回未収編集処理
      *****************************************************************
       3001-MISYU-SEC                  SECTION.
      *
           PERFORM 900-SYUNOU-KEY38-SEL-SEC
      *
           COMPUTE WRK-MISYUMONEY
               =   WKSYU-SKYMONEY  -   WKSYU-NYUKIN-TOTAL
      *
           .
       3001-MISYU-EXT.
           EXIT.
      *****************************************************************
      *    帳票印刷処理
      *****************************************************************
       400-PRINT-SEC                SECTION.
      *
           MOVE    SPA-PTID            TO  SPRT-PTID
           MOVE    "HCM43V02A.red"     TO  SPRT-PRTID
           MOVE    HCM43               TO  SPRT-PRTDATA
           MOVE    "1"                 TO  SPRT-SITEKBN
           CALL    "ORCSPRT"           USING
                                       ORCSPRTAREA
           IF    ( SPRT-RETURN         =   ZERO )
               CONTINUE
           ELSE
               DISPLAY "ORCSPRT ERR !!! PTID = " SPA-PTID
           END-IF
      *
           COMPUTE ORCHCM43-PAGE   =   ORCHCM43-PAGE   +   1
      *
           .
       400-PRINT-EXT.
           EXIT.
      *
      *****************************************************************
      *    複数診療科編集処理
      *****************************************************************
       2021-GRP-SRYKAMEI-HEN-SEC             SECTION.
      *
           MOVE    HCM43S01-GRP-SRYKA-MAX (IDX-SYU)
                                       TO  WRK-SRYKA-MAX
      *
           PERFORM VARYING     IDX1    FROM    1   BY  1
                   UNTIL     ( IDX1    >   WRK-SRYKA-MAX )
      *
               MOVE    HCM43S01-GRP-SRYKA (IDX-SYU IDX1)
                                       TO  WRK-SRYKA
               PERFORM 1000-SRYKAMEI-HEN-SEC
      *
           END-PERFORM
      *
           .
       2021-GRP-SRYKAMEI-HEN-EXT.
           EXIT.
      *
      *****************************************************************
      *    半角全角変換処理
      *****************************************************************
       800-HENKAN-SEC             SECTION.
     *
           INITIALIZE                  ORCSKANACHKAREA
           MOVE    "2"                 TO  KANACHK-SYORI
      *****MOVE    WRK-Z72             TO  KANACHK-MAE-INPUT
           IF      WRK-Z72             <   ZERO
               MOVE    WRK-Z72             TO  WRK-Z72-HS
               MOVE    WRK-Z72-HS          TO  KANACHK-MAE-INPUT
           ELSE
               MOVE    WRK-Z72             TO  WRK-Z72-H
               MOVE    WRK-Z72-H           TO  KANACHK-MAE-INPUT
           END-IF
           CALL    "ORCSKANACHK"       USING
                                       ORCSKANACHKAREA
           IF      KANACHK-RC          =   ZERO
               MOVE    KANACHK-OUT-INPUT(1:18)    TO  WRK-Z72-G
           END-IF
           .
       800-HENKAN-EXT.
           EXIT.
      *
      *****************************************************************
      *    自費名称編集処理
      *****************************************************************
       3102-JIHIMSG-HEN-SEC             SECTION.
      *
           MOVE    WRK-JIHIMEI(1)      TO  HCM43-JIHIMSG1
           MOVE    WRK-JIHIMEI(2)      TO  HCM43-JIHIMSG2
           MOVE    WRK-JIHIMEI(3)      TO  HCM43-JIHIMSG3
           MOVE    WRK-JIHIMEI(4)      TO  HCM43-JIHIMSG4
           MOVE    WRK-JIHIMEI(5)      TO  HCM43-JIHIMSG5
           MOVE    WRK-JIHIMEI(6)      TO  HCM43-JIHIMSG6
           MOVE    WRK-JIHIMEI(7)      TO  HCM43-JIHIMSG7
           MOVE    WRK-JIHIMEI(8)      TO  HCM43-JIHIMSG8
           MOVE    WRK-JIHIMEI(9)      TO  HCM43-JIHIMSG9
           MOVE    WRK-JIHIMEI(10)     TO  HCM43-JIHIMSG10
      *
           .
       3102-JIHIMSG-HEN-EXT.
           EXIT.
      *****************************************************************
      *    西暦日本語変換処理
      *****************************************************************
       31012-SEIWA-HEN-SEC        SECTION.
      *
           INITIALIZE                      STS-AREA-DAY
           INITIALIZE                      LNK-DAY2-AREA
           MOVE    "21"                TO  LNK-DAY2-IRAI
           MOVE    WRK-SYMD            TO  LNK-DAY2-YMD
           CALL    "ORCSDAY"           USING   STS-AREA-DAY
                                               LNK-DAY2-AREA
           MOVE    LNK-DAY2-EDTYMD3    TO  WRK-HENYMDG
           .
       31012-SEIWA-HEN-EXT.
           EXIT.
      *
      *****************************************************************
      *    終了　　処理
      *****************************************************************
       300-END-SEC                 SECTION.
      *
           .
       300-END-EXT.
           EXIT.
      *
      *****************************************************************
      *    システム管理マスタ検索処理(KEY2)
      *****************************************************************
       900-SYSKANRI-KEY2-SEL-SEC      SECTION.
      *
           MOVE    ZERO                TO  FLG-SYSKANRI
      *
           MOVE    PATH-TBL-SYSKANRI-KEY2
                                       TO  MCP-PATH
           PERFORM 910-DBSELECT-SEC
           IF    ( MCP-RC              =   ZERO )
               MOVE    MCPDATA-REC     TO  SYSKANRI-REC
           ELSE
               MOVE    1               TO  FLG-SYSKANRI
               INITIALIZE                  FLG-SYSKANRI
           END-IF
      *
           .
       900-SYSKANRI-KEY2-SEL-EXT.
           EXIT.
      *****************************************************************
      *    システム管理マスタ検索処理(KEY2)
      *****************************************************************
       900-SYSKANRI-KEY2-FET-SEC      SECTION.
      *
           MOVE    PATH-TBL-SYSKANRI-KEY2
                                       TO  MCP-PATH
           PERFORM 910-DBFETCH-SEC
           IF    ( MCP-RC              =   ZERO )
               MOVE    MCPDATA-REC     TO  SYSKANRI-REC
           ELSE
               MOVE    1               TO  FLG-SYSKANRI
               INITIALIZE                  SYSKANRI-REC
           END-IF
      *
           .
       900-SYSKANRI-KEY2-FET-EXT.
           EXIT.
      *****************************************************************
      *    システム管理マスタ検索処理(KEY10)
      *****************************************************************
       900-SYSKANRI-KEY10-SEL-SEC      SECTION.
      *
           MOVE    ZERO                TO  FLG-SYSKANRI
      *
           MOVE    PATH-TBL-SYSKANRI-KEY10
                                       TO  MCP-PATH
           PERFORM 910-DBSELECT-SEC
           IF    ( MCP-RC              =   ZERO )
               MOVE    MCPDATA-REC     TO  SYSKANRI-REC
           ELSE
               MOVE    1               TO  FLG-SYSKANRI
               INITIALIZE                  SYSKANRI-REC
           END-IF
      *
           MOVE    PATH-TBL-SYSKANRI-KEY10
                                       TO  MCP-PATH
           PERFORM 910-DBCLOSECURSOR-SEC
      *
           .
       900-SYSKANRI-KEY10-SEL-EXT.
           EXIT.
      *****************************************************************
      *    保険組合せ検索処理(KEY)
      *****************************************************************
       900-HKNCOMBI-KEY-SEL-SEC        SECTION.
      *
           MOVE    ZERO                TO  FLG-HKNCOMBI
      *
           MOVE    HKNCOMBI-REC        TO  MCPDATA-REC
           MOVE    PATH-TBL-HKNCOMBI-KEY
                                       TO  MCP-PATH
           PERFORM 910-DBSELECT-SEC
           IF    ( MCP-RC              =   ZERO )
               MOVE    MCPDATA-REC     TO  HKNCOMBI-REC
            ELSE
               INITIALIZE                  HKNCOMBI-REC
               MOVE    1               TO  FLG-HKNCOMBI
            END-IF
      *
           MOVE    PATH-TBL-HKNCOMBI-KEY
                                       TO  MCP-PATH
           PERFORM 910-DBCLOSECURSOR-SEC
      *
           .
       900-HKNCOMBI-READ-EXT.
           EXIT.
      *
      *****************************************************************
      *    患者労災保険情報読み込み
      *****************************************************************
       900-PTRSIINF-KEY-SEC            SECTION.
      *
           MOVE    ZERO                TO  FLG-PTRSIINF
      *
           MOVE    PTRSIINF-REC        TO  MCPDATA-REC
           MOVE    PATH-TBL-PTRSIINF-KEY
                                       TO  MCP-PATH
           PERFORM 910-DBSELECT-SEC
           IF    ( MCP-RC              =   ZERO )
               MOVE    MCPDATA-REC     TO  PTRSIINF-REC
           ELSE
               MOVE    1               TO  FLG-PTRSIINF
               INITIALIZE                  PTRSIINF-REC
           END-IF
     *
           MOVE    PATH-TBL-PTRSIINF-KEY
                                       TO  MCP-PATH
           PERFORM 910-DBCLOSECURSOR-SEC
      *
           .
       900-PTRSIINF-KEY-EXT.
           EXIT.
      *****************************************************************
      *    患者マスター読み込み
      *****************************************************************
       900-PTINF-KEY-SEC               SECTION.
      *
           INITIALIZE                  PTINF-REC
           MOVE    SPA-HOSPID      TO  PTINF-HOSPID
           MOVE    SPA-PTID        TO  PTINF-PTID
           MOVE    PTINF-REC       TO  MCPDATA-REC
           MOVE    PATH-TBL-PTINF-KEY  TO  MCP-PATH
           PERFORM 910-DBSELECT-SEC
           IF    ( MCP-RC          =   ZERO )
               MOVE    MCPDATA-REC TO  PTINF-REC
           ELSE
               MOVE    1           TO  FLG-PTINF
               INITIALIZE              PTINF-REC
           END-IF
      *
           MOVE    PATH-TBL-PTINF-KEY  TO  MCP-PATH
           PERFORM 910-DBCLOSECURSOR-SEC
      *
           .
       900-PTINF-KEY-EXT.
           EXIT.
      *****************************************************************
      *    収納検索処理(KEY38)
      *****************************************************************
       900-SYUNOU-KEY38-SEL-SEC        SECTION.
      *
           INITIALIZE                  WKSYUNOU-REC
           MOVE    SPA-HOSPID      TO  WKSYU-HOSPID
           MOVE    SPA-NYUGAIKBN   TO  WKSYU-NYUGAIKBN
           MOVE    SPA-PTID        TO  WKSYU-PTID
           STRING  ORCHCM43-STSRYYM    DELIMITED   BY  SPACE
                   "01"                DELIMITED   BY  SIZE
           INTO    WKSYU-SRYYMD
           END-STRING
      *
           MOVE    WKSYUNOU-REC    TO  MCPDATA-REC
           MOVE    PATH-TBL-SYUNOU-KEY38
                                   TO  MCP-PATH
           PERFORM 910-DBSELECT-SEC
           IF    ( MCP-RC          =   ZERO )
               MOVE    MCPDATA-REC TO  WKSYUNOU-REC
           ELSE
               INITIALIZE              WKSYUNOU-REC
           END-IF
      *
           MOVE    PATH-TBL-SYUNOU-KEY38
                                   TO  MCP-PATH
           PERFORM 910-DBCLOSECURSOR-SEC
      *
           .
      *
       900-SYUNOU-KEY38-SEL-EXT.
           EXIT.
      *****************************************************************
      *    テーブル検索処理
      *****************************************************************
       910-DBSELECT-SEC          SECTION.
      *
           MOVE    "DBSELECT"      TO  MCP-FUNC
           CALL    "MCPSUB"            USING
                                       MCPAREA
                                       MCPDATA-REC
           IF        ( MCP-RC          =   ZERO )
               PERFORM 910-DBFETCH-SEC
           END-IF
      *
           .
       910-DBSELECT-EXT.
           EXIT.
      *****************************************************************
      *    テーブル読込処理
      *****************************************************************
       910-DBFETCH-SEC            SECTION.
      *
           MOVE    "DBFETCH"       TO  MCP-FUNC
           CALL    "MCPSUB"            USING
                                       MCPAREA
                                       MCPDATA-REC
      *
           .
       910-DBFETCH-EXT.
           EXIT.
      *****************************************************************
      *    テーブルクローズ処理
      *****************************************************************
       910-DBCLOSECURSOR-SEC           SECTION.
      *
           MOVE    "DBCLOSECURSOR" TO  MCP-FUNC
           CALL    "MCPSUB"            USING
                                       MCPAREA
                                       MCPDATA-REC
      *
           .
       910-DBCLOSECURSOR-EXT.
           EXIT.

      *******************************************************************
      * Project code name "ORCA"
      * 日医標準レセプトソフト（JMA standard receipt software）
      * Copyright(C) 2002 JMA (Japan Medical Association)
      *
      * This program is part of "JMA standard receipt software".
      *
      *     This program is distributed in the hope that it will be useful
      * for further advancement in medical care, according to JMA Open
      * Source License, but WITHOUT ANY WARRANTY.
      *     Everyone is granted permission to use, copy, modify and
      * redistribute this program, but only under the conditions described
      * in the JMA Open Source License. You should have received a copy of
      * this license along with this program. If not, stop using this
      * program and contact JMA, 2-28-16 Honkomagome, Bunkyo-ku, Tokyo,
      * 113-8621, Japan.
      ********************************************************************
       IDENTIFICATION          DIVISION.
       PROGRAM-ID.             ORCSPTNUM.
      *****************************************************************
      *  システム名        : ＯＲＣＡ
      *  サブシステム名    : 
      *  コンポーネント名  : 患者番号入力処理
      *  返却エラーコード  : 00:正常　99:混在エラー
      *                      10:対象データなし
      *  管理者            : 
      *  作成日付    作業者        記述
      *  02.06.04    多々納        新規作成
      *****************************************************************
      *  プログラム修正履歴
      * Maj/Min/Rev  修正者        日付      内容
      *  02.07.00    NACL-太田     05/10/24  MONFUNC 対応
      *  03.03.00    NACL-多々納   06/10/06  名前検索の追加
      *****************************************************************
      *
       ENVIRONMENT             DIVISION.
       CONFIGURATION               SECTION.
       INPUT-OUTPUT                SECTION.
       FILE-CONTROL.
      *
      *
       DATA                    DIVISION.
      *FILE                        SECTION.
      *
      *
       WORKING-STORAGE             SECTION.
      *
      *    フラグ領域
       01  FLG-AREA.
           03  FLG-PTNUM           PIC 9(01).
           03  FLG-PTINF           PIC 9(01).
           03  FLG-OK              PIC 9(01).
      *
      *    添字領域
       01  IDX-AREA.
           03  IDX                 PIC 9(04).
           03  IDY                 PIC 9(04).
           03  IDX2                PIC 9(04).
           03  IDX3                PIC 9(04).
      *
      *    ワーク領域
       01  WRK-AREA.
           03  WRK-PTNUM           PIC X(20).
           03  WRK-PTID            PIC 9(10).
      *
           03  WRK-NAME            PIC X(50).
           03  WRK-NAME1           PIC X(50).
           03  WRK-NAME2           PIC X(50).
      *
      *
      *****************************************************************
      *    ファイルレイアウト
      *****************************************************************
      *
      *    患者マスタ−
       01  PTINF-REC.
           COPY    "CPPTINF.INC".
      *    患者番号変換マスタ
       01  PTNUM-REC.
           COPY    "CPPTNUM.INC".
      *
           COPY    "MCPAREA".
      *
      *
      *****************************************************************
      *    サブプロ用　領域
      *****************************************************************
      *
      *    全角チェックパラメタ
           COPY    "CPORCSKANACHK.INC".
      *   患者番号変換サブ
           COPY    "CPORCSPTID.INC".
      *
      *    ＤＢ検索
           COPY    "MCPDATA.INC".
      *    COPY    "CPORCMCP.INC".
      *
      *****************************************************************
      *    連絡　領域
      *****************************************************************
       LINKAGE                     SECTION.
      *
           COPY    "CPORCSPTNUM.INC".
      *
      *    スパ領域
           COPY    "COMMON-SPA".
      *
       PROCEDURE                    DIVISION    USING
           ORCSPTNUMAREA
           SPA-AREA
           .
      *
      *****************************************************************
      *    主　　処理
      *****************************************************************
       000-PROC-SEC                 SECTION.
      *
           MOVE    ZERO                TO  SPTNUM-RC
           MOVE    ZERO                TO  SPTNUM-SYUBETU
      *
           INITIALIZE                  ORCSKANACHKAREA
           MOVE    "1"                 TO  KANACHK-SYORI
           MOVE    SPTNUM-PTNUM        TO  KANACHK-MAE-INPUT
           MOVE    20                  TO  KANACHK-MAX-CNT
           CALL    "ORCSKANACHK"       USING
                                       ORCSKANACHKAREA
           MOVE    KANACHK-MAE-INPUT   TO  SPTNUM-PTNUM
           IF      KANACHK-RC      NOT =   ZERO
      *        混在エラー
               MOVE    99              TO  SPTNUM-RC
               MOVE    ZERO            TO  KANACHK-MAX
               MOVE    9               TO  KANACHK-SYUBETU
           END-IF
      *    最大桁数が２０以上の時、２０とする
           IF      KANACHK-MAX         >   20
               MOVE    20                  TO  KANACHK-MAX
           END-IF
      *
           EVALUATE    KANACHK-SYUBETU
               WHEN    1
      *            半角（患者番号）
                   PERFORM 100-PTID-SET-SEC
               WHEN    2
      *            全角（患者氏名）
                   MOVE    KANACHK-OUT-INPUT   TO  SPTNUM-PTNUM
                   PERFORM 200-NAME-SET-SEC
      *
               WHEN    OTHER
                   MOVE    99              TO  SPTNUM-RC
           END-EVALUATE
           .
      *
       000-PROC-EXT.
      *
           EXIT PROGRAM
           .
      *
      *****************************************************************
      *    患者ＩＤ検索処理
      *****************************************************************
       100-PTID-SET-SEC                SECTION.
      *
           MOVE    1                   TO  SPTNUM-SYUBETU
      *
      *    空白を除く(標準構成、数字型）
           IF     (SPA-1009-PTNUMKSIKBN    =   "2" )  OR
                 ((SPA-1009-PTNUMKSIKBN    =   "1" ) AND
                  (SPA-1009-JIYKSIKBN      =   "2" ))
               MOVE    SPACE               TO  WRK-PTNUM
               MOVE    SPTNUM-PTNUM        TO  WRK-PTNUM
               MOVE    SPACE               TO  SPTNUM-PTNUM
               MOVE    ZERO                TO  IDX2
               PERFORM VARYING     IDX     FROM    1   BY  1
                       UNTIL       IDX     >   20
                   IF      WRK-PTNUM(IDX:1)    =   SPACE
                       CONTINUE
                   ELSE
                       ADD     1               TO  IDX2
                       MOVE    WRK-PTNUM(IDX:1)
                                           TO  SPTNUM-PTNUM(IDX2:1)
                   END-IF
               END-PERFORM
           END-IF
      *
           INITIALIZE                      ORCSPTIDAREA
           MOVE    SPA-HOSPNUM          TO  SPTID-HOSPNUM
           MOVE    SPTNUM-PTNUM        TO  SPTID-PTNUM
           CALL    "ORCSPTID"          USING   ORCSPTIDAREA
                                               SPA-1009-TBL
           IF      SPTID-RC        NOT =   ZERO
               MOVE    10                  TO  SPTNUM-RC
               MOVE    SPTID-PTNUM         TO  SPTNUM-PTNUM
           ELSE
               MOVE    SPTID-PTID          TO  SPTNUM-PTID
               MOVE    SPTID-PTNUM         TO  SPTNUM-PTNUM
           END-IF
      *
           .
       100-PTID-SET-EXT.
           EXIT.
      *
      *****************************************************************
      *    患者氏名検索処理
      *****************************************************************
       200-NAME-SET-SEC                SECTION.
      *
           MOVE    2                   TO  SPTNUM-SYUBETU
      *
           MOVE    SPTNUM-PTNUM(1:KANACHK-MAX)
                                       TO  WRK-NAME
           COMPUTE IDX                 =   KANACHK-MAX  +  1
           MOVE    "%"                 TO  WRK-NAME(IDX:1)
      *    部分検索
           PERFORM 201-NAME-HEN-SEC
      *
      *    漢字検索
           INITIALIZE                  PTINF-REC
           MOVE    SPA-HOSPNUM          TO  PTINF-HOSPNUM
           MOVE    SPACE               TO  PTINF-NAME
           MOVE    WRK-NAME
                                       TO  PTINF-NAME
           MOVE    WRK-NAME
                                       TO  PTINF-KANANAME
      *
      *    患者マスターを漢字氏名で検索
grpsys*    MOVE SPA-HOSPNUM TO -HOSPNUM
           MOVE    PTINF-REC           TO  MCPDATA-REC
           MOVE    "DBSELECT"          TO  MCP-FUNC
           MOVE    "tbl_ptinf"         TO  MCP-TABLE
           MOVE    "key5"              TO  MCP-PATHNAME
grpsys     CALL "ORCDBMAIN" USING MCPAREA
                                  MCPDATA-REC
grpsys                            SPA-AREA
           IF      MCP-RC              =   ZERO
               MOVE    "key5"          TO  MCP-PATHNAME
               PERFORM 910-PTINF-READ-SEC
           ELSE
               INITIALIZE                  PTINF-REC
               MOVE    1                   TO  FLG-PTINF
           END-IF
      *
           MOVE    ZERO                TO  IDX
           MOVE    ZERO                TO  WRK-PTID
           PERFORM UNTIL      (IDX             >   1         ) OR
                              (FLG-PTINF       =   1         )
               ADD     1                   TO  IDX
               MOVE    PTINF-PTID          TO  WRK-PTID
      *
               MOVE    "tbl_ptinf"         TO  MCP-TABLE
               MOVE    "key5"              TO  MCP-PATHNAME
               PERFORM 910-PTINF-READ-SEC
           END-PERFORM
           MOVE    "DBCLOSECURSOR"     TO  MCP-FUNC
           MOVE    "tbl_ptinf"         TO  MCP-TABLE
           MOVE    "key5"              TO  MCP-PATHNAME
grpsys     CALL "ORCDBMAIN" USING MCPAREA
                                  MCPDATA-REC
grpsys                            SPA-AREA
      *
      *    患者決定
           IF      IDX                 =   1
               INITIALIZE                      ORCSPTIDAREA
               MOVE    SPA-HOSPNUM          TO  SPTID-HOSPNUM
               MOVE    WRK-PTID            TO  SPTID-PTID
               CALL    "ORCSPTID"          USING   ORCSPTIDAREA
                                                   SPA-1009-TBL
               IF      SPTID-RC        NOT =   ZERO
                   MOVE    10                  TO  SPTNUM-RC
               ELSE
                   MOVE    SPTID-PTID          TO  SPTNUM-PTID
                   MOVE    SPTID-PTNUM         TO  SPTNUM-PTNUM
               END-IF
           END-IF
      *    患者なし
           IF      IDX                 =   0
               MOVE    10                  TO  SPTNUM-RC
           END-IF
      *    患者複数
           IF      IDX                 >   1
               MOVE    20                  TO  SPTNUM-RC
           END-IF
      *
           .
       200-NAME-SET-EXT.
           EXIT.
      *    
      *****************************************************************
      *    氏名部分検索編集処理
      *****************************************************************
       201-NAME-HEN-SEC                 SECTION.
      *
           MOVE    SPACE               TO  WRK-NAME1
           MOVE    SPACE               TO  WRK-NAME2
           MOVE    1                   TO  IDX2
           PERFORM VARYING     IDX     FROM    1   BY  2
                   UNTIL       IDX     >   KANACHK-MAX
               IF      SPTNUM-PTNUM(IDX:2) =   "＊"
                   COMPUTE IDX3            =   IDX     +  2
                   IF      SPTNUM-PTNUM (IDX3:2)    =   "　"
                       COMPUTE IDX3            =   IDX3     +  2
                   END-IF
                   IF      SPTNUM-PTNUM (IDX3:) =   SPACE
                       MOVE    SPACE               TO  WRK-NAME2
                   ELSE
                       MOVE    SPTNUM-PTNUM(IDX3:) TO  WRK-NAME2
                   END-IF
                   MOVE    KANACHK-MAX             TO  IDX
               ELSE
                   MOVE    SPTNUM-PTNUM(IDX:2)     TO  WRK-NAME1
                                                               (IDX2:2)
                   ADD     2                       TO  IDX2
               END-IF
           END-PERFORM
      *
           IF     (WRK-NAME1       NOT =   SPACE ) AND
                  (WRK-NAME2       NOT =   SPACE )
               MOVE    SPACE           TO  WRK-NAME
               STRING  WRK-NAME1       DELIMITED  BY  SPACE
                       "%"             DELIMITED  BY  SIZE
                       "　"            DELIMITED  BY  SIZE
                       WRK-NAME2       DELIMITED  BY  SPACE
                       "%"             DELIMITED  BY  SIZE
                                       INTO    WRK-NAME
               END-STRING
           END-IF
           IF     (WRK-NAME1           =   SPACE ) AND
                  (WRK-NAME2       NOT =   SPACE )
               MOVE    SPACE           TO  WRK-NAME
               STRING  "%"             DELIMITED  BY  SIZE
                       "　"            DELIMITED  BY  SIZE
                       WRK-NAME2       DELIMITED  BY  SPACE
                       "%"             DELIMITED  BY  SIZE
                                       INTO    WRK-NAME
               END-STRING
           END-IF
      *
           .
       201-NAME-HEN-EXT.
           EXIT.
      *
      *****************************************************************
      *    患者マスター読込処理
      *****************************************************************
       910-PTINF-READ-SEC        SECTION.
      *
           MOVE    "tbl_ptinf"         TO  MCP-TABLE
      *
           MOVE    "DBFETCH"           TO  MCP-FUNC
grpsys     CALL "ORCDBMAIN" USING MCPAREA
                                  MCPDATA-REC
grpsys                            SPA-AREA
           IF      MCP-RC              =   ZERO
               MOVE    ZERO                TO  FLG-PTINF
               MOVE    MCPDATA-REC         TO  PTINF-REC
           ELSE
               MOVE    1                   TO  FLG-PTINF
           END-IF
      *
           .
       910-PTINF-READ-EXT.
           EXIT.
      *

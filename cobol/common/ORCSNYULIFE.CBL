      *******************************************************************
      * Project code name "ORCA"
      * 日医標準レセプトソフト（JMA standard receipt software）
      * Copyright(C) 2002 JMA (Japan Medical Association)
      *
      * This program is part of "JMA standard receipt software".
      *
      *     This program is distributed in the hope that it will be useful
      * for further advancement in medical care, according to JMA Open
      * Source License, but WITHOUT ANY WARRANTY.
      *     Everyone is granted permission to use, copy, modify and
      * redistribute this program, but only under the conditions described
      * in the JMA Open Source License. You should have received a copy of
      * this license along with this program. If not, stop using this
      * program and contact JMA, 2-28-16 Honkomagome, Bunkyo-ku, Tokyo,
      * 113-8621, Japan.
      ********************************************************************
       IDENTIFICATION              DIVISION.
       PROGRAM-ID.                 ORCSNYULIFE.
      *****************************************************************
      *  システム名        : ＯＲＣＡ
      *  サブシステム名    : 入院
      *  コンポーネント名  : 入院時生活療養費負担額取得サブ
      *                      （平成１８年１０月改正対応）
      *  管理者            : 
      *  作成日付    作業者        記述
      *  06/09/12    NACL−小豆沢  新規作成
      *****************************************************************
      *  プログラム修正履歴
      * Maj/Min/Rev  修正者       日付      内容
      *****************************************************************
      *
      *
       ENVIRONMENT                 DIVISION.
       CONFIGURATION               SECTION.
       INPUT-OUTPUT                SECTION.
       FILE-CONTROL.
      *
      *
       DATA                        DIVISION.
      *FILE                        SECTION.
      *
       WORKING-STORAGE             SECTION.
      *
      *    フラグ領域
       01  FLG-AREA.
           03  FLG-NYUINACCT               PIC 9(01).
           03  FLG-NYUINACT                PIC 9(01).
           03  FLG-PTINF                   PIC 9(01).
           03  FLG-SYSKANRI                PIC 9(01).
           03  FLG-OK                      PIC 9(01).
           03  FLG-TSYRRK                  PIC 9(01).
           03  FLG-TNKRRK                  PIC 9(01).
           03  FLG-RJN                     PIC 9(01).
      *    添字領域
       01  IDX-AREA.
           03  IDX                         PIC 9(04).
           03  IDX1                        PIC 9(04).
           03  TSY90-IDX                   PIC 9(02).
      *
      *    コンスタント値
       01  CONSTANT-AREA.
      *    療養病棟入院基本料２
           03   CONS-RYOYO2A               PIC X(09) VALUE "190123710".
           03   CONS-RYOYO2B               PIC X(09) VALUE "190123810".
           03   CONS-RYOYO2C               PIC X(09) VALUE "190123910".
           03   CONS-RYOYO2D               PIC X(09) VALUE "190124010".
           03   CONS-RYOYO2E               PIC X(09) VALUE "190124110".
           03   CONS-RYOYO2TOKU            PIC X(09) VALUE "190124210".
      *    有床診療所療養病床入院基本料２
           03   CONS-SRYOYO2A              PIC X(09) VALUE "190124310".
           03   CONS-SRYOYO2B              PIC X(09) VALUE "190124410".
           03   CONS-SRYOYO2C              PIC X(09) VALUE "190124510".
           03   CONS-SRYOYO2D              PIC X(09) VALUE "190124610".
           03   CONS-SRYOYO2E              PIC X(09) VALUE "190124710".
           03   CONS-SRYOYO2TOKU           PIC X(09) VALUE "190124810".
      *    回復期リハビリテーション病棟入院料
           03   CONS-KAIFUKU               PIC X(09) VALUE "190124910".
      *    診療所老人医療管理料
           03   CONS-ROUKAN1               PIC X(09) VALUE "190125010".
           03   CONS-ROUKAN2               PIC X(09) VALUE "190125110".
           03   CONS-ROUKAN3               PIC X(09) VALUE "190125210".
      *    短期滞在手術基本料２
           03   CONS-TANKI2                PIC X(09) VALUE "190125310".
      *
      *    一時領域
       01  WRK-AREA.
      *
           03  WRK-GAIHAKU-SRYCD           PIC X(09).
      *
      *****************************************************************
      *    ファイルレイアウト
      *****************************************************************
      *
      *    システム管理マスタ
           COPY    "CPSYSKANRI.INC".
      *
           COPY    "CPSK5000.INC".
      *
      *    入院診療会計マスタ−
       01  NYUINACCT-REC.
           COPY    "CPNYUINACCT.INC".
      *
      *    入院診療行為マスター
       01  NYUINACT-REC.
           COPY    "CPNYUINACT.INC".
      *
      *    患者マスタ−
       01  PTINF-REC.
           COPY    "CPPTINF.INC".
      *
      *    患者公費情報マスタ−
       01  PTKOHINF-REC.
           COPY    "CPPTKOHINF.INC".
      *
      *    低所得
       01  TSYRRK-REC.
           COPY    "CPTSYRRK.INC".
      *
      *    年金
       01  TNKRRK-REC.
           COPY    "CPTNKRRK.INC".
      *
      *    ＤＢ検索
           COPY    "MCPDATA.INC".
      *
           COPY    "MCPAREA".
      *
      *****************************************************************
      *    サブプロ用　領域
      *****************************************************************
      *
      *   日付変換サブ
           COPY    "CPORCSDAY.INC".
           COPY    "CPORCSLNK.INC".
      *
      *    保険算定用年齢・割合計算サブ
           COPY    "CPORCSAGECHK.INC".
      *
      *****************************************************************
      *    連絡　領域
      *****************************************************************
        LINKAGE                     SECTION.
      *
      *    スパ領域
           COPY    "COMMON-SPA".
      *    入院会計作成用パラメタ
           COPY    "CPORCSNYULIFE.INC".
      *
       PROCEDURE                   DIVISION    USING
      *     SPA-AREA
           ORCSNYULIFE-AREA
           .
      *
      *****************************************************************
      *    主　　処理
      *****************************************************************
       000-PROC-SEC                    SECTION.
      *
           INITIALIZE                  FLG-AREA
           INITIALIZE                  IDX-AREA
           INITIALIZE                  WRK-AREA
           INITIALIZE                  LNK-NYULIFE-HENKYAKU-AREA
           MOVE    ZERO                TO  LNK-NYULIFE-RC
      *
      *    パラメータチェック
           PERFORM 100-PRM-CHK-SEC
           IF    ( LNK-NYULIFE-RC       NOT =   ZERO )
               GO  TO  000-PROC-EXT
           END-IF
      *
      *    主処理
           IF      LNK-NYULIFE-RC      =    ZERO
               PERFORM 200-MAIN-SEC
           END-IF
      *
           .
       000-PROC-EXT.
      *
           EXIT    PROGRAM
           .
      *
      *****************************************************************
      *    パラメータチェック
      *****************************************************************
       100-PRM-CHK-SEC                     SECTION.
      *
      *    算定年月チェック
           IF      LNK-NYULIFE-SANTEIYM    =   SPACE
               MOVE    1               TO  LNK-NYULIFE-RC
               GO  TO  100-PRM-CHK-EXT
           END-IF
      *
      *    患者マスター検索
           INITIALIZE                       PTINF-REC
           MOVE    LNK-NYULIFE-HOSPID   TO  PTINF-HOSPID
           MOVE    LNK-NYULIFE-PTID     TO  PTINF-PTID
           PERFORM 900-PTINF-READ-SEC
           IF      FLG-PTINF            NOT =   ZERO
               MOVE    2                TO  LNK-NYULIFE-RC
               GO  TO  100-PRM-CHK-EXT
           END-IF
      *
      *    入院中のチェック（入院会計マスター検索）
           INITIALIZE                      NYUINACCT-REC
           MOVE    LNK-NYULIFE-HOSPID   TO  NACCT-HOSPID
           MOVE    LNK-NYULIFE-PTID     TO  NACCT-PTID
           MOVE    LNK-NYULIFE-SANTEIYM TO  NACCT-SRYYM
           MOVE    "5"                  TO  NACCT-ZAISKBKBN
           MOVE    "tbl_nyuinacct"      TO  MCP-TABLE
           MOVE    "key17"              TO  MCP-PATHNAME
           PERFORM 910-NYUINACCT-SELECT-SEC
           IF      FLG-NYUINACCT        NOT =   ZERO
               MOVE    3                TO  LNK-NYULIFE-RC
           ELSE
               MOVE    "tbl_nyuinacct"  TO  MCP-TABLE
               MOVE    "key17"          TO  MCP-PATHNAME
               PERFORM 910-NYUINACCT-READ-SEC
               IF   FLG-NYUINACCT       NOT =   ZERO
                    MOVE    3           TO  LNK-NYULIFE-RC
               END-IF
           END-IF
      *
      *    入院会計クローズ
           MOVE    "tbl_nyuinacct"         TO  MCP-TABLE
           MOVE    "key17"                 TO  MCP-PATHNAME
           PERFORM   910-DB-CLOSE-SEC
           .
       100-PRM-CHK-EXT.
           EXIT.
      *
      *****************************************************************
      *    主取得
      *****************************************************************
       200-MAIN-SEC            SECTION.
      *
      *    入院時生活療養費の該当日と軽減区分２（入院料判定）を取得
           MOVE    ZERO                    TO  FLG-OK
           INITIALIZE                          NYUINACCT-REC
           MOVE    LNK-NYULIFE-HOSPID      TO  NACCT-HOSPID
           MOVE    LNK-NYULIFE-PTID        TO  NACCT-PTID
           MOVE    LNK-NYULIFE-SANTEIYM    TO  NACCT-SRYYM
           MOVE    "1"                     TO  NACCT-ZAISKBKBN
           MOVE    "tbl_nyuinacct"         TO  MCP-TABLE
           MOVE    "key17"                 TO  MCP-PATHNAME
           PERFORM 910-NYUINACCT-SELECT-SEC
           IF      FLG-NYUINACCT           =   ZERO
               MOVE    "tbl_nyuinacct"     TO  MCP-TABLE
               MOVE    "key17"             TO  MCP-PATHNAME
               PERFORM   910-NYUINACCT-READ-SEC
               PERFORM   UNTIL   (FLG-NYUINACCT    =   1)
                   IF    NACCT-SRYCDTOTAL     =    99999916
                       CONTINUE
                   ELSE
      *                生活療養に該当する入院料を判定し、
      *                返却値の対象区分を設定する
                       PERFORM    210-NYUINACT-GET-SEC
                       IF   NSRY-SRYCD(1)   =  CONS-RYOYO2A     OR
                                               CONS-RYOYO2B     OR
                                               CONS-RYOYO2C     OR
                                               CONS-RYOYO2D     OR
                                               CONS-RYOYO2E     OR
                                               CONS-RYOYO2TOKU  OR
                                               CONS-SRYOYO2A    OR
                                               CONS-SRYOYO2B    OR
                                               CONS-SRYOYO2C    OR
                                               CONS-SRYOYO2D    OR
                                               CONS-SRYOYO2E    OR
                                               CONS-SRYOYO2TOKU OR
                                               CONS-KAIFUKU     OR
                                               CONS-ROUKAN1     OR
                                               CONS-ROUKAN2     OR
                                               CONS-ROUKAN3     OR
                                               CONS-TANKI2
                          PERFORM    VARYING   IDX1   FROM   1   BY   1
                                  UNTIL     IDX1   >   31
                            IF     NACCT-DAY(IDX1)  NOT =  ZERO
                               MOVE   "1"     TO   LNK-NYULIFE-KBN(IDX1)
                               MOVE    1      TO   FLG-OK
      *                        病状の程度、治療の内容をしん酌して標準
      *                        負担額を軽減する患者の判定（入院料判断）
                               IF  NSRY-SRYCD(1)  =  CONS-RYOYO2A  OR
                                                     CONS-RYOYO2B  OR
                                                     CONS-RYOYO2C  OR
                                                     CONS-SRYOYO2A OR
                                                     CONS-SRYOYO2B OR
                                                     CONS-SRYOYO2C OR
                                                     CONS-KAIFUKU  OR
                                                     CONS-ROUKAN1  OR
                                                     CONS-TANKI2
                                   MOVE   "1"   TO   
                                             LNK-NYULIFE-FTNKBN2(IDX1)
                               END-IF
                            END-IF
                          END-PERFORM
                       END-IF
                   END-IF
                   MOVE    "tbl_nyuinacct"      TO  MCP-TABLE
                   MOVE    "key17"              TO  MCP-PATHNAME
                   PERFORM 910-NYUINACCT-READ-SEC
               END-PERFORM
           END-IF
      *
      *    入院会計クローズ
           MOVE    "tbl_nyuinacct"         TO  MCP-TABLE
           MOVE    "key17"                 TO  MCP-PATHNAME
           PERFORM   910-DB-CLOSE-SEC
      *
      *−−−−医療機関食事情報取得ここから−−−−−−−−−−−
      *医療機関食事情報の取得（システム管理テーブルより）
           MOVE    ZERO                TO  FLG-SYSKANRI
           INITIALIZE                      SYSKANRI-REC
           MOVE    "5000"              TO  SYS-KANRICD
           MOVE    "*"                 TO  SYS-KBNCD
           MOVE    LNK-NYULIFE-SANTEIYM
                                       TO  SYS-STYUKYMD(1:6)
           MOVE    "01"                TO  SYS-STYUKYMD(7:2)
           MOVE    SYS-STYUKYMD        TO  SYS-EDYUKYMD
           MOVE    SYSKANRI-REC        TO  MCPDATA-REC
           MOVE    "DBSELECT"          TO  MCP-FUNC
           MOVE    "tbl_syskanri"      TO  MCP-TABLE
           MOVE    "key10"             TO  MCP-PATHNAME
           CALL    "MONFUNC"               USING
                                           MCPAREA
                                           MCPDATA-REC
           IF      MCP-RC              =   ZERO
               MOVE    "tbl_syskanri"  TO  MCP-TABLE
               MOVE    "key10"         TO  MCP-PATHNAME
               PERFORM  910-SYSKANRI-READ-SEC
      *        食事情報の退避
               IF     SYS-5000-SHOKUJIRYOYOCD  NOT =  SPACE
                   PERFORM    VARYING   IDX1  FROM  1  BY  1
                          UNTIL     IDX1   >    31
                       IF    LNK-NYULIFE-KBN(IDX1)   =   "1"
      *                   入院時食事療養費
                           IF   SYS-5000-SHOKUJIRYOYOCD  = "197000110"
                                MOVE   "1"     TO  
                                          LNK-NYULIFE-SKJKBN(IDX1)
                           ELSE
                                MOVE   "2"     TO  
                                          LNK-NYULIFE-SKJKBN(IDX1)
                           END-IF
                       END-IF
                   END-PERFORM
               END-IF
           END-IF
      *    システム管理クローズ
           MOVE    "tbl_syskanri"      TO  MCP-TABLE
           MOVE    "key10"             TO  MCP-PATHNAME
           PERFORM   910-DB-CLOSE-SEC
      *−−−−医療機関食事情報取得取得ここまで−−−−−−−−−−−
      *
      *
           IF    FLG-OK   =    1
      *
      *        老人保健のチェック
               MOVE    ZERO                 TO  FLG-RJN 
               INITIALIZE                       PTKOHINF-REC
               MOVE    LNK-NYULIFE-HOSPID   TO  PTKOH-HOSPID
               MOVE    LNK-NYULIFE-PTID     TO  PTKOH-PTID
               MOVE    "027"                TO  PTKOH-KOHNUM
               PERFORM 900-PTKOHINF-READ-SEC
      *
      *−−−−所得情報の取得ここから−−−−−−−−−−−−−−−
      *
      *      軽減区分１（所得判定）を取得
      *
      *      低所得情報の取得
               INITIALIZE                          TSYRRK-REC
               MOVE    LNK-NYULIFE-HOSPID      TO  TSYRRK-HOSPID
               MOVE    LNK-NYULIFE-PTID        TO  TSYRRK-PTID
               MOVE    "tbl_tsyrrk"            TO  MCP-TABLE
               MOVE    "key3"                  TO  MCP-PATHNAME
               PERFORM 910-TSYRRK-SELECT-SEC
               IF      FLG-TSYRRK              =   ZERO
                   MOVE    "tbl_tsyrrk"        TO  MCP-TABLE
                   MOVE    "key3"              TO  MCP-PATHNAME
                   PERFORM 910-TSYRRK-READ-SEC
                   PERFORM  UNTIL  
                      ( FLG-TSYRRK          =   1                ) OR
                     (( TSYRRK-NINSTYMD(1:6) <= LNK-NYULIFE-SANTEIYM AND
                        LNK-NYULIFE-SANTEIYM <= TSYRRK-NINEDYMD(1:6) ))
      *
                       MOVE    "tbl_tsyrrk"     TO  MCP-TABLE
                       MOVE    "key3"           TO  MCP-PATHNAME
                       PERFORM 910-TSYRRK-READ-SEC
      *
                   END-PERFORM
               END-IF
               IF      FLG-TSYRRK              =   ZERO
                  IF  (LNK-NYULIFE-SANTEIYM   =  TSYRRK-NINSTYMD(1:6))
                  AND (TSYRRK-NINSTYMD(7:2)      NOT =  "01")
                    CONTINUE
                  ELSE
                    PERFORM   VARYING   IDX1   FROM   1  BY  1
                              UNTIL     IDX1   >    31
                        IF   LNK-NYULIFE-KBN(IDX1)   =   "1"
                            MOVE   "1"    TO  LNK-NYULIFE-FTNKBN1(IDX1)
                        END-IF
                    END-PERFORM
                  END-IF
               END-IF
      *
               MOVE     "tbl_tsyrrk"        TO  MCP-TABLE
               MOVE     "key3"              TO  MCP-PATHNAME
               PERFORM   910-DB-CLOSE-SEC
      *
      *      ９０日越え（低所得情報）
               IF  (TSYRRK-SKJGNSTYMD(1:6)  <= LNK-NYULIFE-SANTEIYM) AND
                   (TSYRRK-SKJGNSTYMD       NOT = SPACE)
                 IF (LNK-NYULIFE-SANTEIYM  = TSYRRK-SKJGNSTYMD(1:6)) AND
                    (TSYRRK-SKJGNSTYMD(7:2)   NOT = "01")
                   CONTINUE
                 ELSE
                   IF  TSYRRK-SKJGNSTYMD(1:6)  <  LNK-NYULIFE-SANTEIYM
                       MOVE   1             TO   TSY90-IDX
                   ELSE
                       MOVE   TSYRRK-SKJGNSTYMD(7:2)   TO  TSY90-IDX
                   END-IF
                   PERFORM   VARYING   IDX1   FROM   TSY90-IDX  BY  1
                             UNTIL     IDX1   >    31
                        IF   LNK-NYULIFE-KBN(IDX1)   =   "1"
                            MOVE   "2"    TO  LNK-NYULIFE-FTNKBN1(IDX1)
                        END-IF
                   END-PERFORM
                 END-IF
               END-IF
      *
      *      低年金情報の取得
               INITIALIZE                          TNKRRK-REC
               MOVE    LNK-NYULIFE-HOSPID      TO  TNKRRK-HOSPID
               MOVE    LNK-NYULIFE-PTID        TO  TNKRRK-PTID
               MOVE    "tbl_tnkrrk"            TO  MCP-TABLE
               MOVE    "key3"                  TO  MCP-PATHNAME
               PERFORM 910-TNKRRK-SELECT-SEC
               IF      FLG-TNKRRK              =   ZERO
                   MOVE    "tbl_tnkrrk"        TO  MCP-TABLE
                   MOVE    "key3"              TO  MCP-PATHNAME
                   PERFORM 910-TNKRRK-READ-SEC
                   PERFORM  UNTIL  
                      ( FLG-TNKRRK          =   1                ) OR
                     (( TNKRRK-STYMD(1:6) <= LNK-NYULIFE-SANTEIYM AND
                        LNK-NYULIFE-SANTEIYM <= TNKRRK-EDYMD(1:6) ))
      *
                       MOVE    "tbl_tnkrrk"       TO  MCP-TABLE
                       MOVE    "key3"             TO  MCP-PATHNAME
                       PERFORM 910-TNKRRK-READ-SEC
      *
                   END-PERFORM
               END-IF
               IF      FLG-TNKRRK              =   ZERO
                  IF  (LNK-NYULIFE-SANTEIYM   =  TNKRRK-STYMD(1:6))
                  AND (TNKRRK-STYMD(7:2)     NOT =  "01")
                    CONTINUE
                  ELSE
                    PERFORM   VARYING   IDX1   FROM   1  BY  1
                              UNTIL     IDX1   >    31
                        IF    LNK-NYULIFE-KBN(IDX1)   =   "1"
                            IF  TNKRRK-RRI-FUKUSHI-KBN = SPACE OR "0"
                                MOVE  "3"   TO
                                            LNK-NYULIFE-FTNKBN1(IDX1)
                            ELSE
                                MOVE  "4"   TO
                                            LNK-NYULIFE-FTNKBN1(IDX1)
                            END-IF
                        END-IF
                    END-PERFORM
                  END-IF
               END-IF
      *
               MOVE     "tbl_tnkrrk"        TO  MCP-TABLE
               MOVE     "key3"              TO  MCP-PATHNAME
               PERFORM   910-DB-CLOSE-SEC
      *
      *−−−−所得情報の取得ここまで−−−−−−−−−−−−−−−
      *
      *−−−−食事療養、環境療養コード、単価設定ここから−−−−−
             PERFORM   VARYING   IDX1   FROM   1  BY  1
               UNTIL   IDX1   >    31
                IF   LNK-NYULIFE-KBN(IDX1)   =   "1"
                    EVALUATE  LNK-NYULIFE-SKJKBN(IDX1)
                        WHEN  "1"
                            MOVE  "197001310"   TO
                                            LNK-NYULIFE-SKJ-SRYCD(IDX1)
                            MOVE   554          TO
                                            LNK-NYULIFE-SKJ-MONEY(IDX1)
                            MOVE  "197001410"   TO
                                            LNK-NYULIFE-KAN-SRYCD(IDX1)
                            MOVE   398          TO
                                            LNK-NYULIFE-KAN-MONEY(IDX1)
                        WHEN  "2"
                            MOVE  "197001710"   TO
                                            LNK-NYULIFE-SKJ-SRYCD(IDX1)
                            MOVE   420          TO
                                            LNK-NYULIFE-SKJ-MONEY(IDX1)
                            MOVE  "197001810"   TO
                                            LNK-NYULIFE-KAN-SRYCD(IDX1)
                            MOVE   398          TO
                                            LNK-NYULIFE-KAN-MONEY(IDX1)
                    END-EVALUATE
                END-IF
             END-PERFORM
      *−−−−食事療養、環境療養コード、単価設定ここまで−−−−−
      *
      *−−−−食事負担、環境療養負担コード、単価設定ここから−−−
      *      生活療養食事標準負担額設定（一般）
             PERFORM   VARYING   IDX1   FROM   1  BY  1
               UNTIL   IDX1   >    31
                IF   LNK-NYULIFE-KBN(IDX1)   =   "1"
                    EVALUATE  LNK-NYULIFE-SKJKBN(IDX1)
                        WHEN  "1"
                            MOVE  "197002010"   TO
                                         LNK-NYULIFE-SKJFTN-SRYCD(IDX1)
                            MOVE   460          TO
                                         LNK-NYULIFE-SKJFTN-MONEY(IDX1)
                        WHEN  "2"
                            MOVE  "197002110"   TO
                                         LNK-NYULIFE-SKJFTN-SRYCD(IDX1)
                            MOVE   420          TO
                                         LNK-NYULIFE-SKJFTN-MONEY(IDX1)
                    END-EVALUATE
                END-IF
             END-PERFORM
      *
      *      生活療養食事標準負担額設定（軽減ありの患者）
             PERFORM   VARYING   IDX1   FROM   1  BY  1
               UNTIL   IDX1   >    31
                IF   LNK-NYULIFE-KBN(IDX1)   =   "1"
                    EVALUATE  LNK-NYULIFE-FTNKBN2(IDX1)
                        WHEN  SPACE
      *                   所得の状況による軽減
                            EVALUATE   LNK-NYULIFE-FTNKBN1(IDX1)
                                WHEN   "1"
                                WHEN   "2"
      *                           （低２）
                                    MOVE  "197002210"   TO
                                         LNK-NYULIFE-SKJFTN-SRYCD(IDX1)
                                    MOVE   210          TO
                                         LNK-NYULIFE-SKJFTN-MONEY(IDX1)
                                WHEN   "3"
      *                           （低１）
                                    IF   FLG-RJN    =   0
                                       MOVE  "197002310"   TO
                                         LNK-NYULIFE-SKJFTN-SRYCD(IDX1)
                                       MOVE   130          TO
                                         LNK-NYULIFE-SKJFTN-MONEY(IDX1)
                                    ELSE
                                       MOVE  "197002410"   TO
                                         LNK-NYULIFE-SKJFTN-SRYCD(IDX1)
                                       MOVE   130          TO
                                         LNK-NYULIFE-SKJFTN-MONEY(IDX1)
                                    END-IF
                                WHEN   "4"
      *                           （低１−老齢福祉年金受給者）
                                    MOVE  "197002510"   TO
                                         LNK-NYULIFE-SKJFTN-SRYCD(IDX1)
                                    MOVE   100          TO
                                         LNK-NYULIFE-SKJFTN-MONEY(IDX1)
                            END-EVALUATE
                        WHEN  "1"
      *                   病状の程度による軽減
                            EVALUATE   LNK-NYULIFE-FTNKBN1(IDX1)
                                WHEN   SPACE
      *                           （医療区分２、３（一般））
                                    MOVE  "197002710"   TO
                                         LNK-NYULIFE-SKJFTN-SRYCD(IDX1)
                                    MOVE   260          TO
                                         LNK-NYULIFE-SKJFTN-MONEY(IDX1)
                                WHEN   "1"
      *                           （医療区分２、３（低２　９０日以下））
                                    MOVE  "197002810"   TO
                                         LNK-NYULIFE-SKJFTN-SRYCD(IDX1)
                                    MOVE   210          TO
                                         LNK-NYULIFE-SKJFTN-MONEY(IDX1)
                                WHEN   "2"
      *                           （医療区分２、３（低２　９０日超））
                                    MOVE  "197002910"   TO
                                         LNK-NYULIFE-SKJFTN-SRYCD(IDX1)
                                    MOVE   160          TO
                                         LNK-NYULIFE-SKJFTN-MONEY(IDX1)
                                WHEN   "3"
                                WHEN   "4"
      *                           （医療区分２、３（低１））
                                    MOVE  "197003010"   TO
                                         LNK-NYULIFE-SKJFTN-SRYCD(IDX1)
                                    MOVE   100          TO
                                         LNK-NYULIFE-SKJFTN-MONEY(IDX1)
                            END-EVALUATE
                    END-EVALUATE
                END-IF
             END-PERFORM
      *
      *      生活療養環境療養標準負担額設定
             PERFORM   VARYING   IDX1   FROM   1  BY  1
               UNTIL   IDX1   >    31
                IF   LNK-NYULIFE-KBN(IDX1)   =   "1"
                    IF  LNK-NYULIFE-FTNKBN2(IDX1)   =   SPACE
                        MOVE  "197001910"       TO
                                        LNK-NYULIFE-KANFTN-SRYCD(IDX1)
                        MOVE  320       TO
                                        LNK-NYULIFE-KANFTN-MONEY(IDX1)
                    END-IF
                END-IF
             END-PERFORM
      *
      *−−−−食事負担、環境療養負担コード、単価設定ここまで−−−
      *
           END-IF
      *
           IF   FLG-OK   =   ZERO
               MOVE   4      TO   LNK-NYULIFE-RC
           END-IF
      *
           PERFORM   VARYING  IDX1   FROM  1   BY  1
                     UNTIL  IDX1   >   31
                DISPLAY "IDX1 = " IDX1
                DISPLAY "LNK-NYULIFE-FTNKBN1(IDX1) = " 
                         LNK-NYULIFE-FTNKBN1(IDX1)
                DISPLAY "LNK-NYULIFE-FTNKBN2(IDX1) = " 
                         LNK-NYULIFE-FTNKBN2(IDX1)
                DISPLAY  "LNK-NYULIFE-SKJ-SRYCD(IDX1) = "
                          LNK-NYULIFE-SKJ-SRYCD(IDX1)
                DISPLAY "LNK-NYULIFE-SKJ-MONEY(IDX1) = "
                         LNK-NYULIFE-SKJ-MONEY(IDX1)
                DISPLAY  "LNK-NYULIFE-SKJFTN-SRYCD(IDX1) = "
                          LNK-NYULIFE-SKJFTN-SRYCD(IDX1)
                DISPLAY "LNK-NYULIFE-SKJFTN-MONEY(IDX1) = "
                         LNK-NYULIFE-SKJFTN-MONEY(IDX1)
                DISPLAY  "LNK-NYULIFE-KAN-SRYCD(IDX1) = "
                          LNK-NYULIFE-KAN-SRYCD(IDX1)
                DISPLAY "LNK-NYULIFE-KAN-MONEY(IDX1) = "
                         LNK-NYULIFE-KAN-MONEY(IDX1)
                DISPLAY  "LNK-NYULIFE-KANFTN-SRYCD(IDX1) = "
                          LNK-NYULIFE-KANFTN-SRYCD(IDX1)
                DISPLAY "LNK-NYULIFE-KANFTN-MONEY(IDX1) = "
                         LNK-NYULIFE-KANFTN-MONEY(IDX1)
           END-PERFORM
           .
       200-MAIN-EXT.
           EXIT.
      *
      *****************************************************************
      *    入院診療行為取得
      *****************************************************************
       210-NYUINACT-GET-SEC            SECTION.
      *
           INITIALIZE                      NYUINACT-REC
           MOVE    NACCT-HOSPID        TO  NSRY-HOSPID
           MOVE    NACCT-NYUGAIKBN     TO  NSRY-NYUGAIKBN
           MOVE    NACCT-PTID          TO  NSRY-PTID
           MOVE    NACCT-SRYYM         TO  NSRY-SRYYM
           MOVE    NACCT-ZAINUM        TO  NSRY-ZAINUM
           MOVE    "tbl_nyuinact"      TO  MCP-TABLE
           MOVE    "key9"              TO  MCP-PATHNAME
           PERFORM 910-NYUINACT-SELECT-SEC
           IF      FLG-NYUINACT        =   ZERO
               MOVE    "tbl_nyuinact"  TO  MCP-TABLE
               MOVE    "key9"          TO  MCP-PATHNAME
               PERFORM   910-NYUINACT-READ-SEC
           END-IF
      *
      *    入院行為クローズ
           MOVE     "tbl_nyuinact"     TO  MCP-TABLE
           MOVE     "key9"             TO  MCP-PATHNAME
           PERFORM   910-DB-CLOSE-SEC
      *
           .
       210-NYUINACT-GET-EXT.
           EXIT.
      *
      *****************************************************************
      *    システム管理マスター読込
      *****************************************************************
       910-SYSKANRI-READ-SEC         SECTION.
      *
           MOVE    "DBFETCH"           TO  MCP-FUNC
           CALL    "MONFUNC"            USING
                                        MCPAREA
                                        MCPDATA-REC
           IF      MCP-RC              =   ZERO
               MOVE    MCPDATA-REC         TO  SYS-5000-REC
               MOVE    ZERO                TO  FLG-SYSKANRI
           ELSE
               INITIALIZE                      SYS-5000-REC
               MOVE    1                   TO  FLG-SYSKANRI
           END-IF
      *
           .
       910-SYSKANRI-READ-EXT.
           EXIT.
      *
      *****************************************************************
      *    患者マスター読込
      *****************************************************************
       900-PTINF-READ-SEC         SECTION.
      *
           MOVE    PTINF-REC           TO  MCPDATA-REC
           MOVE    "DBSELECT"          TO  MCP-FUNC
           MOVE    "tbl_ptinf"         TO  MCP-TABLE
           MOVE    "key"               TO  MCP-PATHNAME
           CALL    "MONFUNC"               USING
                                           MCPAREA
                                           MCPDATA-REC
           IF      MCP-RC              =   ZERO
               MOVE    "DBFETCH"       TO  MCP-FUNC
               MOVE    "tbl_ptinf"         TO  MCP-TABLE
               MOVE    "key"               TO  MCP-PATHNAME
               CALL    "MONFUNC"           USING
                                           MCPAREA
                                           MCPDATA-REC
               IF      MCP-RC          =   ZERO
                   MOVE    MCPDATA-REC TO  PTINF-REC
                   MOVE    ZERO        TO  FLG-PTINF
               ELSE
                   INITIALIZE              PTINF-REC
                   MOVE    1           TO  FLG-PTINF
               END-IF
           ELSE
               INITIALIZE                  PTINF-REC
               MOVE    1               TO  FLG-PTINF
           END-IF
      *
           MOVE    "tbl_ptinf"         TO  MCP-TABLE
           MOVE    "key"               TO  MCP-PATHNAME
           PERFORM   910-DB-CLOSE-SEC
      *
           .
       900-PTINF-READ-EXT.
           EXIT.
      *
      *****************************************************************
      *    患者公費情報マスター読込
      *****************************************************************
       900-PTKOHINF-READ-SEC         SECTION.
      *
           MOVE    PTKOHINF-REC        TO  MCPDATA-REC
           MOVE    "DBSELECT"          TO  MCP-FUNC
           MOVE    "tbl_ptkohinf"      TO  MCP-TABLE
           MOVE    "key4"              TO  MCP-PATHNAME
           CALL    "MONFUNC"               USING
                                           MCPAREA
                                           MCPDATA-REC
           IF      MCP-RC              =   ZERO
               MOVE    "DBFETCH"       TO  MCP-FUNC
               MOVE    "tbl_ptkohinf"  TO  MCP-TABLE
               MOVE    "key4"          TO  MCP-PATHNAME
               CALL    "MONFUNC"           USING
                                           MCPAREA
                                           MCPDATA-REC
               IF      MCP-RC          =   ZERO
                   MOVE    1           TO  FLG-RJN
               END-IF
           ELSE
               INITIALIZE                  PTINF-REC
               MOVE    1               TO  FLG-PTINF
           END-IF
      *
           MOVE    "tbl_ptkohinf"      TO  MCP-TABLE
           MOVE    "key4"              TO  MCP-PATHNAME
           PERFORM   910-DB-CLOSE-SEC
      *
           .
       900-PTKOHINF-READ-EXT.
           EXIT.
      *
      *****************************************************************
      *    入院会計マスター検索
      *****************************************************************
       910-NYUINACCT-SELECT-SEC        SECTION.
      *
           MOVE    NYUINACCT-REC       TO  MCPDATA-REC
           MOVE    "DBSELECT"          TO  MCP-FUNC
           CALL    "MONFUNC"            USING
                                        MCPAREA
                                        MCPDATA-REC
           IF      MCP-RC              =   ZERO
               MOVE    ZERO               TO  FLG-NYUINACCT
           ELSE
               MOVE    1                  TO  FLG-NYUINACCT
           END-IF
      *
           .
       910-NYUINACCT-SELECT-EXT.
           EXIT.
      *
      *****************************************************************
      *    入院会計マスター読込
      *****************************************************************
       910-NYUINACCT-READ-SEC        SECTION.
      *
           MOVE    "DBFETCH"           TO  MCP-FUNC
           CALL    "MONFUNC"            USING
                                        MCPAREA
                                        MCPDATA-REC
           IF      MCP-RC              =   ZERO
               MOVE    MCPDATA-REC        TO  NYUINACCT-REC
               MOVE    ZERO               TO  FLG-NYUINACCT
           ELSE
               INITIALIZE                     NYUINACCT-REC
               MOVE    1                  TO  FLG-NYUINACCT
           END-IF
      *
           .
       910-NYUINACCT-READ-EXT.
           EXIT.
      *
      *****************************************************************
      *    低所得マスター検索
      *****************************************************************
       910-TSYRRK-SELECT-SEC        SECTION.
      *
           MOVE    TSYRRK-REC          TO  MCPDATA-REC
           MOVE    "DBSELECT"          TO  MCP-FUNC
           CALL    "MONFUNC"            USING
                                        MCPAREA
                                        MCPDATA-REC
           IF      MCP-RC              =   ZERO
               MOVE    ZERO               TO  FLG-TSYRRK
           ELSE
               MOVE    1                  TO  FLG-TSYRRK
           END-IF
      *
           .
       910-TSYRRK-SELECT-EXT.
           EXIT.
      *
      *****************************************************************
      *    低所得マスター読込
      *****************************************************************
       910-TSYRRK-READ-SEC        SECTION.
      *
           MOVE    "DBFETCH"           TO  MCP-FUNC
           CALL    "MONFUNC"            USING
                                        MCPAREA
                                        MCPDATA-REC
           IF      MCP-RC              =   ZERO
               MOVE    MCPDATA-REC        TO  TSYRRK-REC
               MOVE    ZERO               TO  FLG-TSYRRK
           ELSE
               INITIALIZE                     TSYRRK-REC
               MOVE    1                  TO  FLG-TSYRRK
           END-IF
      *
           .
       910-TSYRRK-READ-EXT.
           EXIT.
      *
      *****************************************************************
      *    低年金マスター検索
      *****************************************************************
       910-TNKRRK-SELECT-SEC        SECTION.
      *
           MOVE    TNKRRK-REC          TO  MCPDATA-REC
           MOVE    "DBSELECT"          TO  MCP-FUNC
           CALL    "MONFUNC"            USING
                                        MCPAREA
                                        MCPDATA-REC
           IF      MCP-RC              =   ZERO
               MOVE    ZERO               TO  FLG-TNKRRK
           ELSE
               MOVE    1                  TO  FLG-TNKRRK
           END-IF
      *
           .
       910-TNKRRK-SELECT-EXT.
           EXIT.
      *
      *****************************************************************
      *    低年金マスター読込
      *****************************************************************
       910-TNKRRK-READ-SEC        SECTION.
      *
           MOVE    "DBFETCH"           TO  MCP-FUNC
           CALL    "MONFUNC"            USING
                                        MCPAREA
                                        MCPDATA-REC
           IF      MCP-RC              =   ZERO
               MOVE    MCPDATA-REC        TO  TNKRRK-REC
               MOVE    ZERO               TO  FLG-TNKRRK
           ELSE
               INITIALIZE                     TNKRRK-REC
               MOVE    1                  TO  FLG-TNKRRK
           END-IF
      *
           .
       910-TNKRRK-READ-EXT.
           EXIT.
      *
      *****************************************************************
      *    入院診療行為マスター検索
      *****************************************************************
       910-NYUINACT-SELECT-SEC        SECTION.
      *
           MOVE    NYUINACT-REC        TO  MCPDATA-REC
           MOVE    "DBSELECT"          TO  MCP-FUNC
           CALL    "MONFUNC"            USING
                                        MCPAREA
                                        MCPDATA-REC
           IF      MCP-RC              =   ZERO
               MOVE    ZERO               TO  FLG-NYUINACT
           ELSE
               MOVE    1                  TO  FLG-NYUINACT
           END-IF
      *
           .
       910-NYUINACT-SELECT-EXT.
           EXIT.
      *
      *****************************************************************
      *    入院診療行為マスター読込
      *****************************************************************
       910-NYUINACT-READ-SEC         SECTION.
      *
           MOVE    "DBFETCH"           TO  MCP-FUNC
           CALL    "MONFUNC"            USING
                                        MCPAREA
                                        MCPDATA-REC
           IF      MCP-RC              =   ZERO
               MOVE    MCPDATA-REC         TO  NYUINACT-REC
               MOVE    ZERO                TO  FLG-NYUINACT
           ELSE
               INITIALIZE                      NYUINACT-REC
               MOVE    1                   TO  FLG-NYUINACT
           END-IF
      *
           .
       910-NYUINACT-READ-EXT.
           EXIT.
      *
      *****************************************************************
      *    ＤＢクローズ処理
      *****************************************************************
       910-DB-CLOSE-SEC                 SECTION.
      *
           MOVE    "DBCLOSECURSOR"      TO  MCP-FUNC
           CALL    "MONFUNC"            USING
                                        MCPAREA
                                        MCPDATA-REC
      *
           .
      *
       910-DB-CLOSE-EXT.
           EXIT.
